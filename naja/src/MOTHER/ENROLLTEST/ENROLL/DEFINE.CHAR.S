DEFINE_CHAR		LDA	#$40
				STA	PAGE
				JSR	CLEAR2	
				LDA	#GREEN	
				STA	COLOR
				LDY	#$07
				JSR	LINER3	
				LDY	#$08
				JSR	LINER3	
				LDA	#$03
				STA	COUNT
CRLOOP2			LDY	COUNT
				JSR	TEXTER3	
				DEC	COUNT
				BPL	CRLOOP2	
				LDX	#$00
MORNAME			LDA	NAME,X			;DRAW NAME
				STA	TEXTBUF,X
				INX
				CPX	#$0A
				BNE	MORNAME
				LDA	#$8D
				STA	TEXTBUF,X
				LDA	#$04
				LDY	#$48
				JSR	LINE
				LDX	RACE			;DRAW RACE
				LDA	RACEOFF,X
				TAX
				LDY	#$00
MORACE			LDA	RACEDAT,X
				STA	TEXTBUF,Y	
				INX
				INY
				CPY	#$07
				BNE	MORACE
				LDA	#$8D
				STA	TEXTBUF,Y	
				LDA	TEXTBUF+6
				CMP	#$13
				BNE	DOCYB
				LDA	#$18
				STA	TEXTBUF+6
				BNE	PCARED			;Always
DOCYB			CMP	#$0B
				BNE	PCARED
				LDA	#$1E
				STA	TEXTBUF+6
PCARED			LDA	#$04
				LDY	#$5F
				JSR	LINE
				LDA	#$FF
				STA	PROFSNT
				STA	PROFSNT+1
				LDX	RACE
				LDA	MINOFF,X		;LOAD MINIMUM CHARACTERIST VALUES
				TAX
				LDY	#$00
MORMINS			LDA	MINMUM,X
				STA	MINSTAT,Y		;STORE THEM IN MINIMUM TABLE
				STA	PRSNST,Y		;AND IN PRESENT VALUE TABLE
				INX
				INY
				CPY	#$09
				BNE	MORMINS
TOOMUCH			JSR	RNDMIZE
				LDA	RANDOM1
				LDX	RACE
				CMP	ADRANGE,X
				BEQ	NOPROB	
				BCS	TOOMUCH
NOPROB			CLC
				SED
				ADC	PRSNST+7
				STA	PRSNST+7
				CLD
				LDY	#$05
				SED
				LDA	#$00			;CALCULATE INORGANIC BASED ON ORGANIC
				SEC
				SBC	PRSNST,Y
				CLD
				LDY	#$06
				STA	PRSNST,Y		;STORE IT
				LDY	#$08
				STA	PRSNST,Y
				LDA	#$00
				STA	STATNUM
MORNUMS			JSR	STATOUT			;LOOP THROUGH AND DRAW STATS
				INC	STATNUM
				LDA	STATNUM
				CMP	#$09
				BNE	MORNUMS
				LDY	#$07			;CLEAR MINIMUM ADAPTATION POINTS
				LDA	#$00	
				STA	MINSTAT,Y
				LDA	#VIOLET	
				STA	COLOR
				LDA	#$00
				STA	BOXNUM			;SET BOX NUMBER
				JSR	BOXOUT
				JSR	PROFSN
				LDA	#$00
				STA	WHICH
MORGRPH			LDX	WHICH
				JSR	CALLA
				LDA	WHICH
				INC	WHICH
				CMP	#$04
				BNE	MORGRPH
				LDA	PRSNST+5
				JSR	MOD5
				STA	MOD
				LDA	PRSNST+6
				JSR	MOD5
				STA	MOD+1
				LDA	PRSNST+8
				JSR	MOD5
				STA	MOD+2
				LDA	#$00
				STA	WHICH
MORHGRP			LDX	WHICH
				JSR	ADJUSTR
				LDA	MOD,X
				STA	COUNT
				STA	HOLDIT
				LDA	#$00
				STA	DATCNT
				LDA	#$13
				STA	XHOLD
				JSR	HSTART2	
				LDA	WHICH
				INC	WHICH
				CMP	#$02
				BNE	MORHGRP
				LDA	SCNDARY
				LDA	UNSTROB
				JMP	KEYIN	
MOD5			LDY	#$00
				SEC
MOD5A			SED
				SBC	#$05
				CLD
				INY
				BCS	MOD5A
				DEY
				TYA
				RTS

BLINKY			LDA	#$10
				JSR	WAIT
				LDA	#$F0
				JSR	WAIT2
				BMI	KEYGOT
				LDA	#BLACK1
				STA	COLOR
				JSR	BOXOUT
				LDA	#$00
				JSR	WAIT2
				LDA	#VIOLET
				STA	COLOR
				JSR	BOXOUT
KEYIN			BIT	KEYBRD
				BPL	BLINKY	
KEYGOT			LDA	KEYBRD
				BIT	UNSTROB
				JSR	CHECK_LEFT
				BNE	NOLEFAR
LEFAR			LDA	#BLACK1
				STA	COLOR
				LDA	BOXNUM
				CMP	#$02
				BNE	NOREFLX
				LDA	#$06
				STA	BOXNUM
				JSR	BOXOUT
				LDA	#$02
				STA	BOXNUM
NOREFLX			JSR	BOXOUT
				LDA	#VIOLET	
				STA	COLOR
				DEC	BOXNUM
				BPL	NOZEROL
				LDA	#$05
				STA	BOXNUM
NOZEROL			LDA	BOXNUM
				CMP	#$03
				BNE	NORFLX2
				LDA	#$06
				STA	BOXNUM
				JSR	BOXOUT
				LDA	#$03	
				STA	BOXNUM
NORFLX2			JSR	BOXOUT
				JMP	KEYIN
NOLEFAR			JSR	CHECK_RIGHT
				BNE	NORIGAR
RIGAR			LDA	#BLACK1
				STA	COLOR
				LDA	BOXNUM
				CMP	#$03
				BNE	NOREFL
				LDA	#$06
				STA	BOXNUM
				JSR	BOXOUT
				LDA	#$03
				STA	BOXNUM
NOREFL			JSR	BOXOUT
				LDA	#VIOLET	
				STA	COLOR
				INC	BOXNUM
				LDA	BOXNUM
				CMP	#$02
				BNE	NOREFL2
				LDA	#$06
				STA	BOXNUM
				JSR	BOXOUT	
				LDA	#$02
				STA	BOXNUM
				BNE	NOSIXR			;Always
NOREFL2			CMP	#$06
				BNE	NOSIXR
				LDA	#$00
				STA	BOXNUM
NOSIXR			JSR	BOXOUT
				JMP	KEYIN
NORIGAR			JSR	CHECK_UP
				BEQ	UPPER
				JMP	NOUPPR
UPPER			LDX	BOXNUM
				CPX	#$05
				BNE	NOORG
				LDA	PRSNST,X
				CMP	MINSTAT,X
				BNE	OKAYED
				JMP	KEYIN
OKAYED			PHA
				TXA
				PHA
				LDX	#$08
				STX	STATNUM
				JSR	STATOUT
				PLA
				TAX
				PLA
				SED
				SEC	
				SBC	#$05	
				CLD
				JSR	CHNGST
				LDX	#$00
				JSR	ADJUSTR
				LDA	MOD
				SEC
				SBC	#$01
				JSR	HGRAPH	
				LDX	#$06
				LDA	PRSNST,X
				SED
				CLC
				ADC	#$05	
				CLD
				JSR	CHNGST
				LDX	#$08
				STX	STATNUM
				JSR	STATOUT
				LDX	#$01
				JSR	ADJUSTR
				LDA	MOD+1
				CLC
				ADC	#$01
				JSR	HGRAPH	
				LDX	#$02
				JSR	ADJUSTR
				LDA	MOD+2
				CLC
				ADC	#$01
				JSR	HGRAPH	
				JSR	PROFSN
				JMP	KEYIN
NOORG			TXA
				BNE	NOTINT
				LDA	PRSNST,X
				CMP	#$25	
				BNE	ALRIGHT
				JMP	KEYIN
NOTINT			LDA	PRSNST,X
				CMP	#$22	
				BNE	ALRIGHT	
				JMP	KEYIN
ALRIGHT			PHA
				LDX	#$07
				LDA	PRSNST,X
				BNE	GOADAPT
				PLA
				JMP	KEYIN
GOADAPT			SED
				SEC
				SBC	#$01
				CLD
				JSR	CHNGST
				LDX	BOXNUM
				CPX	#$00
				BEQ	UCONN
				CPX	#$04
				BNE	UNOT40
				STX	STATNUM
				JSR	STATOUT
				JMP	UNOT40
UCONN			LDA	PRSNST+4
				BEQ	UNOT40
				LDX	#$04
				STX	STATNUM
				JSR	STATOUT
				LDX	#$00
UNOT40			PLA
				SED
				CLC
				ADC	#$01
				CLD
				LDX	BOXNUM
				PHA
				JSR	CALL	
				PLA
				LDX	BOXNUM
				CPX	#$04
				BNE	UNOT42
				STX	STATNUM
				STA	PRSNST,X
				JSR	STATOUT
				JMP	UNOT43
UNOT42			JSR	CHNGST
				CPX	#$00
				BNE	UNOT43
				LDY	PRSNST+4
				BEQ	UNOT43
				LDX	#$04
				STX	STATNUM
				JSR	STATOUT
UNOT43			JSR	PROFSN
				JMP	KEYIN
NOUPPR			JSR	CHECK_DOWN
				BEQ	DOWNER	
				JMP	NODWNR
DOWNER			LDX	BOXNUM
				CPX	#$05
				BNE	NOINORG
				LDX	#$06
				LDA	PRSNST,X
				CMP	MINSTAT,X
				BNE	OKAY
				JMP	KEYIN
OKAY			PHA
				TXA
				PHA
				LDX	#$08
				STX	STATNUM
				JSR	STATOUT
				PLA
				TAX
				PLA
				SED
				SEC
				SBC	#$05	
				CLD
				JSR	CHNGST
				LDX	#$01
				JSR	ADJUSTR
				LDA	MOD+1
				SEC
				SBC	#$01
				JSR	HGRAPH	
				LDX	#$02
				JSR	ADJUSTR
				LDA	MOD+2
				SEC
				SBC	#$01
				JSR	HGRAPH
				LDX	#$05
				LDA	PRSNST,X
				SED	
				CLC
				ADC	#$05	
				CLD
				JSR	CHNGST
				LDX	#$08
				STX	STATNUM
				JSR	STATOUT
				LDX	#$00
				JSR	ADJUSTR
				LDA	MOD
				CLC
				ADC	#$01
				JSR	HGRAPH	
				JSR	PROFSN
				JMP	KEYIN
NOINORG			LDA	PRSNST,X
				CMP	MINSTAT,X
				BNE	GODOWN
				JMP	KEYIN
GODOWN			PHA
				LDX	#$07
				LDA	PRSNST,X
				SED
				CLC
				ADC	#$01
				CLD
				JSR	CHNGST
				LDX	BOXNUM
				CPX	#$00
				BEQ	CONNECT
				CPX	#$04
				BNE	NOT40
				STX	STATNUM
				JSR	STATOUT
				JMP	NOT40
CONNECT			LDA	PRSNST+4
				BEQ	NOT40
				LDX	#$04
				STX	STATNUM
				JSR	STATOUT
				LDX	#$00
NOT40			PLA
				SED
				SEC
				SBC	#$01
				CLD	
				LDX	BOXNUM
				PHA
				JSR	CALL	
				PLA
				LDX	BOXNUM
				CPX	#$04
				BNE	NOT42
				STX	STATNUM
				STA	PRSNST,X
				JSR	STATOUT
				JMP	NOT43
NOT42			JSR	CHNGST
				CPX	#$00
				BNE	NOT43
				LDY	PRSNST+4
				BEQ	NOT43
				LDX	#$04
				STX	STATNUM
				JSR	STATOUT
NOT43			JSR	PROFSN
				JMP	KEYIN
NODWNR			JSR	CHECK_ENTER
				BEQ	SPACIT
				JMP	KEYIN
SPACIT			LDA	#BLACK1
				STA	COLOR
				JSR	BOXOUT
				LDY	#$06
				JSR	LINER3
				LDA	PRSNST+7
				BEQ	ADAPOUT
				LDA	#$03
				JSR	WBLINK
				JMP	WBCOM
ADAPOUT			LDA	PROFSNT
				CMP	#$FF
				BNE	DOSLECT
				LDA	#$02
				JSR	WBLINK
WBCOM			LDA	#VIOLET
				STA	COLOR
				JSR	BOXOUT
				LDA	BOXNUM
				CMP	#$02
				BEQ	ISUPLOW
				CMP	#$03
				BNE	NOUPLOW
ISUPLOW			LDY	#$06
				JSR	LINER3
NOUPLOW			JMP	KEYIN
DOSLECT			LDA	#BLACK1
				STA	COLOR
				LDY	#$08
				JSR	LINER3
				LDY	#$01
				JSR	TEXTER3
				JSR	SETTOFF
				LDA	#$00
				STA	LEGALS
				STA	LEGALS+2
				LDA	#$7F
				STA	LINTABL
				LDA	#$89
				STA	LINTABL+1
				STA	LINTABL+2
				LDA	PROFSNT+1
				CMP	#$FF
				BEQ	FINEAS
				LDA	#$00
				STA	LEGALS+1
				LDA	#$94
				STA	CANCMOD
				STA	LINTABL+2
FINEAS			LDA	#$02
				STA	CURSOR
				LDA	#$03	
				STA	XHOLD
				LDY	#$04
				JSR	TEXTER3
				JSR	KDOWN2
				LDA	#$89
				STA	CANCMOD
				LDA	CURSOR
				CMP	#$02
				BNE	NOCANCL
				LDA	#$20
				STA	PAGE
				JSR	CLRBOX
				STA	PRIMARY
				PLA
				PLA
				JMP	ANYONE	
NOCANCL			TAX
				LDA	PROFSNT,X	
GOOD1			ASL
				ASL
				ASL
				ASL
				ORA	RACE	
				STA	PROFRAC
				LDA	#$20
				STA	PAGE
				JSR	CLRBOX	
				STA	PRIMARY
				RTS
WBLINK			STA	TEHOLD
				LDA	#$01	
				STA	COUNT
WBLOOP			JSR	OFFON
				DEC	COUNT
				BPL	WBLOOP
				RTS
OFFON			LDA	#$00
				JSR	WAIT2
				LDY	TEHOLD
				JSR	TEXTER3
				LDA	#$00
				JSR	WAIT2
				LDY	TEHOLD
				JMP	TEXTER3
TEHOLD			DB	$00

STATOUT			LDX	STATNUM
				CPX	#$04
				BNE	NORMST
				LDA	#$02
				STA	MISC
				LDY	RACE
				LDA	BASECRD,Y
				STA	MISC+1
				LDA	PRSNST,X
				BEQ	ADDEDIT
				PHA
CREDADD			LDA	PRSNST
				CLC
				SED
				ADC	MISC+1
				STA	MISC+1
				LDA	MISC
				ADC	#$00
				STA	MISC
				PLA
				SEC
				SBC	#$01
				CLD
				PHA
				BNE	CREDADD
				PLA
ADDEDIT			LDA	MISC
				JSR	SPLIT
				LDA	FEEDERH
				STA	TEXTBUF
				LDA	FEEDERL
				STA	TEXTBUF+1
				LDA	MISC+1
				JSR	SPLIT
				LDA	FEEDERH
				STA	TEXTBUF+2
				LDA	FEEDERL
				STA	TEXTBUF+3
				LDA	#$8D
				STA	TEXTBUF+4
				LDY	#$00
ZEROUT			LDA	TEXTBUF,Y
				BNE	ZEROD
				LDA	#$0A
				STA	TEXTBUF,Y
				INY
				CPY	#$03
				BNE	ZEROUT
ZEROD			LDX	#$04
				JMP	TOUT
NORMST			CPX	#$08
				BNE	NORM2
				LDA	#$20
				CLC
				SED
				ADC	PRSNST+6
				STA	MISC2+1
				LDA	#$03
				ADC	#$00
				STA	MISC2
				CLD
				LDA	MISC2
				JSR	SPLIT
				LDA	#$0A	
				STA	TEXTBUF
				LDA	FEEDERL
				STA	TEXTBUF+1
				LDA	MISC2+1
				JSR	SPLIT
				LDA	FEEDERH
				STA	TEXTBUF+2
				LDA	FEEDERL
				STA	TEXTBUF+3
				LDA	#$8D
				STA	TEXTBUF+4
				LDA	#$64
				LDY	#$29
				JSR	LINE
				LDA	TEXTBUF+1
				ASL
				ASL
				ASL
				ASL
				ORA	TEXTBUF+2
				STA	MISC2
				LDA	TEXTBUF+3
				ASL
				ASL
				ASL
				ASL
				STA	MISC2+1
				RTS
NORM2			LDA	PRSNST,X
				JSR	SPLIT
				LDA	FEEDERH
				BEQ	NOTHER
				STA	TEXTBUF
				JMP	OTHER1
NOTHER			LDA	#$0A
				STA	TEXTBUF
OTHER1			LDA	FEEDERL	
				STA	TEXTBUF+1
				LDA	#$8D
				STA	TEXTBUF+2
				JMP	TOUT
CHNGST			PHA
				LDA	PRSNST,X
				LSR
				LSR
				LSR
				LSR
				STA	HOLDIT
				PLA
				PHA
				LSR
				LSR
				LSR
				LSR
				CMP	HOLDIT
				BEQ	NOFIRST
				PHA
				LDA	HOLDIT
				BEQ	OVERIT
				STA	TEXTBUF
				LDA	#$8D
				STA	TEXTBUF+1
				JSR	TOUT
OVERIT			PLA	
				BEQ	NOFIRST
				STA	TEXTBUF
				LDA	#$8D
				STA	TEXTBUF+1
				JSR	TOUT
NOFIRST			LDA	PRSNST,X
				AND	#$0F
				STA	TEXTBUF+1
				LDA	#$0A
				STA	TEXTBUF
				LDA	#$8D
				STA	TEXTBUF+2
				JSR	TOUT
				PLA
				STA	PRSNST,X	
				AND	#$0F
				STA	TEXTBUF+1
				LDA	#$0A
				STA	TEXTBUF
				LDA	#$8D
				STA	TEXTBUF+2
				JSR	TOUT
				RTS
TOUT			LDA	EXPNT,X
				STA	XCOORD
				LDA	XSHFT,X
				STA	SHIFT
				LDA	YPNT2,X
				STA	YCOORD
				TXA
				PHA
				JSR	LINE2
				PLA
				TAX
				RTS
GRAPHA			LDA	#$9F
				STA	YLEVEL
				LDA	#$00
				STA	PICPNT
				LDA	PRSNST,X
				STA	COUNT
				BNE	NEXTLIN
				JMP	TOPOUT
GRAPH			STA	HOLDIT
				LDA	PRSNST,X
				SED
				SEC
				SBC	HOLDIT
				CLD
				BPL	GODWN
				LDA	HOLDIT
				SED
				SEC
				SBC	PRSNST,X
				STA	COUNT
				CLD
NEXTLIN			LDA	#$00
				STA	COUNTR4
				LDA	PICPNT
PNT1			CMP	#$4B
				BNE	LIN2
PNTA			LDA	#$00
				STA	PICPNT
LIN2			LDX	YLEVEL
				LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	PAGE
				STA	SCREENH
PNT2			LDY	#$09
LINEUP			LDX	PICPNT
PNT3			LDA	$FFFF,X			;Modified
				ORA	(SCREENL),Y
				STA	(SCREENL),Y
				INY
				INC	PICPNT
PNT4			CPY	#$0E
				BNE	LINEUP
				CPY	#$21
				BNE	NOFIXIT
				LDX	#$80
				DEY
				LDA	(SCREENL),Y
				CMP	#$D5
				BNE	FIXIT
				LDX	#$81
FIXIT			TXA
				INY
				STA	(SCREENL),Y
NOFIXIT			INC	COUNTR4
				DEC	YLEVEL
				LDA	PICPNT
PNT5			CMP	#$4B
				BNE	NORESET
PNTB			LDA	#$00
				STA	PICPNT
NORESET			LDA	YLEVEL
				CMP	#$3C
				BEQ	TOPOUT
				LDA	COUNTR4
				CMP	#$04
				BNE	LIN2
				SED
				LDA	COUNT
				SEC
				SBC	#$01
				STA	COUNT
				CLD
				BNE	NEXTLIN
TOPOUT			RTS
GODWN			LDA	PICPNT
				BNE	GOON
PNTC			LDA	#$4B
				STA	PICPNT
GOON			DEC	PICPNT
				LDA	#$00
				STA	COUNTR4
				INC	YLEVEL
GODOWN2			LDX	YLEVEL
				LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	PAGE
				STA	SCREENH
PNT6			LDY	#$09
				LDA	PNT7+1
				CMP	#$15
				BNE	UPLINE
				LDA	#$E0
				STA	PNT8+1
				LDA	#$1B
				STA	PNT7+1
UPLINE			LDA	#$00
PNT7			CPY	#$09
				BNE	CSTORE
				LDA	(SCREENL),Y
PNT8			AND	#$07
CSTORE			STA	(SCREENL),Y
				INY
				DEC	PICPNT
PNT9			CPY	#$0E
				BNE	UPLINE
				CPY	#$21
				BNE	NOFIX2
				LDA	#$00
				STA	(SCREENL),Y
NOFIX2			INC	COUNTR4
				INC	YLEVEL
				LDA	PICPNT
PNTD			CMP	#$FF
				BNE	NOCLEER
PNT10			LDA	#$4A
				STA	PICPNT
NOCLEER			LDA	YLEVEL
				CMP	#$40	
				BEQ	GOTOP	
				LDA	COUNTR4
				CMP	#$04
				BNE	GODOWN2
GOTOP			INC	PICPNT
				DEC	YLEVEL
				JMP	TOPOUT
CALLA			TXA
				PHA
				JSR	ADJUST
				JSR	GRAPHA
SAMETH			PLA
				TAX
				LDA	YLEVEL
				STA	LEVEL,X
				LDA	PICPNT
				STA	POINTER,X
				RTS
CALL			PHA
				LDA	LEVEL,X
				STA	YLEVEL
				LDA	POINTER,X
				STA	PICPNT
				JSR	ADJUST
				PLA
				TAY
				TXA
				PHA
				TYA
				JSR	GRAPH
				JMP	SAMETH
ADJUST			LDA	ROWB,X
				STA	PNT2+1
				STA	PNT6+1
				STA	PNT7+1
				LDA	ROWE,X
				STA	PNT4+1
				STA	PNT9+1
				LDA	LIMIT,X
				STA	PNT1+1
				STA	PNT5+1
				STA	PNTC+1
				SEC
				SBC	#$01
				STA	PNT10+1
				LDA	ANDNUM,X
				STA	PNT8+1
				LDA	LWLIMIT,X
				STA	PNTA+1
				STA	PNTB+1
				LDA	ECHECK,X
				STA	PNTD+1
				TXA
				ASL
				TAX
				LDA	DATPNT,X
				STA	PNT3+1
				INX
				LDA	DATPNT,X
				STA	PNT3+2
				DEX
				TXA
				LSR
				TAX
				RTS
HGRAPH			STA	HOLDIT
				LDA	MOD,X
				SEC
				SBC	HOLDIT
				BPL	HDOWN
				LDA	HOLDIT
				SEC
				SBC	MOD,X
				STA	COUNT
				LDA	MOD,X
				STA	DATCNT
				CLC
				ADC	#$13
				STA	XHOLD
HSTART2			LDA	#$FF			;PNT.5  Modified
				STA	LINENUM
POINT1			LDA	#$FF			;Modified
				STA	DATPNTL	
POINT2			LDA	#$FF			;Modified
				STA	DATPNTH	
MOROFIT			LDY	LINENUM
				LDA	LOBYTES,Y
				STA	SCREENL
				LDA	HIBYTES,Y
				ORA	PAGE
				STA	SCREENH
				LDY	DATCNT
				LDA	(DATPNTL),Y
				LDY	XHOLD
				STA	(SCREENL),Y
				LDA	LINENUM
				INC	LINENUM
POINT3			CMP	#$FF			;Modified
				BEQ	NOMOROF
				LDA	DATPNTL	
				CLC
				ADC	#$13
				STA	DATPNTL	
				BCC	MOROFIT
				INC	DATPNTH	
				JMP	MOROFIT
NOMOROF			INC	XHOLD
				INC	DATCNT
				DEC	COUNT
				BEQ	ALLDONE
				JMP	HSTART2
ALLDONE			LDA	HOLDIT
				STA	MOD,X
				RTS
HDOWN			LDA	MOD,X
				CLC
				ADC	#$13
				STA	XHOLD
				DEC	XHOLD
POINT4			LDA	#$FF			;Modified
				STA	LINENUM
MORDOWN			LDY	LINENUM
				LDA	LOBYTES,Y
				STA	SCREENL
				LDA	HIBYTES,Y
				ORA	PAGE
				STA	SCREENH
				LDA	#$00
				LDY	XHOLD
				STA	(SCREENL),Y
				LDA	LINENUM
				INC	LINENUM
POINT5			CMP	#$FF			;Modified
				BNE	MORDOWN
				LDA	HOLDIT
				STA	MOD,X
				RTS
ADJUSTR			LDA	BEGLINS,X
				STA	HSTART2+1
				STA	POINT4+1
				LDA	ENDLINS,X
				STA	POINT3+1
				STA	POINT5+1
				TXA
				ASL
				TAX
				LDA	DATAS,X
				STA	POINT1+1
				INX
				LDA	DATAS,X
				STA	POINT2+1
				DEX
				TXA
				LSR
				TAX
				RTS
PROFSN			LDA	#$00
				STA	MINPNT
				STA	BCHECKD
PRFLP			LDY	#$00
				LDX	MINPNT
PRFLOOP			LDA	PRSNST,Y
				CMP	PROFMIN,X
				BCC	FAILS
				INX
				INY
				CPY	#$07
				BNE	PRFLOOP
				JSR	PRFDRAW
SKIPLOP			LDA	BCHECKD
				INC	BCHECKD
				CMP	#$04	
				BEQ	PRFDONE
				LDA	#$07
				CLC
				ADC	MINPNT
				STA	MINPNT
				JMP	PRFLP
FAILS			JSR	PRFERAS
				JMP	SKIPLOP
PRFDONE			LDA	PROFSNT+1	
				TAX
				CMP	#$FF
				BEQ	DONE2
				LDA	PROFSNT
				CMP	#$FF
				BNE	DONE2
				TXA
				STA	BCHECKD
				JSR	PRFERAS
				JSR	PRFDRAW
DONE2			RTS
PRFDRAW			LDX	#$01	
ANOTHER			LDA	PROFSNT,X
				CMP	BCHECKD
				BEQ	ITSON
				DEX
				BPL	ANOTHER
				LDX	#$00
CONTINU			LDA	PROFSNT,X
				CMP	#$FF
				BEQ	OPENING
				INX
				CPX	#$02	
				BNE	CONTINU
				JMP	ITSON
OPENING			LDA	BCHECKD
				STA	PROFSNT,X
SPLICE			LDY	BCHECKD
				LDA	PROFOFF,Y
				TAY
				TXA
				PHA
				LDX	#$00	
LOADMOR			LDA	PROFDAT,Y
				STA	TEXTBUF,X
				INX
				INY
				CPX	#$08	
				BNE	LOADMOR
				LDA	BCHECKD
				CMP	#$04
				BNE	NOPHYSN
				LDA	#$18
				STA	TEXTBUF+6
				DEX	
NOPHYSN			LDA	#$8D
				STA	TEXTBUF,X
				PLA
				TAX
				LDA	#$10
				LDY	PROFY,X
				JSR	LINE
ITSON			RTS
PRFERAS			LDX	#$01	
NOTHERA			LDA	PROFSNT,X
				CMP	BCHECKD
				BEQ	FOUNDED
				DEX
				BPL	NOTHERA	
				RTS
FOUNDED			LDA	#$FF
				STA	PROFSNT,X
				JMP	SPLICE	
LEVEL			DB	$00,$00,$00
				DB	$00,$00
POINTER			DB	$00,$00,$00
				DB	$00,$00
MINOFF			DB	$00,$08,$10
				DB	$18,$20
MINMUM			DB	$07,$11,$10
				DB	$11,$00,$25
				DB	$20,$28
				DB	$13,$17,$05
				DB	$06,$00,$35
				DB	$25,$32
				DB	$11,$13,$09
				DB	$07,$00,$30
				DB	$35,$29
				DB	$16,$06,$10
				DB	$10,$00,$20
				DB	$40,$31
				DB	$03,$05,$14
				DB	$14,$00,$40
				DB	$15,$30
ADRANGE			DB	$05,$02,$09
				DB	$02,$05
EXPNT			DB	$0A,$10,$17
				DB	$1D,$22,$10
				DB	$10,$04,$0E
XSHFT			DB	$06,$04,$04
				DB	$04,$06,$00
				DB	$00,$00,$02
YPNT2			DB	$A1,$A1,$A1
				DB	$A1,$A1,$15
				DB	$03,$B5,$28
PRSNST			DB	$00,$00,$00
				DB	$00,$00,$00
				DB	$00,$00,$00
MINSTAT			DB	$00,$00,$00
				DB	$00,$00,$00
				DB	$00,$00,$00
ROWB			DB	$09,$0F,$15
				DB	$1B,$22
ROWE			DB	$0E,$14,$1C
				DB	$21,$27
LIMIT			DB	$4B,$D7,$70
				DB	$4E,$41
ANDNUM			DB	$07,$00,$00
				DB	$83,$00
MISC			DB	$00,$00
MISC2			DB	$00,$00

BASECRD			DB	$50,$75,$25
				DB	$00,$50
DATPNT			DW	BRAIN	
				DW	ARM	
				DW	LEGS	
				DW	ARROWS	
				DW	CREDITS
LWLIMIT			DB	$00,$23,$00
				DB	$06,$00
ECHECK			DB	$FF,$22,$FF
				DB	$05,$FF
MOD				DB	$00,$00,$00
DATAS			DW	DNA	
				DW	TRONICS	
				DW	LBOLT
BEGLINS			DB	$14,$03,$22
ENDLINS			DB	$1B,$0A,$36
PROFY			DB	$7F,$89
PROFMIN			DB	$06,$21,$19
				DB	$20,$00,$45
				DB	$20
				DB	$25,$10,$16
				DB	$15,$00,$20
				DB	$60
				DB	$22,$14,$11
				DB	$11,$08,$45
				DB	$30
				DB	$14,$18,$17
				DB	$17,$00,$30
				DB	$45
				DB	$23,$10,$14
				DB	$19,$00,$60
				DB	$15
