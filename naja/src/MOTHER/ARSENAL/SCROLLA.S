TALKER			BIT	UNSTROB
				JSR	DRIVE_OFF
				JSR	CLEAR2
				LDA	#$40
				STA	PAGE
				JSR	DOTEXT
				LDA	KEYBRD
				BPL	LOOP2
KEYCOM			BIT	UNSTROB
				CMP	#$9B
				BNE	:SKIPA
				JMP	GOBACK
:SKIPA			JSR	DRIVE_ON
				JMP	SMAINER

LOOP2			LDA	#$00
				STA	COUNT
LOOP3			LDX	COUNT
				DEC	DELWAIT,X
				BNE	SKIP3B
				LDA	DELMAX,X
				STA	DELWAIT,X
				TXA
				JSR	SCROLL
				LDX	COUNT
				INC	XSPOTS,X
				LDA	XSPOTS,X
				CMP	#$28
				BNE	SKIP3A
				LDA	#$00
				STA	XSPOTS,X
				LDA	YSPOTS,X
				CLC
				ADC	#$07
				STA	YSPOTS,X
				CMP	YENDS+1,X
				BNE	SKIP3A
				LDA	YENDS,X
				STA	YSPOTS,X
SKIP3A			LDA	OPENER,X
				CMP	#$CC
				BNE	GOPEN
				DEC	MOUNUMB,X
				BNE	MOUCOMN
				LDA	#$AA
				STA	OPENER,X
				BNE	MOUCOMN
GOPEN			LDA	MOUNUMB,X
				CMP	#$03
				BEQ	REVERSE
				INC	MOUNUMB,X
				JSR	RNDMIZE
				LDX	COUNT
				LDA	RANDOM1
				AND	#$03
				BNE	MOUCOMN
REVERSE			LDA	#$CC
				STA	OPENER,X
MOUCOMN			LDA	MOUNUMB,X
				JSR	MOUTHER
SKIP3B			INC	COUNT
				LDA	COUNT
				CMP	#$03
				BNE	LOOP3
				LDA	#$10
				JSR	WAIT
				JMP	LOOP2

MOUTHER			ASL
				ASL
				ASL
				STA	POINTER
				LDX	COUNT
				LDA	MOUTHX,X
				TAX
				CLC
				ADC	#$04
				STA	ENDLINE2
:LOOP1			LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	#$20
				STA	SCREENH
				LDY	POINTER
				LDA	MOUTHS,Y
				LDY	#$0D
				STA	(SCREENL),Y
				INC	POINTER
				LDY	POINTER
				LDA	MOUTHS,Y
				LDY	#$0E
				STA	(SCREENL),Y
				INC	POINTER
				INX
				CPX	ENDLINE2
				BNE	:LOOP1
				RTS

MOUTHX			DB	$10,$15,$1A
MOUTHS			DB	$55,$2A,$55
				DB	$2A,$51,$22
				DB	$05,$28
				DB	$55,$2A,$55
				DB	$2A,$01,$20
				DB	$05,$28
				DB	$55,$2A,$05
				DB	$28,$01,$20
				DB	$05,$28
				DB	$05,$28,$01
				DB	$20,$01,$20
				DB	$05,$28
OPENER			DB	$AA,$CC,$AA
MOUNUMB			DB	$00,$02,$00
DELWAIT			DB	$20,$40,$60
DELMAX			DB	$68,$7C,$88

SCROLL			TAY
				LDA	YLINES,Y
				STA	LCOUNT1
				CLC
				ADC	#$07
				STA	ENDLINE2
				LDA	YSPOTS,Y
				PHA
				LDA	#$01
				STA	COLUMN
:LOOP1			LDX	LCOUNT1
:LOOP2			LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	#$20
				STA	SCREENH
				LDY	COLUMN
				LDA	(SCREENL),Y
				DEY
				STA	(SCREENL),Y
				INX
				CPX	ENDLINE2
				BNE	:LOOP2
				INC	COLUMN
				LDA	COLUMN
				CMP	#$28
				BNE	:LOOP1
				PLA
				STA	LCOUNT2
				CLC
				ADC	#$07
				STA	ENDLINE2
:LOOP3			LDX	LCOUNT1
				LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	#$20
				STA	SCREENH
				LDX	LCOUNT2
				LDA	LOBYTES,X
				STA	DESTINL
				LDA	HIBYTES,X
				ORA	#$40
				STA	DESTINH
				LDX	COUNT
				LDY	XSPOTS,X
				LDA	(DESTINL),Y
				LDY	#$27
				STA	(SCREENL),Y
				INC	LCOUNT1
				INC	LCOUNT2
				LDA	LCOUNT2
				CMP	ENDLINE2
				BNE	:LOOP3
				LDA	KEYBRD
				BPL	:EXIT
				BIT	UNSTROB
				CMP	#$93			;Control-S
				BEQ	:SKIPB
				JMP	KEYCOM
:SKIPB			BIT	KEYBRD
				BPL	:SKIPB
				BIT	UNSTROB
:EXIT			RTS

YLINES			DB	$9E,$AA,$B6
YSPOTS			DB	$00,$3F,$7E
XSPOTS			DB	$00,$00,$00
YENDS			DB	$00,$3F,$7E
				DB	$AF

DOTEXT			LDA	#$00
				STA	LINECNT2
				LDA	#<TSET1
				STA	ADPNTL
				LDA	#>TSET1
				STA	ADPNTH
:LOOP1			LDY	#$00
				LDX	#$00
:LOOP2			LDA	(ADPNTL),Y
				CMP	#$FF
				BEQ	:EXIT
				STA	TEXTBUF,X
				INY
				INX
				CPX	#$2F
				BNE	:LOOP2
				TYA
				CLC
				ADC	ADPNTL
				STA	ADPNTL
				LDA	ADPNTH
				ADC	#$00
				STA	ADPNTH
				LDA	#$8D
				STA	TEXTBUF,X
:LOOP3			DEX
				LDA	TEXTBUF,X
				CMP	#$0A
				BNE	:LOOP3
				LDA	#$8D
				STA	TEXTBUF,X
				TXA
				PHA
:LOOP4			DEX
				LDA	TEXTBUF,X
				CMP	#$0A
				BNE	:LOOP4
				LDA	#$8D
				STA	TEXTBUF,X
				TXA
				PHA
				LDA	#$00
				LDY	LINECNT2
				JSR	LINE
				PLA
				JSR	TEXSUB
				PLA
				JSR	TEXSUB
				LDA	#$00
				STA	LINE2+4
				LDA	KEYBRD
				BPL	:SKIPA
				JMP	KEYCOM
:SKIPA			LDA	LINECNT2
				CLC
				ADC	#$07
				STA	LINECNT2
				BNE	:LOOP1
:EXIT			RTS

TEXSUB			TAX
				LDA	#$0A
				STA	TEXTBUF,X
				STX	LINE2+4
				LDA	SHIFT
				BNE	:SKIPA
				LDA	#$07
				STA	SHIFT
				DEC	XCOORD
:SKIPA			DEC	SHIFT
				JMP	LINE2
