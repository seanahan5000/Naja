COLOR_BYTE		EQU	$00
LINE_NUMBER		EQU	$01

FLOOR_FILL		LDA	#189
				STA	LINE_NUMBER
:1				LDX	LINE_NUMBER
				LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	PAGE
				STA	SCREENH

				LDA	FLOOR_COLOR
				EOR	#$7F
				STA	COLOR_BYTE
				LDY	#19
				LDA	(SCREENL),Y
				BNE	:6
				TXA
				AND	#%00000001
				BEQ	:5
				BNE	:21				;Always
:2				LDA	(SCREENL),Y
				BNE	:3
:21				LDA	COLOR_BYTE
				STA	(SCREENL),Y
				EOR	#$7F
				STA	COLOR_BYTE
				DEY
				BPL	:2
				BMI	:40				;Always

:3				LDX	#6
:31				LDA	(SCREENL),Y
				AND	LEFT_MASKS,X
				BNE	:32
				DEX
				BNE	:31				;Always
:32				LDA	LEFT_MASKS+1,X
				ORA	#%10000000
				AND	COLOR_BYTE
				ORA	(SCREENL),Y
				STA	(SCREENL),Y

:40				LDA	FLOOR_COLOR
				STA	COLOR_BYTE
				LDY	#20
:4				LDA	(SCREENL),Y
				BNE	:41
				LDA	COLOR_BYTE
				STA	(SCREENL),Y
				EOR	#$7F
				STA	COLOR_BYTE
				INY
				CPY	#40
				BNE	:4
				BEQ	:5				;Always

:41				LDX	#6
:42				LDA	(SCREENL),Y
				AND	RIGHT_MASKS,X
				BNE	:43
				DEX
				BNE	:42				;Always
:43				LDA	RIGHT_MASKS+1,X
				ORA	#%10000000
				AND	COLOR_BYTE
				ORA	(SCREENL),Y
				STA	(SCREENL),Y

:5				DEC	LINE_NUMBER
				BNE	:1				;Always
:6				RTS

FLOOR_COLOR		DB	BLUE

LEFT_MASKS		DB	%01111111
				DB	%01111110
				DB	%01111100
				DB	%01111000
				DB	%01110000
				DB	%01100000
				DB	%01000000
				DB	%00000000

RIGHT_MASKS		DB	%01111111
				DB	%00111111
				DB	%00011111
				DB	%00001111
				DB	%00000111
				DB	%00000011
				DB	%00000001
				DB	%00000000
