ANIMATE			LDA	#$04
				STA	BUBBLE
				LDA	KEYBRD
				BMI	KEY_WAS_HIT
:LOOP1			JSR	DRAW_BUBBLE
:SKIPB			LDY	BUBBLE
				LDX	BUBBLE
				LDA	HEIGHT,Y
				DEC	HEIGHT,X
				CMP	POP_HEIGHT,Y
				BNE	:SKIPA
:LOOP2			JSR	RNDMIZE
				LDA	RANDOM1
				AND	#$07
				CMP	#$04
				BCS	:LOOP2
				STA	MASK_OF_5,Y
:LOOP3			JSR	RNDMIZE
				LDA	RANDOM1
				AND	#$07
				CMP	#$07
				BEQ	:LOOP3
				CLC
				ADC	#BASE_HEIGHT
				STA	POP_HEIGHT,Y
				LDA	#BASE_HEIGHT+7
				STA	HEIGHT,Y
:SKIPA			JSR	DRAW_BUBBLE
				DEC	BUBBLE
				BPL	:LOOP1
				LDA	#$A0
				JSR	WAIT
				LDA	KEYBRD
				BPL	ANIMATE
KEY_WAS_HIT		BIT	UNSTROB
				CMP	#$9B
				BEQ	:EXIT
				JSR	CHECK_ENTER
				BNE	ANIMATE
				CLC
:EXIT			RTS

DRAW_BUBBLE		LDY	BUBBLE
				LDX	HEIGHT,Y
				LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	PAGE
				STA	SCREENH
				LDA	MASK_OF_5,Y
				ASL
				TAX
				LDA	MASKS,X
				LDY	#BASE_COLUMN
				EOR	(SCREENL),Y
				STA	(SCREENL),Y
				INY
				LDA	MASKS+1,X
				EOR	(SCREENL),Y
				STA	(SCREENL),Y
				RTS

MASKS			DB	%00010000,%00000000
				DB	%01000000,%00000000
				DB	%00000000,%00000010
				DB	%00000000,%00001000

HEIGHT			DB	$6E,$6E,$70,$6C,$6B
POP_HEIGHT		DB	$6C,$6A,$6B,$69,$69
MASK_OF_5		DB	$02,$00,$02,$01,$00
