DRAW_EDATA		LDA	ENTRY_TYPE
				ASL
				TAX
				LDA	:DRAWS+1,X
				PHA
				LDA	:DRAWS,X
				PHA
				RTS
:DRAWS			DW	DRAW_TRANSFER-1
				DW	DRAW_NAME-1
				DW	DRAW_0_99-1
				DW	DRAW_0_255-1
				DW	DRAW_0_9999-1
				DW	0
				DW	DRAW_YESNO-1
				DW	DRAW_SOMEVERY-1
				DW	DRAW_SIZE-1
				DW	DRAW_BLASTX-1
				DW	DRAW_BLASTY-1
				DW	DRAW_INTNSITY-1
				DW	DRAW_STATUS-1
				DW	DRAW_ATTACK-1
				DW	DRAW_RUSHDEL-1
				DW	DRAW_PERCENT-1
				DW	DRAW_AIM-1
				DW	DRAW_SHOTS-1
				DW	DRAW_RUNAFTER-1
				DW	DRAW_RUNUNTIL-1


DRAW_NAME		LDY	ENTRY_OFFSET
				LDX	#0
:1				LDA	ALIEN_BUFFER,Y
				STA	TEXTBUF,X
				INY
				INX
				CPX	#16
				BNE	:1
				BEQ	DRAW_COM2		;Always

*-------------------------------

DRAW_0_255		LDY	ENTRY_OFFSET
				LDA	ALIEN_BUFFER,Y
				CMP	#$FF
				BNE	:0
				LDA	#0
				LDX	#<:NO_LIMIT
				LDY	#>:NO_LIMIT
				JMP	STRING_GROUP
:NO_LIMIT		USR	(UNLIMITED)

:0				STA	TEMP
				LDX	#0
:1				LDA	TEMP
				SEC
				SBC	#100
				BCC	:2
				STA	TEMP
				INX
				BNE	:1				;Always
:2				STX	TEXTBUF

				LDX	#0
:3				LDA	TEMP
				SEC
				SBC	#10
				BCC	:4
				STA	TEMP
				INX
				BNE	:3				;Always
:4				STX	TEXTBUF+1
				LDA	TEMP
				STA	TEXTBUF+2
*LDX #0
*:5 LDA TEXTBUF,X
*BNE :6
*LDA #$0A
*STA TEXTBUF,X
*INX
*CPX #2
*BNE :5
:6				LDX	#3
				BNE	DRAW_COM2		;Always

*-------------------------------

DRAW_0_9999		LDY	ENTRY_OFFSET
				LDA	ALIEN_BUFFER+1,Y
				PHA
				LDA	ALIEN_BUFFER,Y
				PHA
				LDX	#2
				BNE	DRAW_COM1		;Always

DRAW_0_99		LDY	ENTRY_OFFSET
				LDA	ALIEN_BUFFER,Y
				PHA
				LDX	#1
DRAW_COM1		TXA
				LDX	#0
				LDY	#0
				JSR	RIGHT_DIGITS+2
DRAW_COM2		LDA	#$8D
				STA	TEXTBUF,X
				LDA	DATA_XCRD
				LDY	ENTRY_YCRD
				JMP	LINE

*-------------------------------

EDIT_YESNO		JSR	DRAW_YESNO
				LDX	ENTRY_OFFSET
				LDA	ALIEN_BUFFER,X
				LDY	SUB_OFFSET
				EOR	BIT_MASKS,Y
				STA	ALIEN_BUFFER,X

DRAW_YESNO		LDY	ENTRY_OFFSET
				LDA	ALIEN_BUFFER,Y
				LDY	SUB_OFFSET
				AND	BIT_MASKS,Y
				BEQ	:1
				LDA	#1
:1				LDX	#<:YESNO
				LDY	#>:YESNO
				JMP	STRING_GROUP

:YESNO			USR	(NO)
				USR	(YES)

BIT_MASKS		DB	%10000000
				DB	%01000000
				DB	%00100000
				DB	%00010000
				DB	%00001000
				DB	%00000100
				DB	%00000010
				DB	%00000001

*-------------------------------

EDIT_SOMEVERY	JSR	DRAW_SOMEVERY
				JSR	INCR_OF_4

DRAW_SOMEVERY	JSR	EXTRACT_OF_4
				LDX	#<:SUSCEPTS
				LDY	#>:SUSCEPTS
				JMP	STRING_GROUP

:SUSCEPTS		USR	(NONE__<0>)
				USR	(LITTLE<1>)
				USR	(NORMAL<2>)
				USR	(VERY__<3>)

*-------------------------------

EDIT_SIZE		JSR	DRAW_SIZE
				JSR	INCR_OF_4

DRAW_SIZE		JSR	EXTRACT_OF_4
				LDX	#<:SIZES
				LDY	#>:SIZES
				JMP	STRING_GROUP

:SIZES			USR	(TINY__<0>)
				USR	(SMALL_<1>)
				USR	(NORMAL<2>)
				USR	(BIG___<3>)

*-------------------------------

EDIT_BLASTX		JSR	DRAW_BLASTX
				LDY	ENTRY_OFFSET
				LDA	ALIEN_BUFFER,Y
				CLC
				ADC	#%00000001
				AND	#%00111111
				EOR	ALIEN_BUFFER,Y
				AND	#%00111111
				EOR	ALIEN_BUFFER,Y
				STA	ALIEN_BUFFER,Y

DRAW_BLASTX		LDY	ENTRY_OFFSET
				LDA	ALIEN_BUFFER,Y
				TAX
				AND	#%00110000
				LSR
				LSR
				LSR
				LSR
				BNE	:2
				LDA	#$0A
:2				STA	TEXTBUF
				TXA
				AND	#%00001111
				CMP	#$0A
				BCC	:1
				ADC	#0
:1				STA	TEXTBUF+1
				LDA	#$8D
				STA	TEXTBUF+2
				LDA	DATA_XCRD
				LDY	ENTRY_YCRD
				JMP	LINE

*-------------------------------

EDIT_BLASTY		JSR	DRAW_BLASTY
				JSR	INCR_OF_4

DRAW_BLASTY		JSR	EXTRACT_OF_4
				LDX	#<:YTEXTS
				LDY	#>:YTEXTS
				JMP	STRING_GROUP

:YTEXTS			USR	(_0)
				USR	(-1)
				USR	(-2)
				USR	(-3)

*-------------------------------

EDIT_INTNSITY	JSR	DRAW_INTNSITY
				JSR	INCR_OF_4

DRAW_INTNSITY	JSR	EXTRACT_OF_4
				LDX	#<:INTENSITIES
				LDY	#>:INTENSITIES
				JMP	STRING_GROUP

:INTENSITIES	USR	(WEAKEST__<0>)
				USR	(WEAK_____<1>)
				USR	(STRONG___<2>)
				USR	(STRONGEST<3>)

*-------------------------------

EDIT_STATUS		JSR	DRAW_STATUS
				LDY	ENTRY_OFFSET
				LDA	ALIEN_BUFFER,Y
				AND	#%00111111
				LDX	#0
:1				INX
				CMP	aeSTAT_NUMS-1,X
				BNE	:1
				LDA	aeSTAT_NUMS,X
				CMP	#critical
				BNE	:2
				LDX	#0
				LDA	aeSTAT_NUMS,X
:2				EOR	ALIEN_BUFFER,Y
				AND	#%00111111
				EOR	ALIEN_BUFFER,Y
				STA	ALIEN_BUFFER,Y

DRAW_STATUS		LDY	ENTRY_OFFSET
				LDA	ALIEN_BUFFER,Y
				AND	#%00111111
				JSR	STATUSR2
				LDA	DATA_XCRD
				LDY	ENTRY_YCRD
				JMP	LINE

*-------------------------------

EDIT_ATTACK		JSR	DRAW_ATTACK
				LDX	ENTRY_OFFSET
				LDA	ALIEN_BUFFER,X
				LDY	SUB_OFFSET
				EOR	BIT_MASKS,Y
				STA	ALIEN_BUFFER,X

DRAW_ATTACK		LDY	ENTRY_OFFSET
				LDA	ALIEN_BUFFER,Y
				LDY	SUB_OFFSET
				AND	BIT_MASKS,Y
				BEQ	:1
				LDA	#1
:1				LDX	#<:ATTACKS
				LDY	#>:ATTACKS
				JMP	STRING_GROUP

:ATTACKS		USR	(PHYSICAL)
				USR	(ELECTRICAL)

*-------------------------------

EDIT_RUSHDEL	JSR	DRAW_RUSHDEL
				LDY	ENTRY_OFFSET
				LDA	ALIEN_BUFFER,Y
				CLC
				ADC	#%00000001
				AND	#%00000111
				EOR	ALIEN_BUFFER,Y
				AND	#%00000111
				EOR	ALIEN_BUFFER,Y
				STA	ALIEN_BUFFER,Y

DRAW_RUSHDEL	LDY	ENTRY_OFFSET
				LDA	ALIEN_BUFFER,Y
				AND	#%00000111
				LDX	#<:DELAYS
				LDY	#>:DELAYS
				JMP	STRING_GROUP

:DELAYS			USR	(0)
				USR	(1)
				USR	(2)
				USR	(3)
				USR	(4)
				USR	(5)
				USR	(6)
				USR	(NEVER_RUSH)

*-------------------------------

EDIT_PERCENT	JSR	DRAW_PERCENT
				JSR	INCR_OF_4

DRAW_PERCENT	JSR	EXTRACT_OF_4
				LDX	#<:PERCENTS
				LDY	#>:PERCENTS
				JMP	STRING_GROUP

:PERCENTS		USR	(NEVER)
				USR	(33%)
				USR	(66%)
				USR	(ALWAYS)

*-------------------------------

EDIT_AIM		JSR	DRAW_AIM
				LDY	ENTRY_OFFSET
				LDA	ALIEN_BUFFER,Y
				CLC
				ADC	#%00000100
				AND	#%00011100
				EOR	ALIEN_BUFFER,Y
				AND	#%00011100
				EOR	ALIEN_BUFFER,Y
				STA	ALIEN_BUFFER,Y

DRAW_AIM		LDY	ENTRY_OFFSET
				LDA	ALIEN_BUFFER,Y
				LSR
				LSR
				AND	#%00000111
				LDX	#<:ACCURACIES
				LDY	#>:ACCURACIES
				JMP	STRING_GROUP

:ACCURACIES		USR	(0_<BEST>)
				USR	(1)
				USR	(2)
				USR	(3)
				USR	(4)
				USR	(5)
				USR	(6)
				USR	(7_<WORST>)

*-------------------------------

EDIT_SHOTS		JSR	DRAW_SHOTS
				JSR	INCR_OF_4

DRAW_SHOTS		JSR	EXTRACT_OF_4
				LDX	#<:SHOTS
				LDY	#>:SHOTS
				JMP	STRING_GROUP

:SHOTS			USR	(1)
				USR	(2)
				USR	(4)
				USR	(8)

*-------------------------------

EDIT_RUNAFTER	JSR	DRAW_RUNAFTER
				LDY	ENTRY_OFFSET
				LDA	ALIEN_BUFFER,Y
				CLC
				ADC	#%00001000
				STA	ALIEN_BUFFER,Y

DRAW_RUNAFTER	LDY	ENTRY_OFFSET
				LDA	ALIEN_BUFFER,Y
				LSR
				LSR
				LSR
				CMP	#$1F
				BNE	:1
				LDA	#0
				LDX	#<:NEVER
				LDY	#>:NEVER
				JMP	STRING_GROUP
:1				TAX
				LSR
				LSR
				LSR
				LSR
				BNE	:3
				LDA	#$EE
:3				STA	TEXTBUF
				TXA
				AND	#%00001111
				CMP	#$0A
				BCC	:2
				ADC	#0
:2				STA	TEXTBUF+1
				LDA	#$8D
				STA	TEXTBUF+2
				LDA	DATA_XCRD
				LDY	ENTRY_YCRD
				JMP	LINE

:NEVER			USR	(NO_ALWAYS)

*-------------------------------

EDIT_RUNUNTIL	JSR	DRAW_RUNUNTIL
				LDY	ENTRY_OFFSET
				LDA	ALIEN_BUFFER,Y
				CLC
				ADC	#%00000001
				AND	#%00000111
				EOR	ALIEN_BUFFER,Y
				AND	#%00000111
				EOR	ALIEN_BUFFER,Y
				STA	ALIEN_BUFFER,Y

DRAW_RUNUNTIL	LDY	ENTRY_OFFSET
				LDA	ALIEN_BUFFER,Y
				AND	#%00000111
				STA	TEXTBUF
				LDA	#$8D
				STA	TEXTBUF+1
				LDA	DATA_XCRD
				LDY	ENTRY_YCRD
				JMP	LINE

*-------------------------------

DRAW_TRANSFER	RTS

*-------------------------------

INCR_OF_4		LDX	ENTRY_OFFSET
				LDA	ALIEN_BUFFER,X
				LDY	SUB_OFFSET
				CLC
				ADC	ADD_MASKS,Y
				AND	TWOBIT_MASKS,Y
				EOR	ALIEN_BUFFER,X
				AND	TWOBIT_MASKS,Y
				EOR	ALIEN_BUFFER,X
				STA	ALIEN_BUFFER,X
				RTS

ADD_MASKS		DB	%01000000
				DB	%00010000
				DB	%00000100
				DB	%00000001

*-------------------------------

EXTRACT_OF_4	LDY	ENTRY_OFFSET
				LDA	ALIEN_BUFFER,Y
				LDY	SUB_OFFSET
				AND	TWOBIT_MASKS,Y
				LDX	TWOBIT_SHFTS,Y
				BEQ	:2
:1				LSR
				LSR
				DEX
				BNE	:1
:2				RTS

TWOBIT_MASKS	DB	%11000000
				DB	%00110000
				DB	%00001100
				DB	%00000011

TWOBIT_SHFTS	DB	3,2,1,0

*-------------------------------

STRING_GROUP	STX	TEMP
				STY	TEMP+1
				LDY	#0
				TAX
				BEQ	:2
:1				LDA	(TEMP),Y
				INY
				CMP	#$8D
				BNE	:1
				DEX
				BNE	:1
:2				LDA	(TEMP),Y
				STA	TEXTBUF,X
				INY
				INX
				CMP	#$8D
				BNE	:2
				LDA	DATA_XCRD
				LDY	ENTRY_YCRD
				JMP	LINE

*-------------------------------
