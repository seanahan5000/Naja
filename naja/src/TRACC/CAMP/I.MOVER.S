MOVESUB			JSR	SETVTAB
				JSR	PICMOV1
				JSR	LEGAL1
				CMP	#$FF
				BNE	NOMOUT
				JMP	MOVOUT
NOMOUT			LDA	#$02
				JSR	PRESUB	
				JSR	COMNKEY
				PHA
				JSR	CURSIT2
				PLA
				CMP	#$C3
				BNE	NOMO2	
				JSR	POSTSUB
				LDY	#$02
				JSR	TEXTER2
				JMP	MOVOUT
NOMO2			JSR	MARKIT
				LDA	MCURSOR
				STA	CURHOLD
				JSR	PICMOV2
				JSR	LEGAL2	
				STX	MCURSOR
				JSR	USE2TO1
				LDY	#$02
				JSR	TEXTER2
				LDY	#$01
				JSR	TEXTER2
				LDY	#$04
				JSR	TEXTER2
				JSR	CURSIT2
				JSR	COMNKEY
				PHA
				JSR	CURSIT2
				PLA
				CMP	#$C3
				BNE	NOCANCL
				LDA	CURHOLD
				STA	MCURSOR
				JSR	MARKIT
				LDY	#$04
				JSR	TEXTER2
				LDY	#$00
				JSR	TEXTER2
				JMP	MOVESUB	
NOCANCL
				LDA	MCURSOR
				PHA
				LDA	CURHOLD
				STA	MCURSOR
				JSR	MARKIT
				PLA
				STA	MCURSOR
				JSR	FLIPPER
				LDA	MCURSOR
				CMP	#$0E
				BEQ	PROGMOV
				LDA	CURHOLD
				CMP	#$0E
				BEQ	PROGMOV
				BNE	CSAME1			;Always
PROGMOV			LDY	MCURSOR
				CPY	#$0E
				BNE	:SKIPA
				LDY	CURHOLD
:SKIPA			LDA	CORESPS,Y
				STA	OTHER
				INC	OTHER
				LDY	#$6E	
				JSR	MODER
				LDY	OTHER
				JSR	MODER
CSAME1			LDA	CURHOLD
				CMP	#$02	
				BCC	NOT_BACK
				CMP	#$08	
				BCS	NOT_BACK
				LDY	MCURSOR
				LDA	CORESPS,Y
				TAY
				LDA	(CHARDL),Y
				CMP	#$FF
				BNE	NOT_BACK
				JSR	NOBSORC
				LDA	MCURSOR
				PHA
				LDA	CURHOLD
				STA	MCURSOR
				JSR	REMOVER
				PLA
				STA	MCURSOR
				JMP	MOVOUT2	

NOT_BACK		JSR	NOBSORC
MOVOUT2			LDA	#BLACK1
				STA	COLOR
				LDY	#$09	
				JSR	LINER
				LDY	#$04
				JSR	TEXTER2
				LDY	#$00
				JSR	TEXTER2
				JSR	CALC_ALL
MOVOUT			JSR	IMARKER
				LDA	#$06
				JMP	RDOPTN

NOBSORC			LDY	MCURSOR
				LDA	CORESPS,Y
				STA	COUNT1
				LDY	CURHOLD
				LDA	CORESPS,Y
				STA	COUNT2
				LDA	#$03
				STA	COUNT
:LOOP1			LDY	COUNT1
				LDA	(CHARDL),Y
				PHA
				LDY	COUNT2
				LDA	(CHARDL),Y
				LDY	COUNT1
				STA	(CHARDL),Y
				LDY	COUNT2
				PLA
				STA	(CHARDL),Y
				INC	COUNT1
				INC	COUNT2
				DEC	COUNT
				BPL	:LOOP1
				RTS

MODER			LDA	(CHARDL),Y
				PHA
				AND	#$7F
				LDY	#level+1
				STA	(CHARDL),Y
				LSR
				LSR
				LSR
				LSR
				STA	TEXTBUF
				PLA
				PHA
				AND	#$0F
				STA	TEXTBUF+1
				LDA	#$0A
				STA	TEXTBUF+2
				LDA	#$8D
				STA	TEXTBUF+3
				PLA
				BPL	:SKIPA
				LDA	#$0B
				STA	TEXTBUF+2
:SKIPA			LDA	TEXTBUF
				BNE	:SKIPB
				LDA	#$EE
				STA	TEXTBUF
:SKIPB			LDA	#$CC
				LDY	#$16
				JMP	LINE

DROPSUB			JSR	SETVTAB
				JSR	PICDROP
				JSR	LEGAL1
				CMP	#$FF
				BEQ	DOUT2
				LDA	#$03
				JSR	PRESUB
RECOMN			JSR	COMNKEY
				PHA
				JSR	CURSIT2
				PLA
				CMP	#$C3
				BEQ	DOUT3
				JSR	MARKIT
				LDY	#$03
				JSR	TEXTER2
				LDY	#$01
				JSR	TEXTER2
				LDY	#$0D
				JSR	TEXTER2
VERKEY			LDA	KEYBRD
				BPL	VERKEY
				BIT	UNSTROB
				JSR	CHECK_ENTER
				BEQ	VERIED
				CMP	#$C3
				BNE	VERKEY	
				LDY	#$0D
				JSR	TEXTER2
				LDY	#$03
				JSR	TEXTER2
				LDY	#$01
				JSR	TEXTER2
				JSR	MARKIT
				JSR	CURSIT2
				JMP	RECOMN
VERIED			LDY	#$0D
				JSR	TEXTER2
				LDY	#$00
				JSR	TEXTER2
				JSR	POSTS2
				JSR	MARKIT
				JSR	REMOVER
				JSR	CALC_ALL
				JMP	DOUT2
DOUT3			LDY	#$03
				JSR	TEXTER2
				JSR	POSTSUB
DOUT2			JSR	IMARKER
				LDA	#$00
				JMP	RDOPTN

PICDROP			JSR	RESET_EQLIST
				JSR	ESCAN_DROPABL
				LDX	#14
:LOOP1			LDA	EQUIP_LIST,X
				STA	USABLE,X
				DEX
				BPL	:LOOP1
				RTS

PRESUB			PHA
				STX	MCURSOR
				LDA	#GREEN
				STA	COLOR
				LDY	#$09	
				JSR	LINER
				PLA
				TAY
				JSR	TEXTER2
				LDY	#$01
				JSR	TEXTER2
				LDY	#$00
				JSR	TEXTER2
				JMP	CURSIT2

POSTSUB			LDY	#$00
				JSR	TEXTER2
				LDY	#$01
				JSR	TEXTER2
POSTS2			LDA	#BLACK1
				STA	COLOR
				LDY	#$09	
				JMP	LINER

PICMOV1			JSR	VISUSE
				LDY	#e2_SLOT
				LDA	(CHARDL),Y
				CMP	#$FF
				BEQ	NOATACH			;No helm if slot full
				LDA	#$FF
				STA	USABLE
NOATACH			LDX	#$0E	
NOAT2			LDA	USABLE,X
				BNE	OKAYED
				LDA	CORESPS,X
				TAY
				LDA	(CHARDL),Y
				CMP	#$FF
				BNE	OKAYED			;No empties
				STA	USABLE,X
OKAYED			DEX
				BPL	NOAT2

				LDX	#$FF
				LDA	CRFLAG
				BPL	:SKIPB
				LDY	#e4_ROBLWITH	;Only allow Hand Attachment
				LDA	(CHARDL),Y		;  as source if nothing
				CMP	#$FF			;  is being held by that hand
				BEQ	:SKIPA
				STX	USABLE+8
:SKIPA			LDY	#e6_ROBRWITH
				LDA	(CHARDL),Y
				CMP	#$FF
				BEQ	:SKIPB
				STX	USABLE+10
:SKIPB

				LDA	#$0E	
				STA	COUNT3	
HASLOOP			LDX	COUNT3	
				LDA	USABLE,X
				BNE	HASORCE	
				STX	MCURSOR
				JSR	PICMOV2			;Check if it will have a destination
				JSR	LEGAL2
				CMP	#$FF
				BNE	HASORCE
				LDX	COUNT3	
				STA	USABLE,X
HASORCE			DEC	COUNT3	
				BPL	HASLOOP
				RTS

PICMOV2			LDX	#14
				LDA	#$FF
CLRLOOP			STA	USABLE2,X
				DEX
				BPL	CLRLOOP
				LDA	CRFLAG
				BMI	RPMOV2
				LDY	#back
				LDA	(CHARDL),Y
				CLC
				ADC	#$01	
				STA	MAXBACK
				LDY	MCURSOR
				LDA	SOURCES,Y
				STA	SCRATCH
				ASL
				ASL
				CLC
				ADC	SCRATCH
				TAX	
				LDA	#$00
				STA	COUNTER	
SLOOP0			LDA	SCODES,X
				ASL
				TAY
				LDA	CHECKS,Y
				STA	JUMP+1
				LDA	CHECKS+1,Y
				STA	JUMP+2
				TXA
				PHA
JUMP			JSR	$FFFF			;DUMMY ADDRESS
				PLA
				TAX
				LDA	COUNTER	
				INC	COUNTER	
				INX
				CMP	#$04
				BNE	SLOOP0
				LDY	MCURSOR
				LDA	#$FF
				STA	USABLE2,Y
				LDY	#e1_HEAD
				LDA	(CHARDL),Y
				CMP	#$FF
				BNE	PMOUT2
				STA	USABLE2+1
PMOUT2			RTS

RPMOV2			LDY	#e15_PROGRAM+1
				LDA	(CHARDL),Y
				AND	#$07
				CLC
				ADC	#$02
				STA	MAXCARY
				LDY	MCURSOR
				LDA	RSOURCE,Y
				ASL
				STA	SCRATCH
				ASL
				CLC
				ADC	SCRATCH
				TAX
				LDA	#$00
				STA	COUNTER
RLOOP0			LDA	RCODES,X
				ASL
				TAY
				LDA	RCHECKS,Y
				STA	RJUMP+1
				LDA	RCHECKS+1,Y
				STA	RJUMP+2
				TXA
				PHA
RJUMP			JSR	$FFFF
				PLA
				TAX
				LDA	COUNTER
				INC	COUNTER
				INX
				CMP	#$05
				BNE	RLOOP0

				LDX	#$FF
				LDY	#e3_ROBLFT
				LDA	(CHARDL),Y
				CMP	#$59			;Hand Attachment
				BEQ	:SKIPA
				STX	USABLE2+roblwith
:SKIPA			LDY	#e4_ROBLWITH
				LDA	(CHARDL),Y
				CMP	#$FF
				BEQ	:SKIPB
				STX	USABLE2+8
:SKIPB			LDY	#e5_ROBRGT
				LDA	(CHARDL),Y
				CMP	#$59			;Hand Attachment
				BEQ	:SKIPC
				STX	USABLE2+robrwith
:SKIPC			LDY	#e6_ROBRWITH
				LDA	(CHARDL),Y
				CMP	#$FF
				BEQ	:SKIPD
				STX	USABLE2+10
:SKIPD			LDX	#$0E
ASLOOP1			LDA	VISIBLE,X
				ORA	USABLE2,X
				STA	USABLE2,X
				DEX
				BPL	ASLOOP1
				LDX	MCURSOR
				LDA	#$FF
				STA	USABLE2,X
				CPX	#$08	
				BEQ	HANDHLD
				CPX	#$0A
				BNE	RPMOUT2
HANDHLD			INX
				STA	USABLE2,X
RPMOUT2			RTS

SOURCES			DB	$00,$01,$05
				DB	$05,$05,$05
				DB	$05,$05,$04
				DB	$04,$04,$04
				DB	$03,$02
SCODES			DB	$02,$07,$00
				DB	$03,$07,$02
				DB	$07,$00,$03
				DB	$07,$02,$07
				DB	$00,$03,$07
				DB	$02,$07,$00
				DB	$03,$07,$02
				DB	$05,$01,$00
				DB	$06,$02,$05
				DB	$01,$06,$07
CHECKS			DW	EB1				;FIRST EMPTY BACK
				DW	AFB				;ANY FULL BACK
				DW	EMH				;EMPTY HANDS
				DW	FHC				;FULL HANDS WITH CRITERIA
				DB	$00,$00			;FULL BACK WITH CRITERIA
				DW	AFH				;ANY FULL HANDS
				DW	STS				;SAME TYPE AS SOURCE
				DW	DOP				;DUMMY OPERATION
EB1				INC	MAXBACK
				LDX	#$02	
ELOOP1			LDA	CORESPS,X
				TAY
				LDA	(CHARDL),Y
				CMP	#$FF
				BEQ	GOTFRST
				INX
				CPX	MAXBACK
				BNE	ELOOP1
				BEQ	EOUT
GOTFRST			LDA	#$00
				STA	USABLE2,X
EOUT			DEC	MAXBACK
				RTS
AFB				INC	MAXBACK
				LDX	#$02	
AFLOOP1			LDA	CORESPS,X
				TAY
				LDA	(CHARDL),Y
				CMP	#$FF
				BEQ	EMBACK
				LDA	#$00
				STA	USABLE2,X
EMBACK			INX
				CPX	MAXBACK
				BNE	AFLOOP1
				DEC	MAXBACK
				RTS
EMH				LDX	#$08
EHLOOP1			LDA	VISIBLE,X
				CMP	#$FF
				BEQ	EMHAND	
				LDA	CORESPS,X
				TAY
				LDA	(CHARDL),Y
				CMP	#$FF
				BNE	EMHAND
				LDA	#$00
				STA	USABLE2,X
EMHAND			INX
				CPX	#$0C	
				BNE	EHLOOP1
				RTS
FHC				LDY	MCURSOR
				JSR	TOCRITE
				LDA	#$02	
				STA	COUNT
FHLOOP1			JSR	CRSUB
				LDA	COUNT
				INC	COUNT
				CMP	#$0B	
				BEQ	FCOUT	
				CMP	MAXBACK
				BNE	FHLOOP1
				LDA	#$08
				STA	COUNT
				BNE	FHLOOP1
FCOUT			RTS
TOCRITE			LDA	CORESPS,Y
				TAY
				LDA	(CHARDL),Y
				STA	CRITE1
				INY
				LDA	(CHARDL),Y
				STA	CRITE2
				JSR	CATEGET
				STA	SCRATCH
				RTS
CRSUB			LDY	COUNT
				LDA	VISIBLE,Y
				BMI	FCOM
				LDA	CORESPS,Y
				TAY
				LDA	(CHARDL),Y
				CMP	#$FF
				BEQ	FCOM
				STA	CRITE1
				INY
				LDA	(CHARDL),Y
				STA	CRITE2
				JSR	CATEGET
				CMP	SCRATCH
				BNE	FCOM
				PHA
				INY
				INY
				LDA	(CHARDL),Y
				STA	CRITE2
				PLA
				CMP	#$06
				BNE	NOPROGO
				LDY	COUNT
				CPY	#$0E
				BEQ	NOPROGO
				LDA	CORESPS,Y
				TAY
				INY
				LDA	(CHARDL),Y
				AND	#$08
				BNE	FCOM
NOPROGO			JSR	MASKUM	
				AND	CRITE2
				BEQ	FCOM
				LDY	COUNT
				LDA	#$00
				STA	USABLE2,Y
FCOM			RTS

AFH				LDX	#$08	
AFLOOP2			LDA	VISIBLE,X
				CMP	#$FF
				BEQ	NEMHAND	
				LDA	CORESPS,X
				TAY
				LDA	(CHARDL),Y
				CMP	#$FF
				BEQ	NEMHAND
				LDA	#$00
				STA	USABLE2,X
NEMHAND			INX
				CPX	#$0C	
				BNE	AFLOOP2
				RTS
STS				LDY	MCURSOR
				LDA	CORESPS,Y
				TAY
				LDA	(CHARDL),Y
				STA	CRITE1
				INY
				LDA	(CHARDL),Y
				STA	CRITE2
				JSR	CATEGET
				TAX	
				INY
				INY
				LDA	(CHARDL),Y
				STA	CRITE2
				JSR	MASKUM
				AND	CRITE2
				BEQ	STOUT
				LDA	CURCORS,X	
				BMI	STOUT
				TAY
				LDA	#$00
				STA	USABLE2,Y
				CPY	#$08
				BNE	STOUT
				STA	USABLE2+10
STOUT			RTS
MASKUM			LDY	#$0D
				LDA	(CHARDL),Y
				LSR
				LSR
				LSR
				LSR
				TAY
				LDA	PROUSAB,Y
				RTS
CURCORS			DB	$00,$01,$0D
				DB	$0C,$FF,$08
				DB	$0E
DOP				RTS
CRITE1			DB	$00
CRITE2			DB	$00
CATEGET			LDA	CRITE1
				AND	#$1F
				TAX
				CMP	#$18
				BCS	ROBCRIT
				LDA	SLOTS,X
				BPL	CATOUT
				CMP	#$FE
				BEQ	MISARM
				LDA	CRITE2
				AND	#$07
				CMP	#$05
				BNE	NOBOOTS
				LDA	#$02
				BNE	CATOUT
NOBOOTS			LDA	#$04
				BNE	CATOUT
MISARM			LDA	CRITE2
				AND	#$03
				TAX
				LDA	MARMS,X
CATOUT			RTS
ROBCRIT			LDA	ROBSTUF-24,X
				RTS
SLOTS			DB	$01,$04,$FF
				DB	$04,$03,$00
				DB	$03,$FE,$04
				DB	$80,$80,$80
				DB	$04,$04,$04
				DB	$04
MARMS			DB	$04,$02,$03
				DB	$03
ROBSTUF			DB	$05,$05,$06
				DB	$04,$05
CHECK			LDY	#$6E
				LDA	(CHARDL),Y
				AND	#$08
				RTS
MARKIT			LDY	MCURSOR
				LDA	LINTABL,Y
				STA	YPNT	
				LDA	#$3A	
				STA	XPNT
				LDA	#$04	
				JMP	SPCHARS
VISUSE			LDY	#$0E	
VLOOP1			LDA	VISIBLE,Y
				STA	USABLE,Y
				DEY
				BPL	VLOOP1
				RTS

CBLINK			LDA	#$10
				JSR	WAIT
				LDA	#$F0
				JSR	WAIT2
				BMI	CKEYHIT
				JSR	CURSIT2
				LDA	#$00
				JSR	WAIT2
				JSR	CURSIT2
COMNKEY			LDA	KEYBRD
				BPL	CBLINK
CKEYHIT			BIT	UNSTROB
				JSR	CHECK_UP
				BEQ	UPCM	
				JSR	CHECK_DOWN
				BEQ	DOWNCM	
COMMOD			CMP	#$C3
				BEQ	COMNOUT
				JSR	CHECK_ENTER
				BNE	COMNKEY
COMNOUT			RTS
UPCM			JSR	CURSIT2
MUP2			LDA	CRFLAG
				BPL	NOROBUP
				LDA	MCURSOR
				CMP	#$0E
				BNE	NOPUP
				LDA	#$03
				STA	MCURSOR
				BNE	NOROBUP
NOPUP			CMP	#$02
				BNE	NOBUP
				LDA	#$0D
				STA	MCURSOR
NOBUP			CMP	#$03
				BNE	NOROBUP
				LDA	#$02
				STA	MCURSOR
NOROBUP			DEC	MCURSOR
				BPL	NOTTOP
				LDA	#$0E	
				STA	MCURSOR
NOTTOP			LDY	MCURSOR
				LDA	USABLE,Y
				BNE	MUP2
				JSR	CURSIT2
				JMP	COMNKEY
DOWNCM			JSR	CURSIT2
MDOWN2			LDA	CRFLAG
				BPL	NOROBDN
				LDA	MCURSOR
				CMP	#$0D
				BNE	NOPDWN
				LDA	#$01
				STA	MCURSOR
				BNE	NOROBDN
NOPDWN			CMP	#$02
				BNE	NOBDWN
				LDA	#$0D
				STA	MCURSOR
NOBDWN			CMP	#$01
				BNE	NOROBDN
				LDA	#$02	
				STA	MCURSOR
NOROBDN			LDA	MCURSOR
				INC	MCURSOR
				CMP	#$0E	
				BNE	NOTBOTM
				LDA	#$00	
				STA	MCURSOR
NOTBOTM			LDY	MCURSOR
				LDA	USABLE,Y
				BNE	MDOWN2
				JSR	CURSIT2
				JMP	COMNKEY

REMOVER			LDA	MCURSOR
				CMP	#$02	
				BCC	SINGLE1
				CMP	#$08	
				BCS	SINGLE1
				TAY
				LDA	CRFLAG
				BPL	NOTSING
				TYA
				CMP	#$02
				BEQ	SINGLE1
NOTSING			LDA	LINTABL,Y
				STA	BEGLINE
				LDY	#$1D
				LDA	(CHARDL),Y
				CLC
				ADC	#$01	
				TAY	
				LDA	LINTABL,Y
				CLC
				ADC	#$08
				STA	ENDLINE
				BIT	CRFLAG
				BPL	GOSCRO
				LDA	#$53
				STA	ENDLINE
GOSCRO			JSR	SCROLL
				JMP	ERASE
SINGLE1			TAY
				LDA	LINTABL,Y
				STA	BEGLINE
				CLC
				ADC	#$08
				STA	ENDLINE
				JSR	SCROLL
ERASE			LDX	MCURSOR
				JMP	REMOVE_ITEM

SCROLL			LDA	#$07	
				STA	COUNT	
SCLOOP0			LDA	BEGLINE
				STA	LINENUM
				INC	LINENUM
SCLOOP1			LDX	LINENUM
				LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	PAGE
				STA	SCREENH
				DEX
				LDA	LOBYTES,X
				STA	DESTINL
				LDA	HIBYTES,X
				ORA	PAGE
				STA	DESTINH
				LDY	#$09	
SCLOOP2			LDA	(SCREENL),Y
				STA	(DESTINL),Y
				INY
				CPY	#$17
				BNE	SCLOOP2
				INC	LINENUM
				LDA	LINENUM
				CMP	ENDLINE
				BNE	SCLOOP1
				DEC	COUNT
				BPL	SCLOOP0
				RTS

FLIPPER			LDY	MCURSOR
				LDA	LINTABL,Y
				STA	DYTOP
				CLC
				ADC	#$07
				STA	DYBOT
				LDY	CURHOLD
				LDA	LINTABL,Y
				STA	SYTOP
				CLC
				ADC	#$07
				STA	SYBOT
				LDA	DYTOP
				CMP	SYTOP
				BCS	FOKAY
				LDY	SYTOP
				STY	DYTOP
				STA	SYTOP
				LDA	DYBOT
				LDY	SYBOT
				STY	DYBOT
				STA	SYBOT
FOKAY			LDA	#$07
				STA	COUNT
FLOOPZ			LDA	#$09	
				STA	COLUMN
FLOOPY			LDA	DYTOP
				STA	LINENUM
				INC	LINENUM
				JSR	DOLOHI
				LDY	COLUMN
				LDA	(DESTINL),Y
				PHA
FLOOPX			JSR	DOLOHI
				LDY	COLUMN
				LDA	(SCREENL),Y
				STA	(DESTINL),Y
				LDA	LINENUM
				INC	LINENUM
				CMP	DYBOT
				BNE	NOCROSS
				LDA	SYTOP
				STA	LINENUM
				BNE	FLOOPX
NOCROSS			CMP	SYBOT
				BNE	FLOOPX
				PLA
				STA	(SCREENL),Y
				LDA	COLUMN
				INC	COLUMN
				CMP	#$16	
				BNE	FLOOPY
				DEC	COUNT
				BPL	FLOOPZ
				RTS
DOLOHI			LDY	LINENUM
				LDA	LOBYTES,Y
				STA	SCREENL
				LDA	HIBYTES,Y
				ORA	#$40
				STA	SCREENH	
				CPY	SYTOP
				BNE	NOGAP
				LDY	DYBOT
				INY
NOGAP			DEY
				LDA	LOBYTES,Y
				STA	DESTINL
				LDA	HIBYTES,Y
				ORA	#$40
				STA	DESTINH
				RTS

LEGAL1			LDA	CRFLAG
				BPL	CHARLEG
				LDX	#$03
				JSR	CHKNEXT
				BNE	CHARLEG	
				CPX	#$0E
				BNE	ONEFND
CHARLEG			LDX	#$00
CHKNEXT			LDA	USABLE,X
				BEQ	ONEFND
				INX
				CPX	#$0F	
				BNE	CHKNEXT
				LDA	#$FF
ONEFND			RTS
LEGAL2			LDA	CRFLAG
				BPL	CHRLEG2
				LDX	#$03
				JSR	CHK2
				BNE	CHRLEG2	
				CPX	#$0E
				BNE	FOUND1
CHRLEG2			LDX	#$00
CHK2			LDA	USABLE2,X
				BEQ	FOUND1
				INX
				CPX	#$0F	
				BNE	CHK2
				LDA	#$FF
FOUND1			RTS
USE2TO1			LDX	#$0E	
U21				LDA	USABLE2,X
				STA	USABLE,X
				DEX
				BPL	U21
				RTS

CURSIT2			LDY	MCURSOR
				LDA	LINTABL,Y
				STA	YPNT	
				LDA	#$39	
				STA	XPNT
				LDA	#$02	
				JSR	SPCHARS
CURMOD2			RTS
				LDA	#$D7	
				STA	XPNT
				LDA	#$03	
				JMP	SPCHARS

SETVTAB			JSR	RESET_EQLIST
				LDY	#14
				BIT	CRFLAG
				BPL	:SKIPA
				LDY	#29
:SKIPA			LDX	#14
:LOOP1			LDA	CHARLIN,Y
				STA	LINTABL,X
				LDA	EQUIP_LIST,X
				STA	VISIBLE,X
				DEY
				DEX
				BPL	:LOOP1
				RTS

CHARLIN			DB	$09,$11,$2A		;Must be consecutive
				DB	$32,$3A,$42		;   in this order
				DB	$4A,$52,$6B
				DB	$73,$7B,$83
				DB	$9C,$B5,$FF
ROBOLIN			DB	$00,$09,$71
				DB	$2A,$32,$3A
				DB	$42,$4A,$2A
				DB	$32,$3A,$42
				DB	$FF,$FF,$89

LINTABL			DS	15
USABLE			DS	15
USABLE2			DS	15
VISIBLE			DS	15
