
ENDLINU			EQU	$00
ENDLIND			EQU	$01
;BEGLINE			EQU	$02
;COUNTER			EQU	$03
ELIN			EQU	$01

; NOTE: these need to survive calls to LINER and PICKER
BCOL			EQU	$17
ECOL			EQU	$18

; Incoming Acc selects between airlock and viewport

				PHA
				LDX	#<VP_DATA
				LDY	#>VP_DATA
				JSR	SET_TLP_DATA
				PLA
				BEQ	PORTER
				JMP	LOCKER

PORTER			LDA	DIRECTN
				CMP	#$0F
				BEQ	ISOUTH
				JMP	NOSOUTH
ISOUTH
				LDA	XPOS
				CMP	#$05
				BEQ	:LEFT_ONLY
				CMP	#$0D
				BNE	:CENTER_VPORT

:RIGHT_ONLY		JSR	CLEAR1
				JSR	NOSCAN
				LDA	#BLACK1
				STA	COLOR
				LDY	#5
				JSR	LINER
				LDY	#2			; right port picture
				JSR	PICKER
				LDA	#$26
				STA	BCOL
				LDA	#$28
				STA	ECOL
				JSR	COPY_TOP
				JMP	VP_NOASK

:LEFT_ONLY		JSR	CLEAR1
				JSR	NOSCAN
				LDA	#BLACK1
				STA	COLOR
				LDY	#4
				JSR	LINER
				LDY	#1			; left port picture
				JSR	PICKER
				LDA	#$00
				STA	BCOL
				LDA	#$02
				STA	ECOL
				JSR	COPY_TOP
				JMP	VP_NOASK

:CENTER_VPORT	LDA	#$40
				STA	PAGE
				JSR	CLEAR2
				LDY	#0
				JSR	PICKER

				LDA	#$04
				STA	BCOL
				LDA	#$24
				STA	ECOL

				LDA	XPOS
				CMP	#$06
				BEQ	:NO_LEFT
				LDY	#2			; draw right partial port
				JSR	PICKER
				LDA	#$28
				STA	ECOL
				LDA	XPOS
				CMP	#$0C
				BEQ	:NO_RIGHT
:NO_LEFT		LDY	#1			; draw left partial port
				JSR	PICKER
				LDA	#$00
				STA	BCOL
:NO_RIGHT
				LDA	VP_IS_OPEN
				BEQ	:NOT_OPEN
				JSR	COPY_TOP
				LDY	#5			; open port top
				JSR	PICKER2		; no transparency
				LDY	#6			; open port bottom
				JSR	PICKER2		; no transparency
				JMP	ONOFA

:NOT_OPEN		LDX	#$00
				JSR	HLINES
				JSR	VLINES
				LDX	#$01
				JSR	HLINES
				LDA	#BLACK2
				STA	COLOR
				LDY	#$02
				JSR	LINER
				JSR	COPY_TOP
				JMP	ONOFA

ONOFA			LDA	#WHITE1
				STA	COLOR
				LDA	XPOS
				CMP	#$07
				BEQ	ISAT3
				CMP	#$0B
				BNE	NOTAT7
ISAT3			LDY	#3
				JSR	PICKER
NOTAT7			LDA	#WHITE1
				STA	COLOR
				LDY	#$03
				JSR	LINER
				LDA	XPOS
				CMP	#$07
				BEQ	DOBUTS
				CMP	#$0B
				BNE	NOBUTS
DOBUTS			LDY	#4
				JSR	PICKER
NOBUTS			STA	SCNDARY
				JSR	MOVETO1
				STA	PRIMARY
				LDA	#$20
				STA	PAGE
				LDA	XPOS
				CMP	#$07
				BEQ	ISAT7
				CMP	#$0B
				BNE	VP_NOASK
ISAT7			LDA	VP_IS_OPEN
				BNE	VP_NOASK
				LDY	#$00
				JSR	TEXTER

				LDA	#$00
				STA	MAXTEXT
				JSR	INPUT2
RECHECK			LDA	TEXTBUF
				CMP	#$8D
				BNE	NOTRET
				JSR	CHAROUT
NOTRET			LDA	TEXTBUF
				CMP	#"Y"
				BEQ	YES
				CMP	#"N"
				BEQ	NO
				JSR	:RECALL
				JMP	RECHECK

; NOTE: This is an ugly way to call back into the INPUT function.
:RECALL			LDA	HOLDIT
				PHA
				JMP	KEYREAD

NO				JSR	CHAROUT
				LDY	#$00
				JSR	TEXTER
				JMP	TO_RDKEY
YES				JSR	CHAROUT
				LDY	#$00
				JSR	TEXTER
				JSR	ANIMATE
				LDA	#$FF
				STA	VP_IS_OPEN
VP_NOASK		BIT	KEYBRD
				BPL	VP_NOASK
				LDA	KEYBRD
				JSR	CPARER2
				BNE	VP_NOASK
				BEQ	TO_RDKEY	; always

NOSOUTH			LDA	FLAG12
				CMP	#$02
				BNE	SCANNER
				JSR	SCANONE
				JMP	TO_RDKEY
SCANNER			JSR	CLEAR1
				JSR	NOSCAN
TO_RDKEY		LDA	#$FF		; check keyboard before redraw
				RTS

HLINES			LDA	SLOFFS,X
				STA	COUNT
				LDA	#WHITE2
				STA	COLOR
:1				LDY	COUNT
				LDA	YCORDS,Y
				CMP	#$FF
				BEQ	HOUT
				STA	VP_LINE0+2
				STA	VP_LINE0+5
				LDA	#$01
				STA	COUNTER
:2				LDY	COUNTER
				LDA	PLUS,Y
				CLC
				ADC	VP_LINE0+2
				STA	VP_LINE0+2
				STA	VP_LINE0+5
				LDY	#$00
				JSR	LINER
				DEC	COUNTER
				BPL	:2
				INC	COUNT
				BNE	:1
HOUT			RTS

SLOFFS			DFB	$00,$05
YCORDS			DFB	$37,$39,$41
				DFB	$43,$FF
				DFB	$3B,$3D,$3F
				DFB	$FF
PLUS			DFB	$12,$00

VLINES			LDA	#BLACK2
				STA	COLOR
				LDA	#$43
				STA	COUNT
VLOOP			STA	VP_LINE1+1
				STA	VP_LINE1+4
				LDY	#$01
				JSR	LINER
				LDA	COUNT
				CLC
				ADC	#$30
				STA	COUNT
				CMP	#$03
				BNE	VLOOP
				RTS

LOCKER			LDA	DIRECTN
				CMP	#$0F
				BEQ	ISSOUTH
				JMP	NOSOUTH
ISSOUTH
				JSR	CLEAR2
				LDA	#$40
				STA	PAGE
				JSR	NOSCAN
				LDY	#7
				JSR	PICKER
				LDY	#8
				JSR	PICKER
				LDY	#9
				JSR	PICKER
				LDA	#BLACK1
				STA	COLOR
				LDY	#$06
				JSR	LINER
				LDA	#WHITE1
				STA	COLOR
				LDA	#$20
				STA	PAGE
AIRHERE			STA	SCNDARY
				JSR	MOVETO1
				STA	PRIMARY
				BIT	UNSTROB
KEYER			BIT	KEYBRD
				BPL	KEYER
				LDA	KEYBRD
				JSR	CPARER2
				BEQ	NOLSOUT
				JSR	CHECK_UP
				BNE	KEYER
***SUCKED OUT!!!!
				BEQ	KEYER			;TEMP
NOLSOUT			JMP	TO_RDKEY

ANIMATE			LDA	#$40
				STA	PAGE
				JSR	CLEAR2
				LDY	#5
				JSR	PICKER
				LDY	#6
				JSR	PICKER
				LDA	#$20
				STA	PAGE
				LDA	#WHITE1
				STA	COLOR
				LDA	#$57
				STA	VP_LINE0+2
				STA	VP_LINE0+5
				STA	ENDLINU
				LDY	#$00
				JSR	LINER
				LDA	#$73
				STA	VP_LINE0+2
				STA	VP_LINE0+5
				STA	ENDLIND
				LDY	#$00
				JSR	LINER
UDLOOP			JSR	GOUP1
				JSR	GODOWN1
				INC	ENDLIND
				DEC	ENDLINU
				LDA	ENDLIND
				CMP	#$95
				BNE	UDLOOP
				JSR	UWINDOW
				JSR	DWINDOW
				JMP	MOVETO2

GODOWN1			LDA	#$94
				STA	BEGLINE
MORMOVD			LDA	#$C8
				STA	DIMOD
				JSR	BSUB
				LDA	BEGLINE
				DEC	BEGLINE
				CMP	ENDLIND
				BNE	MORMOVD
DWINDOW			LDX	ENDLIND
MORWN2D			JSR	ASUB
				DEX
				CPX	#$72
				BNE	MORWN2D
				RTS

GOUP1			LDA	#$36
				STA	BEGLINE
MORMOVU			LDA	#$88
				STA	DIMOD
				JSR	BSUB
				LDA	BEGLINE
				INC	BEGLINE
				CMP	ENDLINU
				BNE	MORMOVU
UWINDOW			LDX	ENDLINU
:1				JSR	ASUB
				INX
				CPX	#$58
				BNE	:1
				RTS

ASUB			LDA	LOBYTES,X
				STA	SCREENL
				STA	DESTINL
				LDA	HIBYTES,X
				ORA	#$40
				STA	SCREENH
				EOR	#$60
				STA	DESTINH
				LDY	#$06
:1				LDA	(SCREENL),Y
				STA	(DESTINL),Y
				STA	CLICK
				STA	CLICK
				INY
				CPY	#$22
				BNE	:1
				RTS

BSUB			LDY	BEGLINE
				LDA	LOBYTES,Y
				STA	SCREENL
				LDA	HIBYTES,Y
				ORA	#$20
				STA	SCREENH
DIMOD			DEY
				LDA	LOBYTES,Y
				STA	DESTINL
				LDA	HIBYTES,Y
				ORA	#$20
				STA	DESTINH
				LDY	#$06
:1				LDA	(SCREENL),Y
				STA	(DESTINL),Y
				INY
				CPY	#$22
				BNE	:1
				RTS

; replicate lines $2D through $5F to $6B through $9D

COPY_TOP		LDX	#$2D
:1				LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	PAGE
				STA	SCREENH
				TXA
				CLC
				ADC	#$3E
				TAY
				LDA	LOBYTES,Y
				STA	DESTINL
				LDA	HIBYTES,Y
				ORA	PAGE
				STA	DESTINH
				LDY	BCOL
:2				LDA	(SCREENL),Y
				STA	(DESTINL),Y
				INY
				CPY	ECOL
				BNE	:2
				INX
				CPX	#$60
				BNE	:1
				RTS
