				LST	OFF
LINENUM			EQU	$00
COLUMN			EQU	$01
SCRATCH			EQU	$02
COUNTER			EQU	$03	
MAXTEXT			EQU	$05
TBLPNTL			EQU	$0D
DESTINL			EQU	$0D
TBLPNTH			EQU	$0E
DESTINH			EQU	$0E
TOTAL			EQU	$0F	
ENDLINE			EQU	$11	
POINTL			EQU	$12
POINTH			EQU	$13
HOLDIT			EQU	$16
SCREENL			EQU	$26
SCREENH			EQU	$27
ALIEN1			EQU	$61	
ALIEN2			EQU	$62	
ALIEN3			EQU	$63	
HMANY1			EQU	$64	
ALTYPE1			EQU	$67	
CHARDL			EQU	$91	
SSTOREH			EQU	$B2	
STRACK			EQU	$BA	
SSECTOR			EQU	$BB	
READNUM			EQU	$BC	
COUNT			EQU	$C0	
COLOR			EQU	$E4
PAGE			EQU	$E6
SRANGE			EQU	$F0
DIRECTN			EQU	$F3
TEXTBUF			EQU	$0200
MOVETO1			EQU	$0326	
WAIT2			EQU	$0395
SREADIT			EQU	$0669	
LOBYTES			EQU	$0800
HIBYTES			EQU	$08C0
LINEOUT			EQU	$0980
TEXTOUT			EQU	$09E0
INPUT			EQU	$0A15
INPUT2			EQU	$0A1C
KEYREAD			EQU	$0A2A
CHAROUT			EQU	$0AFB
EXPANDR			EQU	$0C60
CLEAR1			EQU	$11AA
CLEAR2			EQU	$1212
POINTIT			EQU	$12D3	
RDKEY			EQU	$6000
GETNSEW			EQU	$614A	
NOSCAN			EQU	$6653	
AFTMANY			EQU	$7706			;IN FIGHT LOADER
RUUSTOP			EQU	$791B			;IN FIGHT LOADER
RIFTABL			EQU	$9FF0	
CPARER2			=	$B423
GRPNUMB			EQU	$BC7F	
KEYBRD			EQU	$C000
UNSTROB			EQU	$C010
PRIMARY			EQU	$C054
SCNDARY			EQU	$C055
BLUE			EQU	$D5
ORANGE			EQU	$AA
				ORG	$9000
				JSR	CLEAR2
				LDA	#$40
				STA	PAGE
				LDA	#$FF	
				STA	DIRECTN
				LDA	SRANGE
				PHA
				LDA	#$01
				STA	SRANGE
				JSR	GETNSEW
				JSR	NOSCAN
				PLA
				STA	SRANGE
				LDY	#$00
				JSR	UNPACK
				JSR	FLIP
				LDY	#$01
				JSR	UNPACK
				LDY	#$02
				JSR	UNPACK
				STA	SCNDARY
				LDA	#$20
				STA	PAGE
				LDA	#BLUE
				STA	COLOR
				JSR	CLEAR1
				LDY	#$00
				JSR	LINER
				LDY	#$00
				JSR	TEXTER
				JSR	MOVE212
				LDA	#$40
				STA	PAGE
				LDA	#$00
				STA	MAXTEXT
				JSR	INPUT2
LOOPYN			LDA	TEXTBUF
				CMP	#$8D
				BNE	NORET
				JSR	CHAROUT
				JSR	RECALL
				JMP	LOOPYN
NORET			CMP	#$D9
				BEQ	YESWANT
				CMP	#$CE
				BEQ	NOWANT
				JSR	RECALL
				JMP	LOOPYN
NOWANT			JSR	CLRBOX
				LDY	#$04
				JSR	UNPACK
				JMP	FSCREEN
YESWANT			JSR	CLRBOX
				LDY	#$04
				JSR	UNPACK
				JSR	RIFLER
				CPX	#$FF
				BEQ	BSCREEN
				BNE	CSCREEN
BSCREEN			LDY	#$01
				JSR	TEXTER
				LDY	#$02
				JSR	TEXTER
				BIT	UNSTROB
				LDY	#$14	
				JSR	WAITER
				JSR	CLRBOX
				JMP	FSCREEN
CSCREEN			LDY	#$01
				JSR	TEXTER
				LDY	#$03
				JSR	TEXTER
				BIT	UNSTROB
				JSR	INPUT2
LOOPYN2			LDA	TEXTBUF
				CMP	#$8D
				BNE	NORET2
				JSR	CHAROUT
				JSR	RECALL
				JMP	LOOPYN2
NORET2			CMP	#$D9
				BEQ	YESIS
				CMP	#$CE
				BEQ	NOIS
				JSR	RECALL
				JMP	LOOPYN2
NOIS			JSR	CLRBOX
				JMP	ESCREEN
YESIS			JSR	CLRBOX
				LDY	#$04
				JSR	TEXTER
				LDA	#ORANGE
				STA	COLOR
				LDY	#$01
				JSR	LINER
				BIT	UNSTROB
				LDA	#$DA
				LDY	#$1B
				JSR	INPUT
				LDA	TEXTBUF
				CMP	#$8D
				BEQ	ISRET
				JSR	CHAROUT
ISRET			JSR	CLRBOX
				LDA	#$07	
				STA	COUNTER
REMLOOP			LDX	COUNTER	
				LDA	RIFTABL-1,X
				JSR	POINTIT
				LDX	COUNTER
				LDA	RIFTABL,X
				JSR	REMOVE
				DEC	COUNTER
				DEC	COUNTER
				BPL	REMLOOP
				JMP	FSCREEN
ESCREEN			LDY	#$05
				JSR	TEXTER
				LDY	#$04	
				JSR	WAITER
				LDY	#$06
				JSR	TEXTER
				LDA	#$23
				STA	ALIEN1
				LDA	#$08
				STA	HMANY1
				LDA	#$42
				STA	ALTYPE1
				LDA	#$FF
				STA	ALIEN2
				STA	ALIEN3
				LDX	#$05	
XXLOOP			LDA	XXDATA,X
				STA	$1194,X
				DEX
				BPL	XXLOOP
				LDA	#$0F
				STA	DIRECTN
				JSR	GETNSEW
				JSR	MOVETO1
				STA	PRIMARY
				LDA	#$20
				STA	PAGE
				LDA	#$77
				STA	SSTOREH
				LDA	#$00
				STA	STRACK
				STA	SSECTOR
				LDA	#$19
				STA	READNUM
				LDA	#$11	
				PHA
				LDA	#$93	
				PHA
				JMP	SREADIT
XXDATA			DFB	$CE	
				DW	RUUSTOP-1	
				DFB	$4C	
				DW	AFTMANY
FSCREEN			LDY	#$07
				JSR	TEXTER
				LDY	#$16	
				JSR	WAITER
				JSR	MOVE212
KEYLOOP			BIT	KEYBRD
				BPL	KEYLOOP
				LDA	KEYBRD
				JSR	CPARER2
				BEQ	GORD
				BIT	UNSTROB
				JMP	KEYLOOP
GORD			LDA	#$20
				STA	PAGE
				JSR	CLEAR1
				STA	PRIMARY
				JMP	RDKEY
RECALL			LDA	HOLDIT
				PHA
				JMP	KEYREAD
WAITER			LDA	#$00
				JSR	WAIT2
				BNE	WAITEND
				DEY
				BPL	WAITER	
WAITEND			BIT	UNSTROB
				RTS
RIFLER			LDA	#$00
				STA	COUNT
				STA	TOTAL
RILOOP1			LDA	#$00
				STA	COUNTER
				LDA	COUNT
				JSR	POINTIT
				LDY	#$0A
				LDA	(CHARDL),Y
				CMP	#$05
				BEQ	RISKIP2	
RILOOP2			LDA	COUNTER
				JSR	CORCALC
				LDA	(CHARDL),Y
				AND	#$7F
				CMP	#$4D
				BNE	RISKIP1
				INY
				LDA	(CHARDL),Y
				DEY
				CMP	#$00
				BNE	RISKIP1
				LDA	TOTAL
				ASL	A
				TAX
				LDA	COUNT
				STA	RIFTABL,X
				INX
				TYA
				STA	RIFTABL,X
				INC	TOTAL
				LDA	TOTAL
				CMP	#$04
				BEQ	RIFLOUT
RISKIP1			INC	COUNTER
				LDA	COUNTER
				CMP	#$0A
				BNE	RILOOP2
RISKIP2			INC	COUNT
				LDA	COUNT
				CMP	GRPNUMB
				BNE	RILOOP1
				LDX	#$FF
				RTS
RIFLOUT			LDX	#$00
				RTS
REMOVE			STA	SCRATCH
				TAY
				CMP	#$4D	
				BCC	SINGLE
				CPY	#$61
				BEQ	SINGLE
				LDY	#$0D
				LDA	(CHARDL),Y
				BPL	NOTSING
				LDY	SCRATCH
				CPY	#$4D	
				BEQ	SINGLE
NOTSING			LDY	#$1D
				LDA	(CHARDL),Y
				CLC
				ADC	#$03
				JSR	CORCALC
				CLC
				ADC	#$04
				STA	ENDLINE
				LDY	SCRATCH
				BNE	SNEAKY
SLIDES			LDA	(CHARDL),Y
				DEY
				DEY
				DEY
				DEY
				STA	(CHARDL),Y
				INY
SNEAKY			INY
				INY
				INY
				INY
				CPY	ENDLINE
				BNE	SLIDES
				TYA
				SEC
				SBC	#$04
				TAY
SINGLE			LDX	#$04
				LDA	#$FF
ERASEM			STA	(CHARDL),Y
				INY
				DEX
				BNE	ERASEM
				RTS
CORCALC			ASL	A
				ASL	A
				CLC
				ADC	#$3D
				TAY
				RTS
MOVE212			LDA	#$00
				STA	LINENUM
MLOOP1			LDX	LINENUM
				LDA	LOBYTES,X
				STA	SCREENL
				STA	DESTINL
				LDA	HIBYTES,X
				ORA	#$20
				STA	SCREENH
				EOR	#$60
				STA	DESTINH
				LDY	#$07
MLOOP2			LDA	(SCREENL),Y
				TAX
				LDA	(DESTINL),Y
				STA	(SCREENL),Y
				TXA
				STA	(DESTINL),Y
				INY
				CPY	#$21
				BNE	MLOOP2
				LDA	LINENUM
				INC	LINENUM
				CMP	#$2A
				BNE	MLOOP1
				RTS
CLRBOX			LDX	#$01
CLOOP1			LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	#$40
				STA	SCREENH
				LDY	#$08
				LDA	#$00
CLOOP2			STA	(SCREENL),Y
				INY
				CPY	#$20
				BNE	CLOOP2
				INX
				CPX	#$27
				BNE	CLOOP1
				RTS
FLIP			LDA	#$00
				STA	LINENUM
LOOP1			LDX	LINENUM
				LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	#$40
				STA	SCREENH
				CLC
				PHP
				LDA	#$00
				STA	COLUMN
LOOP2			LDY	COLUMN
				LDA	(SCREENL),Y
				TAY
				PLP
				ROL	A
				ASL	A
				PHP
				LDX	#$00
				STX	SCRATCH
				LDX	#$07
ROTLOOP			ASL	A
				ROR	SCRATCH
				DEX
				BPL	ROTLOOP
				TYA
				AND	#$80
				ORA	SCRATCH
				TAX
				LDA	#$27
				SEC
				SBC	COLUMN
				TAY
				TXA
				STA	(SCREENL),Y
				INC	COLUMN
				CPY	#$14
				BNE	LOOP2
				PLP
				LDA	LINENUM
				INC	LINENUM
				CMP	#$BF
				BNE	LOOP1
				RTS

				PUT	RUUIK.DATA
				SAV	RUUIK
