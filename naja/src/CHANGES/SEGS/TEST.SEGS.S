				DSK	TEST.SEGS
				LST	OFF
				TR	ON

LEFT_COLUMN		=	$0
RIGHT_COLUMN	=	$1
TOP_LINE		=	$2
BOTTOM_LINE		=	$3
DATAPTR			=	$4				;$5
SCREENL			=	$26
SCREENH			=	$27

HBUFFER			=	$1F02

				ORG	$4000

TEST_HALLS		LDX	#0
:1				LDA	HBUFFER,X
				CMP	#$FF
				BEQ	:2
				INX
				CPX	#10
				BNE	:1
:2
				DEX
				TXA
				LSR
				STA	DEPTH
				STX	SEG_COUNT

				LDA	HBUFFER+1,X
				CMP	#$FF
				BNE	:22
				JSR	DRAW_CFRONT
:22

:0				LDX	SEG_COUNT
				LDA	HBUFFER,X
				LDX	DEPTH
				TAY
				ASL
				ROL
				ROL
				AND	#%00000011
				BNE	:3
				TYA
				AND	#%00000011
				BEQ	:4
				JSR	DRAW_RFRONT
				JMP	:4
:3				JSR	DRAW_RSIDE

:4				DEC	SEG_COUNT
				LDX	SEG_COUNT
				LDA	HBUFFER,X
				LDX	DEPTH
				TAY
				ASL
				ROL
				ROL
				AND	#%00000011
				BNE	:5
				TYA
				AND	#%00000011
				BEQ	:6
				JSR	DRAW_LFRONT
				JMP	:6
:5				JSR	DRAW_LSIDE
:6				DEC	SEG_COUNT
				DEC	DEPTH
				BPL	:0
				RTS

SEG_COUNT		DB	0
DEPTH			DB	0

*-------------------------------

DRAW_LSIDE		LDX	DEPTH
				JMP	DRAW_SEG

DRAW_LFRONT		LDX	DEPTH
				LDA	LFL_COLUMNS,X
				STA	LEFT_COLUMN
				LDA	LFR_COLUMNS,X
				CLC
				SBC	LEFT_COLUMN
				STA	RIGHT_COLUMN
				LDA	FTOP_LINES,X
				STA	TOP_LINE
				LDA	FBOTTOM_LINES,X
				STA	BOTTOM_LINE
				TXA
				CLC
				ADC	#15
				TAX
				JMP	DRAW_FSEG

LFL_COLUMNS		DB	0,0,8,14,17
LFR_COLUMNS		DB	4,12,16,18,19

DRAW_RSIDE		LDA	DEPTH
				CLC
				ADC	#5
				TAX
				JMP	DRAW_SEG

DRAW_RFRONT		LDX	DEPTH
				LDA	RFL_COLUMNS,X
				STA	LEFT_COLUMN
				LDA	RFR_COLUMNS,X
				CLC
				SBC	LEFT_COLUMN
				STA	RIGHT_COLUMN
				LDA	FTOP_LINES,X
				STA	TOP_LINE
				LDA	FBOTTOM_LINES,X
				STA	BOTTOM_LINE
				TXA
				CLC
				ADC	#20
				TAX
				JMP	DRAW_FSEG

RFL_COLUMNS		DB	36,28,24,22,21
RFR_COLUMNS		DB	40,40,32,26,23

DRAW_CFRONT		LDX	DEPTH
				LDA	CFL_COLUMNS,X
				STA	LEFT_COLUMN
				LDA	CFR_COLUMNS,X
				CLC
				SBC	LEFT_COLUMN
				STA	RIGHT_COLUMN
				LDA	FTOP_LINES,X
				STA	TOP_LINE
				LDA	FBOTTOM_LINES,X
				STA	BOTTOM_LINE
				TXA
				CLC
				ADC	#10
				TAX
				JMP	DRAW_FSEG

CFL_COLUMNS		DB	4,12,16,18,19
CFR_COLUMNS		DB	36,28,24,22,21

FTOP_LINES		DB	15,71,99,113,120
FBOTTOM_LINES	DB	186,158,144,137,133

*-------------------------------

DRAW_SEG		LDA	LEFT_COLUMNS,X
				STA	LEFT_COLUMN
				LDA	RIGHT_COLUMNS,X
				CLC
				SBC	LEFT_COLUMN
				STA	RIGHT_COLUMN
				LDA	BOTTOM_LINES,X
				STA	BOTTOM_LINE
				LDA	TOP_LINES,X
				STA	TOP_LINE
DRAW_FSEG		TXA
				ASL
				TAX
				LDA	SEG_TABLE,X
				STA	DATAPTR
				LDA	SEG_TABLE+1,X
				STA	DATAPTR+1
:2				LDX	TOP_LINE
				LDA	LOBYTES,X
				CLC
				ADC	LEFT_COLUMN
				STA	SCREENL
				LDA	HIBYTES,X
				ADC	#$20
				STA	SCREENH
				LDY	RIGHT_COLUMN
:3				LDA	(DATAPTR),Y
				STA	(SCREENL),Y
				DEY
				BPL	:3
				LDA	DATAPTR
				SEC
				ADC	RIGHT_COLUMN
				STA	DATAPTR
				BCC	:4
				INC	DATAPTR+1
:4				INC	TOP_LINE
				LDA	TOP_LINE
				CMP	BOTTOM_LINE
				BNE	:2
				RTS

LEFT_COLUMNS	DB	0,4,12,16,18
				DB	36,28,24,22,21

RIGHT_COLUMNS	DB	4,12,16,18,19
				DB	40,36,28,24,22

TOP_LINES		DB	0,16,72,100,114
				DB	0,15,71,99,113

BOTTOM_LINES	DB	192,185,157,143,136
				DB	192,186,158,144,137

SEG_TABLE		DW	SW0L+2
				DW	SW1L+2
				DW	SW2L+2
				DW	SW3L+2
				DW	SW4L+2
				DW	SW0R+2
				DW	SW1R+2
				DW	SW2R+2
				DW	SW3R+2
				DW	SW4R+2
				DW	FW0C+2
				DW	FW1C+2
				DW	FW2C+2
				DW	FW3C+2
				DW	FW4C+2
				DW	FW0L+2
				DW	FW1L+2
				DW	FW2C+2
				DW	FW3C+2
				DW	FW4C+2
				DW	FW0R+2
				DW	FW1R+2
				DW	FW2C+2
				DW	FW3C+2
				DW	FW4C+2

SW0L
				PUT	SW0L
SW1L
				PUT	SW1L
SW2L
				PUT	SW2L
SW3L
				PUT	SW3L
SW4L
				PUT	SW4L
SW4R
				PUT	SW4R
SW3R
				PUT	SW3R
SW2R
				PUT	SW2R
SW1R
				PUT	SW1R
SW0R
				PUT	SW0R
FW0C
				PUT	FW0C
FW1C
				PUT	FW1C
FW2C
				PUT	FW2C
FW3C
				PUT	FW3C
FW4C
				PUT	FW4C
FW0L
				PUT	FW0L
FW0R
				PUT	FW0R
FW1L
				PUT	FW1L
FW1R
				PUT	FW1R

				PUT	/CHANGES/HIRES.TABLES
