ROBMOST			LDX	#0
				LDY	#e3_ROBLFT
				JSR	:SUBA
				LDX	#2
				LDY	#e5_ROBRGT
:SUBA			LDA	(CHARDL),Y
				CMP	#$FF
				BEQ	NOB
				CMP	#$59	
				BNE	NOB
				INY
				INY
				INY
				INY
				LDA	(CHARDL),Y
				CMP	#$FF
				BNE	NOA
				RTS

ROBSOME			LDX	#0
				LDY	#e3_ROBLFT
				JSR	SOMER
				LDX	#2
				LDY	#e5_ROBRGT
SOMER			JSR	NOA
				LDA	(CHARDL),Y
				CMP	#$59
				BEQ	SOMOUT
NOB				INX
NOA				LDA	#$FF
				STA	LEGALS,X	
SOMOUT			RTS

VISISUB			JSR	SETPNTS
				LDA	#$FF
				LDY	#$0A
VISLOOP			STA	(tVPNTL),Y
				DEY
				BPL	VISLOOP
				LDY	#profrace
				LDA	(CHARDL),Y
				BPL	TROBSKIP
				LDY	#$03
				LDA	#$00
TRHLOOP			STA	(tVPNTL),Y
				DEY
				BPL	TRHLOOP
VISKIP			LDY	#$04
				STA	(tVPNTL),Y
				BNE	NOBACKS			;Always
TROBSKIP		LDA	#$00
				TAY	
				STA	(tVPNTL),Y
				INY
				STA	(tVPNTL),Y
				LDY	#profrace
				LDA	(CHARDL),Y
				AND	#%00001111
				CMP	#deneb
				BNE	NOTDENE
				LDA	#$00
				LDY	#$02
				STA	(tVPNTL),Y
				INY
				STA	(tVPNTL),Y
NOTDENE			LDY	#back
				LDA	(CHARDL),Y
				BEQ	NOBACKS
				CLC
				ADC	#$04
				STA	SCRATCH
				LDY	#$04
				LDA	#$00
VBLOOP			STA	(tVPNTL),Y
				INY
				CPY	SCRATCH
				BNE	VBLOOP
NOBACKS			LDY	#$0A	
				STA	(tVPNTL),Y
				RTS

FILLINS			LDA	#$62	
				STA	TLINTABL
				STA	LINTABR
				LDA	#$6A
				STA	TLINTABL+1
				STA	LINTABR+1
				LDA	#$72
				STA	COUNT
				LDA	LVISIBL+2
				BEQ	ONEDEN
				LDA	RVISIBL+2
				BNE	NODENS
ONEDEN			LDA	#$72
				STA	TLINTABL+2
				STA	LINTABR+2
				LDA	#$7A
				STA	TLINTABL+3
				STA	LINTABR+3
				LDA	#$82
				STA	COUNT
NODENS			LDY	#$04
LNTLOOP			LDA	LVISIBL,Y
				BEQ	CANLIN
				LDA	RVISIBL,Y
				BNE	LEACANC
CANLIN			LDA	COUNT
				STA	TLINTABL,Y
				STA	LINTABR,Y
				CLC
				ADC	#$08
				STA	COUNT
				INY
				CPY	#$0A
				BNE	LNTLOOP
LEACANC			LDA	COUNT
				CLC
				ADC	#$02	
				STA	TLINTABL+10
				STA	LINTABR+10
				RTS

WRITCOL			STA	XPNT
				JSR	SETPNTS
				LDY	#$03
WRTLOOP			LDA	WRDATA,Y
				STA	TEXTBUF,Y
				DEY
				BPL	WRTLOOP
				LDY	#profrace
				LDA	(CHARDL),Y
				BPL	ROSKIP2
				LDA	#$00
				STA	COUNT
				STA	COLMOD+1
COLOOP1			LDA	#$0A
COLMOD			STA	TEXTBUF
				LDA	#$01
				STA	COLMOD+1
COLOOP2			LDY	COUNT
				LDA	(tLPNTL),Y
				TAY
				LDA	XPNT
				JSR	LINE
				INC	COUNT
				LDA	COUNT
				CMP	#$04
				BEQ	DENNO
				LDA	TEXTBUF+1
				CMP	#$0A
				BNE	COLOOP1
				LDA	#$1C
				STA	TEXTBUF+1
				BNE	COLOOP2			;Always

ROSKIP2			LDY	#$02
				LDA	(tVPNTL),Y
				BEQ	DENYES
				LDA	#$0A
				STA	TEXTBUF
DENYES			LDA	#$00
				JSR	WRITSUB
				LDA	TEXTBUF
				CMP	#$0A
				BEQ	DENNO
				LDA	#$16
				STA	TEXTBUF
				STA	TEXTBUF+1
				LDA	#$02
				JSR	WRITSUB
DENNO			LDA	#$0C
				STA	TEXTBUF
				LDA	#$01
				STA	TEXTBUF+1
				LDA	#$04
				STA	COUNT
VSLOOPA			LDY	COUNT
				LDA	(tVPNTL),Y
				BNE	VISDOUT
				LDA	(tLPNTL),Y
				TAY
				LDA	XPNT
				JSR	LINE
ROSKIP1			INC	COUNT
				INC	TEXTBUF+1	
				LDA	TEXTBUF+1	
				CMP	#$07
				BNE	VSLOOPA
VISDOUT			RTS

WRDATA			USR	(UL:)

WRITSUB			TAY
				PHA
				LDA	(tLPNTL),Y
				TAY
				LDA	XPNT
				JSR	LINE
				LDA	#$1C
				STA	TEXTBUF+1	
				PLA
				TAY
				INY
				LDA	(tLPNTL),Y
				TAY
				LDA	XPNT
				JMP	LINE

VISIS			DW	LVISIBL
				DW	RVISIBL
LINTS			DW	TLINTABL
				DW	LINTABR

EMENTER			STA	XPNT
				JSR	SETPNTS	
				LDA	#$00
				STA	COUNT
EPMLOOP			TAY
				LDA	(tVPNTL),Y
				BNE	NOVISI
				TYA
				JSR	ECALC
				LDX	#$00
TOETAB			LDA	(CHARDL),Y
				STA	ETABLE,X
				INY
				INX
				CPX	#$04
				BNE	TOETAB
				LDY	COUNT
				LDA	(tLPNTL),Y
				STA	YPNT
				JSR	EQUIP1
				LDA	XPNT
				LDY	YPNT
				JSR	LINE
NOVISI			INC	COUNT
				LDA	COUNT
				CMP	#$0A
				BNE	EPMLOOP
				RTS

SETPNTS			LDA	VISIS,X
				STA	tVPNTL
				LDA	VISIS+1,X
				STA	tVPNTH
				LDA	LINTS,X
				STA	tLPNTL
				LDA	LINTS+1,X
				STA	tLPNTH
				RTS

CLRMID			LDX	#$7B	
				BNE	CLRCOMN
CLRMOST			LDX	#$BF
CLRCOMN			JSR	GET_SCREEN
				LDY	#$27
				LDA	#$00
CLROOPX			STA	(SCREENL),Y
				DEY
				BPL	CLROOPX
				DEX
				CPX	#$44
				BNE	CLRCOMN
				RTS

SDMARK			LDY	tSMAN
				JSR	TMARKIT
DMARK			LDY	tDMAN
TMARKIT			LDA	YPOINTS,Y
				STA	YPNT	
				LDA	XPNT
				PHA
				LDA	#$06
				STA	XPNT
				LDA	#$04	
				JSR	SPCHARS
				PLA
				STA	XPNT
				RTS

				DUMMY	$BB00
LVISIBL			DB	$00,$00,$00
				DB	$00,$00,$00
				DB	$00,$00,$00
				DB	$00,$00
RVISIBL			DB	$00,$00,$00
				DB	$00,$00,$00
				DB	$00,$00,$00
				DB	$00,$00
TLINTABL		DB	$00,$00,$00
				DB	$00,$00,$00
				DB	$00,$00,$00
				DB	$00,$00
LINTABR			DB	$00,$00,$00
				DB	$00,$00,$00
				DB	$00,$00,$00
				DB	$00,$00
BUFFERL			DB	$00,$00,$00
				DB	$00,$00,$00
				DB	$00,$00
BUFFERS			DB	$00,$00,$00
				DB	$00
TANSWER			DB	$00,$00,$00
				DB	$00
TRADE_TYPE		DB	$00
				DEND
