EXCHANGE		JSR	CLRBOXZ
				JSR	DOTIME
				JSR	CANCEL_IN
				JSR	EQUIP_DRAW
				BIT	ROBDEN_FLAG
				BVS	DCHOICE
				LDY	#$3B
				JSR	TEXTER
				JSR	SCROLLER
				LDY	#$3C
				JSR	TEXTER
				JSR	SPACE_WAIT
				BCS	EXENT
				JMP	DIDCANC
EXENT			LDA	#$10
				JMP	EXCOMN
DCHOICE			LDY	#$39
				JSR	TEXTER
				LDA	#$80
				JSR	GET_EXCHANGE
				BCS	:SKIPA
				JMP	DIDCANC
:SKIPA			LDY	#$39
				JSR	TEXTER
				LDY	#$3A
				JSR	TEXTER
				LDA	CURSOR
				PHA
				TAY
				LDA	#$FF
				STA	LEGALS,Y
				LDA	HAND_YPNTS,Y
				STA	YPNT
				LDA	#$8F
				STA	XPNT
				LDA	#4
				JSR	SPCHARS
				LDA	#$00
				JSR	GET_EXCHANGE
				BCS	:SKIPB
				PLA
				JMP	DIDCANC
:SKIPB			PLA
				ROR
				ROR
				ROR
				AND	#$C0
				STA	INSTR1
				LDA	CURSOR
				ASL
				ASL
				ASL
				ASL
				ORA	INSTR1
EXCOMN			ORA	#cmdEXCHANGE
				TAX
				LDY	#$00
				LDA	TIMES+timeEXCHANGE
				JSR	GENERAL_POST
				JMP	DIDCANC


GET_EXCHANGE	STA	EXCH_CDEF+1
				LDX	#<EXCH_CDEF
				LDY	#>EXCH_CDEF
				JMP	FULL_GETKEY
*-------------------------------
EXCH_CDEF		DB	$03
				DB	$80				;Modified
				DW	HAND_CURSOR
*-------------------------------

SCROLLER		LDA	#$91
				STA	ENDLINE
:LOOP1			LDA	#$BD
				STA	LINENUM
:LOOP2			LDY	LINENUM
				LDA	LOBYTES,Y
				STA	DESTINL
				LDA	HIBYTES,Y
				ORA	#$20
				STA	DESTINH
				DEY
				LDA	LOBYTES,Y
				STA	SCREENL
				LDA	HIBYTES,Y
				ORA	#$20
				STA	SCREENH
				LDY	#$0D
:LOOP3			LDA	(SCREENL),Y
				STA	(DESTINL),Y
				INY
				CPY	#$1F	
				BCC	:LOOP3
				LDA	LINENUM
				CMP	#$B6
				BCS	:SKIPA
				CPY	#$28
				BNE	:LOOP3
:SKIPA			LDA	LINENUM
				DEC	LINENUM
				CMP	ENDLINE
				BNE	:LOOP2
				LDA	ENDLINE
				INC	ENDLINE
				CMP	#$94
				BNE	:LOOP1
				RTS
