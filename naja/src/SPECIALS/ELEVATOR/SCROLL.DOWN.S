DOWN			LDA	#<DOWN_BEGIN
				STA	LODPNTL
				LDA	#>DOWN_BEGIN
				STA	LODPNTH
				LDA	#THICK
				STA	COUNTER
				LDA	#DBEGLIN
				STA	LINENUM
:DLOOPA			JSR	DSCROLL
:DLOOPB			LDY	LINENUM
				LDA	LOBYTES,Y
				STA	DESTINL
				LDA	HIBYTES,Y
				ORA	#$20
				STA	DESTINH
				LDY	#$27
:DLOOPC			LDA	(LODPNTL),Y
				STA	(DESTINL),Y
				DEY
				BPL	:DLOOPC
				INC	LINENUM
				DEC	COUNTER
				LDA	LODPNTL
				CLC
				ADC	#$28
				STA	LODPNTL
				LDA	LODPNTH
				ADC	#$00
				STA	LODPNTH
				LDA	LODPNTL
				CMP	#<DOWN_END
				BNE	:DSKIP1
				LDA	LODPNTH
				CMP	#>DOWN_END
				BEQ	:DMAIN
:DSKIP1			LDA	COUNTER
				CMP	#$00
				BNE	:DLOOPB
				BIT	KEYBRD
				BMI	DOUT
				LDA	#THICK
				STA	COUNTER
				LDA	#DBEGLIN
				STA	LINENUM
				JMP	:DLOOPA
:DMAIN			LDA	#$00
				STA	LOADLIN
				JMP	:DSKIP2
:DLOOPD			JSR	DSCROLL
:DLOOPE			LDY	LOADLIN
				LDA	LOBYTES,Y
				STA	SCREENL
				LDA	HIBYTES,Y
				ORA	#$40
				STA	SCREENH
				LDY	LINENUM
				LDA	LOBYTES,Y
				STA	DESTINL
				LDA	HIBYTES,Y
				ORA	#$20
				STA	DESTINH
				LDY	#$27
:DLOOPF			LDA	(SCREENL),Y
				STA	(DESTINL),Y
				DEY
				BPL	:DLOOPF
				LDA	LOADLIN
				INC	LOADLIN
				CMP	#$BF
				BEQ	DOUT
				INC	LINENUM
				DEC	COUNTER
:DSKIP2			LDA	COUNTER
				CMP	#$00
				BNE	:DLOOPE
				BIT	KEYBRD
				BMI	DOUT
				LDA	LOADLIN
				CMP	#DBEGLIN
				BCC	:DSKIP3
				LDA	#THICK
				STA	COUNTER
				LDA	#$C0
				SEC
				SBC	LOADLIN
				STA	FROMCNT
				LDA	LOADLIN
				STA	LINENUM
				JSR	DSCRLL2
				JMP	:DLOOPE
:DSKIP3			LDA	#DBEGLIN
				STA	LINENUM
				LDA	#THICK
				STA	COUNTER
				JMP	:DLOOPD
DOUT			RTS
DSCROLL			LDA	#THICK
				STA	FROMCNT
DSCRLL2			LDA	#$00
				STA	TOCNT
DLOOPG			LDX	FROMCNT
				LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	#$20
				STA	SCREENH
				LDX	TOCNT
				LDA	LOBYTES,X
				STA	DESTINL
				LDA	HIBYTES,X
				ORA	#$20
				STA	DESTINH
				LDY	#$27
DLOOPH			LDA	(SCREENL),Y
				STA	(DESTINL),Y
				DEY
				BPL	DLOOPH
				LDA	FROMCNT
				CMP	#$BF
				BEQ	DEXIT
				INC	FROMCNT
				INC	TOCNT
				JMP	DLOOPG
DEXIT			RTS
