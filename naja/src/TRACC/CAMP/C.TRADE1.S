TRADE			LDY	#$03
				JSR	TEXTER
				JSR	MOVETO2
				LDX	#$0A
				LDA	#0
:LOOP1			STA	LEGALS,X
				DEX
				BMI	:SKIPA
				CPX	#$08
				BCS	:LOOP1
				LDA	#$FF
				BNE	:LOOP1			;Always
:SKIPA			LDA	#$08
				STA	GCURPOS
				JSR	PRE_ARROW
				JSR	GETKEY
				LDA	CURSOR
				CMP	#$0A
				BEQ	TCOPOUT
				STA	TRADE_TYPE

				PHA
				JSR	CLRMID
				LDY	#0
				LDA	#1
				JSR	DBL_TEXTER
				JSR	RESET_CHRLIST
				PLA
				CMP	#$08			;Trade equipment
				BEQ	:SKIPB
				LDA	#robot*16
				JSR	CSCAN_NOTPROF
				LDA	#$60
				STA	POSTOP
				LDX	#$FF
				JSR	TRADE_SUB1		;Call other bank
				JSR	POOLER
				LDA	#$20
				STA	POSTOP
				LDX	#$17
				JSR	TRADE_SUB1		;Call other bank
				JSR	MOVE212
				LDA	#$20
				STA	PAGE
:SKIPB			JSR	GRPSUB2
				LDY	CURSOR
				CPY	#$07
				BEQ	TCOPOUT
				STY	tSMAN
				LDA	#$FF
				STA	LEGALS,Y
				TYA
				JSR	POINTIT
				LDY	tSMAN
				JSR	TMARKIT
				LDY	#$01	
				JSR	TEXTER
				LDX	#$FF
				JSR	NOTEMED
				LDA	#$98	
				LDY	#$58
				JSR	LINE
				LDY	#$02	
				JSR	TEXTER
				JSR	DOWN2
				LDA	CURSOR
				CMP	#$07
				BNE	NOCANT2
				LDY	tSMAN
				JSR	TMARKIT
TCOPOUT			JSR	CLRMID
				LDA	TRADE_TYPE
				CMP	#$08
				BEQ	:SKIPA
				JSR	MOVE212
:SKIPA			LDY	#$00
				JSR	CMARKER
				LDA	#$05
				JMP	COPTION

NOCANT2			STA	tDMAN
				JSR	POINTIT
				JSR	DMARK	
				JSR	CLRMID
				LDA	tSMAN
				JSR	POINTIT

				LDA	TRADE_TYPE
				CMP	#$09
				BEQ	TCREDIT
				JMP	TEMENT

TCREDIT			JSR	CLRMID
				LDA	tSMAN
				JSR	POINTIT
				LDX	#<TFROM_NAME
				LDY	#>TFROM_NAME
				JSR	TRADE_SUB5
				LDA	tDMAN
				JSR	POINTIT
				LDX	#<TTO_NAME
				LDY	#>TTO_NAME
				JSR	TRADE_SUB5
				LDY	#$0B
				JSR	TEXTER
				LDA	#8
				STA	MAXTEXT
				JSR	INPUT2
REINPUT			LDA	TEXTBUF
				CMP	#$8D
				BEQ	CCANCEL
				TXA
				PHA
				JSR	GOODBAD
				PLA
				CPX	#$FF
				BNE	CCANCEL
				TAX
				STX	HOLDIT
				JSR	CHAROUT
				JSR	RECALL
				JMP	REINPUT
CCANCEL			JSR	CLRMID
				JSR	SDMARK
				JMP	TCOPOUT

GOODBAD			LDX	#$00
GBLOOP1			LDA	TEXTBUF,X
				CMP	#$8D
				BEQ	GBSKIP1
				EOR	#$B0
				CMP	#$0A
				BCS	GOISBAD	
				INX
				BNE	GBLOOP1
GOISBAD			LDX	#$FF
				RTS
GBSKIP1			LDY	#$07
				LDA	#$00
GBLOOP2			STA	BUFFERL,Y
				DEY
				BPL	GBLOOP2
				DEX
				LDY	#$07
GBLOOP3			LDA	TEXTBUF,X
				AND	#$0F
				STA	BUFFERL,Y
				DEY
				DEX
				BPL	GBLOOP3
				LDX	#$00
				LDY	#$00
GBLOOP4			LDA	BUFFERL,X
				INX
				ASL
				ASL
				ASL
				ASL
				ORA	BUFFERL,X
				INX
				STA	BUFFERS,Y
				INY
				CPY	#$04
				BNE	GBLOOP4

				LDA	tSMAN
				JSR	POINTIT
				CLC
				JSR	:SUBA
				BCS	:SKIPA
				LDX	#-1
				RTS
:SKIPA			JSR	:SUBA			;Carry assumed set

				LDA	tDMAN
				JSR	POINTIT
				LDX	#<BUFFERS
				LDY	#>BUFFERS
				JSR	CHAR_EARN
				LDX	#$00
				RTS	

:SUBA			LDX	#<BUFFERS
				LDY	#>BUFFERS
				JMP	CHAR_SPEND

TEMENT			JSR	MOVETO2
				LDA	#$40
				STA	PAGE
				JSR	CLRMOST
				LDA	tSMAN
				JSR	POINTIT
				LDX	#$00
				JSR	VISISUB
				LDX	#$FF
				JSR	NOTEMED
				LDA	#$29	
				LDY	#$58
				JSR	LINE
				LDA	tDMAN
				JSR	POINTIT	
				LDX	#$02
				JSR	VISISUB
				LDX	#$FF
				JSR	NOTEMED
				LDA	#$B5	
				LDY	#$58
				JSR	LINE
				JSR	FILLINS
				LDA	TLINTABL+10
				CLC
				ADC	#$0A
				JSR	TRADE_SUB2		;Call other bank
				LDA	#RED
				STA	COLOR
				LDY	#$01	
				JSR	LINER2	
				LDA	tSMAN
				JSR	POINTIT
				LDA	#$09
				LDX	#$00
				JSR	WRITCOL
				LDA	tDMAN
				JSR	POINTIT
				LDA	#$95
				LDX	#$02
				JSR	WRITCOL
				LDX	TLINTABL+10
				JSR	TRADE_SUB4		;Call other bank
				LDY	#6
				LDA	#4
				JSR	DBL_TEXTER
				STA	SCNDARY
				LDA	tSMAN
				JSR	POINTIT
				LDA	#$23
				LDX	#$00
				JSR	EMENTER
				LDA	tDMAN
				JSR	POINTIT
				LDA	#$AF
				LDX	#$02
				JSR	EMENTER
RETRADE			LDA	#$0A
				STA	CURSOR
REFIRST			LDX	#$00
				JSR	SETPNTS
				LDA	#$1B
				STA	XPNT
				LDA	tSMAN
				JSR	FORTDWN
				BNE	NOLEAVS
				LDA	#$20
				STA	PAGE
				JSR	SDMARK
				JSR	CLRMID
				STA	PRIMARY
				LDA	tSMAN
				JSR	:SUBA
				LDA	tDMAN
				JSR	:SUBA
				JMP	TCOPOUT
:SUBA			JSR	POINTIT
				JMP	CALC_ALL
NOLEAVS			STA	tSEMENT
				LDY	tSEMENT
				JSR	TMARKIT2
				LDY	#4
				LDA	#5
				JSR	DBL_TEXTER
				LDY	#6
				JSR	TEXTER
				JSR	TRADE_SUB3		;Call other bank
				LDY	#$0A	
				JSR	TEXTER
				LDA	#$0A
				STA	CURSOR
				LDX	#$02
				JSR	SETPNTS
				LDA	#$A7
				STA	XPNT
				LDA	tDMAN
				JSR	FORTDWN
				BNE	NOCANCS
				LDA	#$1B
				STA	XPNT
				LDA	tSEMENT
				STA	CURSOR
				TAY
				JSR	TMARKIT2
				DEC	CURSOR
				JSR	TEXTY
				JMP	REFIRST

FORTDWN			JSR	POINTIT
				JSR	TOLEGAL
				JSR	TBLINK
				LDA	CURSOR
				CMP	#$0A
				RTS

NOCANCS			STA	tDEMENT
				LDA	#$1B
				STA	XPNT
				LDY	tSEMENT
				JSR	TMARKIT2
				JSR	TRADER
				JSR	TEXTY
				JMP	RETRADE

TEXTY			LDY	#6
				LDA	#$0A
				JSR	DBL_TEXTER
				LDY	#5
				LDA	#4
				JMP	DBL_TEXTER

TBLINK			LDX	#<TRADE_CDEF
				LDY	#>TRADE_CDEF
				JSR	INIT_CURSOR
				JMP	GETKEY
*-------------------------------
TRADE_CDEF		DB	$0A
				DB	$00
				DW	:CURSIT

:CURSIT			LDY	CURSOR
				LDA	(tLPNTL),Y
				STA	YPNT	
				LDA	#2
				JMP	SPCHARS
*-------------------------------

TMARKIT2		LDA	(tLPNTL),Y
				STA	YPNT	
				INC	XPNT
				LDA	#4
				JSR	SPCHARS
				DEC	XPNT
				RTS

TRADER			LDA	tSEMENT
				JSR	ECALC
				STY	tSEOFF
				LDA	tDEMENT
				JSR	ECALC
				STY	tDEOFF
				LDA	tDMAN
				JSR	POINTIT
				LDA	CHARDL
				STA	POINTL
				LDA	CHARDH
				STA	POINTH
				LDA	tSMAN
				JSR	POINTIT
				LDA	tSEOFF
				STA	COUNT
				LDA	tDEOFF
				STA	COUNTER
				LDX	#$03
SWAPS			LDY	COUNT
				LDA	(CHARDL),Y
				PHA
				LDY	COUNTER
				LDA	(POINTL),Y
				LDY	COUNT
				STA	(CHARDL),Y
				PLA
				LDY	COUNTER
				STA	(POINTL),Y
				INC	COUNT
				INC	COUNTER
				DEX
				BPL	SWAPS
				LDA	#$0E	
				STA	COUNTER
SCLOOPD			LDY	tSEMENT
				LDA	TLINTABL,Y
				STA	tLLINE
				LDY	tDEMENT
				LDA	LINTABR,Y
				STA	tRLINE
				LDA	#$07
				STA	COUNT
SCLOOPC			LDX	tLLINE
				JSR	GET_SCREEN
				LDX	tRLINE
				JSR	GET_DESTIN
				LDY	#$26	
				LDA	(DESTINL),Y
				PHA
				DEY
SCLOOPA			LDA	(DESTINL),Y
				INY
				STA	(DESTINL),Y
				DEY
				DEY
				CPY	#$18	
				BNE	SCLOOPA
				LDY	#$12	
				LDA	(SCREENL),Y
				LDY	#$19	
				STA	(DESTINL),Y
				LDY	#$11	
SCLOOPB			LDA	(SCREENL),Y
				INY
				STA	(SCREENL),Y
				DEY
				DEY
				CPY	#$04	
				BNE	SCLOOPB
				INY
				PLA
				STA	(SCREENL),Y
				INC	tLLINE
				INC	tRLINE
				DEC	COUNT
				BNE	SCLOOPC
				DEC	COUNTER
				BNE	SCLOOPD
				LDA	#$05
				STA	XPNT
				LDA	tSMAN
				JSR	POINTIT
				LDA	tSEMENT
				STA	CURSOR
				LDX	#$00
				JSR	SETPNTS
				JSR	CRUNCH
				LDA	#$19	
				STA	XPNT
				LDA	tDMAN
				JSR	POINTIT
				LDA	tDEMENT
				STA	CURSOR
				LDX	#$02
				JSR	SETPNTS
CRUNCH			LDA	CURSOR
				CMP	#$04
				BCC	GOCRUT	
				CMP	#$09
				BNE	NOCRUT	
GOCRUT			JMP	CRUOUT
NOCRUT			LDY	#profrace
				LDA	(CHARDL),Y
				BPL	NOCRUT2
				LDA	CURSOR
				CMP	#$04
				BEQ	GOCRUT
NOCRUT2			LDA	CURSOR
				JSR	ECALC
				LDA	(CHARDL),Y
				CMP	#$FF	
				BNE	GOCRUT
				LDY	CURSOR	
				LDA	(tLPNTL),Y
				STA	BEGLINE	
				INC	BEGLINE	
				LDY	#$0A
				LDA	(tLPNTL),Y
				STA	ENDLINE
				DEC	ENDLINE
				LDA	#$07
				STA	COUNT
CRLOOP3			LDX	BEGLINE
CRLOOP2			JSR	GET_SCRNDEST
				INX
				INX
				LDA	XPNT
				CLC
				ADC	#$0E
				STA	CMOD+1
				LDY	XPNT
CRDLOOP1		LDA	(SCREENL),Y
				STA	(DESTINL),Y
				INY
CMOD			CPY	#$00
				BNE	CRDLOOP1
				CPX	ENDLINE
				BNE	CRLOOP2
				DEC	COUNT
				BPL	CRLOOP3
				LDA	CURSOR
				JSR	ECALC
				LDA	#$65
				STA	ENDLINE
				BNE	TSNEAKY
TSLIDES			LDA	(CHARDL),Y
				DEY
				DEY
				DEY
				DEY
				STA	(CHARDL),Y
				INY
TSNEAKY			INY
				INY
				INY
				INY
				CPY	ENDLINE
				BNE	TSLIDES
				DEY	
				DEY	
				DEY	
				DEY	
				LDX	#$04
				LDA	#$FF
TERASEM			STA	(CHARDL),Y
				INY
				DEX
				BNE	TERASEM
CRUOUT			RTS

ECALC			ASL
				ASL
				CLC
				ADC	#e3_UPLFT
				TAY
				RTS

TOLEGAL			LDA	#$04
				STA	TOSKIP+1
				LDY	#$0A
TOLOOP1			LDA	(tVPNTL),Y
				STA	LEGALS,Y
				DEY
				BPL	TOLOOP1
				LDA	tVPNTL
				CMP	#<LVISIBL
				BNE	DODESTI
				LDY	#profrace
				LDA	(CHARDL),Y
				BPL	NORMCHR
				INC	TOSKIP+1
				JSR	ROBMOST
NORMCHR			JMP	TOSKIP

DODESTI			LDY	#profrace
				LDA	(CHARDL),Y
				BPL	NORMDST
				LDA	tSMAN
				JSR	POINTIT
				LDA	tSEMENT
				JSR	ECALC
				LDA	(CHARDL),Y
				PHA
				LDA	tDMAN
				JSR	POINTIT
				PLA
				JSR	ROB_EQUIP
				BNE	NOTMOST
				JSR	ROBMOST
				JMP	NORMDST
NOTMOST			JSR	ROBSOME
NORMDST			LDA	tSMAN
				JSR	POINTIT
				LDY	#profrace
				LDA	(CHARDL),Y
				PHP
				LDA	tDMAN
				JSR	POINTIT
				PLP
				BPL	TOSKIP
				LDA	tSEMENT
				BEQ	ISOR
				CMP	#$02
				BNE	TOSKIP
ISOR			LDX	#$09	
ISOLOOP			LDA	LEGALS,X	
				BNE	ISOKAY
				TXA
				JSR	ECALC
				LDA	(CHARDL),Y
				JSR	ROB_EQUIP
				BEQ	ISOKAY
				JSR	NOA
ISOKAY			DEX
				BPL	ISOLOOP
TOSKIP			LDA	#$04			;Modified
				STA	COUNT
TOLOOP2			JSR	ECALC
				LDA	(CHARDL),Y
				CMP	#$FF
				BEQ	TOLOOP3
				INC	COUNT
				LDA	COUNT
				CMP	#$0A
				BNE	TOLOOP2
TOOUT			RTS
TOLOOP3			LDA	COUNT
				CMP	#$09
				BEQ	TOOUT
				INC	COUNT
				LDY	COUNT
				LDA	#$FF
				STA	LEGALS,Y
				BNE	TOLOOP3			;Always

ROB_EQUIP		CMP	#$FF
				BEQ	OKAY
				CMP	#$58
				BEQ	OKAY
				CMP	#$59
				BEQ	OKAY
				CMP	#$5C
OKAY			RTS	
