INFO_GUY		JSR	MOVETO2
				LDA	#$40
				STA	PAGE
				LDY	#$03
				JSR	PICKER
				LDA	#$FF
				STA	PICHERE
				JSR	BLCKENA
				LDA	#RED
				STA	COLOR
				LDY	#$06
				JSR	LINER

				LDA	GRPNUMB
				CMP	#1
				BEQ	:SKIPA
				LDX	#5
:LOOP1			LDA	GROUP_TEXT,X
				STA	PERGRP,X
				DEX
				BPL	:LOOP1
:SKIPA			LDY	#$1D
				JSR	TEXTER

				STA	SCNDARY
				LDA	#0
				STA	MAXTEXT
				JSR	YES_OR_NO
				BCS	ISINT
NOTINT			JSR	CHAROUT
				JSR	BLCKENB
				LDY	#$20
				JMP	INFO_OUT

ISINT			JSR	CHAROUT
				JSR	BLCKENB
				LDY	#$21
				JSR	TEXTER
				JSR	YES_OR_NO
				BCS	HASWORD
NOWORD			JSR	BLCKENB
				LDY	#$22
				JMP	INFO_OUT

HASWORD			JSR	BLCKENB
				LDY	#$23
				JSR	TEXTER
				JSR	SPACE_INOUT
				JSR	BLCKENB
				LDY	#$24
				JSR	TEXTER
*WRITE DATA CARDS ON SCREEN
				LDY	#$25
				JSR	TEXTER
				JSR	YES_OR_NO
				BCS	DOWANT
NOWANT			JSR	CHAROUT
				JMP	RETUNNL

DOWANT			JSR	CHAROUT
				JSR	BLCKENC
*ADD MAXIMUM NUMBER TO SENTENCE
				LDY	#$26
				JSR	TEXTER
				JSR	INPUT2
GETIN4			LDA	TEXTBUF
				CMP	#$8D
				BEQ	RETUNNL
				CMP	#$9B			;Escape
				BEQ	RETUNNL
				CMP	#"1"
				BCS	ISBIGNF
				JSR	RECALL
				JMP	GETIN4

ISBIGNF			SEC
				SBC	#"0"
				STA	PICKED1
*MOVE DATA TO POSS. AND LOWER CREDITS, STORE ON DISK
* SAVE ORB STATUS DATA IF NECESSARY
RETUNNL			JSR	BLCKENB
				LDY	#$27
INFO_OUT		JSR	TEXTER
				JSR	SPACE_IN
				JSR	SPACE_WAIT
				LDA	#$20
				STA	PAGE
				JSR	CLEAR1
				JSR	NOSCAN
				STA	PRIMARY
				LDA	#WHITE1
				STA	COLOR
				JMP	TLEAVE

*-------------------------------
*
*-------------------------------

YES_OR_NO		BIT	UNSTROB
				JSR	INPUT2
:LOOP1			LDA	TEXTBUF
				CMP	#"Y"
				BEQ	:EXIT
				CMP	#"N"
				CLC
				BEQ	:EXIT
				CMP	#$8D
				BNE	:SKIPA
				JSR	CHAROUT
:SKIPA			JSR	RECALL
				JMP	:LOOP1
:EXIT			RTS

*-------------------------------
*
*-------------------------------

SPACE_WAIT		BIT	UNSTROB
:LOOP1			LDA	KEYBRD
				BPL	:LOOP1
				BIT	UNSTROB
				JSR	CHECK_ENTER
				BNE	:LOOP1
				RTS

SPACE_IN		LDA	#BLACK1
				STA	COLOR
				LDY	#$15
				JSR	LINER
				LDA	#RED
				STA	COLOR
				LDY	#$16
				JSR	LINER
				LDY	#$1E
				JMP	TEXTER

SPACE_INOUT		JSR	SPACE_IN
				JSR	SPACE_WAIT
				LDY	#$1E
				JSR	TEXTER
				LDA	#BLACK1
				STA	COLOR
				LDY	#$16
				JSR	LINER
				LDA	#RED
				STA	COLOR
				LDY	#$15
				JMP	LINER

*-------------------------------

BLCKENA			LDA	#$50
				STA	ENDLINE
				LDA	#$04
				STA	BEGROW
				LDA	#$23
				STA	ENDROW
				LDX	#$1B
				BNE	TO_BLACKEN		;Always
BLCKENB			LDX	#$1D
				BNE	COMMONC			;Always
BLCKENC			LDX	#$46
COMMONC			LDA	#$4D
				STA	ENDLINE
				LDA	#$05
				STA	BEGROW
				LDA	#$22
				STA	ENDROW
TO_BLACKEN		JMP	BLACKEN

*-------------------------------

GROUP_TEXT		USR	(GROUP)-
				DB	$EE

INFO0			PLOTAT	36;32;(OOPS!_EXCUSE_ME!_HEY,_WOULD_A_FINE,)
				PLOTAT	36;41;(INTELLIGENT_)-
PERGRP			USR	(PERSON_LIKE_YOU_WITH)
				PLOTAT	36;50;(SUCH_WEALTH_AND_EXPERIENCE_BE)
				PLOTAT	36;59;(INTERESTED_IN_A_LITTLE_BUSINESS)
				PLOTAT	36;68;(PROPOSITION?_)+

INFO1			PLOTAT	206;72;(<SPACE>)+

INFO3			PLOTAT	36;32;(YOU_ARE_GOING_TO_REGRET_THAT)
				PLOTAT	36;41;(DECISION_FOR_THE_REST_OF_YOUR)
				SETXY	36;50
				USR	(SERVICE.__GOOD_LUCK,_YOU'RE)
				PLOTAT	36;59;(GOING_TO_NEED_IT!)+

INFO4			PLOTAT	36;30;(YOU_ARE?_GOOD!_BUT_FIRST,_YOU_MUST)
				PLOTAT	36;38;(PROMISE_NOT_TO_DISCUSS_OUR_LITTLE)
				PLOTAT	36;46;(TRANSACTION_WITH_ANYONE.__YOU_SEE,)
				SETXY	36;54
				USR	(THE_TACTICAL_FORCE_AND_I_DON'T_SEE)
				PLOTAT	36;62;(EYE-TO-EYES_ON_THE_TOPIC_OF_FREE-)
				PLOTAT	36;70;(LANCERS.__DO_I_HAVE_YOUR_WORD?_)+

INFO5			SETXY	36;50
				USR	(THEN_I'M_WASTING_MY_TIME._GOOD_BYE.)+

INFO6			SETXY	36;32
				USR	(THEN_LET'S_GET_DOWN_TO_BUSINESS.)
				SETXY	36;41
				USR	(OH,_BY_THE_WAY,_SINCE_I'VE_CORNERED)
				PLOTAT	36;50;(THE_ENTIRE_MARKET_OF_INFORMATION)
				PLOTAT	36;59;(SELLING,_I_HAVE_NO_REASON_TO_HAGGLE)
				PLOTAT	36;68;(OVER_PRICES.)+

INFO7			PLOTAT	36;30;(I_HAVE_THESE_DATA_CARDS:)+

INFO8			PLOTAT	36;70;(ANY_OF_THEM_LOOK_INTERESTING?_)+

INFO9			PLOTAT	36;70;(WHICH_ONE_<1-)-
WHICH1			USR	(N>?_)+

INFOA			PLOTAT	36;50;(REMEMBER..._NOT_A_WORD_TO_ANYONE.)+

LINFO1			DB	$00,$1F,$1C
				DB	$00,$F9,$1C
				DB	$00,$F9,$4E
				DB	$00,$1F,$4E
				DB	$00,$1F,$1C
				DB	$FF
LINFO2			DB	$00,$C9,$4E
				DB	$00,$F9,$4E
				DB	$00,$F9,$46
				DB	$FF
LINFO3			DB	$00,$C9,$4E
				DB	$00,$C9,$46
				DB	$00,$F9,$46
				DB	$FF
