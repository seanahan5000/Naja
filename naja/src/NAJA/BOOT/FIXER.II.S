********************************
*
*  FIXER II
*
********************************

TIMEH			EQU	$41				;These labels
TIMEL			EQU	$42				;  have changed
SSTOREL			EQU	$B1	
SSTOREH			EQU	$B2	
LAST1			EQU	$B3
STRACK			EQU	$BA
SSECTOR			EQU	$BB
READNUM			EQU	$BC
COLOR			EQU	$E4
PAGE			EQU	$E6
DELMODE			EQU	$DE	
XPOS			EQU	$F1
YPOS			EQU	$F2
DIRECTN			EQU	$F3
BYTEWID			EQU	$F5
FACE			EQU	$FA
SRANGE			EQU	$F0
SREADIT			EQU	$0669
FREAD			EQU	$071E	
OPENBUF			EQU	$0749
CLEAR2			EQU	$1212
RDKEY			EQU	$6000
GETNSEW			EQU	$610F
NOSCAN			EQU	$65DD
PITCH			EQU	$A08A	
LENGTH			EQU	$A08B	
SOUND			EQU	$A08C	
GRPNUMB			EQU	$BC7F
PRIMARY			EQU	$C054
SCNDARY			EQU	$C055
DRIVOFF			EQU	$C0E8
WHITE1			EQU	$7F

				ORG	$9000

				LDX	#$00
MD2				LDA	$AC00,X
				STA	$0300,X
				LDA	$AD00,X
				STA	$0200,X
				TAY
				LDA	$AA00,X
				STA	$0600,X
				TYA
				STA	$AA00,X
				INX
				BNE	MD2	

				LDA	#$00
				STA	LAST1
				STA	SSTOREL

				LDA	$C083			;READ/WRITE RAM BANK 2
				LDA	$C083
				LDA	$C083			;IS THIS NEEDED???

				LDX	#$00
				JSR	LOADER

				LDX	#$01
				JSR	LOADER
				LDX	#$0A
MOVOPEN			LDA	$9F80,X
				STA	OPENBUF,X
				DEX
				BPL	MOVOPEN

				LDX	#$02
				JSR	LOADER

				LDA	$C08B			;READ/WRITE BANK 1
				LDA	$C08B
				LDA	$C08B			;IS THIS NEEDED???

				LDX	#$03
				JSR	LOADER

				LDX	#$04
				JSR	LOADER
				LDX	#$05
				JSR	LOADER
				LDX	#$06
				JSR	LOADER

				STA	DRIVOFF

				LDA	#$00	
				STA	GRPNUMB
				LDA	#$FF
				STA	DIRECTN
				LDA	#$01
				STA	XPOS
				LDA	#$05
				STA	YPOS
				LDA	#$01
				STA	FACE
				LDA	#$04
				STA	BYTEWID
				LDA	#$06
				STA	SRANGE
				LDA	#WHITE1
				STA	COLOR
				LDA	#$20
				STA	PAGE
				LDA	#$30
				STA	PITCH
				LDA	#$60
				STA	LENGTH
				JSR	SOUND
				JSR	CLEAR2
				STA	SCNDARY
				JSR	GETNSEW
				JSR	NOSCAN
				STA	PRIMARY
				LDA	#$A0
				STA	PITCH
				LDA	#$40
				STA	LENGTH
				JSR	SOUND
				LDA	#$25			;These have been
				STA	TIMEH			;  changed to DELAY_TIME (1 byte)
				LDA	#$00			;
				STA	TIMEL			;
				LDA	#$01
				STA	DELMODE
				JMP	RDKEY

LOADER			LDA	STORES,X
				STA	SSTOREH
				LDA	TRACKS,X
				STA	STRACK
				LDA	SECTORS,X
				STA	SSECTOR
				LDA	READNUMS,X
				STA	READNUM
				TXA
				BEQ	:SKIPA
				JMP	FREAD
:SKIPA			JMP	SREADIT

STORES			DFB	$E0				;CAMP (MAIN)
				DFB	$9F				;OPENBUFF
				DFB	$D0				;CAMP (BANK 2)
				DFB	$D0				;CAMP (BANK 1)
				DFB	$11				;COMMONS
				DFB	$60				;TUNNELS/GRAPHICS
				DFB	$A0				;HALL CONTROL/TCOMMONS

TRACKS			DFB	$05				;CAMP (MAIN)
				DFB	$07				;OPENBUFF
				DFB	$07				;CAMP (BANK 2)
				DFB	$08				;CAMP (BANK 1)
				DFB	$09				;COMMONS
				DFB	$0A				;TUNNELS/GRAPHICS
				DFB	$0C				;HALL CONTROL/TCOMMONS

SECTORS			DFB	$00				;CAMP (MAIN)
				DFB	$0A				;OPENBUFF
				DFB	$0B				;CAMP (BANK 2)
				DFB	$0B				;CAMP (BANK 1)
				DFB	$0B				;COMMONS
				DFB	$0A				;TUNNELS/GRAPHICS
				DFB	$0A				;HALL CONTROL/TCOMMONS

READNUMS		DFB	$20				;CAMP (MAIN)
				DFB	$01				;OPENBUFF
				DFB	$10				;CAMP (BANK 2)
				DFB	$10				;CAMP (BANK 1)
				DFB	$0F				;COMMONS
				DFB	$20				;TUNNELS/GRAPHICS
				DFB	$1B				;HALL CONTROL/TCOMMONS
