OTHER			JSR	OTHER_SUB
				LDA	PLANNER
				JSR	POINTIT
				LDX	PLANNER
				BIT	ORIGRED
				BPL	:SKIPA
				LDA	#$FF
				STA	LEGALS,X
				LDX	GRPNUMB
:SKIPA			DEX
				TXA
				PHA
				JSR	CLRBOX
				JSR	DOTIME
				JSR	CANCEL_IN
				LDY	#$04
				JSR	TEXTER
				PLA
				JSR	SELECT_CHAR
				PHP
				JSR	CANCEL_OUT
				PLP
				BCC	KOUTOF
				JSR	CLRBOX
				JSR	DOCOM3
				LDY	#$05
				JSR	TEXTER
KEYSPAC			JSR	SPACE_WAIT
				BCC	KEYSPAC
KOUTOF			JSR	CLRBOX
				LDA	#BLACK1
				STA	COLOR
				LDY	#$05
				JSR	LINER
				JSR	CHAR_CURSOR2
				JSR	DOTIME
				JSR	OPTIONS
				JMP	GETOP2

DOCOM3			LDA	CURSOR
				ASL
				ASL
				ASL
				ASL
				ASL
				STA	POINTR2
				LDA	#$00
				STA	CFLAG
				LDA	COMCNT
				CMP	#$0A
				BCS	FULLBOX
				LDY	POINTR2
				LDX	#$00
DOCLOOP			LDA	COMMAND,Y
				CMP	#$8A	
				BEQ	DFOUND
				INY
				INY
				INX
				BNE	DOCLOOP
DFOUND			CPX	#$0B	
				BCS	FULLBOX
				LDA	#$05
				STA	CFLAG
FULLBOX			LDA	#GREEN
				STA	COLOR
				LDA	CFLAG
				AND	#$01
				CLC
				ADC	#$05
				TAY
				JSR	LINER
DOC2			LDY	POINTR2
				LDA	COMMAND,Y
				CMP	#$8A	
				BEQ	DOOUT
				STA	INSTR1
				INY
				INC	POINTR2
				LDA	COMMAND,Y
				STA	INSTR2
				INC	POINTR2
				LDY	CFLAG
				LDA	COMXS,Y
				STA	XPNT
				LDA	COMYS,Y
				CLC
				ADC	#$38
				STA	YPNT
				INC	CFLAG
				JSR	COMR2
				JMP	DOC2
DOOUT			RTS

OTHER_SUB		JSR	SET_LEGALS
				LDX	PLANNER			;Also called by CONTROL
				BIT	ORIGRED
				BPL	:LOOP1
				LDX	GRPNUMB
				DEX
:LOOP1			LDA	#$00
				STA	LEGALS,X
				TXA
				JSR	POINTIT
				JSR	BAD_STATUS
				BNE	:SKIPB
:SKIPA			LDA	#$FF
				STA	LEGALS,X
:SKIPB			DEX
				BPL	:LOOP1
				RTS
