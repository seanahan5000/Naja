				TR	ON
*==================================
*
* NDOS (Naja Disk Operating System)
*    for 5.25 inch Disk ][ disks
*
*==================================
				LST	OFF

NEW_DOS			=	1				;***

STRACK			=	$B1
SSTOREH			=	$B2
DISK_SIDE		=	$B3

MOTCNTL			=	$B8
MOTCNTH			=	$B9
HOLDA			=	$BA
HOLDB			=	$BB
HOLDD			=	$BC
DESTL			=	$BD
DESTH			=	$BE
*

PTR1			=	MOTCNTL			;and PTR1+1

*NUM_DRIVES = $3C0 ;0 or 1
*SLOT_X0 = $3C1
*EXTRA_64K = $3C2

UN_WRITE_PRO	=	$1FFA
ASK_VECTOR		=	$1FFD

BUFFER1			=	$BB00
BUFFER2			=	$BC00

ARM				=	$C080
DRIVOFF			=	$C088
DRIVEON			=	$C089
DRIVE1			=	$C08A
READER			=	$C08C
LATCHIN			=	$C08E

			do NEW_DOS
D_HEAD1			=	$D5
D_HEAD2			=	$AA
D_HEAD3			=	$AD
			else
A_SYNC			=	$AF
A_HEAD1			=	$DC
A_HEAD3			=	$CD
D_HEAD1			=	$CD
D_HEAD2			=	$FF
D_HEAD3			=	$DC
			fin
RECALIBS		=	1
RETRIES			=	20

				ORG	$0400
DRIVE_ON		JMP	UP_TO_SPEED
DRIVE_OFF		JMP	TURN_OFF
READ_VECTOR		JMP	READ_SECTORS

			do NEW_DOS

* SIDE_FOUND:
*    0 = BOOT, 1 = UNUSED
*    2 = TRACC 17,15,  3 = TRACC 13,11,9
*    4 = FIGHT 17,15,  5 = FIGHT 13,11,9
				
READ_ADDRESS	LDX	SLOT_X0
				LDY	#$F0
				STY	HOLDA
:RETRY1			INY
				BNE	:1
				INC	HOLDA
				BEQ	:ERROR
				
:1				LDA	READER,X
				BPL	:1
:RETRY2			CMP	#$D5
				BNE	:RETRY1
				NOP
				
:2				LDA	READER,X
				BPL	:2
				CMP	#$AA
				BNE	:RETRY2
				LDY	#3
				
:3				LDA	READER,X
				BPL	:3
				CMP	#$96
				BNE	:RETRY2
				
				LDA	#$00
:4				STA	HOLDB			; clear checksum
:5				LDA	READER,X
				BPL	:5
				ROL					; carry set
				STA	HOLDA
:6				LDA	READER,X
				BPL	:6
				AND	HOLDA
				STA	CHECKSUM_FOUND,Y
				EOR	HOLDB			; update checksum
				DEY
				BPL	:4
				TAY
				BNE	:ERROR			; bad checksum
				
:7				LDA	READER,X
				BPL	:7
				CMP	#$DE
				BNE	:ERROR
				NOP
				
:8				LDA	READER,X
				BPL	:8
				CMP	#$AA
				BNE	:ERROR
				
				LDX	SECTOR_FOUND
				LDA	INTERLEAVE,X	; physical to logical
				EOR	SECTOR
				BMI	:SUCCESS		; don't care which sector
				BNE	READ_ADDRESS
:SUCCESS		CLC
				RTS
				
:ERROR			SEC
				RTS
				
			else	; NEW_DOS = 0
			
*** D_HEAD1,SIDE CODE,D_HEAD2,TRACK (5+5),SECTOR (5+5)
*** SIDE CODE = 11111AAA where AAA = SIDE+1
*    0 = BOOT, 1 = UNUSED
*    2 = TRACC 17,15,  3 = TRACC 13,11,9
*    4 = FIGHT 17,15,  5 = FIGHT 13,11,9

READ_ADDRESS	LDX	SLOT_X0
				LDY	#$F0
				STY	HOLDA
ERRCHCK			INY
				BNE	LOOPX0
				INC	HOLDA
				BEQ	ERROR
LOOPX0			LDA	READER,X
				BPL	LOOPX0
				CMP	#A_SYNC
				BNE	ERRCHCK
LOOPX1			LDA	READER,X
				BPL	LOOPX1
				CMP	#A_SYNC
				BNE	ERRCHCK
LOOPX2			LDA	READER,X
				BPL	LOOPX2
				CMP	#A_SYNC
				BEQ	LOOPX2
				BNE	CHECKA
LOOPA			LDA	READER,X
				BPL	LOOPA
CHECKA			CMP	#A_HEAD1
				BNE	ERRCHCK
				NOP
LOOPB			LDA	READER,X
				BPL	LOOPB
				STA	SIDE_FOUND
				NOP
				NOP
				NOP
LOOPC			LDA	READER,X
				BPL	LOOPC
				CMP	#A_HEAD3
				BNE	CHECKA
:LOOP1			LDA	READER,X
				BPL	:LOOP1
				STA	HOLDA
				NOP
:LOOP2			LDA	READER,X
				BPL	:LOOP2
				ROL					;Carry set
				AND	HOLDA
				STA	TRACK_FOUND
LOOPD			LDA	READER,X
				BPL	LOOPD
				ROL					;Carry set
				STA	HOLDA
LOOPE			LDA	READER,X
				BPL	LOOPE
				AND	HOLDA
				EOR	SECTOR
				BMI	:SKIPA
				BNE	READ_ADDRESS
:SKIPA			CLC
				RTS
ERROR			SEC
				RTS
			fin	; NEW_DOS = 0


READ_DATA		LDX	SLOT_X0
				LDY	#$20
ERRCNT			DEY
				BEQ	ERROR2
LOOP1			LDA	READER,X
				BPL	LOOP1
CHECK1			EOR	#D_HEAD1
				BNE	ERRCNT
				NOP
LOOP2			LDA	READER,X
				BPL	LOOP2
				CMP	#D_HEAD2
				BNE	CHECK1
				LDY	#$56
LOOP3			LDA	READER,X
				BPL	LOOP3
				CMP	#D_HEAD3
				BNE	CHECK1
				LDA	#$00
CHECK2			DEY
				STY	HOLDA
LOOP4			LDY	READER,X
				BPL	LOOP4
				EOR	CODETAB,Y
				LDY	HOLDA
				STA	BUFFER2,Y
				BNE	CHECK2
CHECK3			STY	HOLDA
LOOP5			LDY	READER,X
				BPL	LOOP5
				EOR	CODETAB,Y
				LDY	HOLDA
				STA	BUFFER1,Y
				INY
				BNE	CHECK3
LOOP6			LDY	READER,X
				BPL	LOOP6
				EOR	EOVALUE
				CMP	CODETAB,Y
				BNE	ERROR2
				CLC
				RTS
ERROR2			SEC
				RTS


DENIBLR
				LDX	BANK
*** HIT BANK SWITCH HERE IF NEEDED
				LDY	#$00
:LOOP1			LDX	#$56
:LOOP2			DEX
				BMI	:LOOP1
				LDA	BUFFER1,Y
				LSR	BUFFER2,X
				ROL
				LSR	BUFFER2,X
				ROL
				STA	(PTR1),Y
				INY
				CPY	HOLDA
				BNE	:LOOP2
				LDX	BANK
*** HIT BANK SWITCH HERE IF NEEDED
				RTS


********************************
* A = NUMBER OF SECTORS TO READ
* X = BEGINNING SECTOR LOW
* Y = BEGINNING SECTOR HIGH
* SIDE = 0,1,2,3 OR 4
* SSTOREL = DESTINATION ADDRESS (LOW)
* SSTOREH = DESTINATION ADDRESS (HIGH)

READ_SECTORS	STA	SECOUNT
				TXA
				AND	#%00001111
				STA	SECHOLD
				STX	STRACK
				TYA
				LDX	#4
:LOOP0			LSR
				ROR	STRACK
				DEX
				BNE	:LOOP0

*SEI
				NOP					;Remove when SEI put in

				BIT	ON_OFF_FLAG
				BMI	:LOOP1
				JSR	GET_SLOW_SIDE
				JMP	:SKIPA
:LOOP1			JSR	GET_FAST_SIDE
:SKIPA			LDA	SSTOREL
				STA	DESTL
				LDA	SSTOREH
				STA	DESTH
				LDA	SECHOLD
				STA	SECTOR
:LOOP3			LDA	#RETRIES
				STA	ECOUNT2
:LOOP2			JSR	READ_ADDRESS
				BCS	:ERROR
				JSR	READ_DATA
				BCC	:SKIPC
:ERROR			DEC	ECOUNT2
				BNE	:LOOP2
				LDA	#$88
				STA	TRACK
				LDA	#$00
				JSR	ARMOVE
				JMP	:LOOP1
:SKIPC			LDA	DESTL
				STA	PTR1
				LDA	DESTH
				BEQ	:SKIPE
				STA	PTR1+1
				LDX	#$00
				LDA	SECOUNT
				CMP	#$01
				BNE	:SKIPD
				LDX	LAST1
:SKIPD			STX	HOLDA
				JSR	DENIBLR
:SKIPE			INC	DESTH
				INC	SECTOR
				DEC	SECOUNT
				BEQ	READ_DONE
				LDA	SECTOR
				CMP	#$10
				BNE	:LOOP3
				LDA	#$00
				STA	SECTOR
				LDA	TRACK
				CLC
				ADC	#$02
				JSR	ARMOVE
				JMP	:LOOP3
READ_DONE

				BIT	ON_OFF_FLAG
				BVS	RETURN
TURN_OFF		LDX	SLOT_X0
				STA	DRIVOFF,X
RETURN			RTS

WRITE_PRO		JSR	GET_FAST_SIDE	;This routine assumes that
:LOOP1			LDX	SLOT_X0			;   drive is up to speed on entry
				LDA	$C08D,X
				LDA	LATCHIN,X
				BPL	RETURN
				STA	DRIVOFF,X
				JSR	UN_WRITE_PRO
				JSR	GET_SLOW_SIDE
				JMP	:LOOP1

GET_FAST_SIDE	JSR	FIND_SIDE
				CMP	DISK_SIDE
				BEQ	GOT_THE_SIDE
				JSR	TURN_OFF

GET_SLOW_SIDE	LDX	#0
				LDA	IN_DRIVE1
				CMP	DISK_SIDE
				BEQ	:SKIPA
				LDA	NUM_DRIVES
				BEQ	:SKIPA
				LDA	IN_DRIVE2
				CMP	DISK_SIDE
				BNE	:SKIPA
				INX
:SKIPA			JSR	SLOW_SIDE
				BEQ	GOT_THE_SIDE
				LDA	NUM_DRIVES
				BEQ	:SKIPB
				JSR	TURN_OFF
				LDA	DRIVENUM
				EOR	#$01
				TAX
				JSR	SLOW_SIDE
				BEQ	GOT_THE_SIDE
:SKIPB			JSR	TURN_OFF
				LDA	DISK_SIDE
				JSR	ASK_VECTOR		;Ask for side
				JMP	GET_SLOW_SIDE

				NOP					;FREE SPACE

SLOW_SIDE		STX	DRIVENUM
				TXA
				ORA	SLOT_X0
				TAX
				STA	DRIVE1,X
				JSR	UP_TO_SPEED
				JSR	FIND_SIDE
				LDX	DRIVENUM
				STA	IN_DRIVE1,X
				CMP	DISK_SIDE
				RTS

GOT_THE_SIDE	LDA	TRACK_FOUND
				ASL
				STA	TRACK
				LDA	STRACK
				ASL
				JMP	ARMOVE


FIND_SIDE		LDA	#RECALIBS
				STA	ECOUNT1
				LDA	#$FF
				STA	SECTOR
:LOOP0			LDA	#RETRIES
				STA	ECOUNT2
:LOOP1			JSR	READ_ADDRESS
				BCC	:SKIPA
				DEC	ECOUNT2
				BNE	:LOOP1
				DEC	ECOUNT1
				BMI	:ERROR
				LDA	#$88
				STA	TRACK
				LDA	#$00
				JSR	ARMOVE
				JMP	:LOOP0
:ERROR			LDA	#$FF
				RTS
:SKIPA			LDA	SIDE_FOUND
			do NEW_DOS
			else
				AND	#$07
				SEC
				SBC	#$01
			fin
				RTS


UP_TO_SPEED		LDX	SLOT_X0
				STA	DRIVEON,X
				LDA	#$D8
				STA	MOTCNTH
				LDA	LATCHIN,X
				LDA	READER,X
				LDY	#$08
TIMER			LDA	READER,X
				PHA
				PLA
				PHA
				PLA
				STX	HOLDC
				CMP	READER,X
				BNE	AT_SPEED
				DEY
				BNE	TIMER
				LDY	MOTCNTH
				BPL	AT_SPEED
DEL12			LDY	#$12
DELSPED			DEY
				BNE	DELSPED
				INC	MOTCNTL
				BNE	DEL12
				INC	MOTCNTH
				BNE	DEL12
AT_SPEED		RTS
HOLDC			DB	$00

CODETAB			=	*-$96
			do NEW_DOS
				DB	$00,$01,$98
				DB	$99,$02,$03
				DB	$9C,$04,$05
				DB	$06,$A0,$A1
				DB	$A2,$A3,$A4
				DB	$A5,$07,$08
				DB	$A8,$A9,$AA
				DB	$09,$0A,$0B
				DB	$0C,$0D,$B0
				DB	$B1,$0E,$0F
				DB	$10,$11,$12
				DB	$13,$B8,$14
				DB	$15,$16,$17
				DB	$18,$19,$1A
				DB	$C0,$C1,$C2
				DB	$C3,$C4,$C5
				DB	$C6,$C7,$C8
				DB	$C9,$CA,$1B
				DB	$CC,$1C,$1D
				DB	$1E,$D0,$D1
				DB	$D2,$1F,$D4
				DB	$D5,$20,$21
				DB	$D8,$22,$23
				DB	$24,$25,$26
				DB	$27,$28,$E0
				DB	$E1,$E2,$E3
				DB	$E4,$29,$2A
				DB	$2B,$E8,$2C
				DB	$2D,$2E,$2F
				DB	$30,$31,$32
				DB	$F0,$F1,$33
				DB	$34,$35,$36
				DB	$37,$38,$F8
				DB	$39,$3A,$3B
				DB	$3C,$3D,$3E
				DB	$3F
				
INTERLEAVE		DB	$00,$08,$01,$09,$02,$0A,$03,$0B
				DB	$04,$0C,$05,$0D,$06,$0E,$07,$0F
			else
				DB	$3F,$3E,$98
				DB	$99,$3D,$3C
				DB	$9C,$3B,$3A
				DB	$39,$A0,$A1
				DB	$A2,$A3,$A4
				DB	$A5,$14,$13
				DB	$A8,$A9,$12
				DB	$11,$10,$0F
				DB	$0E,$0D,$B0
				DB	$B1,$38,$37
				DB	$36,$35,$34
				DB	$33,$B8,$32
				DB	$31,$30,$2F
				DB	$2E,$2D,$2C
				DB	$C0,$C1,$C2
				DB	$C3,$C4,$C5
				DB	$C6,$C7,$C8
				DB	$C9,$CA,$17
				DB	$CC,$CD,$16
				DB	$15,$D0,$D1
				DB	$D2,$2B,$D4
				DB	$2A,$29,$28
				DB	$D8,$27,$26
				DB	$25,$DC,$24
				DB	$23,$22,$E0
				DB	$E1,$E2,$E3
				DB	$E4,$21,$20
				DB	$1F,$E8,$1E
				DB	$1D,$1C,$1B
				DB	$1A,$19,$18
				DB	$F0,$F1,$0C
				DB	$0B,$0A,$09
				DB	$08,$07,$F8
				DB	$06,$05,$04
				DB	$03,$02,$01
				DB	$00
			fin

ARMOVE			LDX	SLOT_X0
				STA	HOLDD
				CMP	TRACK
				BEQ	MOVDONE
				LDA	#$EF
				STA	MOTCNTL
				LDA	#$D8
				STA	MOTCNTH
				LDA	#$00
				STA	HOLDA
NOTDONE			LDA	TRACK
				STA	HOLDB
				SEC
				SBC	HOLDD
				BEQ	EQUAL
				BCS	POSITIV
				EOR	#$FF
				INC	TRACK
				BCC	NOADD
POSITIV			ADC	#$FE
				DEC	TRACK
NOADD			CMP	HOLDA
				BCC	NOLOAD
				LDA	HOLDA
NOLOAD			CMP	#$0C
				BCS	NOTRANS
				TAY
NOTRANS			SEC
				JSR	HALFSUB
				LDA	DELAYS,Y
				JSR	DELAYER
				LDA	HOLDB
				CLC
				JSR	HALF2
				LDA	DELAYS2,Y
				JSR	DELAYER
				INC	HOLDA
				BNE	NOTDONE
EQUAL			JSR	DELAYER
				CLC
HALFSUB			LDA	TRACK
HALF2			AND	#$03
				ROL
				ORA	SLOT_X0
				TAX
				LDA	ARM,X
				LDX	SLOT_X0
MOVDONE			RTS

DELAYER			LDX	#$11
DELOOP			DEX
				BNE	DELOOP
				INC	MOTCNTL
				BNE	NOCARRY
				INC	MOTCNTH
NOCARRY			SEC
				SBC	#$01
				BNE	DELAYER
				RTS

DELAYS2			DB	$70,$2C,$26
				DB	$22,$1F,$1E
				DB	$1D,$1C,$1C
				DB	$1C,$1C,$1C

DELAYS			DB	$01,$30,$28
				DB	$24,$20,$1E
				DB	$1D,$1C,$1C
				DB	$1C,$1C,$1C

TRACK			DS	1
SECTOR			DS	1
SECHOLD			DS	1
SECOUNT			DS	1
DRIVENUM		DS	1
			do NEW_DOS
CHECKSUM_FOUND	DS	1				; order dependent
SECTOR_FOUND	DS	1				;
TRACK_FOUND		DS	1				;
SIDE_FOUND		DS	1				;
			else
SIDE_FOUND		DS	1
TRACK_FOUND		DS	1
			fin
ECOUNT1			DS	1
ECOUNT2			DS	1

IN_DRIVE1		DB	$00				;Must be consecutive
IN_DRIVE2		DB	$FF				;  in this order

NUM_DRIVES		DB	$01				;TEMP***
SLOT_X0			DB	$60				;TEMP***
EXTRA_64K		DB	$00				;TEMP***

				DS	$800-8-*

ON_OFF_FLAG		DS	1
EOVALUE			DB	$00
LAST1			DB	$00
SSTOREL			DB	$00
BANK			DB	$00				;0 OR 1 ???

CHECK_WRITPRO	JMP	WRITE_PRO

				SAV	NDOS.525

				PUT	NDOS.525W
				SAV	NDOS.525W
