********************************
*
*  SCREEN III
*  (FAST BUT USES CLEAR1 AND CLEAR2)
*
********************************

				LST	OFF
				PUT	/NAJA/VARS/ZPAGE
				PUT	/NAJA/VARS/NCOM.VARS
				LST	ON

FROML			EQU	$11
FROMH			EQU	$12
TOL				EQU	$13
TOH				EQU	$14
ENDPAGE			EQU	$15
KEYBRD			EQU	$C000
UNSTROB			EQU	$C010
GRAPHIC			EQU	$C050
NOTEXT			EQU	$C052	
PRIMARY			EQU	$C054
SCNDARY			EQU	$C055
HIRES			EQU	$C057

				ORG	$0300

HGR				STA	PRIMARY			;DESIGNATE PRIMARY PAGE FOR DISPLAY 
				LDA	#$20
				STA	PAGE
				STA	NOTEXT			;DISPLAY HIRES SCREEN 
				STA	HIRES
				STA	GRAPHIC
				JMP	CLEAR1	

HGR2			STA	SCNDARY			;DESIGNATE SECONDARY PAGE FOR DISPLAY
				LDA	#$40
				STA	PAGE
				STA	NOTEXT			;DISPLAY HIRES SCREEN 
				STA	HIRES
				STA	GRAPHIC
				JMP	CLEAR2	

MOVETO1			LDX	#$20
				LDY	#$40
				BNE	START			;Always
				DS	5,$EA			;FREE SPACE
MOVETO2			LDX	#$40
				LDY	#$20
START			STX	TOH
				STY	FROMH
				LDA	#0
				STA	TOL
				STA	FROML
				LDX	#64
:1				LDY	#77
:2				LDA	(FROML),Y
				STA	(TOL),Y
				DEY
				BPL	:2
				LDA	#$80
				CLC
				ADC	TOL
				STA	TOL
				STA	FROML
				BCC	:1
				INC	TOH
				INC	FROMH
				DEX
				BNE	:1
				RTS

				DS	7,$EA			;FREE SPACE

RNDOUT			LDA	RANDOM1
				RTS

RNDMIZE			LDA	RANDOM1+5		;Must not change Y register
				LDX	#4
				CLC
:1				ADC	RANDOM1,X
				STA	RANDOM1,X
				DEX
				BPL	:1
				LDX	#5
:2				INC	RANDOM1,X
				BNE	RNDOUT
				DEX
				BPL	:2
				BMI	RNDOUT			;Always

RANDOM1			DB	$B4,$07,$EA
				DB	$71,$4C,$3B

WAIT			SEC
WAITA			PHA
WAITB			SBC	#$01
				BNE	WAITB
				PLA
				SBC	#$01
				BNE	WAITA
				RTS

WAIT2			SEC
WAITA2			PHA
WAITB2			BIT	KEYBRD
				BMI	HITAKEY
				SBC	#$01
				BNE	WAITB2
				PLA
				SBC	#$01
				BNE	WAITA2
				RTS
HITAKEY			PLA
				LDA	KEYBRD
				RTS

LINMOVE			LDY	#$27
LMOVE			LDA	(SCREENL),Y
				STA	(DESTINL),Y
				DEY
				BPL	LMOVE
				RTS

LINMOV2			STA	ENDCOLM
				INC	ENDCOLM
LMOVE2			LDA	(SCREENL),Y
				STA	(DESTINL),Y
				INY
				CPY	ENDCOLM
				BNE	LMOVE
				RTS
ENDCOLM			DB	$00

MOVE_ZPAGE		LDX	#0				;For debugging
:1				LDA	$00,X
				STA	$1F00,X
				INX
				BNE	:1
				BRK
				BRK
