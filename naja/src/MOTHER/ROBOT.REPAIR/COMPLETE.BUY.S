*-------------------------------
*
*-------------------------------

COMPLETE_BUY	LDA	#$00
				STA	PARTALL
				STA	MINUPR
				STA	MINLOW
				STA	EXPFLAG
				JSR	COUNT_CYBS
				LDA	CYB_COUNT
				CMP	#1
				BEQ	ONECYB
				JSR	PPEOR
				JSR	ALLICON
				JSR	DRAW_GROUP
				JSR	CLRMIDL
				LDY	#$03
				JSR	TEXTER
				JSR	VIEWER
				LDA	#$07
				STA	CURSOR
				LDY	#$07
LTLOOP2			LDA	RYPOINTS,Y
				STA	LINTABL,Y
				DEY
				BPL	LTLOOP2
				LDA	#$03
				STA	XHOLD
				JSR	KDOWN2
				JSR	RCURSIT
				LDA	CURSOR
				STA	ASSIGN2
				CMP	#$07
				BNE	ONECYB
				LDX	PICK
				LDA	#$FF
				STA	ON1,X
				JMP	OPTIONS
ONECYB			JSR	WHOSE_CREDITS
				LDA	#12
				STA	COUNT
VALOOP			LDX	COUNT
				LDY	#$00
				JSR	VALUSUB
				DEC	COUNT
				BPL	VALOOP
				JSR	RTSUB
				LDA	#$00
				STA	COUNT
				LDA	PICK
				EOR	#$01
				TAX	
				LDA	ON1,X	
				ORA	#$0F
				STA	ON1,X	
NEXT_AREA		JSR	PPEOR
				LDA	COUNT
				JSR	DRAW_SCREEN
				LDX	PICK
				LDA	#$F4
				STA	ON1,X	
				JSR	VIEWER
				LDA	COUNT
				AND	#%11111110
				CMP	#scrnWEAPONL
				BNE	NEXTC
NEXTB			JSR	WEPONLY
				JMP	NEXT
NEXTC			JSR	LIMITER
				JSR	BOXCURS
				JSR	TOSLOT		
NEXT			INC	COUNT
				LDA	COUNT
				CMP	#scrnPROGRAM+1
				BNE	NEXT_AREA

ALL_DONE		JSR	MINISUB
				LDX	#3
				SEC
:1				LDA	RTOTNUM,X
				STA	RTOTAL,X
				SED
				SBC	TOTAL_CREDITS,X
				CLD
				STA	THE_PRICE,X
				DEX
				BPL	:1
				BCC	NOVER
				LDA	THE_PRICE
				ORA	THE_PRICE+1
				ORA	THE_PRICE+2
				ORA	THE_PRICE+3
				BNE	ISOVER
NOVER			LDA	#$D0	
				STA	PARTALL
				JSR	PPEOR
				JSR	BTRANS
				LDY	#$1D
				JSR	TEXTER
				JSR	ALLICON
				JSR	OPENUP
				JSR	VOMAIN
				LDY	#$2E
				JSR	TEXTER
				JSR	VIEWER
				BIT	UNSTROB
NOVKEY			LDA	#7
				JSR	SET_LEGALS
:1				LDA	KEYBRD
				BPL	:1
				BIT	UNSTROB
				JSR	CHECK_ENTER
				BNE	:2
				JSR	TAKE_RTOTNUM
				JMP	SAVE_ROBOT
:2				CMP	#"A"			;Not CHECK_UP
				BEQ	ADJUST
				CMP	#"C"
				BNE	:1
				JMP	OPTIONS

PBACK			LDY	#$2F
				JSR	TEXTER
				LDY	#$2E
				JSR	TEXTER
				BIT	UNSTROB
				JMP	NOVKEY

ADJUST			LDY	#$2E
				JSR	TEXTER
				LDY	#$2F
				JSR	TEXTER
:1				JSR	ADJOVER_SUB
				JSR	PPEOR
				JSR	VIEWER
				JMP	:1

ISOVER			JSR	PPEOR
				LDA	#$E0
				STA	PARTALL
				JSR	NUMBER_TEXT
				JSR	BTRANS
				LDY	#$1D
				JSR	TEXTER
				JSR	ALLICON
				LDY	#$2A
				JSR	TEXTER
				JSR	OPENUP
				JSR	VOMAIN
				JSR	VIEWER
:1				JSR	ADJOVER_SUB
				JSR	PPEOR
				JSR	VIEWER
				JMP	:1

ADJOVER_SUB		LDA	#0
				STA	CURSOR
				JSR	GBOXSUB
				JSR	TOTALUP
				JSR	ADDBODS
				LDX	CURSOR
				INX
				TXA
				ASL
				ASL
				TAX
				LDY	#$03
				SEC
:1				LDA	RTOTAL,Y
				SED
				DEX
				SBC	BODTOT,X
				CLD
				STA	RTOTAL,Y
				STA	RTOTNUM,Y
				DEY
				BPL	:1
				JSR	PPEOR
				JSR	BTRANS
				JSR	CLOSEUP
				LDA	CURSOR
				JSR	DRAW_SCREEN
				JSR	DELABLE
				LDY	#$25
				JSR	TEXTER
				JSR	TOPLEVL
				LDX	PICK
				LDA	#$F4
				STA	ON1,X
				JSR	PPEOR
				JSR	CURSUB
				JSR	PPEOR
				JSR	VIEWER

				LDA	SCRPICK
				AND	#%11111110
				CMP	#scrnWEAPONL
				BNE	:2
				JSR	WEPONLY
				BCC	:4
				RTS

:2				JSR	ZOOMUP
				JSR	LIMITER
				JSR	BOXCURS
				BCC	:3
				RTS
:3				JSR	TOSLOT
:4				PLA
				PLA
				JMP	ALL_DONE

BACKEA			LDY	#$02
				LDA	#$EA
NOPLOOP			STA	KUPMOD,Y
				STA	KDOMOD,Y
				DEY
				BPL	NOPLOOP
				RTS

WPYPNTS			DB	$35,$3D,$45
				DB	$4D,$55,$5D

WPSUB			LDA	#$00
				STA	CURSOR
				LDA	#$05			;????????
				STA	CURHOLD
				LDA	PARTALL
				BEQ	WPSKIP1
				STX	CURSOR
				STX	CURHOLD
WPSKIP1			JSR	RCURSIT
				JMP	UPDSUB2

TOSLOT			LDY	SCRPICK
				LDA	BEGCATS,Y
				TAX
				CLC
				ADC	NUMCATS,Y
				STA	SCRATCH
				LDY	#$00
TABLOOP			LDA	POINTLV,Y
				STA	SLOT,X
				INY
				INX
				CPX	SCRATCH
				BNE	TABLOOP
				RTS

WEPONLY			LDA	#$20
				STA	KUPMOD
				STA	KDOMOD
				LDA	#<UPDSUB2
				STA	KUPMOD+1
				STA	KDOMOD+1
				LDA	#>UPDSUB2
				STA	KUPMOD+2
				STA	KDOMOD+2
				LDA	#6
				JSR	SET_LEGALS
				LDY	#5
WPLOOP			LDA	WPYPNTS,Y
				STA	LINTABL,Y
				DEY
				BPL	WPLOOP
				LDA	#$80
				STA	XHOLD
				LDY	SCRPICK
				LDX	LWEAPON-scrnWEAPONL,Y
				JSR	WPSUB	
				LDA	#"C"
				STA	CKEYMOD+1
WASKIPA			JSR	KEYMOVE	
				CMP	#"C"
				BEQ	WASKIP1
				LDX	EXPFLAG
				BEQ	WASKIP1
				JSR	EBLINK
				JMP	WASKIPA
WASKIP1			PHA
				JSR	RCURSIT
				LDA	#$00
				STA	CKEYMOD+1
				JSR	BACKEA
				PLA
				CMP	#"C"
				BNE	NOCANC1
				JMP	VCANC
NOCANC1			LDX	SCRPICK
				LDA	CURSOR
				STA	LWEAPON-scrnWEAPONL,X
				LDY	#3
				LDA	SCRPICK
				SEC
				SBC	#scrnWEAPONL-1
				ASL
				ASL
				TAX
XLOOP1			LDA	EVALNUM,Y
				STA	CATT,Y
				DEX
				STA	WEAPTOT,X
				LDA	RTOTNUM,Y
				STA	RTOTAL,Y
				DEY
				BPL	XLOOP1
				RTS
