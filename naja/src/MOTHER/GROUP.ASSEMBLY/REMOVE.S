REMOVE			JSR	ACURSIT2		;Remove from main stat box
				JSR	MARKIT
				JSR	ONGRP
				LDY	#$06
				JSR	TEXTER
				LDY	#$0E
				JSR	TEXTER
				JSR	RISUB	
				JSR	KDOWN2
				JSR	ACURSIT2
				LDY	#$06
				JSR	TEXTER
				LDY	#$04
				JSR	TEXTER
				LDA	CURSOR
				CMP	#$07
				BNE	NORCANC
				LDA	#$73
				STA	YPNT
				JSR	MARKIT
				LDY	#$0E
				JSR	TEXTER
				LDA	#$00
				STA	CURSOR
				JMP	COMMONX
NORCANC			CLC
				ADC	#$01
				STA	MEMBNUM
				CMP	GRPNUMB
				BNE	YUP	
				JSR	MISCYUP	
				JSR	GOSTATL	
				JSR	REMOVE_LAST
				JMP	GOBCOMN	

YUP				JSR	MISCYUP	
				LDX	MEMBNUM
				LDA	YPOINTS,X
				STA	BEGEND
				LDA	YPOINTS-1,X
				STA	BEGLINE
MOREUP			JSR	SCROLL
				DEC	BEGEND
				LDA	BEGEND
				CMP	BEGLINE
				BNE	MOREUP
				JSR	REMOVE_MIDDLE
GOBCOMN			LDY	#$0E
				JSR	TEXTER
				DEC	GRPNUMB
				LDA	#$00
				STA	CURSOR
				LDA	#$73
				STA	YPNT
				JSR	MARKIT
				JMP	COMMONX	

REMOVE_MIDDLE	LDX	MEMBNUM
				LDA	SAVE_TABLE-1,X
				BEQ	:SKIPA
				JSR	MEMBER_SAVE
				LDX	MEMBNUM
:SKIPA			STX	COUNT
:LOOP1			LDX	COUNT
				DEX
				TXA
				JSR	POINTIT
				LDA	CHARDL
				STA	TEMP
				LDA	CHARDH
				STA	TEMP+1
				INX
				TXA
				CMP	#$07
				BCS	:SKIPB
				JSR	POINTIT
				LDA	SAVE_TABLE,X
				STA	SAVE_TABLE-1,X
				LDY	#$7F
:LOOP2			LDA	(CHARDL),Y
				STA	(TEMP),Y
				DEY
				BPL	:LOOP2
				INC	COUNT
				BNE	:LOOP1			;Always
:SKIPB			LDA	#0
				STA	SAVE_TABLE+6
				RTS

REMOVE_LAST		LDX	MEMBNUM
				DEX
				LDA	SAVE_TABLE,X
				BEQ	:EXIT
				JMP	MEMBER_SAVE
:EXIT			RTS

SCROLL			LDX	BEGLINE
SCROLL2			LDA	LOBYTES,X
				STA	DESTINL
				LDA	HIBYTES,X
				ORA	#$40
				STA	DESTINH
				INX
				LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	#$40
				STA	SCREENH
				LDY	#$02	
				LDA	(SCREENL),Y
				AND	#$FC
				STA	SCRATCH
				LDA	(DESTINL),Y
				AND	#$03
				ORA	SCRATCH
				JMP	COLM3
MOREROW			LDA	(SCREENL),Y
COLM3			STA	(DESTINL),Y
				INY
				CPY	#$28
				BNE	MOREROW
				CPX	#$43	
				BNE	SCROLL2
				RTS

MISCYUP			LDA	MEMBNUM
				STA	COUNT
				DEC	COUNT
				LDA	#$7F
				STA	MASK
				JMP	NEXTG1

REMOVER			JSR	CLRBOTL			;Remove from roster
				LDA	RCURSOR
				STA	CURSOR
				LDA	#$40
				STA	RFLAG
				LDY	#$06	
				JSR	TEXTER
				LDY	#$12
				JSR	TEXTER
				JSR	RDOWN2
				CPX	#$FF
				BNE	NOCANCR
				DEC	HCURSOR
				JMP	SEER	
NOCANCR			JSR	CURSAD
				LDY	CURSOR
				DEY
				JSR	POINTR2
				LDY	#$00
				LDA	(CHARPTR),Y
				AND	#$7F
				STA	(CHARPTR),Y
				LDA	#$02
				JSR	DOSPECL
				JSR	FINDHIM
				INC	COUNT
				LDA	COUNT
				STA	MEMBNUM
				CMP	GRPNUMB
				BEQ	:SKIPA
				JSR	REMOVE_MIDDLE
				JMP	REMCOM
:SKIPA			JSR	REMOVE_LAST
REMCOM			DEC	GRPNUMB
				LDA	#$FF
				STA	REDRAW
				LDA	CURSOR
				STA	RCURSOR
				DEC	HCURSOR
				JSR	COUNTUP
				LDA	REMABLE
				BNE	GOROST3	
				LDA	#$C0
				JSR	WAIT
RWKEY			BIT	KEYBRD
				BPL	GOROST3	
				LDA	KEYBRD
				BIT	UNSTROB
				JSR	CHECK_ENTER
				BNE	GOROST3	
				LDA	#$C0
				JSR	WAIT
				JMP	RWKEY
GOROST3			JSR	CLRBOTL
				JMP	ROSTO2	
