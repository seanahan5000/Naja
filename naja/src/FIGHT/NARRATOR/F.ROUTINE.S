BITMASKS		DB	%10000000		;| Used in MON_SWING
HIT_MASKS		DB	%01000000		;|  | Used in PICK_MCOMMAND
				DB	%00100000		;|  |
				DB	%00010000		;|  |
				DB	%00001000		;|  |
				DB	%00000100		;|  |
				DB	%00000010		;|
				DB	%00000001		;|

GET_CMD_XY		LDX	COMPNTR
				LDA	COMMAND+1,X
				STA	SQUARE_HIT
				RTS

CLRBOX			LDX	#$80
CLOOP2			LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	PAGE
				STA	SCREENH
				LDA	#$00
				LDY	#$0D
CLRLOOP			STA	(SCREENL),Y
				INY
				CPY	#$27
				BNE	CLRLOOP
				LDA	(SCREENL),Y
				AND	#$70
				STA	(SCREENL),Y
				INX
				CPX	#$BF
				BNE	CLOOP2
				RTS

XYTEXT			PHA
				LDY	CHAR_NUMBER
				LDA	YPOINTS,Y
				STA	YPNT
				PLA
				JSR	DOXY2
				LDA	#$71	
				LDY	YPNT
				JMP	LINE

COMCALC			TAX
				ASL
				ASL
				ASL
				ASL
				ASL
				CLC
				ADC	COMNUMS,X
				STA	COMPNTR
				TAX
				LDA	COMMAND,X
				AND	#$0F
				RTS

OVALSUB			TAX					;MAKE THIS ROUTINE USE DRAW_TOPVIEW
				LDA	TEMPCHAR_XY,X
				TAX	
				TYA
				PHA
				TXA
				JSR	FROM11B
				LDA	XPNTS,X
				STA	XPNT
				LDA	YPNTS,Y
				STA	YPNT
				INC	YPNT
				PLA
				ASL
				PHA
				LDX	#<OVAL_DATA
				LDY	#>OVAL_DATA
				JSR	DRAW_SHAPES
				LDA	XPNT
				CLC
				ADC	#$07
				STA	XPNT
				PLA
				CLC
				ADC	#$01
				JMP	REDRAW_SHAPES

OVAL_DATA		DB	%01111000
				DB	%00001100
				DB	%00000110
				DB	%00000110
				DB	%00000110
				DB	%00001100
				DB	%01111000

				DB	%00001111
				DB	%00011000
				DB	%00110000
				DB	%00110000
				DB	%00110000
				DB	%00011000
				DB	%00001111

				DB	%01110000
				DB	%00011000
				DB	%00001100
				DB	%00001100
				DB	%00001100
				DB	%00011000
				DB	%01110000

				DB	%00000111
				DB	%00001100
				DB	%00011000
				DB	%00011000
				DB	%00011000
				DB	%00001100
				DB	%00000111

				DB	%00000000
				DB	%01110000
				DB	%00011000
				DB	%00011000
				DB	%00011000
				DB	%01110000
				DB	%00000000

				DB	%00000000
				DB	%00000111
				DB	%00001100
				DB	%00001100
				DB	%00001100
				DB	%00000111
				DB	%00000000

				DB	%00000000
				DB	%00000000
				DB	%01100000
				DB	%00110000
				DB	%01100000
				DB	%00000000
				DB	%00000000

				DB	%00000000
				DB	%00000000
				DB	%00000011
				DB	%00000110
				DB	%00000011
				DB	%00000000
				DB	%00000000

				DB	%00000000
				DB	%00000000
				DB	%01000000
				DB	%01100000
				DB	%01000000
				DB	%00000000
				DB	%00000000

				DB	%00000000
				DB	%00000000
				DB	%00000001
				DB	%00000011
				DB	%00000001
				DB	%00000000
				DB	%00000000

DELAYER			LDA	DELAY_MODE
				BNE	SPCWAIT
				BIT	KEYBRD
				BMI	KEYAHIT
				LDA	DELAY_TIME
				STA	SCREENH
				LDA	#$00	
				STA	SCREENL
DELOOP			LDA	SCREENH
				ORA	SCREENL
				BEQ	TIMESUP
				LDA	SCREENL
				SEC
				SED
				SBC	#$01
				STA	SCREENL
				LDA	SCREENH
				SBC	#$00
				CLD
				STA	SCREENH
				LDA	#$08	
				JSR	WAIT2
				BPL	DELOOP
KEYAHIT			LDA	KEYBRD
				BIT	UNSTROB
				CMP	#$A0
				BNE	DELOOP
SPACING			LDA	#$01
				STA	DELAY_MODE
				LDY	#0
				JSR	TEXTER
				LDY	#1
				JSR	TEXTER
				JMP	DELAYER
SPCWAIT			LDA	KEYBRD
				BPL	SPCWAIT	
				BIT	UNSTROB
				CMP	#$9B			;TEMP***
				BEQ	ABORT			;TEMP***
				CMP	#$A0
				BEQ	TIMESUP
				CMP	#$8D
				BNE	SPCWAIT	
				LDA	#$00
				STA	DELAY_MODE
				LDY	#1
				JSR	TEXTER
				LDY	#0
				JMP	TEXTER
TIMESUP			RTS
ABORT			BIT	$C061			;TEMP***
				BPL	:2				;TEMP***
				LDA	#0				;TEMP***
				STA	COUNT			;TEMP***
:1				JSR	POINTIT			;TEMP***
				LDY	#status			;TEMP***
				LDA	#dead			;TEMP***
				STA	(CHARDL),Y		;TEMP***
				INC	COUNT			;TEMP***
				LDA	COUNT			;TEMP***
				CMP	GRPNUMB			;TEMP***
				BNE	:1				;TEMP***
:2				JMP	BATTLE_OVER		;TEMP***

GETIME			LDX	POINTER
				LDA	PECKS,X
				AND	#$7F
				JSR	COMCALC
				JSR	TIMTABL
				LDX	COMPNTR
				LDA	COMMAND,X
				AND	#$0F
				LSR
				PHP
				TAY
				LDA	TIMTYPE,Y
				PLP
				BCC	HIGH1
				AND	#$0F
				BPL	DOCOMN			;Always
HIGH1			LSR
				LSR
				LSR
				LSR
DOCOMN			TAY
				LDA	TIMES,Y
				RTS

TIMTYPE			DB	$11,$13,$21
				DB	$10,$22,$50
				DB	$10

TIMTABL			LDY	#upreflex
				LDA	(CHARDL),Y
				JSR	MUCHT	
				STA	TIMES+1
				LDY	#lwreflex
				LDA	(CHARDL),Y
				JSR	MUCHT	
				STA	TIMES
				STA	TIMES+3
				SED
				CLC
				ADC	TIMES	
				STA	TIMES
				LDA	TIMES+1
				CLC
				ADC	TIMES+1
				STA	TIMES+2
				LDA	TIMES+1
				CLC
				ADC	TIMES+3
				BCC	NOVER
				LDA	#$99
NOVER			STA	TIMES+3
				CLD
				LDA	#$01
				STA	TIMES+4
				LDX	COMPNTR
				INX
				LDA	COMMAND,X
				STA	TIMES+5
				RTS

TLWCALC			LDY	#monLOREF
				JSR	MUCHTIM
				STA	SCRATCH
				CLC
				SED
				ADC	SCRATCH
				CLD
				RTS

TUPCALC			LDY	#monUPREF
MUCHTIM			LDA	(MONS_PTR),Y
MUCHT			LDY	#0
:1				CMP	MAXCUTS,Y
				BCC	:2
				BEQ	:2
				INY
				BNE	:1				;Always
:2				LDA	TMUNITS,Y
				RTS

MAXCUTS			DB	$16,$33,$50
				DB	$66,$82,$FF
TMUNITS			DB	$36,$18,$12
				DB	$09,$07,$06

CSBOT2			LDX	#$40
CSLOOPB			LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	#$40
				STA	SCREENH
				LDY	#$27
				LDA	#$00
CSLOOPA			STA	(SCREENL),Y
				DEY
				BPL	CSLOOPA
				INX
				CPX	#$C0
				BNE	CSLOOPB
				RTS

TEXTER			TYA
				ASL
				TAY
				LDA	TEXTS,Y
				STA	POINTL
				LDA	TEXTS+1,Y
				STA	POINTH
				JMP	TEXTOUT

TEXTS			DW	TDELMOD
				DW	TSPAMOD
				DW	OPEN_TEXT		;In Running code

TDELMOD			PLOTAT	210;75;(DELAY_MODE)
				PLOTAT	207;109;(<SPACE>_FOR)
				PLOTAT	213;117;(STEP_MODE)+

TSPAMOD			PLOTAT	213;75;(STEP_MODE)
				PLOTAT	225;85;(PRESS)
				PLOTAT	219;93;(<SPACE>)
				PLOTAT	207;109;(<RETRN>_FOR)
				PLOTAT	210;117;(DELAY_MODE)+

LINER			TYA	
				ASL
				TAY
				LDA	LINES,Y
				STA	POINTL
				LDA	LINES+1,Y
				STA	POINTH
				JMP	LINEOUT

LINES			DW	SHOOTBX
				DW	DIVIDER

SHOOTBX			DB	$00,$07,$47
				DB	$00,$55,$47
				DB	$00,$55,$B7
				DB	$00,$07,$B7
				DB	$00,$07,$47
				DB	$FE
				DB	$00,$06,$47
				DB	$00,$06,$B7
				DB	$FE
				DB	$00,$56,$47
				DB	$00,$56,$B7
				DB	$FE
				DB	$00,$59,$47
				DB	$00,$59,$B7
				DB	$FE
				DB	$00,$00,$40
				DB	$00,$00,$46
				DB	$FE
				DB	$00,$01,$46
				DB	$00,$01,$40
				DB	$00,$5B,$40
				DB	$00,$5B,$46
				DB	$FE
				DB	$00,$5C,$40
				DB	$00,$5C,$46
				DB	$FF

DIVIDER			DB	$00,$CE,$68
				DB	$01,$11,$68
				DB	$FF
