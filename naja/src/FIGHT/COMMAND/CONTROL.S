FIGHT_COMMAND	LDA	#$20
				STA	PAGE
				JSR	CLRBOX
				LDA	ROUND
				BNE	NOTRAP
				LDA	#GREEN
				STA	COLOR
				LDY	#$00
				JSR	LINER
				LDA	MONS_TOTAL
				STA	ATALLY

				LDA	ALIEN1			;*17
				CMP	#$02			;*17
				BEQ	NOTRAP			;*17 
				CMP	#$20			;*17
				BEQ	NOTRAP			;*17
				CMP	#$23			;*17
				BEQ	NOTRAP			;*17 
GETKEY1			BIT	KEYBRD
				BPL	GETKEY1
NOTRAP			BIT	UNSTROB

				LDY	#$40
				LDA	SUPRISED_FLAG
				BEQ	:2
				BMI	:1
				LDY	#$41
				LDX	MONS_TOTAL
				DEX
				BEQ	:1
				INY
:1				LDA	ROUND
				BNE	:2
				JSR	TEXTER
				LDY	#$43
				JSR	TEXTER
:2
				STA	PRIMARY
				JSR	REFRESH_STATS

				LDA	SUPRISED_FLAG
				BEQ	:6
				BMI	:4
				LDX	#0
:3				LDA	#$8A
				STA	COMMAND,X
				LDA	#$72
				STA	COMMAND+1,X
				TXA
				CLC
				ADC	#$20
				TAX
				CMP	#$E0
				BNE	:3
				JMP	SUPOVER

:4				LDA	ROUND
				BNE	:6
				LDX	#$03
:5				LDA	#0				;*** THIS SHOULD BE
				JSR	WAIT2			;  CHANGED TO A KEYPRESS LATER !!!
				BMI	:6
				DEX
				BPL	:5
:6
				LDY	#$00
				JSR	TEXTER
				JSR	LINBACK
				BIT	UNSTROB
				JSR	CLRBOXZ	
REDSPOT			LDX	#$1F
				LDA	#$FF	
ENTLOOP			STA	ENTABLE,X
				DEX
				BPL	ENTLOOP
				LDA	#$00
				STA	PLANNER
				STA	ORIGRED
NXTPLAN			LDA	PLANNER
				JSR	POINTIT
				JSR	BAD_STATUS
				BEQ	NEXTMAN
NOMNEXT			JSR	FROM11
				JSR	GROUP_CURSOR1
				LDY	PLANNER
				JSR	CHAR_SQUARE
				LDY	PLANNER
				JSR	CHAR_POINT
				JSR	PLANSUB
				JSR	FROM11
				JSR	GROUP_CURSOR1
				LDY	PLANNER
				JSR	CHAR_POINT
NEXTMAN			INC	PLANNER
				LDA	PLANNER
				CMP	GRPNUMB
				BEQ	DOOVER	
				JMP	NXTPLAN
DOOVER			JSR	CLRBOX2
				JSR	CLRBOX
				JSR	LINFRTH
				LDY	#$06
				JSR	TEXTER
				LDA	#$00
				STA	MAXTEXT
				JSR	INPUT2
KEYCHK			LDA	TEXTBUF
				CMP	#"A"
				BEQ	HITA
				CMP	#"R"
				BEQ	HITR
				CMP	#$8D
				BNE	NORETN
				JSR	CHAROUT
NORETN			JSR	RECALL
				JMP	KEYCHK
HITA			JSR	CLRBOX
				BIT	FLIPPED_FLAG
				BPL	HASKIP
				JSR	FLIPPER	
HASKIP			JSR	DEBOX
SUPOVER			LDA	#$00	
				STA	PLANNER
				LDA	#$62
				PHA
				LDA	#$FF
				PHA
				LDX	#fReNarrate
				LDA	ROUND
				BNE	:1
				LDX	#fNarrate
:1				JMP	SLOAD_FILE

LINFRTH			LDA	#BLACK1
				STA	COLOR
				LDY	#$01
				JSR	LINER
				LDA	#GREEN
				STA	COLOR
				LDY	#$04
				JMP	LINER

DEBOX			LDA	#$00
				STA	COUNT
TTLOOP1			JSR	POINTIT
				JSR	BAD_STATUS
				BEQ	DEDSKIP
				LDY	COUNT
				JSR	CHAR_SQUARE
DEDSKIP			INC	COUNT
				LDA	COUNT
				CMP	GRPNUMB
				BNE	TTLOOP1	
				RTS

HITR			JSR	CLRBOX
				LDY	#$37
				JSR	TEXTER
				LDX	#<REDO_CDEF
				LDY	#>REDO_CDEF
				JSR	INIT_CURSOR
				JSR	GETKEY
				LDX	CURSOR
				BEQ	ENTIRE
				DEX
				BEQ	ONEMEMB
				JSR	CLRBOX
				JMP	DOOVER

*-------------------------------
REDO_CDEF		DB	$02
				DB	$80
				DW	:REDO_CURS
:REDO_CURS		LDX	CURSOR
				LDY	:YTABLE,X
				LDX	#$7A
				JMP	ARROWER
:YTABLE			DB	$99,$A3,$AD
*-------------------------------

ENTIRE			JSR	LINBACK
				JSR	DEBOX
				LDY	#$00
				JSR	TEXTER
				JMP	REDSPOT
ONEMEMB			JSR	CLRBOX
				JSR	LINBACK
				JSR	CANCEL_IN
				LDY	#$3D
				JSR	TEXTER
				JSR	OTHER_SUB
				LDA	#$EE
				STA	ORIGRED
				LDY	PLANNER
				DEY
				TYA
				JSR	SELECT_CHAR
				BCS	:SKIPA
				JSR	CHAR_CURSOR2
				JSR	CANCEL_OUT
				JMP	DOOVER
:SKIPA			JSR	CHAR_CURSOR2
				JSR	CANCEL_OUT
				JSR	CLRBOX
				LDA	PLANNER
				PHA
				LDY	#$00
				JSR	TEXTER
				LDA	CURSOR
				STA	PLANNER
				JSR	POINTIT
				JSR	DENERGY
				JSR	PLANSUB
				LDA	PLANNER
				STA	CURSOR
				PLA
				STA	PLANNER
				JSR	CHAR_CURSOR2
				JMP	DOOVER

LINBACK			LDA	#BLACK1	
				STA	COLOR
				LDY	#$04
				JSR	LINER
				LDA	#GREEN
				STA	COLOR
				LDY	#$01
				JMP	LINER

PLANSUB			LDA	PLANNER
				ASL
				ASL
				ASL
				ASL
				ASL
				STA	COMPNTR
				LDA	#$00
				STA	COMCNT
				LDA	#$72
				STA	TIMLEFT
				JSR	DOTIME3
				LDX	PLANNER
				LDY	#$1E
				LDA	(CHARDL),Y
				STA	XYSPOTS,X
				JSR	CLRBOX
				JSR	OPTIONS
				LDX	#$3F
				LDA	#$FF
CHTOFF			STA	CHENTAB,X
				DEX
				BPL	CHTOFF
				JSR	GETOPTS
				JSR	ENUDATE
				LDY	#$26
				LDX	#$02
TOCHEN1			LDA	CHENTAB,X
				STA	(CHARDL),Y
				DEY
				DEX
				BPL	TOCHEN1
				JMP	ENUDATE

GETOPTS			LDA	#$00
				STA	OPTSPOT
				JSR	DOTIME2
GETOP2			LDA	COMCNT
				ASL
				CLC
				ADC	COMCNT
				TAX
				LDY	#$24
TOCHEN2			LDA	(CHARDL),Y
				STA	CHENTAB,X
				INX
				INY
				CPY	#$27
				BNE	TOCHEN2
				LDA	ROUND
				BNE	GETOP3
				LDY	#$0D
				LDA	(CHARDL),Y
				BPL	GETOP3
				LDY	#$6E
				LDA	(CHARDL),Y
				BMI	GETOP3
				JSR	DOTIME2
				LDA	#$0E
				STA	OPTSPOT
				JSR	DOTIME2
				JSR	OPT_CURSOR
				JMP	END
GETOP3			JSR	OPT_CURSOR
				BIT	UNSTROB
				JMP	OPT_MOVEKEY
OPT_BLINK		LDA	#$10
				JSR	WAIT
				LDA	#$F0
				JSR	WAIT2
				BMI	OPT_GOTKEY
				JSR	OPT_CURSOR
				LDA	#$00
				JSR	WAIT2
				JSR	OPT_CURSOR
OPT_MOVEKEY		LDA	KEYBRD
				BPL	OPT_BLINK
OPT_GOTKEY		BIT	UNSTROB
				JSR	CHECK_LEFT
				BEQ	OPT_LEFT
				JSR	CHECK_RIGHT
				BEQ	OPT_RIGHT
				JSR	CHECK_UP
				BEQ	OPT_UP
				JSR	CHECK_DOWN
				BEQ	OPT_DOWN
				JSR	CHECK_ENTER
				BNE	OPT_MOVEKEY

				LDY	OPTSPOT
				LDA	OPT_LEGALS,Y
				BPL	CALL_COMMAND
				BMI	OPT_MOVEKEY		;Always

OPT_UP			JSR	OPT_CURSOR
				JSR	DOTIME	
				JSR	RANGER
				LDA	OPTSPOT
				CMP	MINOPT
				BNE	:SKIPA
				LDA	MAXOPT
				STA	OPTSPOT
:SKIPA			DEC	OPTSPOT
				BPL	OPT_COM2		;Always

OPT_DOWN		JSR	OPT_CURSOR
				JSR	DOTIME
				JSR	RANGER
				INC	OPTSPOT
				LDA	OPTSPOT
				CMP	MAXOPT
				BCC	OPT_COM2
				LDA	MINOPT
				STA	OPTSPOT
				BPL	OPT_COM2		;Always

OPT_LEFT		JSR	OPT_CURSOR
				JSR	DOTIME
				LDA	OPTSPOT
				SEC
				SBC	#7
				BPL	OPT_COM1
				CLC
				ADC	#21
				BPL	OPT_COM1		;Always

OPT_RIGHT		JSR	OPT_CURSOR
				JSR	DOTIME
				LDA	OPTSPOT
				CLC
				ADC	#7
				CMP	#21
				BCC	OPT_COM1
				SBC	#21				;Carry assumed set
OPT_COM1		STA	OPTSPOT
				JSR	RANGER
OPT_COM2		JSR	OPT_CURSOR
				JSR	DOTIME2
				JMP	OPT_MOVEKEY

CALL_COMMAND	LDA	OPTSPOT
				ASL
				TAY
				LDA	JMPNTS+1,Y
				PHA
				LDA	JMPNTS,Y
				PHA
				RTS

JMPNTS			DW	SWING-1
				DW	SHOOT-1
				DW	ENERGY-1
				DW	THROW-1
				DW	USE-1
				DW	0				;DUMMY
				DW	0				;DUMMY
				DW	MOVE-1
				DW	IDENTIFY-1
				DW	EXCHANGE-1
				DW	PARRY-1
				DW	RUN-1
				DW	0				;DUMMY
				DW	0				;DUMMY
				DW	END-1
				DW	BACK1-1
				DW	REDO-1
				DW	OTHER-1
				DW	FLIP-1
				DW	DELAY-1

*-------------------------------

RANGER			LDA	#$00
				LDY	OPTSPOT
				CPY	#$07
				BCC	:SKIPA
				LDA	#$07
				CPY	#$0E
				BCC	:SKIPA
				LDA	#$0E
:SKIPA			STA	MINOPT
				CLC
				ADC	#$06	
				STA	MAXOPT
				RTS

*-------------------------------

OPT_CURSOR		LDX	#$64
				LDA	OPTSPOT
				CMP	#7
				BCC	:SKIPA
				LDX	#$9F
				SBC	#7				;Carry assumed set
				CMP	#7
				BCC	:SKIPA
				LDX	#$E6
				SBC	#7				;Carry assumed set
:SKIPA			TAY
				LDA	OPTY,Y
				TAY
				JMP	ARROWER

OPTY			DB	$81,$8A,$93
				DB	$9C,$A5,$AE
				DB	$B7
