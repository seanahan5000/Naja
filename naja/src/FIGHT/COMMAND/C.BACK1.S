BACK1			DEC	COMCNT
				DEC	COMPNTR
				DEC	COMPNTR
				LDA	COMCNT
				ASL
				CLC
				ADC	COMCNT
				TAX
				LDA	CHENTAB,X
				CMP	#$FF
				BEQ	NEBACK
				TXA
				PHA
				JSR	ENUDATE
				PLA
				TAX
				LDY	#energylev
BKLOOP			LDA	CHENTAB,X
				STA	(CHARDL),Y
				INX
				INY
				CPY	#energylev+3
				BNE	BKLOOP
				JSR	ENUDATE
NEBACK			JSR	DOTIME3
				LDY	COMCNT
				LDA	CTIMTAB,Y
				STA	TIMLEFT
				JSR	DOTIME3
				LDY	COMPNTR
				LDA	COMMAND,Y
				STA	INSTR1
				LDA	COMMAND+1,Y
				STA	INSTR2
				JSR	COMMER
				JSR	COLLAPS	
				LDY	COMPNTR
				LDA	COMMAND,Y
				AND	#$0F
				CMP	#cmdMOVE
				BEQ	SPMOVE
				CMP	#cmdENERGY
				BNE	NOSPEC1

SPENER			LDY	COMPNTR
				LDA	COMMAND-2,Y
				LSR
				LSR
				LSR
				LSR
				JSR	SPENSUB
				JMP	NOSPEC1

SPMOVE			LDA	COMCNT
				PHA
				LDA	COMPNTR
				PHA
SPMLOOP			LDA	COMCNT
				BEQ	ORIGPOS
				DEC	COMCNT
				DEC	COMPNTR
				DEC	COMPNTR
				LDY	COMPNTR
				LDA	COMMAND,Y
				AND	#%00001111
				CMP	#cmdMOVE
				BNE	SPMLOOP
				LDA	COMMAND+1,Y
				JMP	SPCOMN
ORIGPOS			LDY	#position
				LDA	(CHARDL),Y
SPCOMN			LDY	PLANNER
				STA	XYSPOTS,Y
				PLA
				STA	COMPNTR
				PLA
				STA	COMCNT

NOSPEC1			LDY	COMCNT
				BEQ	NOBAKS
				LDY	COMPNTR
				LDA	COMMAND-2,Y
				AND	#%00001111
				CMP	#cmdAIM
				BNE	NOBAKS	
				JMP	BACK1
NOBAKS			JSR	CLRBOX			;MODIFIED POINT 
				JSR	OPTIONS
				LDA	#$0F
				STA	OPTSPOT
				LDA	COMCNT
				BNE	CABACK
				JSR	DOTIME
				JMP	GETOPTS
CABACK			JMP	GETOP2
COLLAPS			LDA	COMCNT
				CMP	#$0A
				BNE	COLLOUT
				LDA	#$40
				STA	PAGE
				JSR	CLRBOX2
				LDA	COMDAT
				STA	MOVMOD+1
				LDA	COMDAT+1
				STA	MOVMOD+2
				INC	COMPNTR
				INC	COMPNTR
				JSR	MOVBACK
				DEC	COMPNTR
				DEC	COMPNTR
				LDA	CMDAT2
				STA	MOVMOD+1
				LDA	CMDAT2+1
				STA	MOVMOD+2
COLLOUT			RTS
COMDAT			DW	COMMER
CMDAT2			DW	COMR2

SPENSUB			ASL
				ASL
				TAY
				LDX	#$04
				LDA	#$FF
SPELOOP			STA	ENTABLE,Y
				INY
				DEX
				BNE	SPELOOP
				RTS
