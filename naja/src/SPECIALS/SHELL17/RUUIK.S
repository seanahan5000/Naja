
				LST	OFF

COLUMN			EQU	$01
TOTAL			EQU	$0F

				JSR	CLEAR2
				LDA	#$40
				STA	PAGE
				LDA	#$FF
				STA	DIRECTN
				LDA	SRANGE
				PHA
				LDA	#$01
				STA	SRANGE
				JSR	GETNSEW
				JSR	NOSCAN
				PLA
				STA	SRANGE
				LDY	#0
				JSR	PICKER
				JSR	FLIP
				LDY	#1
				JSR	PICKER
				LDY	#2
				JSR	PICKER
				STA	SCNDARY
				LDA	#$20
				STA	PAGE
				LDA	#BLUE
				STA	COLOR
				JSR	CLEAR1
				LDY	#$00
				JSR	LINER
				LDY	#$00
				JSR	TEXTER
				JSR	MOVE212
				LDA	#$40
				STA	PAGE
				LDA	#$00
				STA	MAXTEXT
				JSR	INPUT2
LOOPYN			LDA	TEXTBUF
				CMP	#$8D
				BNE	NORET
				JSR	CHAROUT
				JSR	RECALL
				JMP	LOOPYN
NORET			CMP	#$D9
				BEQ	YESWANT
				CMP	#$CE
				BEQ	NOWANT
				JSR	RECALL
				JMP	LOOPYN
NOWANT			JSR	CLEAR_BOX
				LDY	#3
				JSR	PICKER
				JMP	FSCREEN
YESWANT			JSR	CLEAR_BOX
				LDY	#3
				JSR	PICKER
				JSR	RIFLER
				CPX	#$FF
				BEQ	BSCREEN
				BNE	CSCREEN

BSCREEN			LDY	#$01
				JSR	TEXTER
				LDY	#$02
				JSR	TEXTER
				BIT	UNSTROB
				LDY	#$14
				JSR	RU_WAITER
				JSR	CLEAR_BOX
				JMP	FSCREEN

CSCREEN			LDY	#$01
				JSR	TEXTER
				LDY	#$03
				JSR	TEXTER
				BIT	UNSTROB
				JSR	INPUT2
LOOPYN2			LDA	TEXTBUF
				CMP	#$8D
				BNE	NORET2
				JSR	CHAROUT
				JSR	RECALL
				JMP	LOOPYN2
NORET2			CMP	#$D9
				BEQ	YESIS
				CMP	#$CE
				BEQ	NOIS
				JSR	RECALL
				JMP	LOOPYN2
NOIS			JSR	CLEAR_BOX
				JMP	ESCREEN
YESIS			JSR	CLEAR_BOX
				LDY	#$04
				JSR	TEXTER
				LDA	#RED
				STA	COLOR
				LDY	#$01
				JSR	LINER
				BIT	UNSTROB
				LDA	#$DA
				LDY	#$1B
				JSR	INPUT
				LDA	TEXTBUF
				CMP	#$8D
				BEQ	ISRET
				JSR	CHAROUT
ISRET			JSR	CLEAR_BOX
				LDA	#$07
				STA	COUNTER
REMLOOP			LDX	COUNTER
				LDA	RIFTABL-1,X
				JSR	POINTIT
				LDX	COUNTER
				LDA	RIFTABL,X
				JSR	REMOVE
				DEC	COUNTER
				DEC	COUNTER
				BPL	REMLOOP
				JMP	FSCREEN

ESCREEN			LDY	#$05
				JSR	TEXTER
				LDY	#$04
				JSR	RU_WAITER
				LDY	#$06		; "GUARDS!!!"
				JSR	TEXTER

				; generate an encounter with RUUIKs MEN
				;
				LDA	#$23
				STA	ALIEN1
				LDA	#$08
				STA	HMANY1
				LDA	#$42
				STA	ALTYPE1
				LDA	#$FF
				STA	ALIEN2
				STA	ALIEN3

;****************************************
; TODO: figure out how this is related to giving Ruuiks men the password
			do	0
				LDX	#$05
XXLOOP			LDA	XXDATA,X
				STA	$1194,X
				DEX
				BPL	XXLOOP
			fin
;****************************************

				; turn away from King Ruuik to give background for encounter
				;
				LDA	#$0F
				STA	DIRECTN
				JSR	GETNSEW
				JSR	MOVETO1
				STA	PRIMARY
				LDA	#$20
				STA	PAGE

;****************************************
; TODO: figure out how this is related to giving Ruuiks men the password
			do	0
				LDA	#$77
				STA	SSTOREH
				LDA	#$00
				STA	STRACK
				STA	SSECTOR
				LDA	#$19
				STA	READNUM
				LDA	#$11
				PHA
				LDA	#$93
				PHA
				JMP	SREADIT

AFTMANY			EQU	$7706			;IN FIGHT LOADER
RUUSTOP			EQU	$791B			;IN FIGHT LOADER

XXDATA			DFB	$CE
				DW	RUUSTOP-1
				DFB	$4C
				DW	AFTMANY
			else
				LDA	#$FF		; read key before redrawing tunnel
				RTS				; return to RDKEY
			fin
;****************************************

FSCREEN			LDY	#$07
				JSR	TEXTER
				LDY	#$16
				JSR	RU_WAITER
				JSR	MOVE212
				JSR	TURN_AWAY
				LDA	#$20
				STA	PAGE
				JSR	CLEAR1
				STA	PRIMARY
				LDA	#$FF		; read key before redrawing tunnel
				RTS				; return to RDKEY

; TODO: this should be part of COMMON.S
; TODO: reconcile with CPARER1, CPAPER2

RETURN_AWAY		BIT	UNSTROB
TURN_AWAY		LDA	KEYBRD
				BPL	TURN_AWAY
				JSR	CHECK_DOWN
				BEQ	:1
				JSR	CHECK_LEFT
				BEQ	:1
				JSR	CHECK_RIGHT
				BNE	RETURN_AWAY
:1				RTS

; TODO: this should be part of COMMON.S
RU_WAITER		LDA	#$00
				JSR	WAIT2
				BNE	WAITEND
				DEY
				BPL	RU_WAITER
WAITEND			BIT	UNSTROB
				RTS

RIFLER			LDA	#$00
				STA	COUNT
				STA	TOTAL
RILOOP1			LDA	#$00
				STA	COUNTER
				LDA	COUNT
				JSR	POINTIT
				LDY	#$0A
				LDA	(CHARDL),Y
				CMP	#$05
				BEQ	RISKIP2
RILOOP2			LDA	COUNTER
				JSR	CORCALC
				LDA	(CHARDL),Y
				AND	#$7F
				CMP	#$4D
				BNE	RISKIP1
				INY
				LDA	(CHARDL),Y
				DEY
				CMP	#$00
				BNE	RISKIP1
				LDA	TOTAL
				ASL	A
				TAX
				LDA	COUNT
				STA	RIFTABL,X
				INX
				TYA
				STA	RIFTABL,X
				INC	TOTAL
				LDA	TOTAL
				CMP	#$04
				BEQ	RIFLOUT
RISKIP1			INC	COUNTER
				LDA	COUNTER
				CMP	#$0A
				BNE	RILOOP2
RISKIP2			INC	COUNT
				LDA	COUNT
				CMP	GRPNUMB
				BNE	RILOOP1
				LDX	#$FF
				RTS
RIFLOUT			LDX	#$00
				RTS

RIFTABL			DS	16,0

REMOVE			STA	SCRATCH
				TAY
				CMP	#$4D
				BCC	SINGLE
				CPY	#$61
				BEQ	SINGLE
				LDY	#$0D
				LDA	(CHARDL),Y
				BPL	NOTSING
				LDY	SCRATCH
				CPY	#$4D
				BEQ	SINGLE
NOTSING			LDY	#$1D
				LDA	(CHARDL),Y
				CLC
				ADC	#$03
				JSR	CORCALC
				CLC
				ADC	#$04
				STA	ENDLINE
				LDY	SCRATCH
				BNE	SNEAKY
SLIDES			LDA	(CHARDL),Y
				DEY
				DEY
				DEY
				DEY
				STA	(CHARDL),Y
				INY
SNEAKY			INY
				INY
				INY
				INY
				CPY	ENDLINE
				BNE	SLIDES
				TYA
				SEC
				SBC	#$04
				TAY
SINGLE			LDX	#$04
				LDA	#$FF
ERASEM			STA	(CHARDL),Y
				INY
				DEX
				BNE	ERASEM
				RTS
CORCALC			ASL	A
				ASL	A
				CLC
				ADC	#$3D
				TAY
				RTS

MOVE212			LDA	#$00
				STA	LINENUM
MLOOP1			LDX	LINENUM
				LDA	LOBYTES,X
				STA	SCREENL
				STA	DESTINL
				LDA	HIBYTES,X
				ORA	#$20
				STA	SCREENH
				EOR	#$60
				STA	DESTINH
				LDY	#$07
MLOOP2			LDA	(SCREENL),Y
				TAX
				LDA	(DESTINL),Y
				STA	(SCREENL),Y
				TXA
				STA	(DESTINL),Y
				INY
				CPY	#$21
				BNE	MLOOP2
				LDA	LINENUM
				INC	LINENUM
				CMP	#$2A
				BNE	MLOOP1
				RTS

CLEAR_BOX		LDX	#$01
:LOOP1			LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	#$40
				STA	SCREENH
				LDY	#$08
				LDA	#$00
:LOOP2			STA	(SCREENL),Y
				INY
				CPY	#$20
				BNE	:LOOP2
				INX
				CPX	#$27
				BNE	:LOOP1
				RTS

FLIP			LDA	#$00
				STA	LINENUM
:LOOP1			LDX	LINENUM
				LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	#$40
				STA	SCREENH
				CLC
				PHP
				LDA	#$00
				STA	COLUMN
:LOOP2			LDY	COLUMN
				LDA	(SCREENL),Y
				TAY
				PLP
				ROL	A
				ASL	A
				PHP
				LDX	#$00
				STX	SCRATCH
				LDX	#$07
:ROTLOOP		ASL	A
				ROR	SCRATCH
				DEX
				BPL	:ROTLOOP
				TYA
				AND	#$80
				ORA	SCRATCH
				TAX
				LDA	#$27
				SEC
				SBC	COLUMN
				TAY
				TXA
				STA	(SCREENL),Y
				INC	COLUMN
				CPY	#$14
				BNE	:LOOP2
				PLP
				LDA	LINENUM
				INC	LINENUM
				CMP	#$BF
				BNE	:LOOP1
				RTS
