EXAMINE			JSR	CLEAR1
				LDA	#$20
				STA	PAGE
				LDA	#GREEN
				STA	COLOR
				LDY	#$06	
				JSR	LINER
				LDY	#$17
				JSR	TEXTER2

				JSR	SETVTAB
				JSR	ESCAN_FULSLOT
				LDX	#$00
				STX	COUNT
:LOOP1			LDA	EQUIP_LIST,X
				BNE	:SKIPB
				LDA	LINTABL,X
				STA	YPNT
				LDY	CORESPS,X
				JSR	ENERUSE
				JSR	PARSTAT
				LDA	#165
				LDY	YPNT
				JSR	LINE
				LDA	#$0A
				JSR	CUTUP
				LDA	#196
				LDY	YPNT
				JSR	LINE
:SKIPB			INC	COUNT
				LDX	COUNT
				CPX	#15
				BNE	:LOOP1

				JSR	STAT12A
				BIT	UNSTROB
				LDA	#$40
				STA	PAGE
				LDX	#<:EXAM_DEF
				LDY	#>:EXAM_DEF
				JSR	INIT_CURSOR
				JSR	GETKEY
				JSR	STAT21A
				LDA	#$03
				STA	CURSOR
				JSR	IMARKER
				LDA	#$02	
				JMP	RDOPTN

*-------------------------------
:EXAM_DEF		DB	$00
				DB	$80
				DW	:EXAM_CURS

:EXAM_CURS		LDX	#$C8
				LDY	#$5F
				JMP	ARROWER
*-------------------------------


RSTUFER			LDY	#e1_HEAD
				JSR	BANK_2
				LDA	(CHARDL),Y
				AND	#%00000111
				STA	RHEADS+25
				LDA	#$09
				STA	RHEADS+2
				STA	HEADMOD+2
				JSR	BANK_1
				LDY	#e1_HEAD
				LDA	(CHARDL),Y
				BPL	NOWITH
				LDY	#$20
				JSR	TEXTER2
				JSR	BANK_2
				LDA	#$00
				STA	RHEADS+2
				STA	HEADMOD+2
				JSR	BANK_1
NOWITH			LDY	#$21
				JSR	TEXTER2
				LDY	#e1_HEAD
				LDA	(CHARDL),Y
				AND	#%00011000
				LSR
				LSR
				LSR
				ADC	#$23			;Carry assumed clear
				TAY
				JSR	TEXTER2
				LDA	#$09
				LDY	#$39
				JSR	EMNTSUB
				LDA	#$71
				LDY	#$4D
				JSR	EMNTSUB
				LDA	#$89
				LDY	#$6D
				JSR	EMNTSUB
				LDY	#$27
				JSR	TEXTER2
				LDA	#$2A
				STA	YPNT
				LDA	#$3D
:LOOP1			STA	COUNT
				TAY
				LDA	YPNT
				JSR	EMNTSUB
				LDA	YPNT
				CLC
				ADC	#$08
				STA	YPNT
				LDA	COUNT
				ADC	#$04
				CMP	#$4D
				BNE	:LOOP1
				RTS

RINFOUT			LDY	#$28
				JSR	TEXTER2
				LDA	#$04
				STA	COUNT
				LDY	#$00
				LDX	#$00
				JSR	NAMEIT
				JSR	DRAWITC
				LDY	#$0D
				LDA	(CHARDL),Y
				JSR	CPROF
				JSR	DRAWITC
				LDY	#$6E
				LDA	(CHARDL),Y
				AND	#$08
				BEQ	DOLEVL	
				JSR	DRAWC2
				JMP	NOANEED
DOLEVL			JSR	CLEVEL
				JSR	DRAWITC
NCLSKIP			LDY	#$6E
				LDA	(CHARDL),Y
				BPL	NOANEED
				LDA	#$0B
				STA	TEXTBUF
				LDA	#$8D
				STA	TEXTBUF+1
				JSR	LINE2
NOANEED			LDY	#$2E
				LDA	(CHARDL),Y
				STA	SCRATCH
				LDA	#$00
				STA	COUNTER
FCLOOP			JSR	POINTIT
				LDY	#$2D
				LDA	(CHARDL),Y
				CMP	SCRATCH
				BEQ	GOTACYB
				INC	COUNTER
				LDA	COUNTER
				BNE	FCLOOP
GOTACYB			LDY	#$00
				LDX	#$00
				JSR	NAMEIT
				JSR	DRAWITC
				LDA	LEFT1
				JMP	POINTIT

RSOURCE			DB	$05,$00,$01
				DB	$02,$02,$02
				DB	$02,$02,$03
				DB	$04,$03,$04
				DB	$05,$05,$06
RCODES			DB	$00,$01,$02
				DB	$03,$04,$05
				DB	$06,$01,$07
				DB	$05,$08,$0B
				DB	$06,$00,$09
				DB	$07,$0C,$0C
				DB	$00,$03,$0A
				DB	$0B,$02,$05
				DB	$06,$00,$09
				DB	$05,$08,$0B
				DB	$0C,$0C,$0C
				DB	$0C,$0C,$0C
				DB	$02,$03,$04
				DB	$0C,$0C,$0C
RCHECKS			DW	REB1			;0-FIRST EMPTY BACK
				DW	REC1			;1-FIRST EMPTY CARRY
				DW	RFHCB			;2-FULL HAND (B) WITH CRITERIA
				DW	RFBC			;3-FULL BACK WITH CRITERIA
				DW	RFCC			;4-FULL CARRY WITH CRITERIA
				DW	REHB			;5-EMPTY HAND (B)
				DW	STS				;6-SAME TYPE AS SOURCE
				DW	RAFC			;7-ANY FULL CARRY
				DW	RAFH			;8-ANY FULL HAND (B)
				DW	RAFB			;9-ANY FULL BACK
				DW	RFHCA			;A-FULL HAND (A) WITH CRITERIA
				DW	REHA			;B-EMPTY HAND (A)
				DW	DOP				;C-DUMMY OPERATION
REB1			LDA	#$02
				STA	MAXBACK
				JMP	EB1
REC1			JSR	CHECK
				BEQ	RESKIP2
				LDA	MAXCARY
				STA	MAXBACK
				INC	MAXBACK
				LDX	#$03
				JMP	ELOOP1
RFHCB			LDY	MCURSOR
				JSR	TOCRITE
				LDA	#$09
				STA	COUNT
				JSR	CRSUB
				LDA	#$0B
				STA	COUNT
				JMP	CRSUB
RFBC			LDY	MCURSOR
				JSR	TOCRITE
				LDA	#$02
				STA	COUNT
				JMP	CRSUB
RFCC			JSR	CHECK
				BEQ	RESKIP2
				LDY	MCURSOR
				JSR	TOCRITE
				LDA	#$03
				STA	COUNT
FCLOOP1			JSR	CRSUB
				LDA	COUNT
				INC	COUNT
				CMP	#$07
				BNE	FCLOOP1
				RTS
REHB			LDX	#$00
				LDY	#e4_ROBLWITH
				LDA	(CHARDL),Y
				CMP	#$FF
				BNE	RESKIP1
				STX	USABLE2+9
RESKIP1			LDY	#e6_ROBRWITH
				LDA	(CHARDL),Y
				CMP	#$FF
				BNE	RESKIP2
				STX	USABLE2+11
RESKIP2			RTS
RAFC			JSR	CHECK
				BEQ	RESKIP2
				LDA	MAXCARY
				STA	MAXBACK
				INC	MAXBACK
				LDX	#$03
				JMP	AFLOOP1
RAFH			LDX	#$00
				LDY	#$41
				LDA	(CHARDL),Y
				CMP	#$FF
				BEQ	RASKIP1
				STX	USABLE2+9
RASKIP1			LDY	#$49
				LDA	(CHARDL),Y
				CMP	#$FF
				BEQ	RASKIP2
				STX	USABLE2+11
RASKIP2			RTS
RAFB			LDA	#$02
				STA	MAXBACK
				JMP	AFB
RFHCA			LDY	MCURSOR
				JSR	TOCRITE
				LDA	#$08
				STA	COUNT
				JSR	CRSUB
				LDA	#$0A
				STA	COUNT
				JMP	CRSUB
REHA			LDY	MCURSOR
				JSR	TOCRITE
				LDA	SCRATCH
				CMP	#$05
				BNE	RESKIP4
				LDX	#$00
				LDY	#$3D
				LDA	(CHARDL),Y
				CMP	#$FF
				BNE	RESKIP3
				STX	USABLE2+8
RESKIP3			LDY	#$45
				LDA	(CHARDL),Y
				CMP	#$FF
				BNE	RESKIP4
				STX	USABLE2+10
RESKIP4			RTS
