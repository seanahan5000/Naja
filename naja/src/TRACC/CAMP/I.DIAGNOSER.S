MAIN_MAN		=	LEFHOLD
AUX_MAN			=	RITHOLD

SLINUM			=	$00
DLINUM			=	$01
SCOLUMN			=	$02
DCOLUMN			=	$03
HENDLINE		=	$11
HPOINT			=	$15
COLHOLD			=	$17
PULSE			=	$19
BREATH			=	$1A
PDELAY			=	$1B
RDELAY			=	$1C
GDELAY			=	$1D

DIAGNOSER		STA	DEATH_FLAG
				JSR	CLEAR1
				LDA	#$20
				STA	PAGE
				LDA	#$03
				STA	COUNT
:LOOP1			LDY	COUNT
				JSR	PICKER
				DEC	COUNT
				BPL	:LOOP1
				LDA	#$04
				STA	COUNT
:LOOP2			LDY	COUNT
				LDA	COLORS,Y
				STA	COLOR
				JSR	HLINER
				DEC	COUNT
				BPL	:LOOP2
				LDY	#$00
				JSR	HTEXTER
				JSR	STATTER
				LDA	AUX_MAN
				JSR	POINTIT
				STA	PRIMARY
				JSR	HEARTER
				JSR	INITBOX
				LDA	#$40	
				STA	RDELAY
				LDA	#$08	
				STA	GDELAY
				JSR	BVALUER
				LDA	#$00
				STA	PULSE
				STA	BREATH
				JSR	BEAT1
				LDA	AUX_MAN
				JSR	POINTIT
				BIT	DEATH_FLAG
				BPL	:SKIPB
				JMP	SHOW_DEATH
:SKIPB			LDY	#status
				LDA	(CHARDL),Y
				AND	#%00111111
				BNE	NOT_NORMAL
				SEC
				JMP	GENERAL_HEAL

NOT_NORMAL		CMP	#dead
				BNE	NOT_DEAD
				LDY	#$13
				JSR	HTEXTER
				JMP	HEAL_OUT

NOT_DEAD		LDY	#$09
				JSR	HTEXTER
				LDA	#$41			;Medical equipment
				STA	ETABLE+0
				LDA	#$00
				STA	ETABLE+2
				STA	ETABLE+3
				LDA	STATHOLD
				LSR
				TAY
				LDA	TREATMENTS-4,Y
				STA	ETABLE+1
				LDA	TREATMENTS-3,Y
				PHA
				TAY
				JSR	HTEXTER
				PLA
				CMP	#$0D
				BNE	:SKIPA
				JSR	EQUIP1
				LDA	#$2D
				STA	TEXTBUF,X
				LDA	#$8D
				STA	TEXTBUF+1,X
				LDA	#146
				LDY	#27
				JSR	LINE
:SKIPA			JSR	SEARCH_FOR
				LDA	#$10
				BCC	:SKIPB
				CLC
				JSR	ENERGY_FOR
				LDA	#$0F
				BCS	CAN_TREAT
:SKIPB			PHA
				LDY	#$0E
				JSR	HTEXTER
				PLA
				TAY
				JSR	HTEXTER
				LDY	#$11
				JSR	HTEXTER
				LDY	#$08
				JSR	HTEXTER
				JSR	MULTI_KEY
				JMP	HEAL_OTHER

CAN_TREAT		LDY	#$12
				JSR	HTEXTER
				LDA	#$C3
				STA	KEYMOD1+1
				JSR	MULTI_KEY
				LDX	#$00
				STX	KEYMOD1+1
				CMP	#$C3
				BEQ	HEAL_OTHER
				LDA	MAIN_MAN
				JSR	POINTIT
				LDX	FROM_WHERE
				JSR	REMOVE_ITEM
				SEC
				JSR	ENERGY_FOR
				LDA	AUX_MAN
				JSR	POINTIT
				LDY	#status
				LDA	(CHARDL),Y
				AND	#%11000000
				STA	(CHARDL),Y
				JSR	CALC_ALL
				JSR	BVALUER
				JSR	MULTI_WAIT
HEAL_OTHER		CLC
GENERAL_HEAL	ADC	#$03
				STA	TEMP
				LDY	#damagelev
				LDA	(CHARDL),Y
				INY
				ORA	(CHARDL),Y
				STA	DFLAG
				BNE	:SKIPB
				LDY	#status
				LDA	(CHARDL),Y
				BMI	:SKIPB
:SKIPA			LDY	#$01
				JSR	HTEXTER
				JMP	HEAL_OUT
:SKIPB			LDY	#status
				LDA	(CHARDL),Y
				LDY	#$02
				AND	#critical
				BNE	:SKIPD
:SKIPC			LDY	TEMP
:SKIPD			JSR	HTEXTER
				LDY	#$05
				JSR	HTEXTER
				LDA	#36
				STA	HTEXT7+2
				LDA	DFLAG
				BEQ	:SKIPE
				LDY	#$06
				JSR	HTEXTER
				LDA	#45
				STA	HTEXT7+2
				LDY	#status
				LDA	(CHARDL),Y
				BPL	HEAL_OUT
:SKIPE			LDY	#$07
				JSR	HTEXTER
HEAL_OUT		LDY	#$08
				JSR	HTEXTER
				JSR	MULTI_KEY
HEAL_EXIT		LDA	MAIN_MAN
				STA	CURSOR
				JSR	POINTIT
				JMP	DOSTATS

SHOW_DEATH		JSR	MULTI_WAIT
				LDY	#$14
				JSR	HTEXTER
				LDY	#$08
				JSR	HTEXTER
				JSR	MULTI_KEY
***** SLOWING HEART UNTIL IT STOPS
				LDA	AUX_MAN
				JSR	POINTIT
				LDY	#status
				LDA	#dead
				STA	(CHARDL),Y
				JSR	BVALUER
				JSR	MULTI_WAIT
				LDY	#$15
				JSR	:SUBA
				JSR	BREAK_ROBOT
				BCC	:SKIPA
				LDX	#<HNAME3
				LDY	#>HNAME3
				JSR	FLUSH_NAME
				LDY	#$16
				JSR	:SUBA
:SKIPA			JMP	HEAL_EXIT

:SUBA			JSR	HTEXTER
				LDY	#$08
				JSR	HTEXTER
				JSR	SPACE_KEY
				JMP	CLRBOX

DEATH_FLAG		DB	0
DFLAG			DB	0

SEARCH_FOR		LDA	ETABLE+1
				AND	#$0F
				CMP	#$09
				BNE	:SUBA
				LDA	ETABLE+1
				PHA
				LDA	#$08			;Drug Infusor
				STA	ETABLE+1
				JSR	:SUBA
				PLA
				STA	ETABLE+1
				BCC	:EXIT
				JSR	:SUBA
				STY	FROM_WHERE
:EXIT			RTS
:SUBA			JSR	RESET_EQLIST
				LDX	#<MAIN_SEARCH
				LDY	#>MAIN_SEARCH
				JMP	ESCAN_COMMON

FROM_WHERE		DB	0

MAIN_SEARCH		LDA	(CHARDL),Y
				CMP	#$41
				BNE	:EXIT
				INY
				LDA	(CHARDL),Y
				CMP	ETABLE+1
:EXIT			RTS

ENERGY_FOR		PHP
				LDA	MAIN_MAN
				JSR	POINTIT
				JSR	ENERUSE
				PLP
				LDX	#<ENERGY_USAGE
				LDY	#>ENERGY_USAGE
				JMP	DRAINER_MAIN

MULTI			DEC	PDELAY
				BNE	:SKIPA
				JSR	BEAT1
:SKIPA			DEC	GDELAY
				BNE	:SKIPB
				JSR	GRAPH1
:SKIPB			DEC	RDELAY
				BNE	:SKIPC
				JSR	BREATH1
:SKIPC								;LDA OVERALL_SPEED
				LDA	#$1B			;TEMP****
				NOP					;TEMP****
				JMP	WAIT

OVERALL_SPEED	DB	0

MULTI_WAIT		JSR	MULTI
				LDX	#2
:LOOP1			LDA	NORMLEV,X
				CMP	YCURSOR,X
				BNE	MULTI_WAIT
				DEX
				BPL	:LOOP1
				RTS

MULTI_KEY		JSR	MULTI
				LDA	NORMLEV+1
				EOR	WIGGLE_FLAG
				STA	NORMLEV+1
				LDA	KEYBRD
				BPL	MULTI_KEY
				BIT	UNSTROB
				JSR	CHECK_ENTER
				BEQ	CLRBOX
KEYMOD1			CMP	#$00
				BNE	MULTI_KEY
CLRBOX			PHA
				LDX	#$01
:LOOP1			JSR	SUBS2
				LDY	#$14
				LDA	#$00
:LOOP2			STA	(SCREENL),Y
				INY
				CPY	#$27
				BNE	:LOOP2
				LDA	#$40
				STA	(SCREENL),Y
				INX
				CPX	#$4A
				BNE	:LOOP1
				PLA
				RTS

WIGGLE_FLAG		DB	$00

BEAT1			LDY	#status
				LDA	(CHARDL),Y
				CMP	#dead
				BEQ	CMOUT2
				LDA	#$00
				STA	HPOINT
				STA	ORNFLAG
				STA	COLHOLD
				LDA	#$03
				STA	UDLRMOD+1
				LDY	#profrace
				LDA	(CHARDL),Y
				AND	#$0F
				CMP	#stilicx
				BNE	NOSTIL
				LDX	#$20
				STX	HPOINT
NOSTIL			CMP	#orn
				BNE	NOORN
				DEC	ORNFLAG
NOORN			CMP	#xxtys
				BNE	NOXXYT
				SEC	
				ROR	COLHOLD
NOXXYT			TAX
				BEQ	UDSUB
				DEX
				BEQ	TANDEM
				DEX
				BEQ	UDLRSUB
				DEX	
				DEX
				BEQ	UDLRSUB
LRSUB			LDY	PULSE
				LDA	LRBEATS,Y
				BPL	LRUDCOM
UDSUB			LDY	PULSE
				LDA	UDBEATS,Y
				BPL	LRUDCOM
UDLRSUB			LDA	#$07
				STA	UDLRMOD+1
				BNE	UDSUB
TANDEM			LDY	PULSE
				LDA	UDBEATS,Y
				ORA	#$80
				JSR	HDRAW
				LDY	PULSE
				LDA	LRBEATS,Y
LRUDCOM			JSR	HDRAW
				LDA	PULSE
				INC	PULSE
UDLRMOD			CMP	#$03
				BNE	COMOUT	
				LDA	#$00
				STA	PULSE
COMOUT			LDY	PULSE	
				LDA	PRATES,Y
				STA	PDELAY
CMOUT2			RTS

ORNFLAG			DB	$00

PRATES			DB	$80,$80,$40
				DB	$40
				DB	$80,$80,$40
				DB	$40
UDBEATS			DB	$00,$01,$02
				DB	$01
LRBEATS			DB	$00,$03,$04
				DB	$03

HDRAW			STA	TEMP
				LDA	HPOINT
				STA	SLINUM
				CLC
				ADC	#$1A
				STA	SELMOD+1
				LDA	TEMP
				AND	#$7F
				ASL
				ASL
				STA	SCRATCH
				LSR
				CLC
				ADC	SCRATCH
				STA	SBCMOD+1
				CLC
				ADC	#$05
				STA	SECMOD+1
				LDA	#$64
				STA	DLINUM
				LDA	#$1E
				STA	DBCMOD+1
				LDA	ORNFLAG
				BEQ	DHLOOP1
				LDX	#$20
				LDY	#$74
				BIT	TEMP
				BPL	ORNCOM
				LDX	#$1B
				LDY	#$55
ORNCOM			STX	DBCMOD+1
				STY	DLINUM
DHLOOP1			LDX	SLINUM
				JSR	SUBS4
				LDX	DLINUM
				JSR	SUBD2
SBCMOD			LDA	#$FF
				STA	SCOLUMN
DBCMOD			LDA	#$FF
				STA	DCOLUMN
DHLOOP2			LDY	SCOLUMN
				LDA	(SCREENL),Y
				EOR	COLHOLD
				LDY	DCOLUMN
				STA	(DESTINL),Y
				INC	DCOLUMN
				LDA	SCOLUMN
				INC	SCOLUMN
SECMOD			CMP	#$FF
				BNE	DHLOOP2
				INC	DLINUM
				LDA	SLINUM
				INC	SLINUM
SELMOD			CMP	#$FF
				BNE	DHLOOP1
				RTS

HEARTER			LDA	#$40
				STA	PAGE
				JSR	CLEAR2
				LDY	#$04
				JSR	PICKER
				LDY	#$05
				JSR	PICKER
				LDA	#$00
				JSR	HEART1
				LDA	#$20
HEART1			STA	LINENUM
				CLC
				ADC	#$0D
				STA	HENDLINE
:LOOP1			LDX	LINENUM
				JSR	SUBS4
				LDA	LINENUM
				AND	#$DF
				STA	SCRATCH		
				LDA	HENDLINE
				CLC
				ADC	#$0D
				SEC
				SBC	SCRATCH			
				TAX
				LDA	#$40
				JSR	SUBD24	
				LDY	#$1D
:LOOP2			LDA	(SCREENL),Y
				STA	(DESTINL),Y
				DEY
				BPL	:LOOP2
				LDA	LINENUM
				INC	LINENUM
				CMP	HENDLINE
				BNE	:LOOP1
				LDA	#$20
				STA	PAGE
				RTS

BREATH1			LDY	#status
				LDA	(CHARDL),Y
				CMP	#dead
				BEQ	:EXIT
				LDX	BREATH
				LDA	BRETHX,X
				STA	XPNT
				LDA	#$AC
				STA	YPNT
				LDX	#<BLUBOX
				LDY	#>BLUBOX
				LDA	#0
				JSR	DRAW_SHAPES
				LDX	BREATH
				LDA	BRATES,X
				STA	RDELAY
				INX
				CPX	#$0E	
				BNE	:SKIPA
				LDX	#$00
:SKIPA			STX	BREATH
:EXIT			RTS

BRATES			DB	$40,$40,$40
				DB	$40,$40,$40
				DB	$D0,$38,$38
				DB	$38,$38,$38
				DB	$38,$FF
BRETHX			DB	$C2,$CC,$D6
				DB	$E0,$EA,$F4
				DB	$FE,$FE,$F4
				DB	$EA,$E0,$D6
				DB	$CC,$C2

INITBOX			JSR	DUPCATE
				LDA	#$40
				STA	PAGE
				LDA	#VIOLET
				STA	COLOR
				LDY	#$05
				JSR	HLINER
				LDX	#$02
:LOOP1			LDA	#$99
				STA	YCURSOR,X
				LDA	#$FF
				STA	TCURSOR,X
				DEX
				BPL	:LOOP1
				LDA	#$02
				STA	CURSOR
:LOOP2			JSR	WORDSUB
				DEC	CURSOR
				BPL	:LOOP2
				JSR	SWAPEM
				LDA	#$20
				STA	PAGE
				RTS

BLUBOX			DB	$55,$41,$41
				DB	$41,$41,$41
				DB	$55

DUPCATE			LDX	#$52
DULOOP1			JSR	LOHISUB
				LDY	#$03
DULOOP2			LDA	(SCREENL),Y
				STA	(DESTINL),Y
				INY
				CPY	#$18
				BNE	DULOOP2
				INX
				CPX	#$99
				BNE	DULOOP1
				RTS

SWAPEM			LDX	#$99
SWLOOP1			JSR	LOHISUB
				LDY	#$01
SWLOOP2			LDA	(SCREENL),Y
				PHA
				LDA	(DESTINL),Y
				STA	(SCREENL),Y
				PLA
				STA	(DESTINL),Y
				INY
				CPY	#$19
				BNE	SWLOOP2
				INX
				CPX	#$AE
				BNE	SWLOOP1
				RTS

LOHISUB			JSR	SUBS2	
				EOR	#$60
				STA	DESTINH
				LDA	SCREENL
				STA	DESTINL
				RTS

GRAPH1			LDA	#$00
				STA	CURSOR
GRLOOP1			LDX	CURSOR
				LDA	NORMLEV,X
				CMP	YCURSOR,X
				BEQ	GRSKIP1
				BCS	GRSKIP2
				JSR	UP1
				JMP	GRSKIP1
GRSKIP2			JSR	DOWN1	
GRSKIP1			INC	CURSOR
				LDA	CURSOR
				CMP	#$03
				BEQ	GROUTA	
				JSR	RNDMIZE
				LDA	RANDOM1
				AND	#$03
				BEQ	GRSKIP1
				BNE	GRLOOP1
GROUTA			LDA	#$08	
				STA	GDELAY
GROUT			RTS

UP1				LDY	CURSOR
				LDA	YCURSOR,Y
				CMP	#$4F
				BEQ	GROUT
				TAX
				CLC
				ADC	#$15	
				STA	HENDLINE
				LDA	#$CA			;DEX OPCODE
				BNE	UDSUB0
DOWN1			LDY	CURSOR
				LDA	YCURSOR,Y
				CMP	#$99
				BEQ	GROUT	
				STA	HENDLINE
				DEC	HENDLINE
				CLC
				ADC	#$14
				TAX
				LDA	#$E8			;INX OPCODE
UDSUB0			STA	UPDOWN1
				EOR	#$22
				STA	UPDOWN2
				STA	UPDOWN3
				AND	#$F0
				EOR	#$20
				ORA	#$1E	
				STA	UPDOWN4
				LDA	BEGINS,Y
				STA	SCOLUMN
				LDA	ENDS,Y
				STA	DCOLUMN
UDLOOP0			JSR	SUBS2
UPDOWN1			DEX
				JSR	SUBD2
				JSR	UDSUB1
UPDOWN2			INX
UPDOWN3			INX
				CPX	HENDLINE
				BNE	UDLOOP0
				LDA	SCREENL
				STA	DESTINL
				LDA	SCREENH
				STA	DESTINH
				EOR	#$60
				STA	SCREENH
				JSR	UDSUB1
				LDX	CURSOR
UPDOWN4			DEC	YCURSOR,X
WORDSUB			LDY	CURSOR
				LDA	YCURSOR,Y
				LDX	#$00
FILOOP1			CMP	LEVEL,X
				BCS	FOUND
				INX
				BNE	FILOOP1
FOUND			TXA
				CLC
				ADC	TIMES5,Y
				CMP	TCURSOR,Y
				BEQ	NOCHANG
				PHA
				LDX	TCURSOR,Y
				BMI	WORD1	
				LDA	YCURSOR,Y
				JSR	BWORDER
WORD1			LDY	CURSOR
				PLA
				STA	TCURSOR,Y
				TAX
				LDA	YCURSOR,Y
BWORDER			STA	YPNT
				TXA
				ASL
				TAY
				LDA	BWORDS,Y
				STA	POINTL
				LDA	BWORDS+1,Y
				STA	POINTH
				LDY	#$0B
				CPX	#$05
				BEQ	NOT1
				CPX	#$06
				BEQ	NOT1
				DEY
				CPX	#$08
				BEQ	NOT1
				LDA	YPNT
				CLC
				ADC	#$07
				BNE	NOTIS1
NOT1			LDA	YPNT
				CLC
				ADC	#$0B
				STA	(POINTL),Y
				SEC
				SBC	#$08
NOTIS1			LDY	#$02
				STA	(POINTL),Y
				JMP	TEXTOUT
NOCHANG			RTS

UDSUB1			LDY	SCOLUMN
UDLOOP1			LDA	(SCREENL),Y
				STA	(DESTINL),Y
				INY
				CPY	DCOLUMN
				BNE	UDLOOP1
				RTS

BVALUER			LDA	AUX_MAN
				JSR	POINTIT
				LDA	#$00
				STA	WIGGLE_FLAG
				LDY	#$0A
				LDA	(CHARDL),Y
				TAX
				AND	#%00111111
				BNE	:SKIPA
				TXA
				BEQ	:SKIPA
				AND	#critical
				BNE	:SKIPA
				TXA
				AND	#rundown
:SKIPA			LDX	#$00
:LOOP1			CMP	STARTS,X
				BEQ	:SKIPB
				INX
				INX
				INX
				INX
				BNE	:LOOP1			;Always
:SKIPB			STX	STATHOLD
				LDY	#$00
:LOOP2			INX
				LDA	STARTS,X
				STA	NORMLEV,Y
				INY
				CPY	#$03
				BNE	:LOOP2
				LDA	#$00
				STA	COUNT
				LDA	NORMLEV+2
				BEQ	ISDEAD
				SEC
				SED
				LDA	(CHARDL),Y
				LDY	#damagelev+1
				SBC	(CHARDL),Y
				STA	SCRATCH
				INY
				LDA	(CHARDL),Y
				LDY	#damagelev
				SBC	(CHARDL),Y
				CLD
				BNE	NOTDEAD
				LDA	SCRATCH
				BEQ	ISDEAD
				INC	COUNT
				CMP	#$11
				BCC	ISDEAD
NOTDEAD			LDX	#2
				STX	COUNT
:LOOP3			LDA	PCENTS-2,X
				PHA
				LDY	#damagemax+1
				LDA	(CHARDL),Y
				TAX
				DEY
				LDA	(CHARDL),Y
				TAY
				PLA
				JSR	LESS_PERCENT
				STX	TEMP
				STY	TEMP+1
				LDY	#damagelev+1
				SEC
				SED
				LDA	(CHARDL),Y
				SBC	TEMP
				DEY
				LDA	(CHARDL),Y
				SBC	TEMP+1
				CLD
				BCS	ISDEAD
				INC	COUNT
				LDX	COUNT
				CPX	#4
				BNE	:LOOP3
ISDEAD			LDA	COUNT
				CMP	NORMLEV+2
				BCS	:SKIPC
				STA	NORMLEV+2
:SKIPC			LDX	NORMLEV+2
				LDA	NLEVEL,X
				STA	NORMLEV+2
				CMP	#$99
				BEQ	:EXIT
				INC	WIGGLE_FLAG
:EXIT			LDX	NORMLEV+0
				LDA	SPEEDS,X
				STA	OVERALL_SPEED
				RTS

STATHOLD		DB	0

SPEEDS			DB	1,2,3,4,5		;1,3,6,9,12
PCENTS			DB	$25,$75
NLEVEL			DB	$99,$87,$74
				DB	$61,$4F
*GOOD<=100%
*FAIR<=75%
*POOR<=25%
*CRIT<=5
*DEAD=0
NORMLEV			DB	$00,$00,$00

STARTS			DB	healthy,$74,$74,$04		;NORM.NORM.GOOD
				DB	dead,$99,$99,$00					;COLD.BDED.DEAD
				DB	poisoned,$61,$74,$03	;WARM.NORM.FAIR
				DB	bleeding,$87,$87,$02	;COOL.UNDR.POOR
				DB	radiated,$4F,$74,$03	;???????????????????
				DB	illA,$4F,$74,$03					;FEVR.NORM.FAIR
				DB	illB,$4F,$74,$03					;???????????????????
				DB	illC,$4F,$74,$03					;???????????????????
				DB	illD,$4F,$74,$03					;???????????????????
				DB	blind,$74,$74,$03				;NORM.NORM.FAIR
				DB	confused,$4F,$4F,$02	;FEVR.OVER.POOR
				DB	critical,$87,$87,$01	;COOL.UNDR.CRIT
				DB	rundown,$87,$74,$04		;COOL.NORM.GOOD

TREATMENTS		DB	$09,$0D			;poisoned
				DB	$04,$0A			;bleeding
				DB	$59,$0D			;radiated
				DB	$19,$0D			;illA (fungus)
				DB	$29,$0D			;illB (parasite)
				DB	$49,$0D			;illC (bacteria)
				DB	$39,$0D			;illD (virus)
				DB	$06,$0B			;blind
				DB	$07,$0C			;confused

LEVEL			DB	$91,$7E,$6B
				DB	$58,$4F
BEGINS			DB	$01,$09,$11
ENDS			DB	$08,$11,$19
TIMES5			DB	$00,$05,$0A
YCURSOR			DB	$00,$00,$00
TCURSOR			DB	$00,$00,$00

BWORDS			DA	COLD
				DA	COOL
				DA	NORMAL1
				DA	WARM
				DA	FEVER
				DA	BRAIN
				DA	UNDER
				DA	NORMAL2
				DA	OVER
				DA	SEIZURE
				DA	DEAD
				DA	CRITICL
				DA	POOR
				DA	FAIR
				DA	GOOD

STATTER			LDA	#$02
				STA	YPNT
				LDA	AUX_MAN
				JSR	POINTIT
				LDX	#<TEXTBUF
				LDY	#>TEXTBUF
				JSR	FLUSH_NAME
				LDA	#$8D
				STA	TEXTBUF+10
				LDX	#9
:LOOP0			LDA	TEXTBUF,X
				STA	HTEXT1+3,X
				STA	HTEXT2+3,X
				STA	HTEXT3+3,X
				STA	HTEXT4+3,X
				STA	HTEXT9+3,X
				STA	HTEXT3+3,X
				STA	HNAME1+3,X
				STA	HTEXT13+3,X
				STA	HTEXT15+3,X
				STA	HNAME2,X
				DEX
				BPL	:LOOP0
				JSR	DRAWLIN
				LDY	#profrace
				LDA	(CHARDL),Y
				PHA
				LSR
				LSR
				LSR
				LSR
				TAX
				LDA	PROFOFF,X
				TAX
				LDY	#$00
:LOOP1			LDA	PROFDAT,X
				STA	TEXTBUF,Y
				INX
				INY
				CMP	#$8D
				BNE	:LOOP1
				JSR	DRAWLIN
				PLA
				AND	#$0F
				TAX
				LDA	RACEOFF,X
				TAX
				LDY	#$00
:LOOP2			LDA	RACEDAT,X
				STA	TEXTBUF,Y
				INX
				INY
				CMP	#$8D
				BNE	:LOOP2
				JSR	DRAWLIN
				JSR	DAMAGER
				JSR	DESPACER
				JSR	DRAWLIN
				JSR	SUBENER	
				JSR	DUAL_STATUS
				LDA	MAIN_MAN
				JSR	POINTIT
				LDX	#<TEXTBUF
				LDY	#>TEXTBUF
				JSR	FLUSH_NAME
				LDA	#$8D
				STA	TEXTBUF+10
				LDX	#9
:LOOP3			LDA	TEXTBUF,X
				STA	HTEXTE+3,X
				DEX
				BPL	:LOOP3
				JSR	DRAWLIN
SUBENER			JSR	POWROUT
				JSR	DESPACER
DRAWLIN			LDA	#$3B
				LDY	YPNT
				JSR	LINE
				LDA	YPNT
				CLC
				ADC	#$08
				STA	YPNT
				RTS

REDUAL_STATUS	LDA	#42
				STA	YPNT
DUAL_STATUS		JSR	STATUSR
				JSR	DRAWLIN
				LDA	#$8D
				STA	TEXTBUF
				LDY	#status
				LDA	(CHARDL),Y
				STA	TEMP
				BEQ	:EXIT
				CMP	#dead
				BEQ	:EXIT
				AND	#%00111111
				BEQ	:SKIPA
				BIT	TEMP
				BVS	:SKIPC
				BPL	:EXIT
				BMI	:SKIPB			;Always
:SKIPA			BIT	TEMP
				BVC	:SKIPC
				BPL	:EXIT
:SKIPB			LDA	#rundown
:SKIPC			JSR	STATUSR2
:EXIT			JMP	DRAWLIN

DESPACER		LDX	#$00
DAMLOOP			LDA	TEXTBUF,X
				CMP	#$0A
				BNE	NODAM
				LDA	#$EE
				STA	TEXTBUF,X
				INX
				BNE	DAMLOOP
NODAM			RTS

COLORS			DB	GREEN,WHITE2,BLACK2
				DB	WHITE1,BLACK1

SUBS2			LDA	#$20
				BNE	SUBS24
SUBS4			LDA	#$40
SUBS24			PHA
				LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				STA	SCREENH
				PLA
				ORA	SCREENH
				STA	SCREENH
				RTS

SUBD2			LDA	#$20
SUBD24			PHA
				LDA	LOBYTES,X
				STA	DESTINL
				LDA	HIBYTES,X
				STA	DESTINH
				PLA
				ORA	DESTINH
				STA	DESTINH
				RTS

HTEXTER			TYA
				ASL
				TAY
				LDA	HTEXTS,Y
				STA	POINTL
				LDA	HTEXTS+1,Y
				STA	POINTH
				JMP	TEXTOUT
HTEXTS			DW	HTEXT0			;LABLES
				DW	HTEXT1			;<NAME> IS IN PERFECT...
				DW	HTEXT2			;<NAME> IS IN CRITICAL...
				DW	HTEXT3			;<NAME> ALSO NEEDS
				DW	HTEXT4			;<NAME> ONLY NEEDS
				DW	HTEXT5			;THE FOLLOWING:
				DW	HTEXT6			;GENERAL HEALING
				DW	HTEXT7			;ENERGY TRANSFER
				DW	HTEXT8			;<SPACE>
				DW	HTEXT9			;<NAME> NEEDS
				DW	HTEXTA			;CAUTERIZING
				DW	HTEXTB			;SIGHT RESTORING
				DW	HTEXTC			;BRAIN EQUALIZING
				DW	HTEXTD			;AN INFUSION OF
				DW	HTEXTE			;<NAME> DOES NOT HAVE
				DW	HTEXTF			;ENOUGH ENERGY
				DW	HTEXT10			;THE EQUIPMENT
				DW	HTEXT11			;TO GIVE TREATMENT
				DW	HTEXT12			;PRESS <SPACE> TO...
				DW	HTEXT13			;<NAME> IS DEAD...
				DW	HTEXT14			;THERE IS NOTHING THAT...
				DW	HTEXT15			;<NAME> HAS DIED
				DW	HTEXT16			;BECAUSE OF THE DEATH...

HLINER			TYA
				ASL
				TAY
				LDA	HLINES,Y
				STA	POINTL
				LDA	HLINES+1,Y
				STA	POINTH
				JMP	LINEOUT
HLINES			DA	HLINE0			;MAIN GREEN BOX
				DA	HLINE1			;THERMOMETER1
				DA	HLINE2X			;THERMOMETER2
				DA	HLINE3			;BODY PLUS
				DA	HLINE4			;BODY MINUS
				DA	HLINE5			;VIOLET STATUS BOXES

PICKER			TYA
				ASL
				TAY
				LDA	PICKS,Y
				STA	TBLPNTL
				LDA	PICKS+1,Y
				STA	TBLPNTH
				JMP	EXPANDR
PICKS			DA	THERMT
				DA	THERMB
				DA	WAVES
				DA	BODY
				DA	OHEART
				DA	BHEART
