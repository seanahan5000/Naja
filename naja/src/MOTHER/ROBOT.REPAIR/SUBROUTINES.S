PICK_A_ROBOT	STA	SCRATCH
				JSR	COUNT_ROBOTS
				STX	CURSOR
				LDA	ROBOT_COUNT
				CMP	#1
				BEQ	ONEBOT
				LDA	SCRATCH
				PHA
				JSR	PPEOR
				JSR	ALLICON
				JSR	DRAW_GROUP
				JSR	CLRMIDL
				PLA	
				TAY
				JSR	TEXTER
				JSR	VIEWER
				LDA	#$00
				STA	LEGALS+7
				LDY	#7
				STY	CURSOR
LTLOOP3			LDA	RYPOINTS,Y
				STA	LINTABL,Y
				DEY
				BPL	LTLOOP3
				LDA	#$03
				STA	XHOLD
				JSR	KDOWN2
				JSR	RCURSIT
				LDA	CURSOR
				CMP	#$07
				BNE	ONEBOT
				JMP	OPTIONS
ONEBOT			LDA	CURSOR
				STA	THE_ROBOT
				RTS

VOMAIN			LDX	#$03
VOLOOP1			LDA	CAT1,X
				STA	THE_PRICE,X
				DEX
				BPL	VOLOOP1
				LDA	#$6B
				STA	XPNT
				LDA	CHYPS
				STA	YPNT
				JSR	SEVEN8
				LDX	#$03
VOLOOP2			LDA	CAT0,X
				STA	THE_PRICE,X
				DEX
				BPL	VOLOOP2
				LDY	#$07
VOLOOP3			LDX	#$03
				CLC
VOLOOP4			LDA	CAT2,Y
				SED
				ADC	THE_PRICE,X
				CLD
				STA	THE_PRICE,X
				DEY
				DEX
				BPL	VOLOOP4
				TYA
				BPL	VOLOOP3
				LDA	CHYPS+1
				STA	YPNT
				JSR	SEVEN8
				LDX	#1
				STX	COUNT
VOLOOP6			LDA	CHYPS+1,X
				STA	YPNT
				TXA
				JSR	VOSUB
				INC	COUNT
				LDX	COUNT
				CPX	#scrnPROGRAM+1
				BNE	VOLOOP6
				RTS

VOSUB			CLC
				ADC	#$01
				ASL
				ASL
				TAY
				DEY
				LDX	#$03
VOLOOP5			LDA	BODTOT,Y
				STA	THE_PRICE,X
				DEY
				DEX
				BPL	VOLOOP5

SEVEN8			LDA	PARTALL
				CMP	#$EE
				BNE	NOT78
SEVENX			LDA	#$88			;Modified
				LDX	#<THE_PRICE
				LDY	#>THE_PRICE
				JSR	MULTIPLY_14
				LDX	#3
:LOOP1			LDA	PRODUCT_5,X
				STA	THE_PRICE,X
				DEX
				BPL	:LOOP1
NOT78			NOP	
				LDX	#<THE_PRICE
				LDY	#>THE_PRICE
				JMP	CREDIT_OUT

*-------------------------------

OPENUP			LDX	#$6B	
OPLOOP1			LDA	#$05	
				LDY	#$23
				JSR	OCSUB1
OPLOOP2			LDA	RCOLUMN
				LDY	LCOLUMN
				JSR	OCSUB2
				INC	LCOLUMN
				DEC	RCOLUMN
				LDA	RCOLUMN
				CMP	#$13
				BNE	OPLOOP2
				CPX	#$6B
				BEQ	OPSKIP1
				CPX	#$BF
				BEQ	OPENOUT
				LDY	#$0F
				LDA	#$00
OPLOOP3			STA	(SCREENL),Y
				INY
				CPY	#$18
				BNE	OPLOOP3
OPSKIP1			INX
				BNE	OPLOOP1
OPENOUT			RTS

CLOSEUP			LDX	#$6B
CLOOP1			LDA	#$0E
				LDY	#$18
				JSR	OCSUB1
CLOOP2			LDA	LCOLUMN
				LDY	RCOLUMN
				JSR	OCSUB2
				DEC	LCOLUMN
				INC	RCOLUMN
				LDA	RCOLUMN
				CMP	#$28
				BNE	CLOOP2
				LDY	#$04
				LDA	#$00
CLOOP3			STA	(SCREENL),Y
				DEY
				BPL	CLOOP3
				LDY	#$27
CLOOP4			STA	(SCREENL),Y
				DEY
				CPY	#$23
				BNE	CLOOP4
				CPX	#$6B
				BEQ	CLSKIP1
				CPX	#$BF	
				BEQ	CLOSOUT
				LDY	#$13
				STA	(SCREENL),Y
				INY
				LDA	(SCREENL),Y
				AND	#$C0
				STA	(SCREENL),Y
CLSKIP1			INX
				BNE	CLOOP1	
CLOSOUT			RTS

OCSUB1			STA	LCOLUMN
				STY	RCOLUMN
				LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	PAGE
				STA	SCREENH
				RTS

OCSUB2			PHA
				LDA	(SCREENL),Y
				DEY
				DEY
				DEY
				DEY
				STA	(SCREENL),Y
				PLA
				TAY
				LDA	(SCREENL),Y
				INY
				INY
				INY
				INY
				STA	(SCREENL),Y
				RTS

REVERSE			LDY	#baseupreflex
				LDA	(CHARDL),Y
				SED
				SEC
				SBC	#$25
				CLD
				PHA
				LSR
				LSR
				LSR
				AND	#%11111110
				TAX
				PLA
				AND	#$0F
				BEQ	SKIP12
				INX
SKIP12			STX	UPREF
				INY					;baselwreflex
				LDA	(CHARDL),Y
				SEC
				SBC	#$20			;Works w/o BCD
				PHA
				LSR
				LSR
				LSR
				TAX
				PLA
				AND	#$0F
				BEQ	SKIP34
				INX
SKIP34			STX	LOWREF
				LDY	#e1_HEAD+2
				LDA	(CHARDL),Y
				PHA
				AND	#$0F
				STA	SPBODY
				PLA
				LSR
				LSR
				LSR
				LSR
				STA	SEBODY
				DEY
				LDA	(CHARDL),Y
				PHA
				AND	#$0F
				STA	SPHEAD
				PLA
				LSR
				LSR
				LSR
				LSR
				STA	SEHEAD
				DEY
				LDA	(CHARDL),Y
				PHA
				AND	#$07
				SEC
				SBC	#$02
				STA	RANGE
				LDX	#$00
				PLA	
				BPL	SKIP56
				INX
SKIP56			STX	SLOT
				LDY	#e1_HEAD+3
				LDA	(CHARDL),Y
				STA	ECAPAC
				LDA	#$00	
				STA	PLEVEL	
				STA	AUTO	
				LDY	#e15_PROGRAM+1
				LDA	(CHARDL),Y
				BPL	NOAUT
				INC	AUTO
				AND	#$7F
NOAUT			LDY	#$00
NCLOOP1			CMP	PROGS,Y
				BEQ	NCSKIP1
				INY
				BNE	NCLOOP1
NCSKIP1			STY	PLEVEL	
				LDY	#e3_ROBLFT
				JSR	LRWEAPR
				STA	LWEAPON
				LDY	#e5_ROBRGT
				JSR	LRWEAPR
				STA	RWEAPON
				RTS

LRWEAPR			LDX	#$00
LRLOOP1			LDA	(CHARDL),Y
				CMP	ROBWEPS,X
				BEQ	LRSKIP1
				INX
				INX
LRSKIP2			CPX	#$0E	
				BNE	LRLOOP1
				LDA	#$FF
				RTS
LRSKIP1			INX
				INX
				INY
				LDA	(CHARDL),Y
				DEY
				CMP	ROBWEPS-1,X
				BNE	LRSKIP2
				TXA
				LSR
				SEC
				SBC	#$01
				RTS

TOTALUP			LDA	#$0C
				STA	COUNT
TOLOOP1			LDX	COUNT
				LDY	SLOT,X
				JSR	VALUSUB
				DEC	COUNT
				BPL	TOLOOP1
				LDX	#27
				LDA	#0
TOLOOP2			STA	BODTOT,X
				DEX
				BPL	TOLOOP2
				LDA	#scrnPROGRAM
				STA	COUNT
TOLOOP5			LDY	COUNT
				LDA	BEGCATS,Y
				TAX
				CLC
				ADC	NUMCATS,Y
				STA	SCRATCH
TOLOOP3			LDA	#$03
				STA	COUNTER
				TXA
				PHA
				CLC
				ADC	#$01
				ASL
				ASL
				TAX
				DEX
				LDY	COUNT
				INY
				TYA
				ASL
				ASL
				TAY
				DEY
				CLC
TOLOOP4			LDA	BODTOT,Y
				SED
				ADC	CAT0,X
				CLD
				STA	BODTOT,Y
				DEX
				DEY
				DEC	COUNTER
				BPL	TOLOOP4
				PLA
				TAX
				INX
				CPX	SCRATCH
				BNE	TOLOOP3
				DEC	COUNT
				BPL	TOLOOP5
				RTS

GBLINK			LDA	#$10
				JSR	WAIT
				LDA	#$F0
				JSR	WAIT2
				BMI	GKEYIN
				JSR	CURSUB
				LDA	#$00
				JSR	WAIT2
				JSR	CURSUB
GKEYIN			LDA	KEYBRD
				BPL	GBLINK
				BIT	UNSTROB
				JSR	CHECK_UP
				BEQ	GLEFT
				JSR	CHECK_LEFT
				BEQ	GLEFT
				JSR	CHECK_DOWN
				BEQ	GRIGHT
				JSR	CHECK_RIGHT
				BEQ	GRIGHT
				JSR	CHECK_ENTER
				BEQ	GENTER
				CMP	#$C3
				BNE	GKEYIN
				LDX	PICK
				LDA	ON1,X
				ORA	#$0F
				STA	ON1,X
				LDA	#GREEN
				JSR	CBOXSUB
				LDA	CURSUB
				CMP	#$EA
				BEQ	GOOPTS
				JSR	CURFLIP
GOOPTS			LDA	PARTALL
				CMP	#$D0
				BEQ	GOPBACK
				JMP	OPTIONS
GOPBACK			JMP	PBACK
GENTER			LDA	#GREEN			;TEMP LABEL 
				STA	COLOR
				LDY	CURSOR
				CPY	#3
				BCC	:1
				DEY
:1				JMP	LINER
GLEFT			LDA	#GREEN
				JSR	CBOXSUB
GLEFT1			DEC	CURSOR
				BPL	GLEFT2
				LDA	#$09
				STA	CURSOR
GLEFT2			LDY	CURSOR
				LDA	LEGALS,Y
				BNE	GLEFT1
				BEQ	GBOXSUB	
GRIGHT			LDA	#GREEN
				JSR	CBOXSUB
GRIGHT1			INC	CURSOR
				LDA	CURSOR
				CMP	#$09
				BNE	GRIGHT2
				LDA	#$00
				STA	CURSOR
GRIGHT2			LDY	CURSOR
				LDA	LEGALS,Y
				BNE	GRIGHT1
GBOXSUB			LDA	#WHITE1
				JSR	CBOXSUB
				JMP	GKEYIN

CBOXSUB			STA	COLOR
				LDY	CURSOR
				CPY	#3
				BCC	:1
				DEY
:1				JSR	LINER
				JMP	CURSUB

BOXCURS			LDY	SCRPICK
				LDA	BEGCATS,Y
				STA	MINIMUM
				LDA	NUMCATS,Y
				STA	MAXIMUM
				LDA	#$00
				STA	POINTER
				LDA	#$03
				STA	COUNTER
				JMP	BSTOP

VBLINK			LDA	#$10
				JSR	WAIT
				LDA	#$F0
				JSR	WAIT2
				BMI	BSTOP
				LDA	#BLACK1
				STA	COLOR
				JSR	LINER2A
				LDA	#$00
				JSR	WAIT2
				LDA	#VIOLET
				STA	COLOR
				JSR	LINER2A
BSTOP			DEC	COUNTER
				BMI	VKEYIN
				BIT	KEYBRD
				BPL	VBLINK
VKEYIN			LDA	KEYBRD
				BPL	VKEYIN
				BIT	UNSTROB
				JSR	CHECK_UP
				BEQ	GOVUP
				JSR	CHECK_DOWN
				BEQ	GOVDOWN
				JSR	CHECK_LEFT
				BEQ	VLEFT
				JSR	CHECK_RIGHT
				BEQ	VRIGHT
				JSR	CHECK_ENTER
				BEQ	GOVENT
				CMP	#$C3
				BNE	VKEYIN
VCANC			LDA	PARTALL
				CMP	#$E0
				BEQ	:1
				CMP	#$D0
				BNE	NOPGO
:1				RTS					;Carry assumed set
NOPGO			LDX	PICK
				LDA	#$FF
				STA	ON1,X
				JMP	OPTIONS
GOVUP			JMP	VUP	
GOVDOWN			JMP	VDOWN
GOVENT			JMP	VENTER
VLEFT			LDA	#BLACK1
				STA	COLOR
				JSR	LINER2A
				DEC	POINTER
				BPL	VLEFT2
				LDA	MAXIMUM
				STA	POINTER
				DEC	POINTER
VLEFT2			LDA	#VIOLET
				STA	COLOR
				JSR	LINER2A
				LDA	MINIMUM
				CLC
				ADC	POINTER
				JSR	POINTAT
				JMP	VKEYIN
VRIGHT			LDA	#BLACK1
				STA	COLOR
				JSR	LINER2A
				INC	POINTER
				LDA	POINTER
				CMP	MAXIMUM
				BNE	VRIGHT2
				LDA	#$00
				STA	POINTER
VRIGHT2			JMP	VLEFT2
VUP				LDX	POINTER
				LDA	POINTLV,X
				CMP	NUMTYPE
				BEQ	GOVKEY
				LDY	SCRPICK
				CPY	#scrnPROGRAM
				BNE	NOTPROG
				CMP	LIMIT
				BEQ	GOVKEY
NOTPROG			JSR	UPDBOX
				LDX	POINTER
				INC	POINTLV,X
				JSR	UPDBOX
				JSR	BIGSUB
				JSR	UPDSUB
				LDA	SCRPICK
				CMP	#scrnMOVEMENT
				BNE	GOVKEY
				JSR	UPMOVE
				JMP	VKEYIN
VDOWN			LDX	POINTER
				LDA	POINTLV,X
				BEQ	GOVKEY
				LDY	SCRPICK
				CPY	#scrnBODY
				BNE	NOTA1
				CMP	MINUPR
				BEQ	GOVKEY
NOTA1			CPY	#scrnMOVEMENT
				BNE	NOTA4
				CMP	MINLOW
				BEQ	GOVKEY
NOTA4			JSR	UPDBOX
				LDX	POINTER
				DEC	POINTLV,X
				JSR	UPDBOX
				JSR	BIGSUB
				JSR	UPDSUB
				LDA	SCRPICK
				CMP	#scrnMOVEMENT
				BNE	GOVKEY
				JSR	DWNMOVE
GOVKEY			JMP	VKEYIN
VENTER			LDA	EXPFLAG
				BEQ	CANPIC
				JSR	EBLINK
				JMP	VKEYIN

CANPIC			LDA	SCRPICK
				ASL
				ASL
				TAX
				LDY	#$00
TOTOT			LDA	EVALNUM,Y
				STA	BODTOT,X
				INX
				INY
				CPY	#$04
				BNE	TOTOT
				CLC
				RTS

EBLINK			LDA	#$02	
				STA	COUNT
EXPLOOP			LDA	#$10
				JSR	WAIT
				LDA	#$F0
				JSR	WAIT2
				BMI	EXGOT
				LDY	#$31	
				JSR	TEXTER
				LDA	#$00
				JSR	WAIT2
				LDY	#$31	
				JSR	TEXTER
				DEC	COUNT
				BNE	EXPLOOP
EXGOT			BIT	UNSTROB
				RTS

UPMOVE			LDA	#$40
				STA	PUTMOD+1
				JSR	UPDOWN
				PHA
				JSR	TEXTER
				PLA
				TAY
				INY
				JMP	TEXTER

DWNMOVE			LDA	#$35
				STA	PUTMOD+1
				JSR	UPDOWN
				INY	
				PHA
				JSR	TEXTER
				PLA
				TAY
				JMP	TEXTER

UPDOWN			LDY	POINTER
				LDA	POINTLV,Y
				TAY
				LDA	(VALUEL),Y
				TAX
PUTMOD			LDA	#$40
				STA	SCRATCH
				LDY	#$18
UDLOOP			CPX	SCRATCH
				BNE	NOTAT
				TYA
				RTS
NOTAT			LDA	SCRATCH
				CLC
				ADC	#$20
				STA	SCRATCH
				INY
				CPY	#$1B
				BNE	UDLOOP
				PLA
				PLA
				RTS

UPDSUB			LDY	SCRPICK
				LDA	BEGCATS,Y
				CLC
				ADC	POINTER
				TAX
				LDY	POINTER
				LDA	POINTLV,Y
				TAY
				JSR	VALUSUB
				JSR	CATTOTL
				LDX	#$03
				CLC
UPLOOPA			LDA	RTOTAL,X
				SED
				ADC	CATT,X
				CLD
				STA	RTOTNEW,X
				DEX
				BPL	UPLOOPA
				LDX	#$03
				SEC
UPLOOPB			LDA	CATT,X
				SED
				SBC	ETRDNUM,X
				CLD
				STA	EVALNEW,X
				DEX
				BPL	UPLOOPB
				LDA	EXPFLAG
				PHA
				JSR	TRADSUB
				PLA
				CMP	EXPFLAG
				BEQ	TOUPDAT
				LDY	#$31	
				JSR	TEXTER
TOUPDAT			JMP	UPDATER

UPDSUB2			LDA	SCRPICK
				SEC
				SBC	#scrnWEAPONL-1
				PHA
				CLC
				ADC	#6
				TAX
				LDY	CURSOR
				JSR	VALUSUB
				PLA
				ASL
				ASL
				PHA
				TAY
				LDX	#3
				CLC
UPLA			LDA	RTOTAL,X
				SED
				DEY
				ADC	CAT7,Y
				CLD
				STA	RTOTNEW,X
				DEX
				BPL	UPLA
				PLA
				TAY
				LDX	#3
				SEC
UPLB			DEY
				LDA	CAT7,Y
				SED
				SBC	ETRDNUM,X
				CLD
				STA	EVALNEW,X
				DEX
				BPL	UPLB
				LDA	EXPFLAG
				PHA
				JSR	TRADSUB
				JSR	UPDATER
				PLA
				CMP	EXPFLAG
				BEQ	TOUPD2
				LDY	#$31
				JSR	TEXTER
TOUPD2			LDX	SCRPICK
				LDA	YSPOTS+7-scrnWEAPONL,X
				STA	YPNT
				JSR	WEANUMS
				LDA	#$80
				STA	XHOLD
				RTS

ETRDNUM			DB	$00,$00,$00
				DB	$00
EVALNUM			DB	$00,$00,$00
				DB	$00

UPDBOX			LDY	SCRPICK
				LDA	BEGCATS,Y
				CLC
				ADC	POINTER
				TAX
				LDA	YSPOTS,X
				STA	YPNT
				JSR	MSUB
				PHA
				TXA
				ASL
				TAX
				LDA	NUMS+1,X
				PHA
				LDA	NUMS,X
				PHA
				RTS

NUMS			DW	SLTNUMS-1
				DW	SRANUMS-1
				DW	BHENUMS-1
				DW	BHPNUMS-1
				DW	BODNUMS-1
				DW	BHENUMS-1
				DW	BHPNUMS-1
				DW	WEANUMS-1
				DW	WEANUMS-1
				DW	ENRNUMS-1
				DW	BHPNUMS-1
				DW	PRONUMS-1
				DW	AUTNUMS-1

BODNUMS			PLA
				JSR	TWODIGI
				LDA	#$0A
				STA	TEXTBUF+2
				LDA	#$8D
				STA	TEXTBUF+5
				LDA	#$92
				LDY	YPNT
				JMP	LINE

BHENUMS			PLA
				JSR	GOTWO
				LDA	#$C8
				BNE	BHCOMN
BHPNUMS			PLA
				JSR	GOTWO
				LDA	#$E6
BHCOMN			LDY	YPNT
				JMP	LINE

GOTWO			JSR	TWODIGI
				LDA	#$8D
				STA	TEXTBUF+2
				LDA	TEXTBUF
				BNE	GTOUT
				LDA	#$EE
				STA	TEXTBUF
GTOUT			RTS	

TWODIGI			TAY
				AND	#$0F
				STA	TEXTBUF+1
				STA	TEXTBUF+4
				TYA
				LSR
				LSR
				LSR
				LSR
				STA	TEXTBUF
				STA	TEXTBUF+3
				RTS

WEANUMS			LDA	CURHOLD
				JSR	WEASUB
				LDA	CURSOR
WEASUB			ASL
				TAX
				LDA	ROBWEPS,X
				STA	ETABLE
				LDA	ROBWEPS+1,X
				STA	ETABLE+1
				JSR	EQUIP1
				LDA	#$92
				LDY	YPNT
				JMP	LINE

SLTNUMS			PLA
				AND	#$01
				ASL
				ASL
				TAX
				LDY	#$00
NYLOOP			LDA	YNWORD,X
				STA	TEXTBUF,Y
				INX
				INY
				CPY	#$04
				BNE	NYLOOP
				LDA	#$92
				LDY	YPNT
				JMP	LINE

YNWORD			USR	(YES)
				USR	(NO)

SRANUMS			PLA
				AND	#$0F
				STA	TEXTBUF
				LDA	#$8D
				STA	TEXTBUF+1
				LDA	#$92
				LDY	YPNT
				JMP	LINE

PRONUMS			LDA	VALUEL
				STA	VHOLD
				LDA	VALUEH
				STA	VHOLD+1
				LDY	SCRPICK
				LDA	BEGCATS,Y
				CLC
				ADC	#$01
				JSR	POINTAT
				LDY	#$01
				LDA	POINTLV,Y
				TAY
				LDA	(VALUEL),Y
				TAX
				PLA
				AND	#$F7
				TAY
				TXA	
				PHA
				JMP	PROCOMN

AUTNUMS			LDA	VALUEL
				STA	VHOLD
				LDA	VALUEH
				STA	VHOLD+1
				LDY	SCRPICK
				LDA	BEGCATS,Y
				JSR	POINTAT
				LDA	POINTLV
				TAY
				LDA	(VALUEL),Y
				AND	#$F7
				TAY
				DEC	YPNT
PROCOMN			LDX	#$00
				TYA
				AND	#$F0
				BEQ	DIG1
				TYA
				LSR
				LSR
				LSR
				LSR
				STA	TEXTBUF,X
				INX
DIG1			TYA
				AND	#$0F
				STA	TEXTBUF,X
				INX
				PLA
				CMP	#$AA
				BNE	NOTAF
				LDA	#$0B
				STA	TEXTBUF,X
				INX
NOTAF			LDA	#$8D
				STA	TEXTBUF,X
				LDA	#$B6
				LDY	YPNT
				JSR	LINE
				LDA	VHOLD
				STA	VALUEL
				LDA	VHOLD+1
				STA	VALUEH
				RTS

VHOLD			DB	$00,$00

ENRNUMS			PLA
				TAY
				LDX	#$00
				CPY	#$80
				BCC	NORMNUM
				CPY	#$90
				BCC	PLUS80
				LDA	#$01
				STA	TEXTBUF,X
				INX
PLUS80			TYA	
				AND	#$0F
				STA	TEXTBUF,X
				INX
				BNE	ZERO3
NORMNUM			TYA
				LSR
				LSR
				LSR
				LSR
				BEQ	NODIGT1
				STA	TEXTBUF,X
				INX
NODIGT1			TYA
				AND	#$0F
				STA	TEXTBUF,X
				INX
				LDA	#$00
				STA	TEXTBUF,X
				TYA
				AND	#$0F
				CMP	#$02
				BEQ	DOFIVE
				CMP	#$07
				BNE	NOFIVE
DOFIVE			LDA	#$05
				STA	TEXTBUF,X
NOFIVE			INX
ZERO3			LDY	#$02
				LDA	#$00
ZLOOP			STA	TEXTBUF,X
				INX
				DEY
				BPL	ZLOOP
				LDA	#$8D
				STA	TEXTBUF,X
				LDA	#$B0
				LDY	YPNT
				JMP	LINE

YSPOTS			DB	$78,$6F,$78
				DB	$78,$83,$83
				DB	$83,$8E,$97,$A0
				DB	$A9,$B4,$B5

MSUB			TXA
				JSR	POINTAT
				LDY	POINTER
				LDA	POINTLV,Y
				TAY
				LDA	(VALUEL),Y
				RTS

BIGSUB			LDY	SCRPICK
				LDA	BEGCATS,Y
				CLC
				ADC	POINTER
				PHA
				JSR	POINTAT
				LDY	POINTER
				LDA	POINTLV,Y
				TAY
				LDA	(VALUEL),Y
				TAX
				PLA
				TAY
				CMP	#$09
				BNE	NOTENER
				JMP	ENERNUM
NOTENER			LDA	BXPNTS,Y
				STA	XHOLD
				LDA	#$4D
				STA	YPNT
				CPX	#$FF
				BNE	NONO
				JMP	NOWORD
NONO			CPX	#$AA
				BNE	NOYES
				JMP	YESWORD
NOYES			CPY	#$04	
				BNE	DIGIT2
				TXA
				PHA
				JSR	DIGIT2
				LDA	#$0F
				JSR	TRICK2	
				PLA
				TAX
DIGIT2			TXA
				PHA
				AND	#$F0
				BNE	NODIG1
				LDA	#$10
				BNE	DIGIT1
NODIG1			LSR
				LSR
				LSR
				LSR
DIGIT1			JSR	TRICK2	
				PLA
				AND	#$0F
TRICK2			JSR	BLETTER
				INC	XHOLD
				INC	XHOLD
				RTS

YESWORD			LDA	#$0A
				JSR	TRICK2	
				LDA	#$0B
				JSR	TRICK2	
				LDA	#$0C
				JMP	BLETTER

NOWORD			LDA	#$10
				JSR	BLETTER
				INC	XHOLD
				LDA	#$0D	
				JSR	TRICK2	
				LDA	#$0E
				JSR	TRICK2	
				LDA	#$10
				JMP	BLETTER

ENERNUM			LDA	#$45
				STA	YPNT
				LDA	#$14
				STA	XHOLD
				TXA
				BPL	HIENER
				PHA
				AND	#$7F
				LSR
				LSR
				LSR
				LSR
				TAX
				BNE	NOTWO0
				INC	XHOLD	
				INC	XHOLD
				LDA	#$10
				JSR	TRICK2	
				JMP	TWO0
NOTWO0			JSR	DIGIT2
TWO0			PLA
				AND	#$0F
				JMP	BLETTER
HIENER			PHA
				JSR	DIGIT2
				PLA
				AND	#$0F
				CMP	#$02
				BEQ	USE5
				CMP	#$07
				BEQ	USE5
				LDA	#$00
				BEQ	GOBBR	
USE5			LDA	#$05
GOBBR			JMP	BLETTER

BXPNTS			DB	$03,$0D,$17
				DB	$21,$03,$15
				DB	$21,$FF,$FF,$14
				DB	$13,$13,$1B
NUMCATS			DB	$04,$03,$01,$01
				DB	$01,$01,$02
BEGCATS			DB	$00,$04,$07,$08
				DB	$09,$0A,$0B

UPDATER			LDX	#$00
				LDY	#$00
MORTOT			LDA	RTOTNEW,Y
				JSR	SPLIT
				LDA	FEEDERH
				STA	SCNBUFF,X
				INX
				LDA	FEEDERL
				STA	SCNBUFF,X
				INX
				INY
				CPX	#$08
				BNE	MORTOT
				LDX	#$00
SPRTOT			LDA	SCNBUFF,X
				BNE	TSPACED
				LDA	#$0A
				STA	SCNBUFF,X
				INX
				CPX	#$07
				BNE	SPRTOT
TSPACED			LDX	#$07
ASERASE			LDA	RTOTSCN,X
				CMP	SCNBUFF,X
				BNE	NOSAME1
				LDA	#$0A
NOSAME1			STA	TEXTBUF,X
				DEX
				BPL	ASERASE
				LDA	#$D6
				STA	XPNT
				LDA	#$18
				STA	YPNT
				JSR	ISZEROD
				LDX	#$07
ASDRAW			LDA	SCNBUFF,X
				CMP	RTOTSCN,X
				BNE	NOSAME2
				LDA	#$0A
NOSAME2			STA	TEXTBUF,X
				DEX
				BPL	ASDRAW
				JSR	ISZEROD
				LDX	#$07
MVLOOPA			LDA	SCNBUFF,X
				STA	RTOTSCN,X
				DEX
				BPL	MVLOOPA
				LDX	#$03
MVLOOPB			LDA	RTOTNEW,X
				STA	RTOTNUM,X
				DEX
				BPL	MVLOOPB
				LDX	#$00
				LDY	#$00
MORETOT			LDA	EVALNEW,Y
				JSR	SPLIT
				LDA	FEEDERH
				STA	SCNBUFF,X
				INX
				LDA	FEEDERL
				STA	SCNBUFF,X
				INX
				INY
				CPX	#$08
				BNE	MORETOT
				LDX	#$00
SPETOT			LDA	SCNBUFF,X
				BNE	TSPACE2
				LDA	#$0A
				STA	SCNBUFF,X
				INX
				CPX	#$07	
				BNE	SPETOT
TSPACE2			LDX	#$07
BSERASE			LDA	EVALSCN,X
				CMP	SCNBUFF,X
				BNE	NOSAME3
				LDA	#$0A
NOSAME3			STA	TEXTBUF,X
				DEX
				BPL	BSERASE
				LDA	#$D6
				STA	XPNT
				LDA	#$0E
				STA	YPNT
				JSR	ISZEROD
				LDX	#$07
BSDRAW			LDA	SCNBUFF,X
				CMP	EVALSCN,X
				BNE	NOSAME4
				LDA	#$0A
NOSAME4			STA	TEXTBUF,X
				DEX
				BPL	BSDRAW
				JSR	ISZEROD
				LDX	#$07
MVLOOPC			LDA	SCNBUFF,X
				STA	EVALSCN,X
				DEX
				BPL	MVLOOPC
				LDX	#$03
MVLOOPD			LDA	EVALNEW,X
				STA	EVALNUM,X
				DEX
				BPL	MVLOOPD
				RTS

MINISUB			LDA	#$00
				STA	MINLOW
				STA	MINUPR
				LDY	PLEVEL
				CPY	#$15
				BCS	MINIOUT
				CPY	#$08	
				BCS	OVER9
				LDA	LESS9,Y
				TAX
				AND	#$0F
				STA	MINUPR
				TXA
				LSR
				LSR
				LSR
				LSR
				STA	MINLOW
MINIOUT			RTS

OVER9			TYA
				SEC
				SBC	#$05	
				STA	MINLOW
				STA	MINUPR
				DEC	MINUPR
				RTS

LESS9			DB	$00,$00,$00
				DB	$10,$10,$11
				DB	$21,$21

LIMITER			LDY	LOWREF
				CPY	#$03
				BCS	LISKIP1
				LDA	MAXILOW,Y
				BNE	LISKIP2
LISKIP1			TYA
				CLC
				ADC	#$05	
LISKIP2			STA	SCRATCH
				LDY	UPREF
				CPY	#$02
				BCS	LISKIP3
				LDA	MAXIUP,Y
				BNE	LISKIP4
LISKIP3			TYA
				CLC
				ADC	#$06	
LISKIP4			CMP	SCRATCH
				BCC	LIMOUT
				LDA	SCRATCH
LIMOUT			STA	LIMIT
				RTS

MAXIUP			DB	$04,$06
MAXILOW			DB	$02,$05,$06

CATTOTL			LDY	#$03
				LDA	#$00
CTLOOP1			STA	CATT,Y
				DEY
				BPL	CTLOOP1
				LDY	SCRPICK
				LDA	BEGCATS,Y
				STA	COUNTER
				CLC
				ADC	NUMCATS,Y
				STA	TEMP
CTLOOP3			LDA	COUNTER
				CLC
				ADC	#$01
				ASL
				ASL
				TAX
				DEX
				LDY	#$03
				CLC
CTLOOP2			LDA	CAT0,X	
				SED
				ADC	CATT,Y
				CLD
				STA	CATT,Y
				DEX
				DEY
				BPL	CTLOOP2
				INC	COUNTER
				LDA	COUNTER
				CMP	TEMP
				BNE	CTLOOP3
				RTS

RTSUB			LDY	#$07
				LDA	#$0A
RTLOOP1			STA	RTOTSCN,Y
				DEY
				BPL	RTLOOP1
				LDY	#$03
				LDA	#$00
RTLOOP2			STA	RTOTNUM,Y
				DEY
				BPL	RTLOOP2
				RTS

*-------------------------------

COUNT_CYBS		JSR	RESET_CHLIST
				LDA	#cybernate*16
				JSR	CSCAN_PROF
				PHP					;Carry clear if no cybs at all
				LDX	#<CCALL_UNASS
				LDY	#>CCALL_UNASS
				JSR	CSCAN_COMMON
				STX	ASSIGN2
				STA	CYB_COUNT		;Count of AVAILABLE cybs in group
				LDA	#0
				STA	LEGALS+7
				PLP
				RTS

CYB_COUNT		DB	0

CCALL_UNASS		LDY	#robotcyblink
				LDA	(CHARDL),Y
				CMP	#$FF
				RTS

*-------------------------------

COUNT_ROBOTS	JSR	RESET_CHRLIST
				LDA	#robot*16
				JSR	CSCAN_PROF
				STA	ROBOT_COUNT
				RTS					;X valid on exit

ROBOT_COUNT		DB	0

*-------------------------------

PPEOR			LDA	PAGE
				EOR	#$60
				STA	PAGE
				LDA	PICK
				EOR	#$01
				STA	PICK
				RTS

VIEWER			BIT	UNSTROB
				LDA	PICK
				BEQ	VIEW1
				STA	SCNDARY
				RTS	
VIEW1			STA	PRIMARY
				RTS

PACSUB			JSR	PPEOR
				JSR	ALLICON
CLRBOTM			LDX	#$57
CLROOP1			LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	PAGE
				STA	SCREENH
				LDY	#$27
				LDA	#$00
CLROOP2			STA	(SCREENL),Y
				DEY
				BPL	CLROOP2
				INX
CSTOP			CPX	#$C0
				BNE	CLROOP1
				RTS

CLRTOP			LDX	#$00
				LDA	#$6B
				STA	CSTOP+1
				JSR	CLROOP1
				LDA	#$C0
				STA	CSTOP+1
				RTS

CLRMIDL			LDA	#$6B
				STA	CSTOP+1
				JSR	CLRBOTM
				LDA	#$C0
				STA	CSTOP+1
				RTS

MT1TOP			LDA	#$20
				BNE	MOVTOP			;Always
MT2TOP			LDA	#$40
MOVTOP			STA	PAGER+1
				LDX	#$00
MVLOOP1			LDA	LOBYTES,X
				STA	SCREENL
				STA	DESTINL
				LDA	HIBYTES,X
PAGER			ORA	#$20			;Modified
				STA	SCREENH
				EOR	#$60
				STA	DESTINH
				LDY	#$27
MVLOOP2			LDA	(DESTINL),Y
				STA	(SCREENL),Y
				DEY	
				BPL	MVLOOP2
				INX	
MSTOP			CPX	#$57
				BNE	MVLOOP1
				RTS

MT1BOTM			LDA	#$20
				BNE	MOVBOTM			;Always
MT2BOTM			LDA	#$40
MOVBOTM			STA	PAGER+1
				LDX	#$6B
				LDA	#$C0
				STA	MSTOP+1
				JSR	MVLOOP1
				LDA	#$57
				STA	MSTOP+1
				RTS

SPACEIN			LDA	#$33
				BNE	INCOM			;Always
CANCIN			LDA	#$17
INCOM			PHA
				LDA	#BLACK1
				STA	COLOR
				LDY	#$11
				JSR	LINER
				LDA	#BLUE
				STA	COLOR
				LDY	#$10
				JSR	LINER
				PLA
				TAY
				JMP	TEXTER

SPACEOUT		LDA	#$33
				BNE	OUTCOM			;Always
CANCOUT			LDA	#$17
OUTCOM			PHA
				LDA	#BLACK2
				STA	COLOR
				LDY	#$10
				JSR	LINER
				PLA
				TAY
				JSR	TEXTER
				LDA	#BLUE
				STA	COLOR
				LDY	#$11
				JMP	LINER

KBLINK			LDA	#$10
				JSR	WAIT
				LDA	#$F0
				JSR	WAIT2
				BMI	GOTKEY
				JSR	RCURSIT
				LDA	#$00
				JSR	WAIT2
				JSR	RCURSIT
KEYMOVE			LDA	KEYBRD
				BPL	KBLINK
GOTKEY			BIT	UNSTROB
				JSR	CHECK_UP
				BEQ	KUP
				JSR	CHECK_DOWN
				BEQ	KDOWN
CKEYMOD			CMP	#$00
				BEQ	KEYOUT
				JSR	CHECK_ENTER
				BNE	KEYMOVE
KEYOUT			RTS
KUP				JSR	RCURSIT
				LDA	CURSOR
				STA	CURHOLD
KUP2B			DEC	CURSOR
				BPL	KUP3
				LDA	#$08
				STA	CURSOR
KUP3			LDY	CURSOR
				LDA	LEGALS,Y
				BNE	KUP2B	
				JSR	RCURSIT
KUPMOD			DB	$EA,$EA,$EA
				JMP	KEYMOVE
KDOWN			JSR	RCURSIT
KDOWN2			LDA	CURSOR
				STA	CURHOLD
KDOWN2B			INC	CURSOR
				LDA	CURSOR
				CMP	#$09
				BNE	KDOWN3
				LDA	#$00
				STA	CURSOR
KDOWN3			LDY	CURSOR
				LDA	LEGALS,Y
				BNE	KDOWN2B	
				JSR	RCURSIT
KDOMOD			DB	$EA,$EA,$EA
				JMP	KEYMOVE

RCURSIT			LDX	CURSOR
				LDY	LINTABL,X
				LDX	XHOLD
				JMP	ARROWER

CURFLIP			LDX	#$02
CURLOOP			LDY	CURSUB,X
				LDA	CFSPOT,X
				STA	CURSUB,X
				TYA
				STA	CFSPOT,X
				DEX
				BPL	CURLOOP
				RTS

CFSPOT			JMP	CHURSIT
CURSUB			NOP
				NOP
				NOP
				LDY	CURSOR
				LDA	CHUNKS,Y
				PHA
				AND	#$0F
				JSR	CARP
				PLA
				LSR
				LSR
				LSR
				LSR
				CMP	#$0F
				BEQ	CHOUT
CARP			TAX
				LDA	CHYPS,X
				STA	YPNT
				LDA	#$6E	
				STA	XPNT
				LDA	#$03	
				JSR	SPCHARS
				LDA	#$A3
				STA	XPNT
				LDA	#$02
				JMP	SPCHARS

CHURSIT			LDA	#$8A
				STA	XPNT
				LDY	CURSOR
				LDA	CHUNKS,Y
				PHA
				AND	#$0F
				JSR	CHURP
				PLA
				LSR
				LSR
				LSR
				LSR
				CMP	#$0F
				BNE	CHURP
CHOUT			RTS
CHURP			TAX
				LDA	CHYPS,X
				STA	YPNT
				LDA	#$02
				JMP	SPCHARS

CHUNKS			DB	$10,$F2,$F3,$F4
				DB	$F5,$F6,$F7
CHYPS			DB	$6F,$78,$83
				DB	$8E,$97,$A0
				DB	$A9,$B4

SET_LEGALS		TAY
				LDX	#15
				LDA	#$FF
:LOOP1			STA	LEGALS,X
				DEX
				BPL	:LOOP1
				LDA	#0
:LOOP2			DEY
				BMI	:EXIT
				STA	LEGALS,Y
				BPL	:LOOP2			;Always
:EXIT			RTS

OPTYPNT			DB	$7C,$87,$90
				DB	$9B
RYPOINTS		DB	$7F,$87,$8F
				DB	$97,$9F,$A7
				DB	$AF,$B9
YPOINT2			DB	$77,$7F,$87
				DB	$8F,$97,$9F
				DB	$A7,$B1,$B9

LINER2A			LDA	MINIMUM
				CLC
				ADC	POINTER
				TAX
LINER2			CPX	#$0A
				BNE	NOTA3
				LDY	#$13
				JMP	LINER
NOTA3			CPX	#$09
				BNE	NOTAA
				LDY	#$14
				JMP	LINER
NOTAA			TXA
				ASL
				TAX
				JSR	LSUB
				LDA	SHIFT
				STA	LINE12+0
				STA	LINE12+9
				STA	LINE12+12
				LDA	XCOORD
				STA	LINE12+1
				STA	LINE12+10
				STA	LINE12+13
				INX
				JSR	LSUB
				LDA	SHIFT
				STA	LINE12+3
				STA	LINE12+6
				LDA	XCOORD
				STA	LINE12+4
				STA	LINE12+7
				LDY	#$12
				JMP	LINER

LSUB			LDA	#$00
				STA	SHIFT
				LDA	XDATA,X
				CMP	#$FF
				BNE	NOCAR
				LDA	#$01
				STA	SHIFT
				LDA	#$12
NOCAR			STA	XCOORD
				RTS

XDATA			DB	$0A,$4C
				DB	$5C,$82
				DB	$8A,$D0
				DB	$D2,$FF
				DB	$0A,$6C
				DB	$7C,$C2
				DB	$D2,$FF
				DB	$00,$00
				DB	$00,$00
				DB	$00,$00
				DB	$00,$00
				DB	$7A,$AC
				DB	$C0,$E6

VALUSUB			TXA
				ASL
				TAX
				ASL
				PHA
				LDA	CDATAS,X
				STA	CDPNTL
				LDA	CDATAS+1,X
				STA	CDPNTH
				STY	SCRATCH
				TYA
				ASL
				CLC
				ADC	SCRATCH
				TAY
				PLA
				TAX
				LDA	#$00
				STA	CAT0,X
				INX
				LDA	#$03
				STA	LINENUM
LODLOOP			LDA	(CDPNTL),Y
				STA	CAT0,X
				INY
				INX
				DEC	LINENUM
				BNE	LODLOOP
				RTS

CDATAS			DW	SLT
				DW	SR
				DW	ESH	
				DW	PSH
				DW	URS
				DW	ESB
				DW	PSB
				DW	WEA
				DW	WEA
				DW	EC	
				DW	LR	
				DW	PRO	
				DW	AUT	

BLETTER			ASL
				TAY
				LDA	LETOFF,Y
				STA	LETPNT+1
				LDA	LETOFF+1,Y
				STA	LETPNT+2
				LDX	YPNT
				LDA	#$00
				STA	COUNTER
LETLOOP			LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	PAGE
				STA	SCREENH
				JSR	PUTON
				INC	XHOLD
				JSR	PUTON
				DEC	XHOLD
				LDA	COUNTER
				INX
				CMP	#$1E
				BNE	LETLOOP
				RTS
