				LST	OFF
LINENUM			EQU	$00
COUNTER			EQU	$03	
PAGPICK			EQU	$01	
BITCONT			EQU	$16	
BYTCONT			EQU	$17	
TBLPNTL			EQU	$0D
DESTINL			EQU	$0D
TBLPNTH			EQU	$0E
DESTINH			EQU	$0E
POINTL			EQU	$12
POINTH			EQU	$13	
SCREENL			EQU	$26
SCREENH			EQU	$27
CHARDL			EQU	$91	
COUNT			EQU	$C0	
CURSOR			EQU	$CC	
COLOR			EQU	$E4
PAGE			EQU	$E6
MOVETO1			EQU	$0326	
MOVETO2			EQU	$0331	
WAIT			EQU	$0389	
WAIT2			EQU	$0395	
LOBYTES			EQU	$0800
HIBYTES			EQU	$08C0
LINEOUT			EQU	$0980
TEXTOUT			EQU	$09E0
EXPANDR			EQU	$0C60
CLEAR1			EQU	$11AA
CLEAR2			EQU	$1212
POINTIT			EQU	$12D3	
PROGON			EQU	$4079
RDKEY			EQU	$6000
GETNSEW			EQU	$614A	
NOSCAN			EQU	$6653	
CPARER2			EQU	$B423
CHECK_UP		=	$B6F3
CHECK_DOWN		=	$B702
TRACER			EQU	$BAE0
GRPNUMB			EQU	$BC7F	
KEYBRD			EQU	$C000
UNSTROB			EQU	$C010
CLICK			EQU	$C030	
PRIMARY			EQU	$C054
SCNDARY			EQU	$C055
BLACK1			EQU	$00
GREEN			EQU	$2A	
BLUE			EQU	$D5
				ORG	$9000
				LDA	#$80			;MARK THOSE THAT ARE ALWAYS PRESENT 
				ORA	TRACER
				STA	TRACER	
				JSR	GETNSEW
				LDA	#$40
				STA	PAGE
				JSR	CLEAR2
				JSR	NOSCAN
				LDY	#$00
				JSR	PICKER
				LDA	#BLACK1
				STA	COLOR
				LDY	#$00
				JSR	LINER
				LDA	#BLUE
				STA	COLOR
				LDY	#$01
				JSR	LINER
				JSR	INITER
				LDA	#$00
				STA	CURSOR
				JSR	SETUP
				STA	SCNDARY
BBLINK			LDA	#$10
				JSR	WAIT
				LDA	#$F0
				JSR	WAIT2
				BMI	BKEYHIT
				LDA	#BLACK1
				STA	COLOR
				LDY	#$02
				JSR	LINER
				LDA	#$00
				JSR	WAIT2
				LDA	#GREEN
				STA	COLOR
				LDY	#$02
				JSR	LINER
BKEYER			LDA	KEYBRD
				BPL	BBLINK
BKEYHIT			BIT	UNSTROB
				JSR	CHECK_UP
				BEQ	BDOWN	
				JSR	CHECK_DOWN
				BEQ	BUP	
				CMP	#$A0
				BEQ	GOBENT	
				CMP	#$CC
				BNE	BKEYER
				JSR	CLEAR1
				JSR	CLRBOX
				LDA	#$20
				STA	PAGE
				JSR	DEINIT
GOKEY			BIT	UNSTROB
GOKEYER			LDA	KEYBRD
				BPL	GOKEYER
				JSR	CPARER2
				BNE	GOKEY	
GORDKEY			LDA	#$DC
				STA	PROGON
				JMP	RDKEY
GOBENT			JMP	BENTER
BDOWN			LDY	#$02
				JSR	DRAWCP1
				LDX	CURSOR
				LDY	#$02
				JSR	DRAW1
				LDX	CURSOR
				LDY	#$01
				JSR	DRAW1
				LDY	#$01
				JSR	DRAWCM1
				LDY	#$00
				JSR	DRAWCM1
				LDY	#$00
				JSR	DRAWCM2
				LDA	CURSOR
				JSR	DECURS1
				STA	CURSOR
				JMP	BKEYER
BUP				LDY	#$00
				JSR	DRAWCM1
				LDX	CURSOR
				LDY	#$00
				JSR	DRAW1
				LDX	CURSOR
				LDY	#$01
				JSR	DRAW1
				LDY	#$01
				JSR	DRAWCP1
				LDY	#$02
				JSR	DRAWCP1	
				LDY	#$02
				JSR	DRAWCP2
				LDA	CURSOR
				JSR	INCURS1
				STA	CURSOR
				JMP	BKEYER
BENTER			LDX	CURSOR
				LDA	LEGALS,X
				BEQ	BUP
				BPL	FROCARD
				JMP	FROMEMO
FROCARD			LDA	#$80
				STA	LEGALS,X
				LDA	#$20
				STA	PAGE
				JSR	MOVETO1
				LDY	#$01
				JSR	PICKER
				JSR	CARDER1
				LDY	#$02
				JSR	PICKER
				JSR	PLATTO2
				LDA	#$C0
				JSR	WAIT2
				JSR	CLEAR1
				JSR	NOSCAN
				LDY	#$00
				JSR	PICKER
				LDY	#$01
				JSR	PICKER
				JSR	CARDER1
				JSR	PLATTO2
				JSR	CLEAR1
				LDY	#$00
				JSR	PICKER
				LDY	#$01
				JSR	PICKER
				LDA	#$40
				STA	PAGE
				JSR	SCROLDN
				JSR	SHODATA	
				JSR	SCROLUP
				LDA	#$20
				STA	PAGE
				JSR	MOVETO1
				LDY	#$02
				JSR	PICKER
				STA	PRIMARY
				JSR	MOVETO2
				STA	SCNDARY
				LDA	#$C0
				JSR	WAIT2
				JSR	CLEAR1
				JSR	NOSCAN
				LDY	#$00
				JSR	PICKER
				JSR	PLATTO2
DESHOW			LDA	#$20
				STA	PAGE
				JSR	MOVETO1
				JSR	SETUP
				STA	PRIMARY
				JSR	MOVETO2
				STA	SCNDARY
				LDA	#$40
				STA	PAGE
				JMP	BKEYER
FROMEMO			JSR	SHODATA
				JMP	DESHOW	
SETUP			LDA	#$20
				STA	HAVEHAS
				LDA	#$0F
				STA	HAVEHAS+1
				LDY	#$00
				LDX	#$0F
LEGLOOP			LDA	LEGALS,X
				BPL	LEGSKIP
				TYA
				CLC
				SED
				ADC	#$01
				CLD
				TAY
LEGSKIP			DEX
				BPL	LEGLOOP
				TYA
				AND	#$0F
				STA	TEXT0+4
				LDA	#$0A
				STA	TEXT0+3
				TYA
				LSR	A
				LSR	A
				LSR	A
				LSR	A
				BEQ	PRESKIP
				STA	TEXT0+3
PRESKIP			CPY	#$01
				BNE	HAVER
				LDA	#$1D
				STA	HAVEHAS
				LDA	#$EE
				STA	HAVEHAS+1
HAVER			LDY	#$00	
				JSR	TEXTER	
				LDA	#GREEN
				STA	COLOR
				LDY	#$02
				JSR	LINER
				LDY	#$00
				JSR	DRAWCM1
				LDY	#$01
				LDX	CURSOR
				JSR	DRAW1
				LDY	#$02
DRAWCP1			LDA	CURSOR
				JSR	INCURS1
				JMP	DRAW1
DRAWCM1			LDA	CURSOR
				JSR	DECURS1
				JMP	DRAW1
DRAWCP2			LDA	CURSOR
				JSR	INCURS1
				JSR	INCURS1
				JMP	DRAW1
DRAWCM2			LDA	CURSOR
				JSR	DECURS1
				JSR	DECURS1
DRAW1			LDA	YPNTS,Y
				STA	TEXT1+2
				LDA	#$00
				STA	TEXT1+3	
				INX
				STX	TEXT1+4	
				CPX	#$0A
				BCC	LESS10
				LDA	#$01
				STA	TEXT1+3	
				TXA
				SEC
				SBC	#$0A
				STA	TEXT1+4	
LESS10			LDY	#$29
				LDA	LEGALS-1,X
				BEQ	DSKIP2
				BMI	DSKIP1
				LDY	#$0D
				BNE	DSKIP2
DSKIP1			LDY	#$17
DSKIP2			STY	TEXT1+6	
				LDY	#$01
				JMP	TEXTER
YPNTS			DFB	$1D,$27,$31
INCURS1			CLC
				ADC	#$01
				CMP	#$10
				BNE	INCOUT
				LDA	#$00
INCOUT			TAX
				RTS
DECURS1			SEC
				SBC	#$01
				BCS	DECOUT
				LDA	#$0F
DECOUT			TAX
				RTS
INITER			LDA	#$07
				STA	BITCONT
				LDA	#$01
				STA	BYTCONT
				LDA	#$00		
				STA	COUNTER
BBLOOP1			TAX	
				LDA	#$00
				STA	LEGALS,X
				LDX	BYTCONT
				LDA	TRACER,X
				LDX	BITCONT
				AND	MASKS,X
				BEQ	BBSKIP1
				LDX	COUNTER
				LDA	#$80
				STA	LEGALS,X
BBSKIP1			DEC	BITCONT
				BPL	BBSKIP2
				LDA	#$07
				STA	BITCONT
				DEC	BYTCONT
BBSKIP2			INC	COUNTER
				LDA	COUNTER
				CMP	#$10
				BNE	BBLOOP1
				LDA	#$00
				STA	COUNT
BBLOOP2			JSR	POINTIT
				LDY	#$3D
BBLOOP3			LDA	(CHARDL),Y
				AND	#$9F
				CMP	#$03
				BNE	BBSKIP4
				INY
				LDA	(CHARDL),Y
				DEY
				TAX
				AND	#$07
				CMP	#$05
				BNE	BBSKIP4
				TXA
				LSR	A
				LSR	A
				LSR	A
				TAX
				LDA	LEGALS,X
				BMI	BBSKIP3
				LDA	#$40
				STA	LEGALS,X
BBSKIP3			INY
				INY
				INY
				INY
				CPY	#$65
				BNE	BBLOOP3
BBSKIP4			INC	COUNT
				LDA	COUNT
				CMP	GRPNUMB
				BNE	BBLOOP2
				RTS
DEINIT			LDA	#$07
				STA	BITCONT
				LDA	#$01
				STA	BYTCONT
				LDA	#$00	
				STA	COUNTER
DILOOP1			TAX	
				LDA	LEGALS,X
				BPL	DISKIP1
				LDX	BYTCONT
				LDA	TRACER,X
				LDY	BITCONT
				ORA	MASKS,Y
				STA	TRACER,X
DISKIP1			DEC	BITCONT
				BPL	DISKIP2
				LDA	#$07
				STA	BITCONT
				DEC	BYTCONT
DISKIP2			INC	COUNTER
				LDA	COUNTER
				CMP	#$10
				BNE	DILOOP1
				RTS
MASKS			DFB	$80,$40,$20	
				DFB	$10,$08,$04	
				DFB	$02,$01	
SCROLDN			LDA	#$60
				STA	PAGPICK
				LDA	#$84
SDLOOP1			TAX
				LDA	LOBYTES,X
				STA	SCREENL
				STA	DESTINL
				LDA	HIBYTES,X
				ORA	PAGE
				STA	DESTINH
				EOR	#$60
				STA	SCREENH
				LDY	#$11
SDLOOP2			LDA	(SCREENL),Y
				STA	(DESTINL),Y
				INY
				CPY	#$17
				BNE	SDLOOP2
				INX
				CPX	#$91
				BEQ	SDOUT
				TXA
				PHA
				STA	LINENUM
				JSR	CARDSUB
				LDA	#$80
				JSR	WAIT
				PLA
				BNE	SDLOOP1
SDOUT			RTS
SCROLUP			LDA	#$60
				STA	PAGPICK
				LDA	#$90
SULOOP1			PHA
				STA	LINENUM
				JSR	CARDSUB
				LDA	#$80
				JSR	WAIT
				PLA
				SEC
				SBC	#$01
				CMP	#$83
				BNE	SULOOP1
				RTS
CARDER1			LDA	#$00
				STA	PAGPICK
				LDA	#$84
				STA	LINENUM
CARDSUB			LDX	#$00
CDLOOPA			LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	PAGE
				EOR	PAGPICK
				STA	SCREENH
				LDY	LINENUM
				LDA	LOBYTES,Y
				STA	DESTINL
				LDA	HIBYTES,Y
				ORA	PAGE
				STA	DESTINH
				LDY	#$11
CDLOOPB			LDA	(SCREENL),Y
				STA	(DESTINL),Y
				INY
				LDA	PAGPICK
				BEQ	CDSKIP1
				STA	CLICK
CDSKIP1			CPY	#$17
				BNE	CDLOOPB
				INX
				LDA	LINENUM
				INC	LINENUM
				CMP	#$90
				BNE	CDLOOPA
				RTS
CLRBOX			LDX	#$0A
CLLOOP1			LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	#$40
				STA	SCREENH
				LDY	#$03
CLLOOP2			LDA	#$00
				STA	(SCREENL),Y
				INY
				CPY	#$25
				BNE	CLLOOP2
				INX
				CPX	#$5D
				BNE	CLLOOP1
				RTS
PLATTO2			LDX	#$73	
PLOOP1			LDA	LOBYTES,X
				STA	SCREENL
				STA	DESTINL
				LDA	HIBYTES,X
				ORA	#$20
				STA	SCREENH
				EOR	#$60
				STA	DESTINH
				LDY	#$09	
PLOOP2			LDA	(SCREENL),Y
				STA	(DESTINL),Y
				INY	
				CPY	#$28
				BNE	PLOOP2
				INX
				CPX	#$C0
				BNE	PLOOP1
				RTS
LEGALS			DFB	$00,$00,$00
				DFB	$00,$00,$00
				DFB	$00,$00,$00
				DFB	$00,$00,$00
				DFB	$00,$00,$00
				DFB	$00
SHODATA			JSR	CLRBOX
SHOKEY			BIT	KEYBRD			;TEMP  
				BPL	SHOKEY			;TEMP
				BIT	UNSTROB			;TEMP
				RTS					;TEMP
TEXTER			TYA
				ASL	A
				TAY
				LDA	TEXTS,Y
				STA	POINTL
				LDA	TEXTS+1,Y
				STA	POINTH
				JMP	TEXTOUT
TEXTS			DW	TEXT0
				DW	TEXT1
LINER			TYA
				ASL	A
				TAY
				LDA	LINES,Y
				STA	POINTL
				LDA	LINES+1,Y
				STA	POINTH
				JMP	LINEOUT
LINES			DW	LINE0
				DW	LINE1
				DW	LINE2
PICKER			TYA
				ASL	A
				TAY
				LDA	PICKS,Y
				STA	TBLPNTL
				LDA	PICKS+1,Y
				STA	TBLPNTH
				JMP	EXPANDR
PICKS			DW	PFORM
				DW	CARD
				DW	HAND

				PUT	DC.DATA
				SAV	DC.COMPUTER
