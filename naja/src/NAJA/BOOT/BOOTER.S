********************************
*
*  BOOTER
*
********************************

SECTOR			EQU	$20
HOLDA			EQU	$36	
SCREEN1			EQU	$062C	
CODETAB			EQU	$0800	
BUFFER1			EQU	$BD00
BUFFER2			EQU	$BE00
TEXT			EQU	$C051
PRIMARY			EQU	$C054
DRIVOFF			EQU	$C088
READER			EQU	$C08C	
REBOOT			EQU	$C600
MERGE			EQU	$C6EF
ADDHED1			EQU	$DC	
ADDHED2			EQU	$FF	
ADDHED3			EQU	$CD	
DATHED1			EQU	$CD	
DATHED2			EQU	$FF	
DATHED3			EQU	$DC	
SYNCA			EQU	$FF
				ORG	$0800
				DFB	$01	
				SEI
				STX	$03F4
				CPX	#$60
				BEQ	SLOT6
				STA	DRIVOFF,X
				JSR	HOME
				LDY	#$1F
NO6OUT			LDA	NOBOOT6,Y
				STA	SCREEN1,Y
				DEY
				BPL	NO6OUT
				NOP
INFLOOP			JMP	INFLOOP	
NOBOOT6			DFB	$C4,$D2
				DFB	$C9,$D6,$C5	
				DFB	$A0,$CD,$D5
				DFB	$D3,$D4,$A0
				DFB	$C2,$C5,$A0
				DFB	$C2,$CF,$CF	
				DFB	$D4,$C5,$C4
				DFB	$A0,$C6,$D2	
				DFB	$CF,$CD,$A0
				DFB	$D3,$CC,$CF
				DFB	$D4,$A0,$B6
HOME			LDA	#$00	
				STA	MODPNT+1
				LDA	#$04
				STA	MODPNT+2
GOHOME			LDA	#$A0
MODPNT			STA	$0801	
				INC	MODPNT+1
				BNE	MODPNT
				INC	MODPNT+2
				LDA	MODPNT+2
				CMP	#$08	
				BNE	GOHOME
				LDA	PRIMARY
				LDA	TEXT
				RTS
DENIBLR			LDY	#$00
DNLOOP			LDX	#$56
DNLOOP2			DEX
				BMI	DNLOOP
				LDA	BUFFER1,Y
				LSR	BUFFER2,X
				ROL	A
				LSR	BUFFER2,X
				ROL	A
				STA	($21),Y
				INY
				BNE	DNLOOP2
				RTS
SLOT6			LDA	$0800
				CMP	#$01
				BEQ	GO	
				JMP	NOGO
GO				INC	$0800
				STA	$B0
				JMP	MERGE
				DFB	$A9,$EA,$8D		;*GARBAGE DATA 
				DFB	$AB,$03,$8D		;* 
				DFB	$FF,$03,$60		;* 
				DFB	$3F,$3E,$98
				DFB	$99,$3D,$3C	
				DFB	$9C,$3B,$3A	
				DFB	$39,$A0,$A1
				DFB	$A2,$A3,$A4
				DFB	$A5,$14,$13	
				DFB	$A8,$A9,$12	
				DFB	$11,$10,$0F	
				DFB	$0E,$0D,$B0
				DFB	$B1,$38,$37	
				DFB	$36,$35,$34	
				DFB	$33,$B8,$32	
				DFB	$31,$30,$2F	
				DFB	$2E,$2D,$2C	
				DFB	$C0,$C1,$C2
				DFB	$C3,$C4,$C5
				DFB	$C6,$C7,$C8
				DFB	$C9,$CA,$17	
				DFB	$CC,$CD,$16	
				DFB	$15,$D0,$D1
				DFB	$D2,$2B,$D4
				DFB	$2A,$29,$28	
				DFB	$D8,$27,$26	
				DFB	$25,$DC,$24	
				DFB	$23,$22,$E0
				DFB	$E1,$E2,$E3
				DFB	$E4,$21,$20	
				DFB	$1F,$E8,$1E	
				DFB	$1D,$1C,$1B	
				DFB	$1A,$19,$18	
				DFB	$F0,$F1,$0C	
				DFB	$0B,$0A,$09	
				DFB	$08,$07,$F8
				DFB	$06,$05,$04	
				DFB	$03,$02,$01	
				DFB	$00	
NOGO			JSR	HOME
				LDA	#$02	
				STA	SECTOR
AGAIN			LDA	#$60
				STA	$0300
				LDX	#$60
LOOPX0			LDA	READER,X
				BPL	LOOPX0	
				CMP	#SYNCA	
				BNE	LOOPX0	
LOOPX1			LDA	READER,X
				BPL	LOOPX1
				CMP	#SYNCA	
				BNE	LOOPX0	
LOOPX2			LDA	READER,X
				BPL	LOOPX2
				CMP	#SYNCA	
				BEQ	LOOPX2	
				BNE	CHECKA
LOOPA			LDA	READER,X
				BPL	LOOPA
CHECKA			CMP	#ADDHED1	
				BNE	LOOPX0	
				NOP
LOOPB			LDA	READER,X
				BPL	LOOPB
				CMP	#ADDHED2	
				BNE	CHECKA
				NOP	
LOOPC			LDA	READER,X
				BPL	LOOPC
				CMP	#ADDHED3	
				BNE	CHECKA
				LDA	#00
				STA	HOLDA	
LOOPD			LDA	READER,X
				BPL	LOOPD
				ROL	A
				STA	HOLDA
LOOPE			LDA	READER,X
				BPL	LOOPE
				AND	HOLDA
				CMP	SECTOR	
				BNE	LOOPX0	
				LDY	#$20
ERRCNT			DEY
				BEQ	LOOPX0	
LOOP1			LDA	READER,X
				BPL	LOOP1
CHECK1			EOR	#DATHED1	
				BNE	ERRCNT
				NOP
LOOP2			LDA	READER,X
				BPL	LOOP2
				CMP	#DATHED2	
				BNE	CHECK1
				LDY	#$56
LOOP3			LDA	READER,X
				BPL	LOOP3
				CMP	#DATHED3	
				BNE	CHECK1
				LDA	#$00
CHECK2			DEY
				STY	HOLDA
LOOP4			LDY	READER,X
				BPL	LOOP4
				EOR	CODETAB,Y
				LDY	HOLDA
				STA	BUFFER2,Y
				BNE	CHECK2
CHECK3			STY	HOLDA
LOOP5			LDY	READER,X
				BPL	LOOP5
				EOR	CODETAB,Y
				LDY	HOLDA
				STA	BUFFER1,Y
				INY
				BNE	CHECK3
LOOP6			LDY	READER,X
				BPL	LOOP6
				CMP	CODETAB,Y
				BEQ	NOERR	
				JMP	REBOOT
NOERR			LDA	#$00	
				STA	$21
PNT0			LDA	#$03
				STA	$22
				JSR	DENIBLR
PNT1			LDA	$03FD	
				BEQ	EXIT
PNT2			LDA	$03FE
				STA	SECTOR
PNT3			LDA	$03FF	
				PHA
				LDA	#$EA
				LDY	#$FD
NOPMORE			STA	$0300,Y
				INY
				BNE	NOPMORE
				PLA
				STA	PNT0+1	
				STA	PNT1+2
				STA	PNT2+2
				STA	PNT3+2
				STA	NOPMORE+2	
				JSR	$0300
				JMP	AGAIN
EXIT			RTS
				DFB	$60,$A8	
				DFB	$C3,$A9,$A0	
				DFB	$B1,$B9,$B8	
				DFB	$B3,$A0,$07	
				DFB	$AE,$13,$AE	
				DFB	$13,$AE	
				DFB	$60	
