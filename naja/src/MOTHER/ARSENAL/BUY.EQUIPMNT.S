BUY_EQUIPMNT	LDA	#$00
				STA	LINECNT
				JSR	CURSOR3
				LDA	#$00
				STA	STATUS
				JSR	STATUP
				LDY	DISPLAY
				DEY
				LDA	WANTED1
				STA	ON1,Y
				BIT	UNSTROB
BLINK3			LDA	#$10
				JSR	WAIT
				LDA	#$F0
				JSR	WAIT2
				BMI	KEYGOT3
				JSR	CURSOR3
				LDA	#$00
				JSR	WAIT2
				JSR	CURSOR3
GETAKEY			BIT	KEYBRD
				BPL	BLINK3
KEYGOT3			LDA	KEYBRD
				BIT	UNSTROB
				CMP	#$9B
				BEQ	GETOUT2
				JSR	CHECK_UP
				BEQ	GO_UP2
				JSR	CHECK_DOWN
				BEQ	GO_DOWN2
				JSR	CHECK_ENTER
				BEQ	ENTER2
				JMP	GETAKEY
GETOUT2			JMP	ARSOUT

GO_UP2			JSR	CURSOR3
				DEC	LINECNT
				BPL	UPDOWN2
				LDA	LEAVLIN
				STA	LINECNT
				JMP	UPDOWN2

GO_DOWN2		JSR	CURSOR3
				LDA	LINECNT
				INC	LINECNT
				CMP	LEAVLIN
				BNE	UPDOWN2
				LDA	#0
				STA	LINECNT
UPDOWN2			JSR	CURSOR3
				LDA	LINECNT
				CMP	LEAVLIN
				BNE	GOSUP2
				LDA	#0
				STA	COUNT
				JSR	STATUPA
				JMP	GETAKEY
GOSUP2			JSR	STATUP
				JMP	GETAKEY

ENTER2			LDA	LINECNT
				CMP	LEAVLIN
				BNE	NOLEAVN
				LDA	WANTED1
				SEC
				SBC	#$0A
				TAY
				LDA	WANTS,Y
				STA	WANTED1
				JSR	CURSOR3
				LDA	#BLACK1
				STA	COLOR
				JSR	DRASTAT
				LDA	#$00
				STA	STATUS
				JSR	DRAW_SCREEN
				JMP	MAIN_LOOP

WANTS			DB	$06,$06,$06
				DB	$07,$07,$07
				DB	$07,$07,$08
				DB	$08,$08

NOLEAVN			LDA	STATUS
				CMP	#$02
				BCC	:SKIPA
				JSR	BLINKEM
				JMP	GETAKEY
:SKIPA			LDA	WHOPIC
				JSR	POINTIT
				BIT	UNSTROB
				JSR	NOISE
				JSR	BLANKER
				LDY	#$09
				JSR	TEXTER
SPACANC			LDA	KEYBRD			;MAKE THIS A SUBROUTINE
				BPL	SPACANC
				BIT	UNSTROB
				JSR	CHECK_ENTER
				BEQ	BUYIT
				CMP	#"C"
				BNE	SPACANC
SPACOMN			JSR	GOSCOMN
				JMP	GETAKEY

BUYIT			JSR	OFF_CALC
				LDA	CODEOFF,X
				STA	DATPNTL
				LDA	CODEOFF+1,X
				STA	DATPNTH
				TYA
				ASL
				ASL
				TAX
				LDA	#$03
				STA	COUNT
TOPOSS			TXA
				TAY
				LDA	(DATPNTL),Y
				LDY	OPEN_SLOT
				STA	(CHARDL),Y
				INX
				INC	OPEN_SLOT
				DEC	COUNT
				BPL	TOPOSS
				LDX	#$67
				LDY	TEXT2A+2
				JSR	UPDATE_CREDIT
				JSR	OFF_CALC
				LDA	STCKOFF,X
				STA	DATPNTL
				LDA	STCKOFF+1,X
				STA	DATPNTH
				TYA
				JSR	STOCKER
				STY	SCRATCH
				TAY
				LDA	(DATPNTL),Y
				CMP	#$7F
				BEQ	GOGETA
				SED
				SEC
				SBC	#$01
				CLD
				STA	(DATPNTL),Y
				LDA	#$FF
				STA	STCKDIF
				TYA
				JSR	STOCKER
				CPY	SCRATCH
				BEQ	GOGETA
				TYA
				PHA
				LDY	SCRATCH
				JSR	TEXTER
				PLA
				TAY
				JSR	TEXTER
GOGETA			JSR	NOISE
				JMP	SPACOMN

GOSCOMN			JSR	BLANKER
				LDA	#$00
				STA	STATUS
				LDY	#$01
				JSR	TEXTER
				JSR	DONPE
				JMP	STATUP

BLANKER			LDX	#$A4
:LOOP1			LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	PAGE
				STA	SCREENH
				LDY	#$23
				LDA	#$00
:LOOP2			STA	(SCREENL),Y
				DEY
				CPY	#$03
				BNE	:LOOP2
				INX
				CPX	#$BF
				BNE	:LOOP1
				RTS

*-------------------------------

CURSOR3			LDY	LINECNT
				LDA	LINTABL,Y
				STA	YPNT
				LDA	#$05
				STA	XPNT
				LDA	#2
				JSR	SPCHARS
				LDA	LINECNT
				CMP	LEAVLIN
				BEQ	:EXIT
				LDA	#$9B
				STA	XPNT
				LDA	#3
				JSR	SPCHARS
				LDA	#$26
				STA	XCOORD
				LDA	#$03
				STA	SHIFT
				LDX	#6
:LOOP1			LDA	CURSOR_DATA+21,X
				STA	CHARTAB,X
				DEX
				BNE	:LOOP1
				JSR	CHAROUT
:EXIT			RTS

*-------------------------------
