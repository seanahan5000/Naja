				LST	OFF
**************************
*                        *
*     MONSTER EDITOR     *
*        PROGRAM         *
*                        *
*        12/21/85        *
*                        *
**************************

SCRATCH			EQU	$00	
SECTOR			EQU	$00
MINCURS			EQU	$01
MAXCURS			EQU	$02	
COUNTER			EQU	$03
SHIFT			EQU	$04
MAXTEXT			EQU	$05
SECOUNT			EQU	$05
XCOORD			EQU	$08	
YCOORD			EQU	$09	
CHOLD1			EQU	$0A	
CHOLD2			EQU	$0B	
CHOLD3			EQU	$0C	
DESTINL			EQU	$0D
DESTINH			EQU	$0E
POINTL			EQU	$12
POINTH			EQU	$13
SCREENL			EQU	$26
SCREENH			EQU	$27
YPNT			EQU	$93	
SSTOREL			EQU	$B1	
SSTOREH			EQU	$B2	
STRACK			EQU	$BA	
SSECTOR			EQU	$BB	
READNUM			EQU	$BC	
COUNT			EQU	$C0	
NUMBER			EQU	$C1	
XHOLD			EQU	$C2	
XSPOT			EQU	$C3	
SHELLNUM		EQU	$C4
SHELLPNT		EQU	$C5
MAXNUMB			EQU	$C6
LOADFLAG		EQU	$C7
COUNTX			EQU	$C8
COUNTY			EQU	$C9
DATPNTL			EQU	$CA	
DATPNTH			EQU	$CB	
CURSOR			EQU	$CC	
COLOR			EQU	$E4	
PAGE			EQU	$E6	
TEXTBUF			EQU	$0200	
HGR				EQU	$0300	
WAIT			EQU	$0389	
WAIT2			EQU	$0395	
WDATA			EQU	$0400	
RADRESS			EQU	$0498	
NIBBLER			EQU	$0557	
GETHEAD			EQU	$05AB	
SREADIT			EQU	$0669
FREADIT			EQU	$069D	
SREAD			EQU	$0717	
FREAD			EQU	$071E
TRACK			EQU	$0755	
LOBYTES			EQU	$0800
HIBYTES			EQU	$08C0
LINEOUT			EQU	$0980
TEXTOUT			EQU	$09E0
INPUT			EQU	$0A15
INPUT2			EQU	$0A1C
CHARTAB			EQU	$0AF4	
CHAROUT			EQU	$0AFB	
LINE			EQU	$0B33	
LINE2X			EQU	$0B3A
CONVERT			EQU	$0B5B
SHFTCLC			EQU	$0BA1	
TABLER			EQU	$0BD6	
HPLOT			EQU	$0D43
ARMOVE			EQU	$0E50	
CLEAR1			EQU	$11AA	
CLEAR2			EQU	$1212
RDKEY			EQU	$6000	
NOSCAN			EQU	$6653	
BUFF64			EQU	$8000
CLEARSUB		EQU	$BAD4
KEYBRD			EQU	$C000	
UNSTROB			EQU	$C010	
PRIMARY			EQU	$C054
SCNDARY			EQU	$C055
DRIVOFF			EQU	$C0E8
DRIVEON			EQU	$C0E9	
GREEN			EQU	$2A	
VIOLET			EQU	$55	
WHITE1			EQU	$7F
				ORG	$8800
				BIT	UNSTROB
				LDA	#$00
				STA	LOADFLAG
				LDX	#$3F
				LDA	#$00
CLOOP64			STA	BUFF64,X
				DEX
				BPL	CLOOP64
REINPUT			JSR	CLEAR1
				LDY	#$11
				JSR	TEXTER
				LDA	#$02
				STA	MAXTEXT
				JSR	INPUT2
				LDA	TEXTBUF
				ASL	A
				ASL	A
				ASL	A
				ASL	A
				STA	SCRATCH
				LDA	TEXTBUF+1
				AND	#$0F
				ORA	SCRATCH
				STA	SHELLNUM
				LDX	#$04
SHELOOP			CMP	SHELLS,X
				BEQ	GOTSHELL
				DEX
				BPL	SHELOOP
				BMI	REINPUT
GOTSHELL		STX	SHELLPNT
				LDA	MAXNUMS,X
				STA	MAXNUMB
				JSR	CLEARSUB
				LDA	#$01	
				STA	NUMBER
				JSR	TOBUFFR
				JSR	CLEAR1
				LDY	#$00
				JSR	TEXTER
				LDA	#$28
				STA	YPNT
				LDA	#$D6
				STA	XHOLD
				LDA	#$01
				JSR	SPCHARS
				LDA	#$30
				STA	YPNT
				LDA	#$D1
				STA	XHOLD
				LDA	#$02
				JSR	SPCHARS
				JSR	ALLDATA
				LDA	#GREEN
				STA	COLOR
				LDY	#$00
				JSR	LINER
				LDY	#$03
				JSR	TEXTER
				JSR	SHOWNUM
				LDA	#VIOLET
				STA	COLOR
				LDY	#$01
				JSR	LINER
				LDY	#$04
				JSR	TEXTER
				LDA	#$00
				STA	CHOLD1
				STA	CHOLD2
				STA	CHOLD3
				INC	CHOLD1
				INC	CHOLD2
SCREEN1			LDA	#$20
				STA	PAGE
				LDA	CHOLD1
				STA	CURSOR
				LDA	#$8D
				STA	XHOLD
				JSR	CDRAW1
				LDA	#$01
				STA	MINCURS
				LDA	#$17	
				STA	MAXCURS
REKEY1			JSR	KEYSUB1
				CMP	#$88
				BEQ	LRCOMN1
				CMP	#$95
				BNE	NOTLR
LRCOMN1			PHA
				JSR	CDRAW1
				LDA	CURSOR	
				STA	CHOLD1
				JSR	ALLDATA
				JSR	SHOWNUM
				JSR	FROMBUF
				PLA
				CMP	#$95
				BEQ	FORWARD
				DEC	NUMBER
				BNE	LRCOMN2
				LDA	MAXNUMB
				STA	NUMBER
				BNE	LRCOMN2
FORWARD			LDA	NUMBER
				INC	NUMBER
				CMP	MAXNUMB
				BCC	LRCOMN2
				LDA	#$01
				STA	NUMBER
LRCOMN2			JSR	SHOWNUM
				JSR	TOBUFFR
				JSR	ALLDATA
				JMP	SCREEN1
NOTLR			CMP	#$D2	
				BNE	NOTREDO
				JSR	ARESURE
				CPX	#$FF
				BEQ	GOREK1
				JSR	ALLDATA
				JSR	TOBUFFR
				JSR	ALLDATA
				LDA	#$01	
				STA	CURSOR
				JSR	CDRAW1
GOREK1			JMP	REKEY1
NOTREDO			CMP	#$C5
				BNE	NOEXIT
				LDY	#$06
				JSR	TEXTER
				JSR	ARESURE
				TXA
				PHA
				LDY	#$06
				JSR	TEXTER
				PLA
				CMP	#$FF
				BEQ	GOREK1
				JMP	EXITLOAD
NOEXIT			CMP	#$CC
				BNE	NOLOADS
				JSR	ARESURE
				CPX	#$FF
				BEQ	GOREK1
				JSR	ALLDATA
				JSR	LOADSUB
				LDA	#$FF
				STA	LOADFLAG
				JSR	TOBUFFR
				JSR	ALLDATA
				LDA	#$01	
				STA	CHOLD1
				JMP	SCREEN1
NOLOADS			CMP	#$D3
				BNE	NOSAVE
				JSR	ARESURE
				CPX	#$FF
				BEQ	GOREK1
				JSR	FROMBUF
				JSR	SAVESUB
				JMP	SCREEN1
NOSAVE			CMP	#$CA
				BNE	NOJUMP
				JSR	CDRAW1
				LDY	#$08
				JSR	TEXTER
				LDA	#$02
				STA	MAXTEXT
				JSR	INPUT2
				JSR	PRECON
				LDA	#$FC
				LDY	#$78
				JSR	LINE
				JSR	CNVERT
				CPX	#$FF
				BEQ	BADJUMP
				CMP	MAXNUMB
				BEQ	OKJUMP
				BCS	BADJUMP
				CMP	#$00
				BEQ	BADJUMP
OKJUMP			PHA
				LDY	#$08
				JSR	TEXTER
				JSR	SHOWNUM
				JSR	FROMBUF
				JSR	ALLDATA
				PLA
				STA	NUMBER
				JSR	SHOWNUM
				JSR	TOBUFFR
				JSR	ALLDATA
				LDA	#$01	
				STA	CURSOR
				JSR	CDRAW1
				JMP	REKEY1
BADJUMP			LDY	#$08
				JSR	TEXTER
				JSR	CDRAW1
				JMP	REKEY1
NOJUMP			CMP	#$A0
				BEQ	ENTER	
				CMP	#$9B
				BNE	NOESC
				JMP	REINPUT
NOESC			CMP	#$D4
				BNE	NOTVIEW
				BIT	LOADFLAG
				BPL	NOTVIEW
				JSR	TVDRAW
				BIT	UNSTROB
TVKEY			BIT	KEYBRD
				BPL	TVKEY
				BIT	UNSTROB
				JSR	TVERASE
				JMP	REKEY1
NOTVIEW			JMP	REKEY1
ENTER			LDA	#$02
				STA	MAXTEXT
				LDA	#$A8
				STA	XSPOT	
				LDA	CURSOR
				CMP	#$01
				BEQ	NAMEIN
				CMP	#$13	
				BEQ	OTHRSUB
				CMP	#$14	
				BEQ	GOFIGHT	
				CMP	#$17	
				BEQ	OTHRSUB
				CMP	#$0D	
				BNE	GONORML
				LDA	#$04
				STA	MAXTEXT
				LDA	#$B4	
				STA	XSPOT	
GONORML			JMP	NORMSUB
GOFIGHT			JMP	SCREEN2
NAMEIN			JSR	NAMESUB
				LDA	#$10
				STA	MAXTEXT
				LDA	#$96
				LDY	#$08
				JSR	INPUT
				LDY	#$00
NALOOP1			LDA	TEXTBUF,Y
				CMP	#$8D
				BEQ	FORGETN
				CMP	#$A0	
				BNE	OKAY1
				INY
				BNE	NALOOP1
OKAY1			LDY	#$00
NALOOP2			LDA	TEXTBUF,Y
				CMP	#$8D
				BEQ	OKAY2
				JSR	CONVERT
				TXA
				STA	BUFF64,Y
				INY
				BNE	NALOOP2
OKAY2			CPY	#$10
				BEQ	OKAY3
				LDA	#$0A
				STA	BUFF64,Y
				INY
				BNE	OKAY2
OKAY3			JMP	REKEY1
FORGETN			JSR	NAMESUB
				JMP	REKEY1
OTHRSUB			JMP	GROUPING
NORMSUB			JSR	GREATER
				JSR	INPUT2
				JSR	PRECON
				LDY	CURSOR
				LDA	TABLE8,Y
				TAY
				LDA	XSPOT	
				CLC
				ADC	#$06
				JSR	LINE
				JSR	CNVERT
				PHA
				TXA
				PHA
				JSR	GREATER
				PLA
				TAX
				PLA
				CPX	#$FF
				BEQ	FORGET
				PHA
				LDA	MAXTEXT
				CMP	#$04
				BNE	GOOD1
				LDA	TEXTBUF+2
				STA	TEXTBUF
				LDA	TEXTBUF+3
				STA	TEXTBUF+1
				JSR	CNVERT
				CPX	#$FF
				BNE	GOOD2
				PLA
FORGET			JMP	REKEY1
GOOD2			PHA
				JSR	ONEDATA
				LDY	CURSOR
				LDA	CURCOR1,Y
				TAX
				INX
				PLA
				STA	BUFF64,X
				DEX
				PLA
				STA	BUFF64,X
				JMP	COMN12
GOOD1			JSR	ONEDATA
				LDY	CURSOR
				LDA	CURCOR1,Y
				TAX
				PLA
				STA	BUFF64,X
COMN12			JSR	ONEDATA
				JMP	REKEY1
GREATER			LDA	#$32
				STA	TEXTBUF
				LDA	#$8D
				STA	TEXTBUF+1
				LDY	CURSOR
				LDA	TABLE8,Y
				TAY
				LDA	XSPOT	
				JMP	LINE
PRECON			LDY	MAXTEXT
				DEY
PRELOOP			LDA	TEXTBUF,Y
				CMP	#$8D
				BEQ	PRESKIP
				JSR	CONVERT
				TXA
PRESKIP			STA	TEXTBUF,Y
				DEY
				BPL	PRELOOP
				RTS
CNVERT			LDA	TEXTBUF
				JSR	DIGITS
				ASL	A
				ASL	A
				ASL	A
				ASL	A
				STA	SCRATCH
				LDA	TEXTBUF+1
				JSR	DIGITS
				ORA	SCRATCH
				LDX	#$00
				RTS
DIGITS			CMP	#$8D
				BEQ	BADIGIT
				CMP	#$0A	
				BEQ	BADIGIT	
				BCC	NUMDIG	
				CMP	#$11
				BCS	BADIGIT
				SEC	
				SBC	#$01	
NUMDIG			RTS
BADIGIT			PLA
				PLA
				LDX	#$FF
				RTS
NAMESUB			LDX	#$0F	
NAMLOOP			LDA	BUFF64,X
				STA	TEXTBUF,X
				DEX
				BPL	NAMLOOP
				LDA	#$8D
				STA	TEXTBUF+16	
				LDA	#$96
				LDY	#$08
				JMP	LINE
ALLDATA			JSR	NAMESUB
				LDA	#$02	
				STA	CURSOR
ALLOOP			JSR	ONEDATA
				INC	CURSOR
				LDA	CURSOR
				CMP	#$17	
				BNE	ALLOOP
				RTS
SHOWNUM			LDX	#$00
				LDA	NUMBER			
				JSR	BYTESUB
				LDA	#$8D
				STA	TEXTBUF,X
				LDA	#$0C
				LDY	#$08
				JMP	LINE
ONEDATA			LDY	CURSOR
				LDA	CURCOR1,Y
				STA	COUNT
				LDA	CURCOR1+1,Y
				STA	SCRATCH
				LDX	#$00
ODLOOP1			LDY	COUNT
				LDA	BUFF64,Y
				JSR	BYTESUB
				INC	COUNT
				LDA	COUNT
				CMP	SCRATCH
				BEQ	ODSKIP1
				CMP	#$1C	
				BEQ	ODLOOP1
				LDA	#$0A
				STA	TEXTBUF,X
				INX
				BNE	ODLOOP1
ODSKIP1			LDA	#$8D
				STA	TEXTBUF,X
				LDY	CURSOR
				LDA	TABLE8,Y
				TAY
				LDA	#$96
				JMP	LINE
BYTESUB			PHA
				LSR	A
				LSR	A
				LSR	A
				LSR	A
				JSR	BYTE1
				STA	TEXTBUF,X
				INX
				PLA
				AND	#$0F
				JSR	BYTE1
				STA	TEXTBUF,X
				INX
				RTS
BYTE1			CMP	#$0A
				BCC	BYTEOUT
				CLC
				ADC	#$01
BYTEOUT			RTS
ARESURE			JSR	CDRAW1
				LDY	#$07
				JSR	TEXTER
				BIT	UNSTROB
AREKEY			BIT	KEYBRD
				BPL	AREKEY
				LDA	KEYBRD
				BIT	UNSTROB
				CMP	#$CE
				BEQ	NOKEY
				CMP	#$D9
				BNE	AREKEY
				LDY	#$07
				JSR	TEXTER
				LDX	#$00
				RTS
NOKEY			LDY	#$07
				JSR	TEXTER
				JSR	CDRAW1
				LDX	#$FF
				RTS
GROUPING		JSR	CLEAR2
				STA	SCNDARY
				JSR	CDRAW1
				LDA	#$40
				STA	PAGE
				LDY	#$12
				JSR	TEXTER
				LDA	#$00
				STA	COUNTER
GROLOOP			JSR	DRAWBYTE
				INC	COUNTER
				LDA	COUNTER
				CMP	#$13
				BNE	GROLOOP
				LDA	#$00
				STA	CURSOR
				JSR	CDRAW2
GETBLINK		LDA	#$10
				JSR	WAIT
				LDA	#$F0
				JSR	WAIT2
				BMI	KEYHIT2
				JSR	CDRAW2
				LDA	#$00
				JSR	WAIT2
				JSR	CDRAW2
GETKEY2			LDA	KEYBRD
				BPL	GETBLINK
KEYHIT2			BIT	UNSTROB
				CMP	#$88
				BEQ	GOLEFT
				CMP	#$95
				BEQ	GORIGHT
				CMP	#$A0
				BEQ	ENTERIT
				CMP	#$CC
				BNE	GETKEY2
				LDA	CHOLD1
				STA	CURSOR
				LDA	#$20
				STA	PAGE
				STA	PRIMARY
				JMP	SCREEN1
GOLEFT			JSR	CDRAW2
				DEC	CURSOR
				BPL	GOL1
				LDA	#$12
				STA	CURSOR
GOL1			JSR	CDRAW2
				JMP	GETKEY2
GORIGHT			JSR	CDRAW2
				INC	CURSOR
				LDA	CURSOR
				CMP	#$13
				BNE	GOR1
				LDA	#$00
				STA	CURSOR
GOR1			JSR	CDRAW2
				JMP	GETKEY2
ENTERIT			LDA	#$02
				STA	MAXTEXT
				LDX	CURSOR
				LDA	XCOORDS,X
				LDY	#$40
				CPX	#$0A
				BCC	GOINPUT
				LDY	#$5C
GOINPUT			JSR	INPUT
				JSR	PRECON
				LDX	CURSOR
				LDA	XCOORDS,X
				LDY	#$40
				CPX	#$0A
				BCC	GOLINED
				LDY	#$5C
GOLINED			JSR	LINE
				JSR	CNVERT
				INX
				BEQ	NOWAY
				PHA
				LDA	CURSOR
				JSR	DRAWBYTE
				LDX	CURSOR
				PLA
				STA	BUFF64+45,X
				TXA
				JSR	DRAWBYTE
				JMP	GORIGHT
NOWAY			JMP	GETKEY2
DRAWBYTE		TAX
				LDA	XCOORDS,X
				PHA
				LDA	BUFF64+45,X
				STX	SCRATCH
				LDX	#$00
				JSR	BYTESUB
				LDX	SCRATCH
				LDA	#$8D
				STA	TEXTBUF+2
				PLA
				LDY	#$48
				CPX	#$0A
				BCC	GOLINE
				LDY	#$64
GOLINE			JMP	LINE
XCOORDS			DFB	$3F,$4E,$5D
				DFB	$6C,$7B,$8A
				DFB	$99,$A8,$B7
				DFB	$C6
				DFB	$3F,$4E,$5D
				DFB	$6C,$7B,$8A
				DFB	$99,$A8,$B7
SCREEN2			JSR	CLEAR2
				JSR	CDRAW1
				LDA	#$40
				STA	PAGE
				LDY	#$04	
				JSR	TEXTER
				LDA	#GREEN
				STA	COLOR
				LDY	#$00
				JSR	LINER
				JSR	SHOWNUM
				STA	SCNDARY
				LDA	#$01	
				STA	MINCURS
				LDA	#$17
				STA	MAXCURS
				LDA	#$20
				STA	PAGE
				JSR	ONEDATA
				LDA	CURSOR
				STA	CHOLD1
				LDA	#$40
				STA	PAGE
				LDY	#$01
				JSR	TEXTER
				LDA	#VIOLET
				STA	COLOR
				LDY	#$02
				JSR	LINER
				JSR	ALLYENO
				LDA	#$F9
				STA	XHOLD
				LDA	CHOLD2
				STA	CURSOR
				JSR	CDRAW1
REKEY2			JSR	KEYSUB1
KEYGAN			CMP	#$CC
				BEQ	BACK2
				CMP	#$A0
				BEQ	ENTER2
GOREKEY			JSR	GETKEY1
				JMP	KEYGAN
BACK2			JSR	CDRAW1
				LDA	CURSOR
				STA	CHOLD2
				LDA	CHOLD1
				STA	CURSOR
				LDA	#$20
				STA	PAGE
				JSR	ONEDATA
				STA	PRIMARY
				JMP	SCREEN1
ENTER2			LDX	#$00
				LDY	CURSOR	
				CPY	#$0E
				BEQ	WIERD2	
				INX
				CPY	#$12
				BEQ	WIERD2	
				INX
				CPY	#$13
				BEQ	TEMP2A
				CPY	#$17
				BEQ	WIERD2	
				INX
				CPY	#$01
				BEQ	WIERD2
				INX
				CPY	#$14
				BEQ	WIERD2
				JSR	ONEYENO
				LDY	CURSOR
				JSR	CALCSUB
				LDA	BUFF64,X
				EOR	MASKS,Y
				STA	BUFF64,X
				JSR	ONEYENO
TEMP2A			JMP	REKEY2
WIERD2			TXA
				PHA
				JSR	ONEYENO
				LDY	CURSOR
				JSR	CALCSUB
				PLA
				STA	SCRATCH
				LDA	BUFF64,X
				LDX	SCRATCH
				PHA
				AND	BLOCKS,X
				CLC
				ADC	ADDS,X
				AND	BLOCKS,X
				TAY
				LDA	BLOCKS,X
				EOR	#$FF
				STA	SCRATCH
				PLA
				AND	SCRATCH
				STA	SCRATCH
				TYA
				ORA	SCRATCH
				PHA
				LDY	CURSOR
				JSR	CALCSUB
				PLA
				STA	BUFF64,X
				JSR	ONEYENO
				JMP	REKEY2
ALLYENO			LDA	#$01	
				STA	CURSOR
ALLOOP2			JSR	ONEYENO
				LDA	CURSOR
				INC	CURSOR
				CMP	#$17
				BNE	ALLOOP2
				RTS
ONEYENO			LDX	#$00
				LDY	CURSOR
				CPY	#$0E
				BEQ	WIERD1	
				INX
				CPY	#$12
				BEQ	WIERD1	
				INX
				CPY	#$13
				BEQ	TEMP2B
				CPY	#$17
				BEQ	WIERD1	
				INX
				CPY	#$01
				BEQ	WIERD3
				INX
				CPY	#$14
				BEQ	WIERD1	
				LDA	TABLE8,Y
				STA	TEXT9+2
				STA	TEXTA+2
				JSR	CALCSUB
				LDA	MASKS,Y
				LDY	#$09	
				AND	BUFF64,X
				BNE	ISYES
				INY
ISYES			JMP	TEXTER
TEMP2B			RTS
WIERD3			LDA	BUFF64+39	
				ROL	A
				ROL	A
				ROL	A
				AND	#$03
				TAY
				LDA	TDATAS+9,Y
				TAY
				LDX	CURSOR
				LDA	TABLE8,X
				STA	TEXTC+2
				STA	TEXTD+2
				STA	TEXTF+2
				STA	TEXT10+2
				JMP	TEXTER
WIERD1			STX	SCRATCH
				JSR	VALUSUB
				LDY	SCRATCH
				CLC
				ADC	TEXOFFS,Y
				TAY
				JMP	TEXTSUB
TEXOFFS			DFB	$0F,$10,$0B
				DFB	$14,$0F
VALUSUB			LDY	CURSOR
				JSR	CALCSUB
				LDA	BUFF64,X
				LDX	SCRATCH
				AND	BLOCKS,X	
				PHA
				LDA	SHIFTS,X
				TAY
				PLA
VALOOP1			DEY
				BMI	VALOUT
				LSR	A	
				JMP	VALOOP1
VALOUT			RTS
BLOCKS			DFB	$03,$18,$03
				DFB	$C0,$C0
SHIFTS			DFB	$00,$03,$00
				DFB	$06,$06
ADDS			DFB	$01,$08,$01
				DFB	$40,$40
TEXTSUB			LDA	TDATAS-11,Y
				BMI	TSPECS
				STA	TEXTBUF
				LDA	#$8D
				STA	TEXTBUF+1
				LDA	#$25
				STA	XCOORD
				LDA	#$02
				STA	SHIFT
				LDY	CURSOR
				LDA	TABLE8,Y
				STA	YCOORD
				JMP	LINE2X
TSPECS			LDX	CURSOR
				LDA	TABLE8,X
				STA	TEXTB+2
				STA	TEXTE+2
				JMP	TEXTER
TDATAS			DFB	$FF,$01,$02
				DFB	$FF,$00,$01
				DFB	$02,$03,$04
				DFB	$0C,$0D,$0F
				DFB	$10
CALCSUB			LDA	CURCOR2,Y
				PHA
				LSR	A
				LSR	A
				LSR	A
				LSR	A
				CLC
				ADC	#$27
				TAX
				PLA
				AND	#$0F
				TAY
				RTS
KEYSUB1			LDA	#$10
				JSR	WAIT
				LDA	#$F0
				JSR	WAIT2
				BMI	KEYHIT1
				JSR	CDRAW1
				LDA	#$00
				JSR	WAIT2
				JSR	CDRAW1
GETKEY1			BIT	KEYBRD
				BPL	KEYSUB1
				LDA	KEYBRD
KEYHIT1			BIT	UNSTROB
				CMP	#$C1
				BEQ	UP1
				CMP	#$DA
				BEQ	DOWN1
				LDX	#$09
KEYLOOP			CMP	LETTERS,X
				BEQ	OUT1
				DEX
				BPL	KEYLOOP
				BMI	GETKEY1
OUT1			RTS
LETTERS			DFB	$A0,$88,$95
				DFB	$D3,$CA,$CC
				DFB	$C5,$D2,$9B
				DFB	$D4
UP1				JSR	CDRAW1
				LDA	CURSOR
				DEC	CURSOR
				CMP	MINCURS
				BNE	UP2
				LDA	MAXCURS
				STA	CURSOR
UP2				JSR	CDRAW1
				JMP	GETKEY1
DOWN1			JSR	CDRAW1
				LDA	CURSOR
				INC	CURSOR
				CMP	MAXCURS
				BNE	DOWN2
				LDA	MINCURS	
				STA	CURSOR
DOWN2			JSR	CDRAW1
				JMP	GETKEY1
TVERASE			LDX	#$5E
TVELOOP1		LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	#$20
				STA	SCREENH
				LDY	#$1C
				LDA	#$00
TVELOOP2		STA	(SCREENL),Y
				INY
				CPY	#$28
				BNE	TVELOOP2
				INX
				CPX	#$96
				BNE	TVELOOP1
				RTS
TVDRAW			LDA	#$5E
				STA	COUNTY
				LDA	#WHITE1
				STA	COLOR
VLOOP			LDA	#$CA
				STA	COUNTX
HLOOP			LDA	COUNTY
				LDX	COUNTX
				LDY	#$00
				CPX	#$80
				BCS	GODOT
				INY
GODOT			JSR	HPLOT
				LDA	COUNTX
				CLC
				ADC	#$0F
				STA	COUNTX
				CMP	#$24
				BNE	HLOOP
				LDA	COUNTY
				CLC
				ADC	#$0B
				STA	COUNTY
				CMP	#$A0
				BNE	VLOOP
				LDA	#$CB
				STA	XHOLD
				LDA	#$5F
				STA	YPNT
				LDA	DATPNTL
				CLC
				ADC	#$40
				STA	MODPNT+1
				LDA	DATPNTH
				ADC	#$00
				STA	MODPNT+2
				LDY	#$2B
				LDA	(DATPNTL),Y
				STA	COUNTER
				PHA
				INY
				LDA	(DATPNTL),Y
				PHA
				AND	#$0F
				CLC
				ADC	YPNT
				STA	YPNT
				PLA
				LSR	A
				LSR	A
				LSR	A
				LSR	A
				CLC
				ADC	XHOLD
				STA	XHOLD
				PHA
				LDX	#$00
				STX	POINTL
CLOOPY2			LDY	#$00
CLOOPX2			LDX	POINTL
MODPNT			LDA	$6000,X
				BPL	INCX
				AND	#$7F
				CPY	#$06
				BNE	NOINCX
INCX			INC	POINTL
NOINCX			STA	CHARTAB,Y
				INY
				CPY	#$07
				BNE	CLOOPX2
				LDA	YPNT
				STA	YCOORD
				LDA	XHOLD
				STA	XCOORD
				JSR	TABLER
				JSR	SHFTCLC
				JSR	CHAROUT
				LDA	COUNTER
				SEC
				SBC	#$10
				STA	COUNTER
				AND	#$F0
				BEQ	ROWED
				LDA	XHOLD
				CLC
				ADC	#$07
				STA	XHOLD
				BNE	CLOOPY2
ROWED			PLA
				TAX
				STX	XHOLD
				LDA	YPNT
				CLC
				ADC	#$07
				STA	YPNT
				PLA
				SEC
				SBC	#$01
				STA	COUNTER
				AND	#$0F
				BEQ	CHOUT
				LDA	COUNTER
				PHA
				TXA
				PHA
				JMP	CLOOPY2
CHOUT			LDA	#$8D
				STA	XHOLD
				RTS
CDRAW2			LDY	CURSOR
				LDA	#$51
				CPY	#$0A
				BCC	CSKIP2
				LDA	#$6D
CSKIP2			STA	YPNT
				LDA	XHOLD
				PHA
				LDA	XCOORDS,Y
				CLC
				ADC	#$02
				STA	XHOLD
				LDA	#$03
				JSR	SPCHARS
				PLA
				STA	XHOLD
				RTS
CDRAW1			LDY	CURSOR	
				LDA	TABLE8,Y
				STA	YPNT
				LDA	#$00
SPCHARS			TAX
				LDA	SPOFFS,X
				TAX
				LDY	#$00
MORSP			LDA	SPDATA,X
				STA	CHARTAB,Y
				INX
				INY
				CPY	#$07
				BNE	MORSP
				LDA	YPNT
				STA	YCOORD
				LDA	XHOLD
				STA	XCOORD
				JSR	TABLER
				JSR	SHFTCLC
				JMP	CHAROUT
SPOFFS			DFB	$00,$07,$0E	
				DFB	$15
SPDATA			DFB	$03,$07,$0F
				DFB	$3F,$0F,$07
				DFB	$03
				DFB	$00,$0C,$18
				DFB	$7F,$18,$0C
				DFB	$00
				DFB	$00,$18,$0C
				DFB	$7F,$0C,$18
				DFB	$00
				DFB	$08,$1C,$3E
				DFB	$7F,$7F,$7F
				DFB	$7F
TOBUFFR			JSR	PNTODAT	
				LDY	#$3F
TOBLOOP			LDA	(DATPNTL),Y
				STA	BUFF64,Y
				DEY
				BPL	TOBLOOP
				RTS
FROMBUF			JSR	PNTODAT
				LDY	#$3F
FROLOOP			LDA	BUFF64,Y
				STA	(DATPNTL),Y
				DEY
				BPL	FROLOOP
				RTS
PNTODAT			LDA	SHELLPNT
				ASL	A
				TAY
				LDA	DATAS,Y
				STA	DATPNTL
				LDA	DATAS+1,Y
				STA	DATPNTH
				LDA	NUMBER
				SEC
				SBC	#$01
				ASL	A
				TAY
				LDA	(DATPNTL),Y
				CLC
				ADC	#$60
				PHA
				INY
				LDA	(DATPNTL),Y
				STA	DATPNTL
				DEY
				PLA
				STA	DATPNTH
				CPY	#$4A
				BCC	PNTOUT
				LDA	DATPNTH
				CLC
				ADC	#$10
				STA	DATPNTH
PNTOUT			RTS
DATAS			DA	DATDAT17
				DA	DATDAT15
				DA	DATDAT13
				DA	DATDAT11
				DA	DATDAT9
LOADSUB			LDA	#$60
				STA	SSTOREH
				LDA	#$0A
				STA	STRACK
				LDA	#$00
				STA	SSECTOR
				LDA	#$10
				STA	READNUM
				JSR	SREADIT
				LDA	SHELLPNT
				BNE	LCHUNK1
				LDA	#$79
				STA	SSTOREH
				LDA	#$01
				STA	STRACK
				LDA	#$09
				STA	SSECTOR
				LDA	#$07
				STA	READNUM
				JSR	FREAD
LCHUNK1			STA	DRIVOFF
				RTS
SAVESUB			LDA	#$60
				STA	SSTOREH
				LDA	#$0A
				STA	STRACK
				LDA	#$00
				STA	SSECTOR
				LDA	#$10
				STA	READNUM
				JSR	WRITER
				LDA	SHELLPNT
				BNE	SCHUNK1
				STA	DRIVEON
				LDA	#$79
				STA	SSTOREH
				LDA	#$01	
				STA	STRACK
				LDA	#$09
				STA	SSECTOR
				LDA	#$07
				STA	READNUM
				JSR	FWRITE
				STA	DRIVOFF
SCHUNK1			RTS
WRITER			LDA	#$60
				STA	FREADIT
				JSR	SREADIT
				LDA	#$A9
				STA	FREADIT
FWRITE			LDA	STRACK
				ASL	A
				JSR	ARMOVE
				LDA	SSTOREL	
				STA	DESTINL	
				LDA	SSTOREH	
				STA	DESTINH	
				LDA	SSECTOR
				STA	SECTOR
				LDA	READNUM
				STA	SECOUNT
				JSR	GETHEAD
WLOOP			LDA	DESTINL
				STA	$3E
				LDA	DESTINH
				STA	$3F
				JSR	NIBBLER
				JSR	RADRESS
				JSR	WDATA
				INC	DESTINH
				INC	SECTOR
				LDA	SECTOR
				CMP	#$10
				BNE	NOTNEXT
				LDA	#$00
				STA	SECTOR
				LDA	TRACK
				CLC
				ADC	#$02
				JSR	ARMOVE
				JSR	GETHEAD
NOTNEXT			DEC	SECOUNT
				BNE	WLOOP
				LDA	DRIVOFF
				RTS
MAXNUMS			DFB	$34,$23,$1D
				DFB	$17,$13
SHELLS			DFB	$17,$15,$13
				DFB	$11,$09
CURCOR1			DFB	$FF
				DFB	$00,$10,$11
				DFB	$12,$13,$14
				DFB	$15,$16,$17
				DFB	$18,$19,$1A
				DFB	$1B,$1D,$1E
				DFB	$1F,$20,$21
				DFB	$22,$27,$2B	
				DFB	$2C,$2D
CURCOR2			DFB	$00,$01,$02
				DFB	$03,$04,$05
				DFB	$06,$07,$10
				DFB	$11,$12,$13
				DFB	$14,$15,$16
				DFB	$20,$21,$22
				DFB	$23,$25,$30
				DFB	$32,$34,$36
MASKS			DFB	$80,$40,$20
				DFB	$10,$08,$04	
				DFB	$02,$01
TABLE8			DFB	$00
				DFB	$08,$10,$18
				DFB	$20,$28,$30
				DFB	$38,$40,$48
				DFB	$50,$58,$60
				DFB	$68,$70,$78
				DFB	$80,$88,$90
				DFB	$98,$A0,$A8
				DFB	$B0,$B8
DATDAT17		DFB	$01,$00
				DFB	$01,$78	
				DFB	$01,$D4	
				DFB	$02,$22	
				DFB	$02,$96	
				DFB	$03,$16	
				DFB	$03,$79	
				DFB	$03,$F1	
				DFB	$04,$7F	
				DFB	$04,$DB	
				DFB	$05,$37	
				DFB	$05,$A3	
				DFB	$05,$FF	
				DFB	$06,$5B	
				DFB	$06,$FE	
				DFB	$07,$6A	
				DFB	$07,$C0
				DFB	$08,$1C	
				DFB	$08,$6A	
				DFB	$08,$D4	
				DFB	$09,$46	
				DFB	$09,$A2	
				DFB	$09,$FE	
				DFB	$0A,$6A	
				DFB	$0A,$C6	
				DFB	$0B,$5A	
				DFB	$0B,$CE	
				DFB	$0C,$32	
				DFB	$0C,$8E	
				DFB	$0C,$F8	
				DFB	$0D,$78	
				DFB	$0D,$D4	
				DFB	$0E,$30	
				DFB	$0E,$7E	
				DFB	$0E,$DA	
				DFB	$0F,$30		
				DFB	$0F,$9C	
				DFB	$09,$00	
				DFB	$09,$88	
				DFB	$0A,$1C	
				DFB	$0A,$86	
				DFB	$0A,$E2	
				DFB	$0B,$5A	
				DFB	$0B,$D2	
				DFB	$0C,$60	
				DFB	$0C,$E8	
				DFB	$0D,$44	
				DFB	$0D,$AE	
				DFB	$0E,$0A	
				DFB	$0E,$7C	
				DFB	$0E,$E0	
				DFB	$0F,$3C	
DATDAT15		DFB	$00,$00
				DFB	$00,$6B
				DFB	$01,$11
				DFB	$01,$67
				DFB	$01,$F7
				DFB	$02,$60
				DFB	$02,$B6
				DFB	$02,$FC
				DFB	$03,$84
				DFB	$03,$EF
				DFB	$04,$79
				DFB	$05,$04
				DFB	$05,$5A
				DFB	$05,$C6
				DFB	$06,$1A
				DFB	$06,$84
				DFB	$06,$E6
				DFB	$07,$50
				DFB	$07,$BA
				DFB	$08,$3D
				DFB	$08,$B2
				DFB	$09,$3D
				DFB	$09,$A7
				DFB	$0A,$38
				DFB	$0A,$89
				DFB	$0A,$EF
				DFB	$0B,$57
				DFB	$0B,$C3
				DFB	$0C,$53
				DFB	$0C,$B9
				DFB	$0D,$0A
				DFB	$0D,$70
				DFB	$0D,$C6
				DFB	$0E,$1C
				DFB	$0E,$83
DATDAT13		DFB	$00,$00
				DFB	$00,$8C
				DFB	$00,$D6
				DFB	$01,$4A
				DFB	$01,$BB
				DFB	$02,$25
				DFB	$02,$8F
				DFB	$02,$F0
				DFB	$03,$3E
				DFB	$03,$8C
				DFB	$03,$E0
				DFB	$04,$49
				DFB	$04,$9B
				DFB	$04,$F1
				DFB	$05,$76
				DFB	$05,$E7
				DFB	$06,$53
				DFB	$06,$99
				DFB	$06,$FF
				DFB	$07,$7D
				DFB	$07,$D3
				DFB	$08,$27
				DFB	$08,$8D
				DFB	$09,$0B
				DFB	$09,$61
				DFB	$09,$C8
				DFB	$0A,$2F
				DFB	$0A,$82
				DFB	$0A,$EB
DATDAT11		DFB	$00,$00
				DFB	$00,$6C
				DFB	$00,$D6
				DFB	$01,$73
				DFB	$01,$DB
				DFB	$02,$49
				DFB	$02,$97
				DFB	$02,$FB
				DFB	$03,$88
				DFB	$03,$DB
				DFB	$04,$30
				DFB	$04,$7E
				DFB	$04,$D4
				DFB	$05,$6A
				DFB	$05,$DE
				DFB	$06,$46
				DFB	$06,$F6
				DFB	$07,$88
				DFB	$08,$12
				DFB	$08,$7B
				DFB	$08,$D0
				DFB	$09,$58
				DFB	$09,$AA
DATDAT9			DFB	$00,$00
				DFB	$00,$82
				DFB	$01,$0E
				DFB	$01,$64
				DFB	$01,$C7
				DFB	$02,$54
				DFB	$02,$A7
				DFB	$02,$FD
				DFB	$03,$8A
				DFB	$04,$02
				DFB	$04,$58
				DFB	$05,$D8
				DFB	$06,$40
				DFB	$06,$A2
				DFB	$06,$F5
				DFB	$07,$47
				DFB	$07,$93
				DFB	$07,$E7
				DFB	$08,$2E
EXITLOAD		LDA	#$60
				STA	SSTOREH
				LDA	#$15
				STA	STRACK
				LDA	#$00
				STA	SSECTOR
				LDA	#$30
				STA	READNUM
				JSR	SREAD
				JSR	HGR
				JSR	NOSCAN
				JMP	RDKEY
				PUT	!EDITOR.DATA
