SORTER			LDX	GRPNUMB
MORCOR			DEX
				TXA
				STA	CORNUMB,X
				BNE	MORCOR
				JSR	POSILOD
				LDX	GRPNUMB
COPOS			DEX
				LDA	POSITS,X
				STA	POSITS2,X
				CPX	#$00
				BNE	COPOS
				LDX	GRPNUMB
				DEX					;Only sort if more than
				BEQ	SORTED			; one character in the group
NOTDONE			LDA	#$00
				STA	rSWFLAG
				LDX	#$00
				DEC	GRPNUMB	
KEEPGON			INC	GRPNUMB
				LDA	POSITS2,X
				PHA
				AND	#$0F	
				STA	rSCRACH2
				INX	
				LDA	POSITS2,X
				AND	#$0F	
				CMP	rSCRACH2
				BEQ	SAMHIGH
				PLA
				BCC	SORTING
SWITCHN			LDY	POSITS2,X
				LDA	POSITS2-1,X
				STA	POSITS2,X
				TYA
				STA	POSITS2-1,X
				LDY	CORNUMB,X
				LDA	CORNUMB-1,X
				STA	CORNUMB,X
				TYA
				STA	CORNUMB-1,X
				LDA	#$FF
				STA	rSWFLAG
				BNE	SORTING
SAMHIGH			PLA
				AND	#$F0	
				STA	rSCRACH2
				LDA	POSITS2,X
				AND	#$F0	
				CMP	rSCRACH2
				BCC	SWITCHN	
SORTING			DEC	GRPNUMB
				CPX	GRPNUMB
				BNE	KEEPGON
				INC	GRPNUMB
				LDA	rSWFLAG
				BNE	NOTDONE
SORTED			JSR	POSILOD
				LDX	GRPNUMB
MORCOR2			DEX
				TXA
				STA	CORNUM2,X
				BNE	MORCOR2
				LDA	#$00
				STA	COUNT
GOFLIP			LDY	COUNT
				LDA	CORNUMB,Y
				LDY	GRPNUMB
				DEY
FINDCOR			CMP	CORNUM2,Y
				BEQ	GOTHE1
				DEY
				BPL	FINDCOR
GOTHE1			CPY	COUNT
				BEQ	NOFLIP
				LDX	COUNT
				LDA	CORNUM2,X
				PHA		
				LDA	CORNUM2,Y
				STA	CORNUM2,X
				PLA		
				STA	CORNUM2,Y
				JSR	FLPDATA
NOFLIP			INC	COUNT
				LDA	COUNT
				CMP	GRPNUMB
				BNE	GOFLIP
				RTS

FLPDATA			TXA
				PHA
				TYA
				JSR	POINTIT
				LDA	CHARDL
				STA	DESTINL
				LDA	CHARDH
				STA	DESTINH
				PLA
				JSR	POINTIT
				LDY	#$7F
RFLIPPER		LDA	(CHARDL),Y
				TAX
				LDA	(DESTINL),Y
				STA	(CHARDL),Y
				TXA
				STA	(DESTINL),Y
				DEY
				BPL	RFLIPPER
				RTS

POSILOD			LDA	#0
				STA	COUNT
:LOOP1			JSR	POINTIT
				LDY	#position
				LDA	(CHARDL),Y
				PHA
				LDX	COUNT
				LDA	POSITS,X
				STA	(CHARDL),Y
				PLA
				STA	POSITS,X
				INC	COUNT
				LDA	COUNT
				CMP	GRPNUMB
				BNE	:LOOP1
				RTS

*** THESE COULD BE MOVED OUT OF HERE
POSITS			DB	$00,$00,$00
				DB	$00,$00,$00
				DB	$00
POSITS2			DB	$00,$00,$00
				DB	$00,$00,$00
				DB	$00
CORNUMB			DB	$00,$00,$00
				DB	$00,$00,$00
				DB	$00
CORNUM2			DB	$00,$00,$00
				DB	$00,$00,$00
				DB	$00
********************************

REFORMAT		LDA	#$40
				STA	PAGE
				JSR	CLEAR2
				LDA	#GREEN
				STA	COLOR
				LDY	#$00
				JSR	RFLINER
				LDY	#$01
				JSR	RFLINER
				LDY	#$02
				JSR	RFLINER
				LDA	#VIOLET
				STA	COLOR
				LDY	#$03
				JSR	RFLINER
				JSR	LETSIDE
				LDA	#WHITE1
				STA	COLOR
				LDY	#$00
				JSR	RTEXTER
				LDY	#$02
				JSR	RTEXTER
				LDY	#$03
				JSR	RTEXTER
				LDY	#$06
				JSR	RTEXTER
				JSR	LRARROW
				JSR	PICPOSI
				JSR	SORTER
				JSR	DOGRID
				LDA	#$82
				STA	SELECT
				LDA	#$02
				STA	YPNT
				JSR	HEADER
				LDA	#$00
				STA	COUNT
NOTHLIN			LDY	COUNT	
				LDA	YPOINTS,Y
				STA	YPNT
				TYA
				JSR	STATLIN
				INC	COUNT
				LDA	COUNT
				CMP	GRPNUMB
				BNE	NOTHLIN
MLOOP			JSR	POSILOD	
				LDA	POSITS
				STA	CURSOR
				LDA	#$00
				STA	rARRCURS
				JSR	ARROW
				JSR	CURSIT1	
				BIT	UNSTROB
				STA	SCNDARY
				JMP	MOVKEY
BLINK			LDA	#$10
				JSR	WAIT
				LDA	#$F0
				JSR	WAIT2
				BMI	GOTAKEY
				JSR	CURSIT1	
				JSR	ARROW
				LDA	#$00
				JSR	WAIT2	
				JSR	CURSIT1	
				JSR	ARROW
MOVKEY			LDA	KEYBRD
				BPL	BLINK
GOTAKEY			BIT	UNSTROB
				JSR	CHECK_UP
				BEQ	YLEFT1
				JSR	CHECK_LEFT
				BNE	NOLEFTS	
YLEFT1			JMP	LEFTONE	
NOLEFTS			JSR	CHECK_RIGHT
				BEQ	YRIGHT1
				JSR	CHECK_DOWN
				BNE	NORGHTS	
YRIGHT1			JMP	RFRIGHT1
NORGHTS			CMP	#$CC
				BEQ	LEAFIT
				JSR	CHECK_ENTER
				BNE	NOTSPAC	
				JMP	SPACEIT
NOTSPAC			CMP	#$C6
				BNE	MOVKEY
				JMP	FLPSTAT
LEAFIT			LDA	CURSOR	
				PHA
				LDA	#$15
				STA	CURSOR
				JSR	SCANLIN
				CMP	#$FF
				BEQ	CANGOE	
				LDY	#$06
				JSR	RTEXTER
				LDY	#$03
				JSR	RTEXTER
				LDY	#$05
				JSR	RTEXTER
				LDY	#$05	
MORTIME			LDA	#$00
				JSR	WAIT2
				BMI	LESTIME
				DEY
				BPL	MORTIME
LESTIME			BIT	UNSTROB
				LDY	#$06
				JSR	RTEXTER
				LDY	#$05
				JSR	RTEXTER
				LDY	#$03
				JSR	RTEXTER
				PLA	
				STA	CURSOR
				JMP	MOVKEY
CANGOE			JSR	ARROW
				JSR	POSILOD
				PLA
				JSR	CURSIT
				LDA	#$01
				JMP	CAMP	
FLPSTAT			JSR	POSILOD
				JSR	EXFLIP
				JSR	POSILOD
				JMP	MOVKEY	
LEFTONE			LDA	CURSOR
				PHA
GOGOL			JSR	GOLEFT
				JSR	LOOKFOR
				CMP	#$FF
				BNE	GOGOL
				BEQ	COMUDLR
RFRIGHT1		LDA	CURSOR
				PHA
GOGOR			JSR	GORGHT
				JSR	LOOKFOR
				CMP	#$FF
				BNE	GOGOR
COMUDLR			PLA
				JSR	CURSIT
				JSR	CURSIT1	
				JSR	ARROW
				LDA	CURSOR
				JSR	LOOKFOR
				STX	rARRCURS
				JSR	ARROW
				JMP	MOVKEY

SPACEIT			JSR	ARROW
				JSR	MARKER
				LDY	#$00	
				JSR	RTEXTER
				LDY	#$01
				JSR	RTEXTER
				LDY	#$03
				JSR	RTEXTER
				LDY	#$04
				JSR	RTEXTER
				LDA	CURSOR
				STA	rSOURCE
				AND	#$F0
				CMP	#$50
				BEQ	ATLEFT
				LDA	CURSOR
				CLC
				ADC	#$10
				STA	CURSOR
				BNE	GOCURSE
ATLEFT			LDA	CURSOR
				SEC
				SBC	#$10
				STA	CURSOR
GOCURSE			JSR	CURSIT
				LDA	CURSOR
				JSR	LOOKFOR
				STA	rFLPFLAG
				BIT	UNSTROB
				JMP	MOVKEY2	
BLINK2			LDA	#$10
				JSR	WAIT
				LDA	#$F0
				JSR	WAIT2
				BMI	GOTKEY2
				JSR	CURSIT1	
				LDA	#$00
				JSR	WAIT2	
				JSR	CURSIT1	
MOVKEY2			LDA	KEYBRD
				BPL	BLINK2	
GOTKEY2			BIT	UNSTROB
				JSR	CHECK_LEFT
				BEQ	LEFT2
				JSR	CHECK_RIGHT
				BEQ	RIGHT2
				JSR	CHECK_UP
				BEQ	RFUP2
				JSR	CHECK_DOWN
				BEQ	RFDOWN2
				CMP	#$C3
				BEQ	CANCELD	
				JSR	CHECK_ENTER
				BEQ	SPACIT2
				CMP	#$C6
				BNE	MOVKEY2
				JSR	POSILOD
				JSR	EXFLIP	
				JSR	POSILOD
				JMP	MOVKEY2	
LEFT2			LDA	CURSOR
				PHA
NOTLET1			JSR	GOLEFT2	
				CMP	rSOURCE
				BEQ	NOTLET1
				BNE	COMUD2
RIGHT2			LDA	CURSOR
				PHA
NOTLET2			JSR	GORGHT2	
				CMP	rSOURCE
				BEQ	NOTLET2
				BNE	COMUD2
RFUP2			LDA	CURSOR
				PHA
NOTLET3			JSR	GOUP2	
				CMP	rSOURCE
				BEQ	NOTLET3
				BNE	COMUD2
RFDOWN2			LDA	CURSOR
				PHA
NOTLET4			JSR	GODOWN2	
				CMP	rSOURCE
				BEQ	NOTLET4
COMUD2			JSR	LOOKFOR
				STA	rFLPFLAG
				PLA
				JSR	CURSIT
				JSR	CURSIT1	
				JMP	MOVKEY2
CANCELD			JSR	CURSIT1	
				LDA	rSOURCE
				STA	CURSOR
				JSR	MARKER
				JSR	ARROW
				LDY	#$01
				JSR	RTEXTER
				LDY	#$00
				JSR	RTEXTER
				LDY	#$04
				JSR	RTEXTER
				LDY	#$03
				JSR	RTEXTER
				JMP	MOVKEY
SPACIT2			JSR	MARKER	
				LDA	rSOURCE
				JSR	CURSIT
				JSR	CURSIT1	
				BIT	SELECT
				BPL	NOTXY1
				JSR	XYSOURC
NOTXY1			LDA	CURSOR
				JSR	LOOKFOR
				STX	rYHOLD1
				LDA	rSOURCE
				JSR	LOOKFOR
				STX	XPNT
				LDA	rFLPFLAG
				BPL	ONLY1IN
				BIT	SELECT
				BPL	NOTXY2
				JSR	XYCURSR
NOTXY2			LDX	XPNT
				LDY	rYHOLD1
				LDA	POSITS,X
				PHA
				LDA	POSITS,Y
				STA	POSITS,X
				PLA
				STA	POSITS,Y
				JSR	POSILOD
				JSR	FLPVIEW
				LDX	XPNT
				LDY	rYHOLD1
				JSR	FLPDATA
				LDX	XPNT
				LDY	rYHOLD1
				JSR	FLIPLIN
				JMP	BACK2IT
ONLY1IN			LDX	XPNT
				LDA	CURSOR
				STA	POSITS,X
				JSR	POSILOD
				LDA	XPNT
				JSR	POINTIT
				LDY	#profrace
				LDA	(CHARDL),Y
				LSR
				LSR
				LSR
				LSR
				PHA
				LDA	rSOURCE
				JSR	FROM11B
				PLA
				PHA
				JSR	TVIEWIT
				LDA	CURSOR
				JSR	FROM11B
				PLA
				JSR	TVIEWIT
				JSR	SORTER
				LDX	#$00
:LOOP1			STX	COUNT
				LDA	CORNUM2,X
				CMP	COUNT
				BNE	OUTORD
				INX
				CPX	GRPNUMB
				BNE	:LOOP1
				JMP	BACK2IT
OUTORD			TXA
				PHA
				BPL	OLOOP
MORORD2			STX	COUNT
				LDA	CORNUM2,X
				CMP	COUNT
				BEQ	GOTEND1
OLOOP			INX
				CPX	GRPNUMB
				BNE	MORORD2
GOTEND1			DEX
				TXA
				TAY
				PLA
				TAX
				LDA	rSOURCE
				AND	#$0F
				STA	rYHOLD1
				LDA	CURSOR
				AND	#$0F
				CMP	rYHOLD1
				BCC	CLUMPU
				BNE	CLUMPD
				LDA	rSOURCE
				AND	#$F0	
				STA	rYHOLD1
				LDA	CURSOR
				AND	#$F0	
				CMP	rYHOLD1
				BCC	CLUMPD
CLUMPU			JSR	MCLUMP2
				JMP	BACK2IT
CLUMPD			JSR	MCLUMP
				JMP	BACK2IT
MCLUMP			LDA	#$07	
				STA	COUNT
				LDA	YPOINTS,X
				STA	rYHOLD1
				LDA	YPOINTS,Y
				CLC
				ADC	#$07	
				STA	rYHOLD2
GOMOVE1			LDA	rYHOLD2
				STA	LINENUM
				JSR	MOVER1
MOVER2			LDY	LINENUM
				JSR	RSUBB
				DEY
				JSR	RSUBA
				JSR	LINMOVE
				DEC	LINENUM
				LDA	LINENUM
				CMP	rYHOLD1
				BNE	MOVER2
				LDY	rYHOLD1
				JSR	RSUBB
				JSR	RSUBA2
				JSR	LINMOVE
				DEC	COUNT
				BMI	CLOUT
				JMP	GOMOVE1
CLOUT			RTS
MCLUMP2			LDA	#$07
				STA	COUNT
				LDA	YPOINTS,X
				STA	rYHOLD2
				LDA	YPOINTS,Y
				CLC
				ADC	#$07
				STA	rYHOLD1
GOMOVE2			LDA	rYHOLD2
				STA	LINENUM
				JSR	MOVER1
MOVER2B			LDY	LINENUM
				JSR	RSUBB
				INY
				JSR	RSUBA
				JSR	LINMOVE
				INC	LINENUM
				LDA	LINENUM
				CMP	rYHOLD1
				BNE	MOVER2B
				LDY	rYHOLD1
				JSR	RSUBB
				JSR	RSUBA2
				JSR	LINMOVE
				DEC	COUNT
				BPL	GOMOVE2
				RTS
MOVER1			LDY	rYHOLD2
				JSR	RSUBA
				JSR	RSUBB2
				JMP	LINMOVE
BACK2IT			LDY	#$01
				JSR	RTEXTER
				LDY	#$00
				JSR	RTEXTER
				LDY	#$04
				JSR	RTEXTER
				LDY	#$03
				JSR	RTEXTER
				JMP	MLOOP

XYSOURC			LDA	rSOURCE
				JSR	LOOKFOR
				LDA	YPOINTS,X
				STA	YPNT
				LDA	rSOURCE
				JSR	DOXY2
				LDA	#$71
				LDY	YPNT
				JSR	LINE
				LDA	CURSOR
				JSR	DOXY2
				LDA	#$71
				LDY	YPNT
				JMP	LINE

XYCURSR			LDA	CURSOR
				JSR	LOOKFOR
				LDA	YPOINTS,X
				STA	YPNT
				LDA	CURSOR
				JSR	DOXY2
				LDA	#$71
				LDY	YPNT
				JSR	LINE
				LDA	rSOURCE
				JSR	DOXY2
				LDA	#$71
				LDY	YPNT
				JMP	LINE

FLPVIEW			LDA	XPNT
				JSR	POINTIT
				LDY	#$0D
				LDA	(CHARDL),Y
				PHA
				LDA	rSOURCE
				JSR	FROM11B
				PLA
				LSR
				LSR
				LSR
				LSR
				PHA
				JSR	TVIEWIT
				LDA	rYHOLD1
				JSR	POINTIT
				LDY	#$0D
				LDA	(CHARDL),Y
				PHA
				LDA	rSOURCE
				JSR	FROM11B
				PLA
				LSR
				LSR
				LSR
				LSR
				PHA
				JSR	TVIEWIT
				LDA	CURSOR
				JSR	FROM11B
				PLA
				JSR	TVIEWIT
				LDA	CURSOR
				JSR	FROM11B
				PLA
				JMP	TVIEWIT

FLIPLIN			LDA	#$06	
				STA	COUNT
				LDA	YPOINTS,X
				STA	rYHOLD1
				PHA
				LDA	YPOINTS,Y
				STA	rYHOLD2
				PHA
ONEOF7			JSR	RSUBB2
				LDA	rYHOLD2
				CLC
				ADC	#$06
				TAY
				JSR	RSUBA
				JSR	LINMOVE
				LDA	rYHOLD2
				CLC
				ADC	#$05
				STA	rYSORC
				STA	rYDEST
				INC	rYDEST
TRANLOP			LDY	rYSORC
				JSR	RSUBA
				LDY	rYDEST
				JSR	RSUBB
				JSR	LINMOVE
				LDA	rYSORC
				CMP	rYHOLD2
				BNE	NOTGAP1
				LDA	rYHOLD1
				CLC
				ADC	#$06
				STA	rYSORC
				BNE	NOTGAP2	
NOTGAP1			CMP	rYHOLD1
				BEQ	BACKFRM
				DEC	rYSORC
				LDA	rYDEST
				CMP	rYHOLD2
				BNE	NOTGAP2
				LDA	rYHOLD1
				CLC
				ADC	#$06
				STA	rYDEST
				BNE	TRANLOP
NOTGAP2			DEC	rYDEST
				JMP	TRANLOP
BACKFRM			JSR	RSUBA2
				LDY	rYHOLD1
				JSR	RSUBB
				JSR	LINMOVE
				PLA
				STA	rYHOLD2
				PLA
				PHA
				STA	rYHOLD1
				LDA	rYHOLD2
				PHA
				DEC	COUNT
				BMI	FLIPPED
				JMP	ONEOF7
FLIPPED			PLA
				PLA
				RTS
GOLEFT			LDA	CURSOR
				AND	#$F0
				CMP	#$10	
				BNE	LEFT1A
				LDA	CURSOR
				AND	#$0F
				CMP	#$05
				BNE	LEFT1B
				LDA	#$51
				STA	CURSOR
				BNE	LEFTOUT
LEFT1A			LDA	CURSOR
				SEC
				SBC	#$10
				STA	CURSOR
				BCS	LEFTOUT
LEFT1B			CLC
				ADC	#$01
				ORA	#$50
				STA	CURSOR
LEFTOUT			RTS
GORGHT			LDA	CURSOR
				AND	#$F0
				CMP	#$50
				BNE	RIGHT1A
				LDA	CURSOR
				AND	#$0F
				CMP	#$01
				BNE	RIGHT1B
				LDA	#$15
				STA	CURSOR
				BNE	RGHTOUT
RIGHT1A			LDA	CURSOR
				CLC
				ADC	#$10
				STA	CURSOR
				BNE	RGHTOUT
RIGHT1B			SEC
				SBC	#$01
				ORA	#$10
				STA	CURSOR
RGHTOUT			RTS
GOLEFT2			LDA	CURSOR
				AND	#$F0
				CMP	#$10
				BNE	NLEDGE
				LDA	CURSOR
				AND	#$0F
				ORA	#$50
				STA	CURSOR
				BNE	LOUT2
NLEDGE			LDA	CURSOR
				SEC
				SBC	#$10
				STA	CURSOR
LOUT2			RTS
GORGHT2			LDA	CURSOR
				AND	#$F0
				CMP	#$50
				BNE	NREDGE
				LDA	CURSOR
				AND	#$0F
				ORA	#$10
				STA	CURSOR
				BNE	ROUT2
NREDGE			LDA	CURSOR
				CLC
				ADC	#$10
				STA	CURSOR
ROUT2			RTS	
GOUP2			LDA	CURSOR
				AND	#$0F
				CMP	#$05
				BNE	NOTOPD2
				LDA	CURSOR
				AND	#$F0
				ORA	#$01
				STA	CURSOR
				BNE	GOUPOUT
NOTOPD2			LDA	CURSOR
				CLC
				ADC	#$01
				STA	CURSOR
GOUPOUT			RTS
GODOWN2			LDA	CURSOR
				AND	#$0F
				CMP	#$01
				BNE	NOTBOT2
				LDA	CURSOR
				AND	#$F0
				ORA	#$05
				STA	CURSOR
				BNE	GODOUT
NOTBOT2			LDA	CURSOR
				SEC
				SBC	#$01
				STA	CURSOR
GODOUT			RTS

LOOKFOR			LDX	GRPNUMB
:LOOP1			DEX
				CMP	POSITS,X
				BEQ	:SKIPA
				CPX	#$00
				BNE	:LOOP1
				RTS
:SKIPA			LDA	#$FF
				RTS

SCANLIN			LDA	CURSOR
				JSR	LOOKFOR
				CMP	#$FF
				BEQ	SCANOUT
				LDA	CURSOR
				AND	#$F0
				CMP	#$50
				BEQ	NONHERE
				LDA	CURSOR
				CLC
				ADC	#$10
				STA	CURSOR
				BNE	SCANLIN
NONHERE			LDA	#$00
SCANOUT			RTS
RSUBA2			LDY	#$00
				JSR	RSUBA
				LDA	SCREENH	
				EOR	#$60
				STA	SCREENH	
				RTS
RSUBB2			LDY	#$00
				JSR	RSUBB
				LDA	DESTINH	
				EOR	#$60
				STA	DESTINH	
				RTS
RSUBA			LDA	LOBYTES,Y
				STA	SCREENL
				LDA	HIBYTES,Y
				ORA	#$40
				STA	SCREENH
				RTS
RSUBB			LDA	LOBYTES,Y
				STA	DESTINL
				LDA	HIBYTES,Y
				ORA	#$40
				STA	DESTINH
				RTS

CURSIT1			LDA	CURSOR
CURSIT			JSR	FROM11B
				LDA	XPNTS,X
				STA	XCOORD
				DEC	XCOORD
				LDA	YPNTS,Y	
				CLC
				ADC	#$08	
				STA	YCOORD
				JSR	SHFTCLC
				JSR	TABLER
				LDA	#$00
				STA	COUNT
NEXCHAR			LDX	COUNT
				JSR	CHAR02
				INC	XCOORD
				LDA	COUNT
				INC	COUNT
				CMP	#$02
				BNE	NEXCHAR
				RTS

CHAR02			LDA	COFF,X
				TAX
				LDY	#$00
MORCUR			LDA	CDATA,X
				CPY	#$03
				BCC	NOTZERO
				LDA	#$00
NOTZERO			STA	CHARTAB,Y
				INX
				INY
				CPY	#$07
				BNE	MORCUR
				JMP	CHAROUT
COFF			DB	$00,$03,$06
CDATA			DB	$03,$07,$7E
				DB	$00,$40,$7F
				DB	$03,$03,$01

MARKER			LDX	rARRCURS
				LDA	YPOINTS,X
				STA	YPNT
				LDA	#$06	
				STA	XPNT
				LDA	#$04
				JMP	SPCHARS

ARROW			LDX	rARRCURS
				LDA	YPOINTS,X
				TAY
				LDX	#$03
				JMP	ARROWER

LRARROW			LDA	#$77
				STA	YPNT	
				LDA	#$CB	
				STA	XPNT
				LDA	#$00
				JSR	SPCHARS
				LDA	#$9B
				STA	XPNT
				LDA	#$01
				JMP	SPCHARS

PICPOSI			JSR	POSILOD
				LDY	#0
				STY	COUNT
PICANEW			LDA	POSITS,Y
				BNE	NOZERO
				JSR	NEWPICR
NOZERO			INC	COUNT
				LDY	COUNT
				CPY	GRPNUMB
				BNE	PICANEW
				CPY	#1
				BEQ	PICEXIT
ALLOVER			LDA	#0
				STA	rSTART
COMLOP2			LDY	rSTART
				LDA	POSITS,Y	
				INY	
COMLOOP			CMP	POSITS,Y	
				BNE	RNOSAME2
				JSR	NEWPICR
				JMP	ALLOVER
RNOSAME2		INY
				CPY	GRPNUMB
				BNE	COMLOOP
				INC	rSTART
				LDA	GRPNUMB
				SEC
				SBC	#$01
				CMP	rSTART
				BNE	COMLOP2
PICEXIT			JMP	POSILOD

NEWPICR			JSR	RNDMIZE
				LDA	RANDOM1
				AND	#$33
				CLC
				ADC	#$22
				STA	POSITS,Y
				RTS

DOGRID			LDA	#$53
				STA	rCOUNTY
VLOOP			LDA	#$18
				STA	rCOUNTX
HLOOP			LDA	rCOUNTY
				LDX	rCOUNTX
				LDY	#$00
				JSR	HPLOT
				LDA	rCOUNTX
				CLC
				ADC	#$0F
				STA	rCOUNTX
				CMP	#$54
				BNE	HLOOP
SKIPONE			LDA	rCOUNTY
				CLC
				ADC	#$0B
				STA	rCOUNTY
				CMP	#$7F
				BEQ	SKIPONE
				CMP	#$B6
				BNE	VLOOP
				LDA	#$00
				STA	COUNT
MORTOPS			JSR	POINTIT
				LDY	#profrace
				LDA	(CHARDL),Y
				PHA
				JSR	FROM11	
				PLA
				LSR
				LSR
				LSR
				LSR
				JSR	TVIEWIT
				INC	COUNT
				LDA	COUNT
				CMP	GRPNUMB
				BNE	MORTOPS
				RTS

LETSIDE			LDA	#$8D
				STA	TEXTBUF+1
				LDA	#$14
				STA	TEXTBUF
				LDA	#$4B
				STA	rLINECNT
MORLETS			LDA	#$00
				LDY	rLINECNT
				JSR	LINE
				LDA	rLINECNT
				CLC
				ADC	#$0B
				STA	rLINECNT
				LDA	TEXTBUF
				DEC	TEXTBUF
				CMP	#$0B
				BNE	MORLETS
				LDA	#$01
				STA	TEXTBUF
				LDA	#$0E
				STA	rLINECNT
MORNBOT			LDA	rLINECNT
				LDY	#$B9
				JSR	LINE
				LDA	rLINECNT
				CLC
				ADC	#$0F
				STA	rLINECNT
				LDA	TEXTBUF
				INC	TEXTBUF
				CMP	#$05
				BNE	MORNBOT
				RTS

RTEXTER			JSR	BANK_2
				TYA
				ASL
				TAY
				LDA	RTEXTS,Y
				STA	POINTL
				LDA	RTEXTS+1,Y
				STA	POINTH
				JSR	TEXTOUT
				JMP	BANK_1

RFLINER			JSR	BANK_2
				TYA
				ASL
				TAY
				LDA	RLINES,Y
				STA	POINTL
				LDA	RLINES+1,Y
				STA	POINTH
				JSR	LINEOUT
				JMP	BANK_1
