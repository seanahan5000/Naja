CHANGE_NAME		JSR	CLRBOTL
				LDA	GCOUNT
				STA	CURSOR
				LDA	#$01	
				STA	RFLAG
CHANGE2			LDY	#$09
				JSR	TEXTER
				LDY	#$12
				JSR	TEXTER
				JSR	RDOWN2
				CPX	#$FF
				BNE	NOCANCC
				DEC	HCURSOR
				JMP	SEER	
NOCANCC			JSR	CLRBOTL
				LDY	#$0F
				JSR	TEXTER
				LDY	CURSOR
				DEY
				JSR	POINTR2
				LDY	#$09
DONAME			LDA	(CHARPTR),Y
				AND	#$3F
				STA	TEXTBUF,Y
				DEY
				BPL	DONAME
				JSR	NAMEND
				JSR	LINE2
				LDA	XCOORD
				STA	TEXT10
				LDA	SHIFT
				STA	TEXT10+1
				LDA	YCOORD
				STA	TEXT10+2
				LDY	#$10
				JSR	TEXTER
				LDA	#$0A
				STA	MAXTEXT
REINPUT			JSR	INPUT2
SPACHCK			LDA	TEXTBUF
				CMP	#$8D
				BNE	NOCEXIT
				JSR	CLRBOTL
				JSR	CURSAD
				JMP	CHANGE2
NOCEXIT			JSR	CHECK_ENTER
				BNE	NOTSPAC
				STX	HOLDIT
				JSR	CHAROUT
				JSR	RECALL
				JMP	SPACHCK
NOTSPAC			JSR	CONVRTR
				LDY	#$00
NSPAC2			LDA	TEXTBUF,Y
				INY
				CMP	#$8D
				BNE	NSPAC2
				DEY
				LDA	#$0A
CLEANMR			STA	TEXTBUF,Y
				INY
				CPY	#$0B
				BNE	CLEANMR
				LDY	#9
:LOOP1			LDA	TEXTBUF,Y
				CMP	(CHARPTR),Y
				BNE	:SKIPA
				DEY
				BPL	:LOOP1
				JMP	SAME_NAME
:SKIPA			LDA	#$00
				STA	COUNTER
NEXTCOM			LDY	COUNTER
				JSR	POINTR2
				LDY	#$09
COMPARE			LDA	(CHARPTR),Y
				AND	#$3F
				CMP	TEXTBUF,Y
				BNE	PASSIT	
				DEY
				BPL	COMPARE	
				JMP	FAILSIT
PASSIT			INC	COUNTER
				LDA	COUNTER
				CMP	GCOUNT
				BNE	NEXTCOM
				JSR	NAJANAME
				BCC	PASSER
				JSR	CLRBOTL
				LDY	#$05
				JSR	TEXTER
:KEYLOOP		LDA	KEYBRD
				BPL	:KEYLOOP
				BIT	UNSTROB
				JSR	CHECK_ENTER
				BNE	:KEYLOOP
				JMP	NOCANCC
FAILSIT			JSR	CLRBOTL
				JSR	NAMEND
				LDY	#$0A	
MOROVER			LDA	TEXTBUF,Y
				STA	TEXTBUF+1,Y
				DEY
				BPL	MOROVER
				LDA	#$28
				STA	TEXTBUF
				LDA	#$08
				LDY	#$B8
				JSR	LINE
				LDA	XCOORD
				STA	TEXT11
				LDA	SHIFT
				STA	TEXT11+1
				LDA	YCOORD
				STA	TEXT11+2
				LDY	#$11
				JSR	TEXTER	
				JMP	REINPUT
PASSER			LDY	CURSOR	
				DEY
				JSR	POINTR2
				LDY	#$09
REPLACR			LDA	(CHARPTR),Y
				AND	#$C0
				ORA	TEXTBUF,Y
				STA	(CHARPTR),Y
				DEY
				BPL	REPLACR
				INY
				LDA	(CHARPTR),Y
				BPL	NOTINGP
				JSR	FINDHIM
				INX
				BEQ	NOTINGP
				JSR	FROMGRP
				LDY	#$09
MORMAT			LDA	(CHARPTR),Y
				AND	#$3F
				STA	(CHARDL),Y
				DEY
				BPL	MORMAT
				JSR	FROMGRP
				LDX	COUNT
				LDA	SAVE_TABLE,X
				BEQ	NOTINGP
				JSR	MEMBER_SAVE
				JMP	CHNGCOM
NOTINGP			LDA	#>CHARBUF
				STA	SSTOREH
				LDY	#$0E
				LDA	(CHARPTR),Y
				PHA
				AND	#$80
				STA	FSFLAG
				PLA
				AND	#$7F
				ORA	#<charTrack
				TAX
				LDY	#>charTrack
				LDA	#1
				JSR	SLOAD
				LDY	#$09
				LDA	#>CHARBUF
				STA	CHARDH
				LDA	FSFLAG
				STA	CHARDL
MORTRAN			LDA	(CHARPTR),Y
				AND	#$3F
				STA	(CHARDL),Y
				DEY
				BPL	MORTRAN
				LDX	#wNewChar
				JSR	CALL_WRITERS
CHNGCOM			LDY	CURSOR
				DEY
				LDA	LINUMS,Y
				TAX
				CLC
				ADC	#$06
				STA	ENDLINE
				LDA	#$01
				STA	BEGROW
				LDA	#$09
				STA	ENDROW
				JSR	CLEAR
				LDY	CURSOR
				DEY
				JSR	POINTR2
				LDY	#$09
REPLAC2			LDA	(CHARPTR),Y
				AND	#$3F
				STA	TEXTBUF,Y
				DEY
				BPL	REPLAC2
				LDA	#$8D
				STA	TEXTBUF+10
				LDY	CURSOR
				DEY
				LDA	LINUMS,Y
				TAY	
				LDA	#$08
				JSR	LINE
SAME_NAME		JSR	CURSAD
				JSR	CLRBOTL
				DEC	HCURSOR
				JMP	ROSTO2

FROMGRP			LDY	#$09
FROM2			LDA	(CHARDL),Y
				STA	TEXTBUF,Y
				DEY
				BPL	FROM2
				LDA	#$8D
				STA	TEXTBUF+10
				LDA	#$40
				STA	PAGE
				LDY	COUNT
				LDA	YPOINTS,Y
				TAY
				LDA	#$28	
				JSR	LINE
				LDA	#$20
				STA	PAGE
				RTS

NAMEND			LDY	#$09
NAMEND2			LDA	TEXTBUF,Y
				CMP	#$0A
				BNE	LASTSPC
				DEY
				BPL	NAMEND2			;Always
LASTSPC			INY
				LDA	#$8D
				STA	TEXTBUF,Y
				RTS

NAJANAME		LDX	#$09
:LOOP1			LDA	TEXTBUF,X
				CMP	NAJATEXT,X
				CLC
				BNE	:EXIT
				DEX
				BPL	:LOOP1
				SEC
:EXIT			RTS

NAJATEXT		USR	(NAJA______)-
