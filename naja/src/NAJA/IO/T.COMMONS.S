				LST	OFF
XCOORD			EQU	$08	
YCOORD			EQU	$09	
LOADFLAG		EQU	$06
SCRATCH			EQU	$06
CHARDL			EQU	$91
YPNT			EQU	$93
SSTOREH			EQU	$B2
STRACK			EQU	$BA
SSECTOR			EQU	$BB
READNUM			EQU	$BC
COUNT			EQU	$C0
XHOLD			EQU	$C2
PTYPE			EQU	$D0	
SCOUNT			EQU	$E0
PNTL			EQU	$E1
PNTH			EQU	$E2
TEXTBUF			EQU	$0200
SREADIT			EQU	$0669
FREADIT			EQU	$069D
FREAD			EQU	$071E
CHARTAB			EQU	$0AF4	
CHAROUT			EQU	$0AFB	
LINE			EQU	$0B33
SHFTCLC			EQU	$0BA1	
TABLER			EQU	$0BD6	
POINTIT			EQU	$12D3
SPCHARS			EQU	$1E40
GRPNUMB			EQU	$BC7F
				ORG	$B300
*** 
*** 
*** 
FROM11			LDY	#$1E
				LDA	(CHARDL),Y
FROM11B			PHA
				LSR	A
				LSR	A
				LSR	A
				LSR	A
				TAX
				DEX
				PLA
				AND	#$0F
				STA	SCRATCH
				LDA	#$05
				SEC
				SBC	SCRATCH
				TAY
				RTS
*** 
*** 
*** 
XYTOYX			PHA
				LSR	A
				LSR	A
				LSR	A
				LSR	A
				AND	#$07
				STA	SCRATCH
				PLA
				AND	#$0F
				ASL	A
				ASL	A
				ASL	A
				ORA	SCRATCH
				RTS
*** 
*** 
*** 
YXTOXY			PHA
				LSR	A
				LSR	A
				LSR	A
				AND	#$0F
				STA	SCRATCH
				PLA
				AND	#$07
				ASL	A
				ASL	A
				ASL	A
				ASL	A
				ORA	SCRATCH
				RTS
*** 
*** 
*** 
MULTPLY			LDX	#$02
				LDA	#$00
SLOOP0			STA	BCOUNT,X
				STA	SUBVALU,X
				STA	ADDVALU,X
				DEX
				BPL	SLOOP0
				LDA	MPLIER	
				ASL	A
				ASL	A
				ASL	A
				ASL	A
				STA	SUBVALU
				LDA	#$10
				STA	ADDVALU
SLOOP1A			LDX	#$02
				SEC
SLOOP1			SED
				LDA	HOLDER,X
				SBC	SUBVALU,X
				STA	HOLDER2,X
				CLD
				DEX
				BPL	SLOOP1
				BCC	SSKIP1
				LDX	#$02
				CLC
SLOOP2			SED
				LDA	HOLDER2,X
				STA	HOLDER,X
				LDA	BCOUNT,X
				ADC	ADDVALU,X
				STA	BCOUNT,X
				CLD
				DEX
				BPL	SLOOP2
				BMI	SLOOP1A
SSKIP1			LDX	#$04
SLOOP3			LSR	SUBVALU
				ROR	SUBVALU+1
				ROR	SUBVALU+2
				LSR	ADDVALU
				ROR	ADDVALU+1
				ROR	ADDVALU+2
				DEX
				BNE	SLOOP3
				LDA	SUBVALU
				ORA	SUBVALU+1
				ORA	SUBVALU+2
				BNE	SLOOP1A
				RTS
*** 
*** 
*** 
CREDOUT			LDY	#$00
				LDX	#$00
CRLOOP1			LDA	CREDATA,Y
				PHA
				LSR	A
				LSR	A
				LSR	A
				LSR	A
				STA	TEXTBUF,X
				INX
				PLA
				AND	#$0F
				STA	TEXTBUF,X
				INX
				INY
				CPY	#$04
				BNE	CRLOOP1
				LDX	#$00
DEZEROD			LDA	TEXTBUF,X
				BNE	ISZEROD
				LDA	#$0A
				STA	TEXTBUF,X
				INX
				CPX	#$07
				BNE	DEZEROD
ISZEROD			LDA	TEXTBUF+2
				STA	TEXTBUF+8
				LDA	#$8D
				STA	TEXTBUF+2
				LDA	XHOLD
				LDY	YPNT
				JSR	LINE
				LDA	TEXTBUF+8
				STA	TEXTBUF
				LDA	TEXTBUF+3
				STA	TEXTBUF+1
				LDA	TEXTBUF+4
				STA	TEXTBUF+2
				LDA	#$8D
				STA	TEXTBUF+3
				LDA	XHOLD
				CLC
				ADC	#$0E
				LDY	YPNT
				JSR	LINE
				LDX	#$02
SLID			LDA	TEXTBUF+5,X
				STA	TEXTBUF,X
				DEX
				BPL	SLID
				LDA	XHOLD
				CLC
				ADC	#$22
				LDY	YPNT
				JMP	LINE
*** 
*** 
*** 
CPARER1			JSR	CHECK_UP
				BEQ	CPAROUT
CPARER2			JSR	CHECK_DOWN
				BEQ	CPAROUT
				JSR	CHECK_LEFT
				BEQ	CPAROUT
				JSR	CHECK_RIGHT
CPAROUT			RTS

				DS	6,$EA
*** 
*** 
*** 
LOCATE			LDA	#$00
				STA	COUNT
LLOOP1			JSR	POINTIT
				JSR	SINGLE
				LDX	#$00	
LLOOP2			LDA	SINGALL,X
				BEQ	LSKIP1
				INX
				CPX	#$0A
				BNE	LLOOP2
				INC	COUNT
				LDA	COUNT
				CMP	GRPNUMB
				BNE	LLOOP1
				LDA	#$FF
				BNE	LSKIP2	
LSKIP1			STX	SCRATCH
				LDA	COUNT
				ASL	A
				ASL	A
				ASL	A
				ASL	A
				ORA	SCRATCH
LSKIP2			RTS	
SINGLE			LDX	#$09
				LDA	#$FF
SNLOOP1			STA	SINGALL,X
				DEX
				BPL	SNLOOP1
				INX
				LDY	#$0D
				LDA	(CHARDL),Y
				BMI	ROBOMAN
				AND	#$0F
				CMP	#$04
				BNE	NODEN
				STX	SINGALL+8
				STX	SINGALL+9
NODEN			STX	SINGALL+6
				STX	SINGALL+7
				LDY	#$1D
				LDA	(CHARDL),Y
				STA	SCRATCH
				LDY	#$00
				TYA	
SNLOOP2			STA	SINGALL,Y
				INY
				CPY	SCRATCH
				BNE	SNLOOP2
				JMP	SINCOM
ROBOMAN			LDX	#$00
				STX	SINGALL
				LDY	#$6E
				LDA	(CHARDL),Y
				AND	#$08
				BNE	ROCARRY
				LDY	#$3D
				LDA	(CHARDL),Y
				AND	#$1F
				CMP	#$19
				BNE	SNSKIP1
				STX	SINGALL+7
SNSKIP1			LDY	#$45
				LDA	(CHARDL),Y
				AND	#$1F
				CMP	#$19
				BNE	SINCOM	
				STX	SINGALL+9
				BEQ	SINCOM
ROCARRY			LDY	#$6E
				LDA	(CHARDL),Y
				AND	#$07
				STA	SCRATCH
				INC	SCRATCH
				LDY	#$01
SNLOOP3			TXA	
				STA	SINGALL,Y
				INY
				CPY	SCRATCH
				BNE	SNLOOP3
SINCOM			LDX	#$09
SNLOOP4			LDA	CORESPS,X	
				TAY	
				LDA	(CHARDL),Y
				CMP	#$FF
				BEQ	NOTOPEN
				LDA	#$FF	
				STA	SINGALL,X	
NOTOPEN			DEX	
				BPL	SNLOOP4
				RTS
CORESPS			DFB	$4D,$51,$55
				DFB	$59,$5D,$61
				DFB	$3D,$41,$45
				DFB	$49	
*** 
*** 
*** 
ACURSIT			LDA	#$2C
				STA	TEXTBUF
				LDA	#$8D
				STA	TEXTBUF+1
				LDA	XHOLD
				LDY	YPNT
				JSR	LINE
				LDA	XHOLD
				PHA
				CLC
				ADC	#$05
				STA	XHOLD
				LDA	#$00
				JSR	SPCHARS
				PLA
				STA	XHOLD
				RTS
*** 
*** 
*** 
TVIEWIT			STA	PTYPE
				LDA	XPNTS,X
				STA	XCOORD
				LDA	YPNTS,Y
				STA	YCOORD
				JSR	SHFTCLC
				JSR	TABLER
				LDA	PTYPE
				AND	#$7F
				TAY
				LDA	TCHROFF,Y
				PHA
				JSR	TCHRLOD
				INC	XCOORD
				PLA
				CLC
				ADC	#$07
				JSR	TCHRLOD
				DEC	XCOORD
				LDA	YCOORD
				CLC
				ADC	#$07
				STA	YCOORD
				JSR	TABLER
				LDA	PTYPE
				AND	#$7F
				TAY
				LDA	PRBPICK,Y
				PHA
				LSR	A	
				LSR	A
				LSR	A
				LSR	A
				ASL	A
				JSR	BCHRLOD
				INC	XCOORD
				PLA
				AND	#$0F
				ASL	A
				JSR	BCHRLOD
				RTS
TCHRLOD			TAY
				LDX	#$00
MORTCHR			LDA	TCHARS,Y
				STA	CHARTAB,X
				TXA
				LSR	A
				BCC	NIMAGE1
				BIT	PTYPE
				BPL	NIMAGE1
				LDA	#$00
				STA	CHARTAB,X
NIMAGE1			INY
				INX
				CPX	#$07
				BNE	MORTCHR
				JSR	CHAROUT
				RTS
BCHRLOD			TAY
				LDX	#$00
MORBCHR			LDA	PRBOTM,Y
				CPX	#$02
				BCC	NOTA0
				LDA	#$00
NOTA0			STA	CHARTAB,X
				TXA
				LSR	A
				BCS	NIMAGE2
				BIT	PTYPE
				BPL	NIMAGE2
				LDA	#$00
				STA	CHARTAB,X
NIMAGE2			INY
				INX
				CPX	#$07
				BNE	MORBCHR
				JSR	CHAROUT
				RTS
XPNTS			DFB	$0A,$19,$28
				DFB	$37,$46
YPNTS			DFB	$80,$8B,$96
				DFB	$A1,$AC
TCHROFF			DFB	$00,$0E,$1C
				DFB	$2A,$38,$46
				DFB	$54,$62,$70
TCHARS			DFB	$32,$73,$7B
				DFB	$1B,$6F,$6F
				DFB	$6E
				DFB	$26,$67,$6F
				DFB	$6C,$7B,$7B
				DFB	$3B
				DFB	$30,$70,$18
				DFB	$6C,$6F,$6F
				DFB	$6F
				DFB	$06,$07,$0C
				DFB	$1B,$7B,$7B
				DFB	$7B
				DFB	$30,$77,$73
				DFB	$7B,$3F,$1F
				DFB	$1E
				DFB	$06,$77,$67
				DFB	$6F,$7E,$7C
				DFB	$3C
				DFB	$30,$70,$78
				DFB	$1C,$6F,$2F
				DFB	$6F
				DFB	$06,$67,$6F
				DFB	$6C,$7B,$7A
				DFB	$3B
				DFB	$30,$70,$38
				DFB	$5C,$6F,$77
				DFB	$4F
				DFB	$06,$07,$0E
				DFB	$1D,$7B,$77
				DFB	$79
				DFB	$32,$73,$7B
				DFB	$1B,$6F,$2F
				DFB	$6E
				DFB	$26,$67,$6F
				DFB	$6C,$7B,$7A
				DFB	$3B
				DFB	$30,$77,$3B
				DFB	$5B,$6F,$77
				DFB	$4E
				DFB	$06,$77,$6E
				DFB	$6D,$7B,$77
				DFB	$39
				DFB	$30,$73,$7B
				DFB	$7B,$3F,$1F
				DFB	$1E
				DFB	$06,$67,$6F
				DFB	$6F,$7E,$7C
				DFB	$3C
				DFB	$38,$78,$08
				DFB	$6F,$69,$69
				DFB	$6F
				DFB	$0E,$0F,$08
				DFB	$7B,$4B,$4B
				DFB	$7B
PRBPICK			DFB	$01,$67,$89
				DFB	$01,$23,$01
				DFB	$23,$AB,$45
PRBOTM			DFB	$18,$70
				DFB	$0C,$07
				DFB	$3C,$70
				DFB	$1E,$07
				DFB	$08,$78
				DFB	$08,$0F
				DFB	$0C,$78
				DFB	$18,$0F
				DFB	$38,$60
				DFB	$0E,$03
				DFB	$38,$70
				DFB	$0E,$07
*** 
*** 
*** 
SHIESUB			LDA	#$00
				STA	TTAL
				STA	TTAL+1
				STA	TTAL+2
				STA	TENPERC
				STA	ONEPERC
				LDA	#$00
				SEC
				SED
				SBC	SSHIELD
				CLD
				STA	SSHIELD
				LDA	DAMAGL
				STA	ONEPERC+2
				STA	TENPERC+2
				LDA	DAMAGH
				STA	ONEPERC+1
				STA	TENPERC+1
				LDX	#$03
SHILOOP			ASL	TENPERC+2
				ROL	TENPERC+1
				ROL	TENPERC
				DEX
				BPL	SHILOOP
				LDA	SSHIELD
				AND	#$F0
				STA	SCRATCH	
				BEQ	NOTTADD
TADLOOP			JSR	ADDTENS
				LDA	SCRATCH	
				SEC
				SBC	#$10
				STA	SCRATCH	
				BNE	TADLOOP
NOTTADD			LDA	SSHIELD
				AND	#$0F
				STA	SCRATCH	
				BEQ	NOTOADD
OADLOOP			JSR	ADDONES
				DEC	SCRATCH	
				BNE	OADLOOP
NOTOADD			LDA	#$00
				STA	TENPERC
				STA	TENPERC+1
				LDA	#$49
				STA	TENPERC+2
ADDTENS			LDX	#$02
				SED
				CLC
ATLOOP1			LDA	TTAL,X
				ADC	TENPERC,X
				STA	TTAL,X
				DEX
				BPL	ATLOOP1
				CLD
				RTS
ADDONES			LDX	#$02
				SED
				CLC
AOLOOP1			LDA	TTAL,X	
				ADC	ONEPERC,X
				STA	TTAL,X
				DEX
				BPL	AOLOOP1
				CLD
				RTS
***
***
***
CHECK_UP		CMP	#"A"
				BEQ	:SKIPA
				CMP	#"a"
				BEQ	:SKIPA
				CMP	#$8B			;Up Arrow
:SKIPA			RTS

				DS	4,$EA

CHECK_DOWN		CMP	#"Z"
				BEQ	:SKIPA
				CMP	#"z"
				BEQ	:SKIPA
				CMP	#$8A
:SKIPA			RTS

				DS	4,$EA

CHECK_LEFT		CMP	#"J"
				BEQ	:SKIPA
				CMP	#"j"
				BEQ	:SKIPA
				CMP	#$88
:SKIPA			RTS

				DS	4,$EA

CHECK_RIGHT		CMP	#"K"
				BEQ	:SKIPA
				CMP	#"k"
				BEQ	:SKIPA
				CMP	#$95
:SKIPA			RTS

				DS	4,$EA
				DS	39,$EA
*** 
*** 
***
MPLIER			DFB	$00
HOLDER			DFB	$00,$00,$00
HOLDER2			DFB	$00,$00,$00
BCOUNT			DFB	$00,$00,$00
SUBVALU			DFB	$00,$00,$00
ADDVALU			DFB	$00,$00,$00
CREDATA			DFB	$00,$00,$00
				DFB	$00
SINGALL			DFB	$00,$00,$00
				DFB	$00,$00,$00
				DFB	$00,$00,$00
				DFB	$00
SSHIELD			DFB	$00
DAMAGL			DFB	$00
DAMAGH			DFB	$00
ONEPERC			DFB	$00,$00,$00
TENPERC			DFB	$00,$00,$00
TTAL			DFB	$00,$00,$00
PARASUB			JMP	PARSTAT
CUTSUB			JMP	CUTUP
ENERSUB			JMP	ENERUSE
EBYTE1			DFB	$00
EBYTE2			DFB	$00
EBYTE3			DFB	$00
EBYTE4			DFB	$00
USAGE			DFB	$00,$00,$00
PERWHAT			DFB	$00	
CUTUP			STA	CUTMOD+1
				LDX	#$00
				LDY	#$00
CUTLOOP			LDA	USAGE,Y
				PHA
				LSR	A
				LSR	A	
				LSR	A
				LSR	A
				STA	TEXTBUF,X
				INX
				PLA
				AND	#$0F
				STA	TEXTBUF,X
				INX
				INY
				CPY	#$03	
				BNE	CUTLOOP
				LDA	#$2E
				STA	TEXTBUF,X
				INX
				LDA	PERWHAT
				STA	TEXTBUF,X
				INX
				LDA	#$8D
				STA	TEXTBUF,X
				LDY	#$00
COLOOP			LDA	TEXTBUF,Y
				BNE	CUTOUT
CUTMOD			LDA	#$0A
				STA	TEXTBUF,Y
				INY
				CPY	#$05	
				BNE	COLOOP
CUTOUT			RTS
PARSTAT			LDA	#$30
				STA	TEXTBUF
				LDA	#$32
				STA	TEXTBUF+2
				LDA	#$8D
				STA	TEXTBUF+3
				LDX	#$2C
				LDA	EBYTE1	
				CMP	#$FF
				BEQ	NISKIP	
				LDX	#$01
				LDA	EBYTE3	
				AND	#$01
				BEQ	HOSKIP
				INX
HOSKIP			LDA	EBYTE3	
				AND	#$02
				BEQ	NFSKIP
				LDX	#$10
NFSKIP			LDY	#$0D
				LDA	(CHARDL),Y
				LSR	A
				LSR	A
				LSR	A
				LSR	A
				TAY
				LDA	PROUSAB,Y
				AND	EBYTE4	
				BNE	NUSKIP
				LDX	#$22
NUSKIP			LDA	EBYTE1	
				AND	#$80
				BEQ	NBSKIP
				LDX	#$0C
NBSKIP			LDA	EBYTE1	
				AND	#$40
				BNE	NISKIP
				LDX	#$33
NISKIP			STX	TEXTBUF+1
				RTS
PROUSAB			DFB	$01,$02,$04
				DFB	$08,$10,$FF
				DFB	$40,$80,$20
ENERUSE			LDX	#$00
TOEB			LDA	(CHARDL),Y
				STA	EBYTE1,X
				INY
				INX
				CPX	#$04
				BNE	TOEB
ENERGY2			LDA	EBYTE1			;CALLED IN ARSENAL
				AND	#$1F
				CMP	#$04
				BEQ	EXARMOR
				CMP	#$05
				BEQ	EXHELM
				CMP	#$1A
				BEQ	EXPROG
				LDX	#$00
SETLOOP			CMP	SETS,X
				BEQ	GOTSET
				BCC	OUTSET
				INX
				BNE	SETLOOP
GOTSET			LDA	SETOFFS,X
				TAX
				LDA	EBYTE1
				AND	#$1F
				CMP	#$03
				BNE	NOEX
				LDA	EBYTE2	
				AND	#$07
				CMP	#$06
				BCS	NOEX
				CMP	#$03
				BCS	OUTSET
NOEX			LDA	EBYTE2	
SUBLOOP			CMP	SUBSETS,X
				BEQ	GOTSUB
				INX
				BNE	SUBLOOP
GOTSUB			STX	SCOUNT
				LDA	#$00
				STA	PNTH
				TXA
				ASL	A
				ROL	PNTH
				STA	PNTL
				CLC
				LDA	PNTL
				ADC	SCOUNT	
				STA	PNTL
				LDA	PNTH
				ADC	#$00
				STA	PNTH
				CLC
				LDA	#<ENTABLE
				ADC	PNTL
				STA	PNTL
				LDA	#>ENTABLE
				ADC	PNTH
				STA	PNTH
				LDY	#$02	
TOULOOP			LDA	(PNTL),Y
				STA	USAGE,Y
				DEY
				BPL	TOULOOP
				LDX	SCOUNT
				LDA	PERDATA,X
				STA	PERWHAT
				RTS
EXARMOR			NOP
EXHELM			NOP
EXPROG			NOP
OUTSET			LDA	#$FF
				STA	USAGE
				RTS
SETS			DFB	$00,$01,$02
				DFB	$03,$04,$05
				DFB	$06,$07,$08
				DFB	$0C,$0D,$0E
				DFB	$0F,$18,$19
				DFB	$1A,$1C
SETOFFS			DFB	$00,SS01-SUBSETS,SS02-SUBSETS
				DFB	SS03-SUBSETS,$FF,$FF	
				DFB	SS06-SUBSETS,SS07-SUBSETS,SS08-SUBSETS
				DFB	SS0C-SUBSETS,SS0D-SUBSETS,SS0E-SUBSETS
				DFB	SS0F-SUBSETS,SS18-SUBSETS,SS19-SUBSETS
				DFB	$FF,SS1C-SUBSETS
SUBSETS			DFB	$08,$14,$1C
				DFB	$01,$02	
SS01			DFB	$00,$01,$02	
				DFB	$03,$04,$05	
				DFB	$06,$07,$08	
				DFB	$09,$19,$29	
				DFB	$39,$49,$59	
SS02			DFB	$00,$01,$02	
				DFB	$03,$04,$05	
SS03			DFB	$00,$01,$09	
				DFB	$02,$0A,$12	
				DFB	$1A,$22,$03	
				DFB	$04,$05,$06	
SS06			DFB	$01,$02,$03	
				DFB	$04,$05,$06	
				DFB	$07,$08,$09	
				DFB	$0A,$0B,$0C	
SS07			DFB	$0C,$14,$1C	
				DFB	$09,$15,$1D	
				DFB	$02,$03	
SS08			DFB	$00,$01,$04	
				DFB	$05,$06,$07	
				DFB	$0B	
SS0C			DFB	$00,$01,$02	
				DFB	$03,$04,$05	
				DFB	$06	
SS0D			DFB	$00,$01,$02	
				DFB	$03,$04,$05	
				DFB	$06	
SS0E			DFB	$00,$01	
SS0F			DFB	$00,$01,$02	
				DFB	$03,$04,$05	
				DFB	$06,$07,$08	
				DFB	$09,$0A,$0B	
				DFB	$0C	
SS18			DFB	$00,$04	
SS19			DFB	$00	
SS1C			DFB	$00,$01,$02	
ENTABLE			DFB	$00,$00,$00		;2% DISSIPATOR 
				DFB	$00,$00,$00		;5% DISSIPATOR 
				DFB	$00,$00,$00		;7% DISSIPATOR 
				DFB	$00,$00,$00		;IMAGE GENERATOR 
				DFB	$00,$00,$00		;BLAST VISOR 
				DFB	$00,$00,$00		;DIAGNOSER 
				DFB	$00,$00,$00		;CELL REGENERATOR 
				DFB	$00,$00,$00		;CIRCUIT REPAIRER 
				DFB	$00,$00,$00		;ATOM COMPRESSOR 
				DFB	$00,$00,$00		;CAUTERIZER 
				DFB	$00,$00,$00		;NERVE MANIPULATR 
				DFB	$00,$00,$00		;SIGHT RESTORER 
				DFB	$00,$00,$00		;BRAIN EQUALIZER 
				DFB	$00,$00,$00		;DRUG INFUSER 
				DFB	$00,$00,$00		;AAAAAAAA CAPSULE 
				DFB	$00,$00,$00		;BBBBBBBB CAPSULE 
				DFB	$00,$00,$00		;CCCCCCCC CAPSULE 
				DFB	$00,$00,$00		;DDDDDDDD CAPSULE 
				DFB	$00,$00,$00		;EEEEEEEE CAPSULE 
				DFB	$00,$00,$00		;FFFFFFFF CAPSULE 
				DFB	$00,$00,$00		;I.D. SCANNER 
				DFB	$00,$00,$00		;FACE TRACKER 
				DFB	$00,$00,$00		;ORGANIC DETECTOR 
				DFB	$00,$00,$00		;ROBOTIC DETECTOR 
				DFB	$00,$00,$00		;DOOR UNLOCKER 
				DFB	$00,$00,$00		;VIBRATION SENSOR 
				DFB	$00,$00,$00		;ROBOT CONTROLLER 
				DFB	$00,$00,$00		;MUSCLE 
				DFB	$00,$00,$00		;REFLEX 
				DFB	$00,$00,$00		;GREEN ORB 
				DFB	$00,$00,$00		;BLACK ORB 
				DFB	$00,$00,$00		;BLUE ORB   
				DFB	$00,$00,$00		;RED ORB 
				DFB	$00,$00,$00		;PURPLE ORB 
				DFB	$00,$00,$00		;COMPRESSD PERSON 
				DFB	$00,$00,$00		;DATA CARD A 
				DFB	$00,$00,$00		;DATA CARD B 
				DFB	$00,$00,$00		;JUNK 
				DFB	$00,$00,$00		;5% FORCE SHIELD 
				DFB	$00,$00,$00		;10% FORCE SHIELD 
				DFB	$00,$00,$00		;15% FORCE SHIELD 
				DFB	$00,$00,$00		;20% FORCE SHIELD 
				DFB	$00,$00,$00		;25% FORCE SHIELD 
				DFB	$00,$00,$00		;30% FORCE SHIELD 
				DFB	$00,$00,$00		;35% FORCE SHIELD 
				DFB	$00,$00,$00		;40% FORCE SHIELD 
				DFB	$00,$00,$00		;45% FORCE SHIELD 
				DFB	$00,$00,$00		;50% FORCE SHIELD 
				DFB	$00,$00,$00		;55% FORCE SHIELD 
				DFB	$00,$00,$00		;60% FORCE SHIELD 
				DFB	$00,$00,$00		;3% ENERGY CATCHR 
				DFB	$00,$00,$00		;5% ENERGY CATCHR 
				DFB	$00,$00,$00		;7% ENERGY CATCHR 
				DFB	$00,$00,$00		;2% GROUNDERS 
				DFB	$00,$00,$00		;5% GROUNDERS 
				DFB	$00,$00,$00		;7% GROUNDERS 
				DFB	$00,$00,$00		;ABSORBER ARMOR 
				DFB	$00,$00,$00		;EXOSKELETON 
				DFB	$00,$00,$10		;BLUE BEAM DAGGER 
				DFB	$00,$00,$30		;GREEN BEAM DAGGR 
				DFB	$00,$00,$40		;BLUE BEAM SWORD 
				DFB	$00,$00,$60		;GREEN BEAM SWORD 
				DFB	$00,$00,$80		;RED BEAM SWORD 
				DFB	$00,$01,$20		;LASBEAM SWORD 
				DFB	$00,$00,$60		;LASER WHIP 
				DFB	$00,$00,$03		;FLY PISTOL 
				DFB	$00,$00,$06		;LIGHT PISTOL 
				DFB	$00,$00,$15		;MEDIUM PISTOL 
				DFB	$00,$00,$30		;HEAVY PISTOL 
				DFB	$00,$00,$10		;HEAVY ADJ PISTOL 
				DFB	$00,$00,$40		;ZIFFER'S PISTOL 
				DFB	$00,$00,$13		;ZIFFER ADJ PISTL 
				DFB	$00,$00,$40		;LIGHT RIFLE 
				DFB	$00,$00,$60		;MEDIUM RIGLE 
				DFB	$00,$00,$20		;MEDIUM ADJ RIFLE 
				DFB	$00,$00,$90		;HEAVY RIFLE 
				DFB	$00,$00,$30		;HEAVY ADJ RIFLE 
				DFB	$00,$01,$20		;ULTRA RIFLE 
				DFB	$00,$00,$40		;ULTRA ADJ RIFLE 
				DFB	$00,$02,$00		;LIGHT CANNON 
				DFB	$00,$02,$50		;HEAVY CANNON 
				DFB	$00,$00,$00		;SONIC BOOMERANG 
				DFB	$00,$00,$00		;GYRO BOOMERANG 
				DFB	$00,$00,$00		;ACID THROWER 
				DFB	$00,$00,$00		;ADJ ACID THROWER 
				DFB	$00,$00,$00		;DISINTIGRATOR 
				DFB	$00,$00,$00		;DASER 
				DFB	$00,$00,$00		;IMMOBILIZER 
				DFB	$00,$00,$00		;ISOLATER 
				DFB	$00,$00,$00		;3S MEMORY ERASER 
				DFB	$00,$00,$00		;4S MEMORY ERASER 
				DFB	$00,$00,$00		;5S MEMORY ERASER 
				DFB	$00,$00,$00		;6S MEMORY ERASER 
				DFB	$00,$00,$00		;7S MEMORY ERASER 
				DFB	$00,$00,$00		;ROBO-DAGGER 
				DFB	$00,$00,$00		;ROBO-SWORD 
				DFB	$00,$00,$00		;ROBO-BLASTER C 
				DFB	$00,$00,$00		;ROBO-BLASTER B 
				DFB	$00,$00,$00		;ROBO-BLASTER A 
***SWING=00 
****SHOT=01 
*****USE=02 
****HEAL=03 
***ROUND=04 
***THROW=05
PERDATA			DFB	$04				;2% DISSIPATOR 
				DFB	$04				;5% DISSIPATOR 
				DFB	$04				;7% DISSIPATOR 
				DFB	$04				;IMAGE GENERATOR 
				DFB	$04				;BLAST VISOR 
				DFB	$02				;DIAGNOSER 
				DFB	$03				;CELL REGENERATOR 
				DFB	$03				;CIRCUIT REPAIRER 
				DFB	$02				;ATOM COMPRESSOR 
				DFB	$02				;CAUTERIZER 
				DFB	$02				;NERVE MANIPULATR 
				DFB	$02				;SIGHT RESTORER 
				DFB	$02				;BRAIN EQUALIZER 
				DFB	$02				;DRUG INFUSER 
				DFB	$02				;AAAAAAAA CAPSULE 
				DFB	$02				;BBBBBBBB CAPSULE 
				DFB	$02				;CCCCCCCC CAPSULE 
				DFB	$02				;DDDDDDDD CAPSULE 
				DFB	$02				;EEEEEEEE CAPSULE 
				DFB	$02				;FFFFFFFF CAPSULE 
				DFB	$02				;I.D. SCANNER 
				DFB	$02				;FACE TRACKER 
				DFB	$04				;ORGANIC DETECTOR 
				DFB	$04				;ROBOTIC DETECTOR 
				DFB	$02				;DOOR UNLOCKER 
				DFB	$04				;VIBRATION SENSOR 
				DFB	$04				;ROBOT CONTROLLER 
				DFB	$02				;MUSCLE 
				DFB	$02				;REFLEX 
				DFB	$EE				;GREEN ORB 
				DFB	$EE				;BLACK ORB 
				DFB	$EE				;BLUE ORB   
				DFB	$EE				;RED ORB 
				DFB	$EE				;PURPLE ORB 
				DFB	$EE				;COMPRESSD PERSON 
				DFB	$EE				;DATA CARD A 
				DFB	$EE				;DATA CARD B 
				DFB	$EE				;JUNK 
				DFB	$04				;5% FORCE SHIELD 
				DFB	$04				;10% FORCE SHIELD 
				DFB	$04				;15% FORCE SHIELD 
				DFB	$04				;20% FORCE SHIELD 
				DFB	$04				;25% FORCE SHIELD 
				DFB	$04				;30% FORCE SHIELD 
				DFB	$04				;35% FORCE SHIELD 
				DFB	$04				;40% FORCE SHIELD 
				DFB	$04				;45% FORCE SHIELD 
				DFB	$04				;50% FORCE SHIELD 
				DFB	$04				;55% FORCE SHIELD 
				DFB	$04				;60% FORCE SHIELD 
				DFB	$02				;3% ENERGY CATCHR 
				DFB	$02				;5% ENERGY CATCHR 
				DFB	$02				;7% ENERGY CATCHR 
				DFB	$04				;2% GROUNDERS 
				DFB	$04				;5% GROUNDERS 
				DFB	$04				;7% GROUNDERS 
				DFB	$04				;ABSORBER ARMOR 
				DFB	$04				;EXOSKELETON 
				DFB	$00				;BLUE BEAM DAGGER 
				DFB	$00				;GREEN BEAM DAGGR 
				DFB	$00				;BLUE BEAM SWORD 
				DFB	$00				;GREEN BEAM SWORD 
				DFB	$00				;RED BEAM SWORD 
				DFB	$00				;LASBEAM SWORD 
				DFB	$00				;LASER WHIP 
				DFB	$01				;FLY PISTOL 
				DFB	$01				;LIGHT PISTOL 
				DFB	$01				;MEDIUM PISTOL 
				DFB	$01				;HEAVY PISTOL 
				DFB	$01				;HEAVY ADJ PISTOL 
				DFB	$01				;ZIFFER'S PISTOL 
				DFB	$01				;ZIFFER ADJ PISTL 
				DFB	$01				;LIGHT RIFLE 
				DFB	$01				;MEDIUM RIGLE 
				DFB	$01				;MEDIUM ADJ RIFLE 
				DFB	$01				;HEAVY RIFLE 
				DFB	$01				;HEAVY ADJ RIFLE 
				DFB	$01				;ULTRA RIFLE 
				DFB	$01				;ULTRA ADJ RIFLE 
				DFB	$01				;LIGHT CANNON 
				DFB	$01				;HEAVY CANNON 
				DFB	$05				;SONIC BOOMERANG 
				DFB	$05				;GYRO BOOMERANG 
				DFB	$01				;ACID THROWER 
				DFB	$01				;ADJ ACID THROWER 
				DFB	$01				;DISINTIGRATOR 
				DFB	$01				;DASER 
				DFB	$05				;IMMOBILIZER 
				DFB	$05				;ISOLATER 
				DFB	$01				;3S MEMORY ERASER 
				DFB	$01				;4S MEMORY ERASER 
				DFB	$01				;5S MEMORY ERASER 
				DFB	$01				;6S MEMORY ERASER 
				DFB	$01				;7S MEMORY ERASER 
				DFB	$00				;ROBO-DAGGER 
				DFB	$00				;ROBO-SWORD 
				DFB	$01				;ROBO-BLASTER C 
				DFB	$01				;ROBO-BLASTER B 
				DFB	$01				;ROBO-BLASTER A 
***
***
***
				DS	12,$EA
**************** 
**TRACER BYTES** 
**************** 
				DFB	$00,$00,$00
				DFB	$00,$00,$00
				DFB	$00,$00,$00
				DFB	$00,$00,$00
				DFB	$00,$00,$00
				DFB	$00,$00,$00
				DFB	$00,$00,$00
				DFB	$00,$00,$00
				DFB	$00,$00,$00
				DFB	$00,$00,$00
				DFB	$00,$00
				SAV	T.COMMONS
