LOBYTEL			EQU	$11				;INDIRECT ADDRESS OF LOW ORDER BYTES (LOW)
LOBYTEH			EQU	$12				;INDIRECT ADDRESS OF LOW ORDER BYTES (HIGH)
HIBYTEL			EQU	$13				;INDIRECT ADDRESS OF HIGH ORDER BYTES (LOW)
HIBYTEH			EQU	$14				;INDIRECT ADDRESS OF HIGH ORDER BYTES (HIGH)
SCREENL			EQU	$26				;INDIRECT ADDRESS OF PRESENT SCREEN LOCATION (LOW)
SCREENH			EQU	$27				;INDIRECT ADDRESS OF PRESENT SCREEN LOCATION (HIGH)
LOWLOCL			EQU	$00				;IMMEDIATE VALUE OF LOWBYTES ADDRESS (LOW)
LOWLOCH			EQU	$08				;IMMEDIATE VALUE OF LOWBYTES ADDRESS (HIGH)
HILOCL			EQU	$C0				;IMMEDIATE VALUE OF HIGHBYTES ADDRESS (LOW)
HILOCH			EQU	$08				;IMMEDIATE VALUE OF HIGHBYTES ADDRESS (HIGH)
IMMPAGE			EQU	$20				;IMMEDIATE VALUE OF CURRENT PAGE
				ORG	$4000
				LDA	#LOWLOCL		;STORE IMMEDIATE VALUES FOR USE BY 
				STA	LOBYTEL			;INDIRECT STORING
				LDA	#LOWLOCH
				STA	LOBYTEH
				LDA	#HILOCL	
				STA	HIBYTEL
				LDA	#HILOCH	
				STA	HIBYTEH
				LDA	#$00			;SET INITIAL SCREENL,H TO $2000
				STA	SCREENL
				LDA	#$20
				STA	SCREENH
				LDY	#$00			;CLEAR Y-REGISTER
				JMP	STORE			;DON'T ADJUST HPOSN FIRST TIME THROUGH
DOWNER			JSR	DOWN			;JUMP TO HPOSN ADJUSTER SUBROUTINE
STORE			LDA	SCREENL			;STORE RESULTS IN TABLES
				STA	(LOBYTEL),Y		;OF LOW AND HIGH BYTES
				LDA	SCREENH
				EOR	#$20			;GET RID OF PAGE MARKER
				STA	(HIBYTEL),Y
				INY
				CPY	#$C0			;HAVE ALL 192 LINES BEEN DONE?
				BNE	DOWNER			;NO, THEN REPEAT
				RTS					;YES, THEN EXIT
VALUE1C			DFB	$1C
VALUE4			DFB	$04
DOWN			CLC					;CALCULATE ADDRESS FOR NEXT LINE DOWN
				LDA	SCREENH
				ADC	#$04
				BIT	VALUE1C	
				BNE	DOWNOUT
				ASL	SCREENL
				BCC	DOWN1
				ADC	#$E0
				CLC
				BIT	VALUE4	
				BEQ	DOWN2
				LDA	SCREENL			;STORE IN SCREENL
				ADC	#$50
				EOR	#$F0
				BEQ	DOWN3
				EOR	#$F0
DOWN3			STA	SCREENL
				LDA	#IMMPAGE
				BCC	DOWN2
DOWN1			ADC	#$E0
DOWN2			ROR	SCREENL
DOWNOUT			STA	SCREENH			;AND SCREENH
				RTS					;RETURN
