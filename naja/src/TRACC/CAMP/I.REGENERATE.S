********************************
* On Entry:
*   Acc = 0: Cell regenerater
*       = 1: Circuit repairer

REGENERATE		STA	HEAL_TYPE
				JSR	BIGBOXR
RECYCLE			LDY	#$32
				JSR	TEXTER2
				LDA	HEAL_TYPE
				BNE	CIRCUITS
				JSR	RESET_CHLIST
				JMP	BOTH_TYPES
CIRCUITS		JSR	RESET_CHRLIST
				LDA	#robot*16
				JSR	CSCAN_PROF
BOTH_TYPES		LDX	#<NEED_HEAL
				LDY	#>NEED_HEAL
				JSR	CSCAN_COMMON
				LDA	#$00
				STA	CHAR_LIST+7
				STA	GCURPOS
				JSR	PRE_ARROW
				JSR	GETKEY
				LDY	#$32
				JSR	TEXTER2
				LDA	CURSOR
				CMP	#$07
				BNE	CNOCANC
				LDA	#BLACK1
				STA	COLOR
				LDY	#$03
				JSR	LINER
				JSR	SWITCHU
				LDA	LEFT1
				JSR	POINTIT
				LDA	#$02
				STA	CURSOR
				JSR	IMARKER
				LDA	#$01
				JMP	RDOPTN
CNOCANC			LDA	CURSOR
				STA	RITHOLD
				LDA	LEFT1
				STA	LEFHOLD
				JSR	DEMARK

				LDA	RITHOLD
				JSR	POINTIT
				JSR	BANK_2
				LDX	#<NAME2A
				LDY	#>NAME2A
				JSR	FLUSH_NAME
				LDX	#9
:LOOP1			LDA	NAME2A,X
				STA	NAME2B,X
				DEX
				BPL	:LOOP1
				LDY	#$2B
				JSR	TEXTER2

				JSR	CALC_MAX
				LDA	#0
				STA	HBUFFER+0
				STY	HBUFFER+1
				STY	HHOLDER
				STX	HBUFFER+2
				STX	HHOLDER+1
				LDA	#$20
				STA	PAGE
				LDA	#GREEN
				STA	COLOR
				LDY	#$0A
				JSR	LINER

				LDA	LEFHOLD
				JSR	POINTIT
				JSR	BANK_2
				LDX	#<NAME1
				LDY	#>NAME1
				JSR	FLUSH_NAME
				LDY	#$2C
				JSR	TEXTER2
				LDA	#$06	
				STA	MAXTEXT
				JSR	HACKUP
				JSR	LINE2

				LDY	#$2D
				JSR	TEXTER2
				LDX	#$68	
				LDY	#$BF
				LDA	#$E6
				JSR	STATCOM
				LDA	#$40
				STA	PAGE
				LDY	#$03
				JSR	LINER
				LDY	#$12
				JSR	TEXTER2
				LDA	#$DB	
				LDY	#$9B
				JSR	INPUT	
HEALIN			STX	HOLDIT
				LDY	#$04
INLOOP0			LDA	TEXTBUF,Y
				STA	TBUFFER,Y
				DEY
				BPL	INLOOP0
				LDX	#$04
INLOOP1			LDA	TBUFFER,X
				CMP	#$8D
				BEQ	INSKIP0
				DEX
				BPL	INLOOP1
INSKIP0			CPX	#$00
				BEQ	GOHCANC	
INLOOP2			LDA	TBUFFER-1,X
				CMP	#$0A
				BNE	INSKIP1
				DEX
				BNE	INLOOP2
INSKIP1			LDA	#$8D
				STA	TBUFFER,X
				LDA	TBUFFER
				CMP	#$8D
				BNE	INSKIP2
GOHCANC			JMP	HCANCEL
INSKIP2			LDX	#$01
INLOOP3			LDA	TBUFFER,X
				CMP	#$8D
				BEQ	INSKIP3
				INX
				BNE	INLOOP3	
INSKIP3			LDY	#$04
INLOOP4			LDA	TBUFFER,X
				STA	TBUFFER,Y
				DEY
				BMI	INSKIP4
				DEX
				BPL	INLOOP4
				LDA	#$A0
INLOOP5			STA	TBUFFER,Y
				DEY
				BPL	INLOOP5
INSKIP4			LDX	#$00
INLOOP6			LDA	TBUFFER,X
				CMP	#$A0
				BNE	INSKIP5
				LDA	#$00
				STA	TBUFFER,X
				INX
				BNE	INLOOP6
INSKIP5			LDA	TBUFFER,X
				EOR	#$B0
				CMP	#$0A
				BCS	BADNUM	
				STA	TBUFFER,X
				INX
				CPX	#$04
				BNE	INSKIP5
				LDX	#$00
				LDY	#$00
INLOOP7			LDA	TBUFFER,X
				ASL
				ASL
				ASL
				ASL
				INX
				ORA	TBUFFER,X
				INX
				STA	AMHOLD,Y
				INY
				CPY	#$02
				BNE	INLOOP7
				LDA	AMHOLD
				ORA	AMHOLD+1	
				BEQ	BADNUM
				SEC
				SED
				LDA	HHOLDER+1
				SBC	AMHOLD+1
				LDA	HHOLDER
				SBC	AMHOLD
				CLD
				BCS	GOODNUM
BADNUM			JSR	CHAROUT
				JSR	RECALL
				JMP	HEALIN
GOODNUM			JSR	HCLRBOX
				BIT	UNSTROB
				LDY	#$2F
				JSR	TEXTER2
				LDA	#$00
				STA	HBUFFER
				LDA	AMHOLD
				STA	HBUFFER+1
				LDA	AMHOLD+1
				STA	HBUFFER+2
				JSR	HACKUP
				JSR	LINE2
				LDY	#$30
				JSR	TEXTER2
				LDA	#$00
				STA	ENHOLD+2
				STA	HBUFFER+2
				CLC
				SED
				LDA	AMHOLD+1
				ADC	AMHOLD+1
				STA	ENHOLD+1
				STA	HBUFFER+1
				LDA	AMHOLD
				ADC	AMHOLD
				STA	ENHOLD
				STA	HBUFFER
				CLD
				JSR	HACKUP
				LDA	#$AB
				LDY	#$7E
				JSR	LINE
				JSR	BANK_2
				LDA	XCOORD
				STA	TEXT31	
				LDA	SHIFT
				STA	TEXT31+1
				JSR	BANK_1
				LDY	#$31
				JSR	TEXTER2
INKEY			LDA	KEYBRD
				BPL	INKEY
				BIT	UNSTROB
				JSR	CHECK_ENTER
				BEQ	HSPACIN
				CMP	#$C3
				BNE	INKEY
				JMP	HCANCEL
HSPACIN			LDY	RITHOLD
				LDA	YPOINTS,Y
				STA	YPNT
				TYA
				JSR	POINTIT
				JSR	DODAMAG
				LDY	#damagelev+1	;Heal damage
				SEC
				SED
				LDA	(CHARDL),Y
				SBC	AMHOLD+1
				STA	(CHARDL),Y
				DEY
				LDA	(CHARDL),Y
				SBC	AMHOLD
				STA	(CHARDL),Y
				LDY	#damagemax+1	;Remove critical status
				LDA	(CHARDL),Y
				SEC
				LDY	#damagelev+1
				SBC	(CHARDL),Y
				TAX
				LDY	#damagemax
				LDA	(CHARDL),Y
				LDY	#damagelev
				SBC	(CHARDL),Y
				CLD
				BNE	:SKIPA
				CPX	#critical_amnt+1
				BCC	:SKIPB
:SKIPA			LDY	#status
				LDA	(CHARDL),Y
				AND	#%10111111
				STA	(CHARDL),Y
:SKIPB			JSR	CALC_ALL		;Update stats
				JSR	DODAMAG
				LDY	LEFHOLD
				LDA	YPOINTS,Y
				STA	YPNT
				TYA
				JSR	POINTIT
				JSR	DOENER
				LDX	#<ENHOLD
				LDY	#>ENHOLD
				SEC
				JSR	DRAINER_MAIN
				JSR	DOENER
HCANCEL			LDY	#$12
				JSR	TEXTER2
				JSR	HCLEAR2
				LDX	#$BF
				LDY	#$51
				LDA	#$C6
				JSR	STATCOM
				JSR	DEMARK
				JMP	RECYCLE

DEMARK			LDY	LEFT1
				JSR	MARKSUB
				LDY	RITHOLD	
				CPY	LEFT1
				BEQ	DOUBOVR
				JSR	MARKSUB
DOUBOVR			RTS

DODAMAG			JSR	DAMAGR2
				LDA	#$8A
				LDY	YPNT
				JMP	LINE

DOENER			JSR	POWROUT
				LDA	#$8D
				STA	TEXTBUF+6
				LDA	#$C6
				LDY	YPNT
				JMP	LINE

HACKUP			LDX	#$02
				LDY	#$05
HALOOP1			LDA	HBUFFER,X
				PHA
				AND	#$0F
				STA	TEXTBUF,Y
				DEY
				PLA
				LSR
				LSR
				LSR
				LSR
				STA	TEXTBUF,Y
				DEY
				DEX
				BPL	HALOOP1
				LDA	#$8D
				STA	TEXTBUF+6
				LDX	#$00
HALOOP2			LDA	TEXTBUF,X
				BNE	HASKIP1
				LDA	#$EE
				STA	TEXTBUF,X
				DEC	MAXTEXT
				INX
				BNE	HALOOP2
HASKIP1			RTS

HHOLDER			DB	0,0
HBUFFER			DB	0,0,0

CALC_MAX		LDA	LEFHOLD
				JSR	POINTIT
				LDY	#energylev+1
				LDA	(CHARDL),Y
				TAX
				DEY
				LDA	(CHARDL),Y
				TAY
				LDA	#$50
				JSR	PERCENT_OF
				STX	TEMP
				STY	TEMP+1
				LDA	RITHOLD
				JSR	POINTIT
				LDY	#damagelev+1
				SEC
				SED
				LDA	(CHARDL),Y
				SBC	TEMP
				TAX
				DEY
				LDA	(CHARDL),Y
				SBC	TEMP+1
				CLD
				BCS	:SKIPA
				LDA	(CHARDL),Y
				TAX
				INY
				LDA	(CHARDL),Y
:SKIPA			TAY
				RTS

HCLEAR2			LDX	#$51
				BNE	HCLOOP1
HCLRBOX			LDX	#$69
HCLOOP1			LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	#$40
				STA	SCREENH
				LDY	#$18
				LDA	#$00
HCLOOP2			STA	(SCREENL),Y
				INY
				CPY	#$27
				BNE	HCLOOP2
REINX			INX	
				CPX	#$68
				BEQ	REINX
				CPX	#$BF
				BNE	HCLOOP1
				RTS

NEED_HEAL		LDY	#damagelev
				LDX	#$00
				LDA	(CHARDL),Y
				BNE	:EXIT
				INY
				LDA	(CHARDL),Y
				BNE	:EXIT
				INX
:EXIT			TXA
				RTS

AMHOLD			DB	$00,$00
ENHOLD			DB	$00,$00,$00

HEAL_TYPE		DB	$00
