*-------------------------------
*
*-------------------------------

COMPLETE_SELL	LDA	#$21
				JSR	PICK_A_ROBOT
				LDA	THE_ROBOT
				JSR	POINTIT	
				JSR	REVERSE
				JSR	TOTALUP
				JSR	ADDBODS
				JSR	PACSUB
				JSR	ROSTAT2
				JSR	CANCOUT
				LDY	#e2_SLOT
				LDA	(CHARDL),Y
				LDY	#e7_BACK1
				AND	(CHARDL),Y
				CMP	#$FF
				BNE	BADMENT
				LDA	LWEAPON
				CMP	#$06
				BEQ	BADMENT
				LDA	RWEAPON
				CMP	#$06
				BNE	ALLGOOD
BADMENT			LDA	#$39
				JSR	NAMER
				LDY	#$27	
				JSR	TEXTER
				JSR	SPACEIN
				JSR	VIEWER
				BIT	UNSTROB
:1				LDA	KEYBRD
				BPL	:1
				BIT	UNSTROB
				JSR	CHECK_ENTER
				BNE	:1
				JSR	SPACEOUT
				JMP	OPTIONS

ALLGOOD			LDA	#$2C
				JSR	NAMER
				LDX	#$03
VALOOPD			LDA	RTOTAL,X
				STA	THE_PRICE,X
				DEX
				BPL	VALOOPD
				LDA	#$60
				STA	NOT78
				LDA	#$EE
				STA	PARTALL
				JSR	SEVEN8
				LDA	#$EA
				STA	NOT78
				LDX	#$03
VALOOPE			LDA	THE_PRICE,X
				STA	RTOTAL,X
				DEX
				BPL	VALOOPE
				JSR	NUMBER_TEXT
				JSR	CANCIN
				LDY	#$20	
				JSR	TEXTER
				LDA	#$EE
				STA	PARTALL
				JSR	OPENUP
				JSR	VOMAIN
				JSR	VIEWER
				LDX	PICK
				LDA	#$1F	
				STA	ON1,X
				BIT	UNSTROB
YNLOOP			LDA	KEYBRD
				BPL	YNLOOP
				BIT	UNSTROB
				JSR	CHECK_ENTER
				BEQ	YESSER
				CMP	#"C"
				BNE	YNLOOP
				JMP	OPTIONS

YESSER			LDA	PICK
				BEQ	NOVIEWB
				JSR	MOVETO1
				STA	PRIMARY
NOVIEWB			LDA	#$20
				STA	PAGE
				LDA	#$00
				STA	PICK
				LDA	#$FF	
				STA	ON2
				LDA	THE_ROBOT
				JSR	POINTIT	
				LDY	#disklocation
				LDA	(CHARDL),Y
				PHA
				AND	#$7F
				TAX
				PLA
				AND	#$80
				LDY	#$0F
				CMP	#$80
				BEQ	NOTE
				LDY	#$F0
NOTE			TYA
				EOR	OPENBUF,X
				STA	OPENBUF,X
				LDA	THE_ROBOT
				JSR	POINTIT
				LDY	#disklocation
				LDA	(CHARDL),Y
				STA	SCRATCH
				LDA	#$00
				STA	COUNT
COLOOPA			JSR	POINTIT
				LDY	#robotcyblink
				LDA	(CHARDL),Y
				CMP	SCRATCH
				BEQ	AMATCH
				INC	COUNT
				LDA	COUNT
				CMP	GRPNUMB
				BNE	COLOOPA	
AMATCH			LDA	#$FF
				STA	(CHARDL),Y
				LDX	#<RTOTAL
				LDY	#>RTOTAL
				JSR	CHAR_EARN

				JSR	RESET_CHRLIST
				LDX	THE_ROBOT
				LDA	#$FF
				STA	LEGALS,X
				LDX	#wMembers
				JSR	CALL_WRITERS
				LDX	#wOpen
				JSR	CALL_WRITERS

				LDA	THE_ROBOT
				STA	COUNT
				DEC	GRPNUMB
				CMP	GRPNUMB
				BEQ	NOTDEC
MOVIT2			LDX	COUNT
				TXA
				JSR	POINTIT
				LDA	CHARDL
				STA	TEMP
				LDA	CHARDH
				STA	TEMP+1
				INX
				TXA
				JSR	POINTIT
				LDY	#$7F
MOVEIT			LDA	(CHARDL),Y
				STA	(TEMP),Y
				DEY
				BPL	MOVEIT
				INC	COUNT
				LDA	COUNT
				CMP	GRPNUMB
				BNE	MOVIT2
NOTDEC			JMP	OPTIONS

NUMBER_TEXT		LDX	#4				;Used by COMPLETE_BUY also
:LOOP1			LDA	THE_PRICE-1,X
				PHA
				DEX
				BNE	:LOOP1
				LDA	#4
				JSR	LEFT_DIGITS
				LDX	#7
:LOOP2			LDA	TEXTBUF,X
				STA	TEXCRED,X
				STA	TEXOVER,X
				DEX
				BPL	:LOOP2
				RTS

NAMER			PHA
				LDY	#$09
NALOOP1			LDA	(CHARDL),Y
				STA	TEXTBUF,Y
				DEY
				BPL	NALOOP1
				LDY	#$09
NALOOP2			LDA	TEXTBUF,Y
				CMP	#$0A
				BNE	NASKIP1
				DEY
				BPL	NALOOP2
NASKIP1			LDA	#$8D
				STA	TEXTBUF+1,Y
				PLA
				LDY	#$59
				JSR	LINE
				LDA	XCOORD
				STA	TEXT20
				STA	TEXT27
				LDA	SHIFT
				STA	TEXT20+1
				STA	TEXT27+1
				LDA	YCOORD
				STA	TEXT20+2
				STA	TEXT27+2
				RTS

ADDBODS			LDA	#$00
				LDX	#$03
ADLOOP1			STA	RTOTAL,X
				DEX
				BPL	ADLOOP1
				LDY	#$1B
ADLOOP2			LDX	#$03	
				CLC
ADLOOP3			LDA	BODTOT,Y
				SED
				ADC	RTOTAL,X
				CLD
				STA	RTOTAL,X
				DEY
				DEX
				BPL	ADLOOP3
				TYA
				BPL	ADLOOP2
				RTS		
