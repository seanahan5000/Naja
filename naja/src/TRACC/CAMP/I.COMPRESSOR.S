COMPRESS		CLC
				JSR	CALL_DRAIN
				BCS	HASNUFF
				LDA	#GREEN
				JSR	:SUBA
				JSR	SPACE_KEY
				LDA	#BLACK1
				JSR	:SUBA
				JMP	EXIT_COMPRESS
:SUBA			STA	COLOR
				LDY	#$09
				JSR	LINER
				LDY	#$09
				JSR	TEXTER2
				LDY	#$05
				JMP	TEXTER2

HASNUFF			JSR	RESET_EQLIST
				JSR	ESCAN_PUTABLE
				BCS	SPOTGOT
				JSR	BANK_2
				LDX	#<TEXTC+3
				LDY	#>TEXTC+3
				JSR	FLUSH_NAME
				JSR	BANK_1
				LDA	#GREEN
				JSR	FULLSUB
				JSR	SPACE_KEY
				LDA	#BLACK1
				JSR	FULLSUB
				JMP	EXIT_COMPRESS

SPOTGOT			STX	CMPHOLD
				JSR	BIGBOXR
				LDY	#$08
				JSR	TEXTER2
				JSR	RESET_CHRLIST
				LDA	#$00
				STA	CHAR_LIST+7
				LDX	LEFT1
				LDA	#$FF
				STA	CHAR_LIST,X
				LDA	#$00
				STA	GCURPOS
				JSR	PRE_ARROW
				JSR	GETKEY
				LDA	CURSOR
				CMP	#$07
				BNE	NOSCANC
				JSR	ERASE_CURSOR
CANCLY			LDY	#$08
				JSR	TEXTER2
				LDA	#BLACK1
				STA	COLOR
				LDY	#$03	
				JSR	LINER
				JSR	SWITCHU
				LDA	LEFT1
				JSR	POINTIT
				JMP	EXIT_COMPRESS

NOSCANC			STA	RIGHT1
				JSR	POINTIT
				JSR	RESET_EQLIST
				JSR	ESCAN_FULSLOT
				LDX	#<FIND_NODROPS
				LDY	#>FIND_NODROPS
				JSR	ESCAN_COMMON
				BCC	:SKIPA
				LDY	#$12
				JSR	TEXTER2
				LDY	#$0A
				JSR	TEXTER2
				JSR	SPACE_KEY
				JMP	CANCLY
:SKIPA			LDY	#$12
				JSR	TEXTER2
				LDY	#$0B
				JSR	TEXTER2
				BIT	UNSTROB
SGKEYER			LDA	KEYBRD
				BPL	SGKEYER
				BIT	UNSTROB
				JSR	CHECK_ENTER
				BEQ	SGENTER
				CMP	#$C3
				BNE	SGKEYER
				JMP	CANCLY

SGENTER			LDY	#$08
				JSR	TEXTER2
				LDA	#BLACK1
				STA	COLOR
				LDY	#$03	
				JSR	LINER
				JSR	SWITCHU
				LDY	#status
				LDA	#compressd
				STA	(CHARDL),Y
				LDY	#$2D
				LDA	(CHARDL),Y
				PHA
				AND	#$80
				STA	SCRATCH
				PLA
				AND	#$0F
				ASL	A
				ASL	A
				ASL	A
				ASL	A
				ORA	SCRATCH
				ORA	#$03
				PHA
				JSR	BREAK_ROBOT
				LDA	LEFT1
				JSR	POINTIT
				LDX	CMPHOLD
				LDY	CORESPS,X
				LDA	#$43
				STA	(CHARDL),Y
				STA	ETABLE
				INY
				PLA
				STA	(CHARDL),Y
				STA	ETABLE+1
				INY
				LDA	#eq_undropable
				STA	(CHARDL),Y
				STA	ETABLE+2
				INY
				LDA	#$00
				STA	(CHARDL),Y
				STA	ETABLE+3
				JSR	EQUIP1
				LDX	CMPHOLD
				LDY	CHARLIN,X
				BIT	CRFLAG
				BPL	:SKIPA
				LDY	ROBOLIN,X
:SKIPA			LDA	#$42
				JSR	LINE
				SEC
				JSR	CALL_DRAIN
				DEC	GRPNUMB

				LDA	RIGHT1
				STA	COUNT
				CMP	GRPNUMB
				BEQ	:SKIPB
				JSR	HIDE_MEMBER
:SKIPB			LDA	RIGHT1
				CMP	LEFT1
				BCS	EXIT_COMPRESS
				DEC	LEFT1
EXIT_COMPRESS	LDA	LEFT1
				JSR	POINTIT
				LDA	#$02
				STA	CURSOR
				JSR	IMARKER
				LDA	#$01
				JMP	RDOPTN

FIND_NODROPS	INY
				INY
				LDA	(CHARDL),Y
				AND	#eq_undropable
				EOR	#eq_undropable
				RTS

CALL_DRAIN		LDX	#<TAKE_5000
				LDY	#>TAKE_5000
				JMP	DRAINER_MAIN
TAKE_5000		DFB	$00,$50,$00

FULLSUB			STA	COLOR
				LDY	#$09
				JSR	LINER
				LDY	#$0C
				JSR	TEXTER2
				LDY	#$05
				JMP	TEXTER2

CMPHOLD			DFB	$00
