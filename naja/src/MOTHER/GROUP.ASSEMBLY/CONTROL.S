SCRATCH			=	$00
BUBBLE			=	$0A
BEGROW			=	$10
ENDROW			=	$11
ENDLINE			=	$12
BEGEND			=	$12
BEGLINE			=	$13
LINENUM			=	$17
COUNTER			=	$18
SECOUNT			=	$19

MASK			=	ZBLOCK_A0+0
SECHOLD			=	ZBLOCK_A0+1
ADDABLE			=	ZBLOCK_A0+2
REMABLE			=	ZBLOCK_A0+3
HCURSOR			=	ZBLOCK_A0+4
FSFLAG			=	ZBLOCK_A0+5
XHOLD			=	ZBLOCK_A0+6
GCOUNT			=	ZBLOCK_A0+7

CHARPTR			=	ZBLOCK_A8+0		;+1
EXAMFLG			=	ZBLOCK_A8+2
REDRAW			=	ZBLOCK_A8+3
MEMBNUM			=	ZBLOCK_A8+4
*
*
*

LOADBUF			=	$9500

RACECOL			=	23
RACEND			=	29
PROFCOL			=	12
PROFEND			=	20
STATCOL			=	37
STATEND			=	44
BASE_HEIGHT		=	105
BASE_COLUMN		=	27

GROUP_ASSEM		LDA	#$00
				STA	GCOUNT

				LDA	#>LOADBUF
				STA	SSTOREH
				LDX	#11
				LDA	OPENBUF+10
				CMP	#$0F
				BNE	:SKIPA
:LOOP1			DEX
				BEQ	:SKIPB
				LDA	OPENBUF-1,X
				BEQ	:LOOP1
:SKIPA			TXA
				LDX	#<charTrack
				LDY	#>charTrack
				JSR	FLOADER
:SKIPB			JSR	DRIVE_OFF

				LDA	KEYBRD
				CMP	#$9B
				BNE	NORAWAY
				JSR	MOVETO2
				STA	SCNDARY
				JMP	ASSMOUT

NORAWAY			LDX	#$00
NOR2			LDA	OPENBUF,X
				BNE	MAYHAVE
				INX
				BNE	NOR2
MAYHAVE			CPX	#$0A
				BNE	DOHAVE
				AND	#$F0
				BNE	DOHAVE
				LDY	#$00
				JSR	TEXTER
				LDY	#$08
				JSR	TEXTER
				JSR	ANIMATE
				JMP	GOMOUT

DOHAVE			LDY	#$01
				JSR	TEXTER
				LDY	#$08
				JSR	TEXTER
				JSR	ANIMATE
				BCC	NOESC2
GOMOUT			JSR	MOVETO2
				STA	SCNDARY
				JMP	ASSMOUT

NOESC2			LDA	#$40
				STA	PAGE
				JSR	RTMAKER
				JSR	COUNTUP

				LDA	#$00
				STA	EXAMFLG
				LDX	#6
:LOOP1			CPX	GRPNUMB
				BCS	:SKIPA
				LDA	#$FF
:SKIPA			STA	SAVE_TABLE,X
				DEX
				BPL	:LOOP1

				JSR	MARKIN
				JSR	CLEAR2	
				LDA	GCOUNT
				STA	ACURSOR
				STA	RCURSOR
				STA	ECURSOR
				JSR	DOPTION
				STA	SCNDARY	
				LDA	#$02
				STA	CURSOR
COMMONX			LDA	#$27
				STA	XHOLD
				LDA	#$D2
				STA	RESER+1
				JSR	SETTOFF
				LDA	#$00
				STA	ALEGALS
				STA	ALEGALS+2
				LDX	GRPNUMB
				BEQ	LINTRAN
				STA	ALEGALS+1
LINTRAN			LDX	#$02
LINLOOP			LDA	OPLINS,X
				STA	LINTABL,X
				DEX
				BPL	LINLOOP
				JSR	KDOWN2
				LDA	#$00
				STA	RESER+1
				CPX	#$FF
				BNE	NORESN
				JMP	RESIGN
NORESN			LDX	CURSOR
				BEQ	GOSEE
				DEX
				BEQ	:SKIPA
				JMP	ASSMOUT
:SKIPA			JMP	REMOVE

DOPTION			LDA	#GREEN	
				STA	COLOR
				LDY	#$03
				JSR	LINER
				LDA	#VIOLET
				STA	COLOR
				LDY	#$04
				JSR	LINER	
				LDY	#$03
				JSR	TEXTER
				LDA	#$02
				STA	YPNT
				LDA	#$42
				STA	SELECT
				JSR	HEADER
				LDA	GRPNUMB	
				BEQ	PRVDONE
				LDA	#$01
				STA	MEMBNUM
STATTER			JSR	GOSTATL	
				LDA	MEMBNUM
				INC	MEMBNUM
				CMP	GRPNUMB
				BNE	STATTER
PRVDONE			RTS	
GOSEE			JMP	SEE

OPLINS			DB	$69,$73,$7D

SEE				JSR	ACURSIT2
				LDA	#$20
				STA	PAGE
				JSR	ONROS
				JSR	DRAW_ROSTER
				LDA	#$03
				STA	HCURSOR
ROSTO2			LDY	#$03
				LDA	#$00
CLEANS			STA	LEGLOPT,Y
				DEY
				BPL	CLEANS
				JSR	COUNTUP
				LDX	#$FF
				STX	LEGLOPT+4
				LDA	ADDABLE
				BNE	CANADD
				STX	LEGLOPT
CANADD			LDA	REMABLE	
				BNE	CANREM
				STX	LEGLOPT+1
CANREM			LDA	GRPNUMB
				CMP	#$07	
				BNE	CANADD2
				STX	LEGLOPT
CANADD2			LDY	#$0D
				JSR	TEXTER
				STA	PRIMARY
SEER			JSR	HRIGHT1
				LDX	HCURSOR
				BEQ	ADDRTN
				DEX	
				BEQ	REMRTN
				DEX	
				BEQ	CHNRTN
				LDA	#$40
				STA	PAGE
				BIT	REDRAW
				BVC	NOREDR
				JSR	CLEAR2
				JSR	DOPTION
NOREDR			LDA	SCNDARY	
				LDA	#$20
				STA	PAGE
				JSR	CLRBOTL	
				JSR	DOTTER
				LDA	#$40
				STA	PAGE
				LDA	#$02
				STA	CURSOR
				JMP	COMMONX	
ADDRTN			JMP	ADD	
REMRTN			JMP	REMOVER	
CHNRTN			JMP	CHANGE_NAME
ACURSOR			DB	$00
ECURSOR			DB	$00
RCURSOR			DB	$00

BLINK			LDA	#$10
				JSR	WAIT
				LDA	#$F0
				JSR	WAIT2
				BMI	HITAKEY
				JSR	HARROW
				LDA	#$00
				JSR	WAIT2
				JSR	HARROW
LRCOMN			BIT	KEYBRD
				BPL	BLINK
				LDA	KEYBRD
HITAKEY			BIT	UNSTROB
				JSR	CHECK_ENTER
				BEQ	HENTER
				JSR	CHECK_LEFT
				BEQ	HLEFT
				JSR	CHECK_RIGHT
				BEQ	HRIGHT
				CMP	#$9B
				BNE	LRCOMN
				JSR	HARROW
				JSR	MOVETO2
				STA	SCNDARY
				JMP	ASSMOUT
HLEFT			JSR	HARROW
HLEFT1			DEC	HCURSOR
				BPL	HLEFT2
				LDA	#$03
				STA	HCURSOR
HLEFT2			LDY	HCURSOR
				LDA	LEGLOPT,Y
				BNE	HLEFT1
				JSR	HARROW
				JMP	LRCOMN
HRIGHT			JSR	HARROW
HRIGHT1			INC	HCURSOR
				LDA	HCURSOR
				CMP	#$04
				BNE	HRIGHT2
				LDA	#$00
				STA	HCURSOR
HRIGHT2			TAY
				LDA	LEGLOPT,Y
				BNE	HRIGHT1
				JSR	HARROW
				JMP	LRCOMN
HENTER			JMP	HARROW	
LEGLOPT			DB	$00,$00,$00
				DB	$00,$00

FINDHIM			LDY	CURSOR
				DEY
				JSR	POINTR2
				LDA	#$00
				STA	COUNT
NFLOOP			JSR	POINTIT
				LDX	#$00
				LDY	#$2D
				LDA	(CHARDL),Y
				LDY	#$0E
				CMP	(CHARPTR),Y
				BEQ	MATCH
				INC	COUNT
				LDA	COUNT
				CMP	GRPNUMB
				BNE	NFLOOP
				DEX	
MATCH			RTS

HARROW			LDY	HCURSOR
				LDA	HEXS,Y
				STA	XHOLD	
				LDA	#$B8
				STA	YPNT	
				LDY	#$04
				JMP	SPCHARSA
HEXS			DB	34,70,124,208

CURSIT			LDA	#$00
				JMP	DOSPECL
CURSDA			LDX	#$02	
				LDY	#$00
				BEQ	CURS2CE			;Always
CURSAD			LDX	#$00	
				LDY	#$02
CURS2CE			STX	CFIRST+1
				STY	CSECOND+1
				LDY	CURSOR
				DEY
				JSR	POINTR2
				LDY	#$00
				LDA	(CHARPTR),Y
				BPL	CURSIT
				LDA	#$60
				STA	DSTOP
				LDY	CURSOR
				DEY
CFIRST			LDA	#$00
				JSR	DOSPECL
				TAY
				JSR	SPCHARSA
CSECOND			LDA	#$02
				JSR	DOSPECL
				TAY
				JSR	SPCHARSA
				LDA	#$48
				STA	DSTOP
				RTS

DOTTER			LDA	#$00
				STA	COUNTER
MOREDOT			LDY	COUNTER
				JSR	POINTR2
				LDY	#$00
				LDA	(CHARPTR),Y
				BPL	NOTDOT
				LDY	COUNTER
				LDA	#$02
				JSR	DOSPEC2	
NOTDOT			INC	COUNTER
				LDA	COUNTER
				CMP	GCOUNT
				BNE	MOREDOT
				RTS

DOSPECL			LDY	CURSOR
				DEY
DOSPEC2			PHA
				LDA	LINUMS,Y
				STA	YPNT	
				LDA	#$00
				STA	XHOLD	
				PLA
DSTOP			PHA
				TAY
				JSR	SPCHARSA
				LDA	#$27
				STA	XCOORD
				LDA	#$00
				STA	SHIFT
				LDA	#$60
				STA	STOPEM
				PLA
				TAY
				INY
				JSR	SPCHARSA
				JSR	CHAROUT
				LDA	#$A5
				STA	STOPEM
				RTS

MARKIT			LDA	XHOLD
				PHA
				LDA	#$2A
				STA	XHOLD
				LDY	#$06
				JSR	SPCHARSA
				PLA
				STA	XHOLD
				RTS

SPCHARSA		LDA	TABLE7,Y
				TAX
				LDY	#$00
MORSPCH			LDA	SPLETTR,X
				STA	CHARTAB,Y
				INY
				INX
				CPY	#$07	
				BNE	MORSPCH
STOPEM			LDA	YPNT
				STA	YCOORD
				LDA	XHOLD
				STA	XCOORD
				JSR	SHFTCLC
				JSR	TABLER
				JMP	CHAROUT

TABLE7			DB	$00,$07,$0E
				DB	$15,$1C,$23
				DB	$2A
SPLETTR			DB	$03,$07,$0F
				DB	$3F,$0F,$07
				DB	$03
				DB	$60,$70,$78
				DB	$7E,$78,$70
				DB	$60
				DB	$00,$0C,$1E
				DB	$1E,$0C,$00
				DB	$00
				DB	$00,$18,$3C
				DB	$3C,$18,$00
				DB	$00
				DB	$00,$03,$0E
				DB	$3E,$0E,$03
				DB	$00
				DB	$00,$0C,$18
				DB	$7F,$18,$0C
				DB	$00
				DB	$00,$1F,$1F
				DB	$1F,$1F,$1F
				DB	$00

LINUMS			DB	$0D,$15,$1D
				DB	$25,$2D,$35
				DB	$3D,$45,$4D
				DB	$55,$5D,$65
				DB	$6D,$75,$7D
				DB	$85,$8D,$95
				DB	$9D,$A5,$AD

RFLAG			DB	$00
RBLINK			LDA	#$10
				JSR	WAIT
				LDA	#$F0
				JSR	WAIT2
				BMI	RKEYGOT
				JSR	CURSAD	
				LDA	#$00
				JSR	WAIT2
				JSR	CURSDA	
RKEYIN			BIT	KEYBRD
				BPL	RBLINK
RKEYGOT			LDA	KEYBRD
				BIT	UNSTROB
				JSR	CHECK_UP
				BEQ	RUP
				JSR	CHECK_DOWN
				BEQ	RDOWN
				CMP	#"C"
				BEQ	RCANC
				JSR	CHECK_ENTER
				BNE	RKEYIN
				LDX	#$00	
				RTS
RCANC			JSR	CURSAD
				JSR	CLRBOTL
				LDY	#$0D
				JSR	TEXTER
				LDX	#$FF
				RTS
RUP				JSR	CURSAD
RUP2			DEC	CURSOR
				BNE	RUP3
				LDA	GCOUNT
				STA	CURSOR
RUP3			LDY	CURSOR
				LDA	ALEGALS-1,Y
				AND	RFLAG
				BEQ	RUP2
				JSR	CURSDA
				JMP	RKEYIN
RDOWN			JSR	CURSAD
RDOWN2			INC	CURSOR
				LDA	CURSOR
				CMP	GCOUNT
				BEQ	RDOWN3
				BCC	RDOWN3
				LDA	#$01
				STA	CURSOR
RDOWN3			TAY
				LDA	ALEGALS-1,Y
				AND	RFLAG
				BEQ	RDOWN2
				JSR	CURSDA
				JMP	RKEYIN

COUNTUP			LDA	#$00
				STA	ADDABLE
				STA	REMABLE
				STA	LOST_CHARS
				STA	COUNTER
NEXTC2			TAY
				LDA	#$01
				STA	ALEGALS,Y
				JSR	POINTR2
				LDY	#$00
				LDA	(CHARPTR),Y
				BPL	NOTINGR
				LDY	#$0A
				LDA	(CHARPTR),Y
				AND	#$F0
				CMP	#cybernate*16
				BNE	NOTCYB
				LDY	#$0F
				LDA	(CHARPTR),Y
				CMP	#$FF
				BEQ	NOTCYB
				STA	SCRATCH
				JSR	RCSUB
				CPX	#$FF
				BEQ	INGROUP
NOTCYB			LDY	COUNTER	
				LDA	#$41	
				STA	ALEGALS,Y
				INC	REMABLE
				BNE	INGROUP			;Always
NOTINGR			LDY	#$0A
				LDA	(CHARPTR),Y
				BPL	NOTAROB
				LDY	#$0F
				LDA	(CHARPTR),Y
				STA	SCRATCH
				JSR	RCSUB
				CPX	#$FF
				BNE	INGROUP
NOTAROB			LDY	COUNTER
				LDA	ALEGALS,Y
				ORA	#$80
				STA	ALEGALS,Y
				INC	ADDABLE
INGROUP			LDY	#$0D
				LDA	(CHARPTR),Y
				AND	#%00111111
				CMP	#intracc
				BNE	:SKIPA
				INC	LOST_CHARS
:SKIPA			INC	COUNTER
				LDA	COUNTER
				CMP	GCOUNT
				BNE	NEXTC2
				RTS

RCSUB			LDX	#$FF
				LDA	GRPNUMB
				BEQ	:SKIPA
				LDA	#0
				STA	COUNT
:LOOP1			JSR	POINTIT
				LDY	#disklocation
				LDA	(CHARDL),Y
				CMP	SCRATCH
				BEQ	:EXIT
				INC	COUNT
				LDA	COUNT
				CMP	GRPNUMB
				BNE	:LOOP1
:SKIPA			INX
:EXIT			RTS

RISUB			JSR	SETTOFF
				LDX	GRPNUMB
				DEX
REMLOOP			LDA	YPOINTS,X
				STA	LINTABL,X
				LDA	#$00
				STA	ALEGALS,X
				TXA
				JSR	POINTIT
				LDY	#profrace
				LDA	(CHARDL),Y
				AND	#$F0
				CMP	#cybernate*16
				BNE	REMSKIP	
				LDY	#robotcyblink
				LDA	(CHARDL),Y
				CMP	#$FF
				BEQ	REMSKIP	
				STA	SCRATCH
				LDA	#$00
				STA	COUNT
RMLOOP			JSR	POINTIT
				LDY	#disklocation
				LDA	(CHARDL),Y
				CMP	SCRATCH
				BEQ	NOCANDO
				INC	COUNT
				LDA	COUNT
				CMP	GRPNUMB
				BNE	RMLOOP
				BEQ	REMSKIP
NOCANDO			LDA	#$FF
				STA	ALEGALS,X
REMSKIP			DEX
				BPL	REMLOOP
				LDA	#$00
				STA	ALEGALS+7
				LDA	#$46
				STA	LINTABL+7
				LDA	#$07
				STA	CURSOR
				LDA	#$03
				STA	XHOLD
				LDY	#$04
				JMP	TEXTER

*-------------------------------

MEMBER_SAVE		LDY	#0
				LDX	#6
:LOOP1			LDA	SAVE_TABLE,X
				EOR	#$FF
				STA	LEGALS,X
				BNE	:SKIPA
				STA	SAVE_TABLE,X
				INY
:SKIPA			DEX
				BPL	:LOOP1
				TYA
				BEQ	:EXIT
				LDX	#wMembers
				JMP	CALL_WRITERS
:EXIT			RTS

*-------------------------------

ONROS			LDA	#$06
				TAX
				LDY	#$B8
				BNE	ONCOM			;Always
ONGRP			LDA	#$09
				LDX	#$02
				LDY	#$52	
ONCOM			STA	TEXT6
				STX	TEXT6+1
				STY	TEXT6+2
				RTS

ASSMOUT			JSR	CLEAR1
				LDA	#$04	
				STA	XPOS
				LDA	#$07	
				STA	YPOS
				LDA	#$00	
				STA	DIRECTN
				LDA	#$20
				STA	PAGE
				LDA	#WHITE1
				STA	COLOR
				JSR	GETNSEW
				JSR	NOSCAN
				STA	PRIMARY
				JMP	RDKEY

*------------------------------*
*                              *
*        NAME: 00-09           *
*   PROF/RACE: 0A              *
*       LEVEL: 0B-0C           *
*      STATUS: 0D              *
* SECTOR/HALF: 0E              *
* SECTOR/HALF: 0F              *
* (ROBOT-CYBERNATE LINK)       *
*                              *
*  Hit bit of NAME set flags   *
*    character as in group     *
*------------------------------*

RTMAKER			LDA	#>LOADBUF
				STA	:LOOP1+2
				LDA	#$40
				STA	:MOD1+2
				LDY	#11
				LDX	#0
:LOOP1			LDA	LOADBUF,X		;Modified
:MOD1			STA	$4000,X			;Modified
				DEX
				BNE	:LOOP1
				INC	:LOOP1+2
				INC	:MOD1+2
				DEY
				BNE	:LOOP1

				LDA	#$00
				STA	COUNTER
				LDA	#$40
				STA	CHARDH
NOTHERT			LDY	COUNTER
				LDA	OPENBUF,Y
				BEQ	RTOUT1
				PHA
				AND	#$F0
				BEQ	NOTFRST
				LDY	GCOUNT
				JSR	POINTR2
				LDA	#$00
				STA	CHARDL
				JSR	MAKE1
NOTFRST			PLA
				AND	#$0F
				BEQ	RTOUT1
				LDA	COUNTER
				CMP	#$0A
				BEQ	RTOUT2
				LDY	GCOUNT
				JSR	POINTR2
				LDA	#$80
				STA	CHARDL
				JSR	MAKE1
RTOUT1			INC	COUNTER
				INC	CHARDH
				JMP	NOTHERT
RTOUT2			RTS

POINTR2			LDA	#$00
				STA	CHARPTR+1
				STY	CHARPTR
				LDY	#$04
REROT			ASL	CHARPTR
				ROL	CHARPTR+1
				DEY
				BNE	REROT
				LDA	CHARPTR
				CLC
				ADC	#<ROSTBUF
				STA	CHARPTR
				LDA	CHARPTR+1
				ADC	#>ROSTBUF
				STA	CHARPTR+1
				RTS

MAKE1			LDY	#disklocation
				LDA	(CHARDL),Y
				STA	SCRATCH
				LDA	CHARDH
				PHA
				LDA	CHARDL
				PHA
				LDA	GRPNUMB
				BEQ	NONEIN
				LDA	#$00
				STA	COUNT
MAKLOOP1		JSR	POINTIT
				LDY	#disklocation
				LDA	(CHARDL),Y
				CMP	SCRATCH
				BEQ	GOTHIM
				INC	COUNT
				LDA	COUNT
				CMP	GRPNUMB
				BNE	MAKLOOP1
NONEIN			PLA
				STA	CHARDL
				PLA
				STA	CHARDH
				JMP	MAKEIT
GOTHIM			JSR	MAKEIT
				PLA
				STA	CHARDL
				PLA
				STA	CHARDH
				RTS

MAKEIT			LDY	#$09
RENAMOV			LDA	(CHARDL),Y
				STA	(CHARPTR),Y
				DEY
				BPL	RENAMOV
				LDA	#$0A
				STA	COUNT
RETICK			LDY	COUNT
				LDA	TICKS-10,Y
				TAY
				LDA	(CHARDL),Y
				LDY	COUNT
				STA	(CHARPTR),Y
				LDA	COUNT
				INC	COUNT
				CMP	#$0F
				BNE	RETICK
				INC	GCOUNT
				RTS

TICKS			DB	profrace
				DB	level
				DB	level+1
				DB	status
				DB	disklocation
				DB	robotcyblink

MARKIN			LDA	GRPNUMB	
				BEQ	MARKOUT
				LDA	#$00
				STA	COUNT
				LDA	#$FF
				STA	MASK
GOG1			JSR	NEXTG1
				INC	COUNT
				LDA	COUNT
				CMP	GRPNUMB
				BNE	GOG1
				RTS
NEXTG1			LDA	COUNT
				JSR	POINTIT
				LDY	#$2D
				LDA	(CHARDL),Y
				STA	SECHOLD
				LDA	#$00
				STA	COUNTER
NEXTR1			LDY	COUNTER
				JSR	POINTR2
				LDY	#$0E
				LDA	(CHARPTR),Y
				CMP	SECHOLD
				BEQ	GOTMTCH
				INC	COUNTER
				LDA	COUNTER
				CMP	GCOUNT
				BNE	NEXTR1
				BEQ	MARKOUT
GOTMTCH			LDY	#$00
				LDA	(CHARPTR),Y
				ORA	#$80
				AND	MASK
				STA	(CHARPTR),Y
MARKOUT			RTS

KBLINK			LDA	#$10
				JSR	WAIT
				LDA	#$F0
				JSR	WAIT2
				BMI	GOTKEY
				JSR	ACURSIT2
				LDA	#$00
				JSR	WAIT2
				JSR	ACURSIT2
KEYMOVE			BIT	KEYBRD
				BPL	KBLINK
GOTKEY			LDA	KEYBRD
				BIT	UNSTROB
				JSR	CHECK_UP
				BEQ	KUP
				JSR	CHECK_DOWN
				BEQ	KDOWN
				LDX	#$FF
RESER			CMP	#$00	
				BEQ	RESIGNR
				CMP	#$9B
				BEQ	ESCAPED
				JSR	CHECK_ENTER
				BNE	KEYMOVE
				LDX	#$00
RESIGNR			RTS
ESCAPED			PLA
				PLA
				JMP	ASSMOUT
KUP				JSR	ACURSIT2
KUP2			DEC	CURSOR
				BPL	KUP3
				LDA	#$07
				STA	CURSOR
KUP3			LDY	CURSOR
				LDA	ALEGALS,Y
				BNE	KUP2
				JSR	ACURSIT2
				JMP	KEYMOVE
KDOWN			JSR	ACURSIT2
KDOWN2			INC	CURSOR
				LDA	CURSOR
				CMP	#$08
				BNE	KDOWN3
				LDA	#$00
				STA	CURSOR
KDOWN3			LDY	CURSOR
				LDA	ALEGALS,Y
				BNE	KDOWN2
				JSR	ACURSIT2
				JMP	KEYMOVE
SETTOFF			LDX	#$14	
				LDA	#$FF
SETLOOP			STA	ALEGALS,X
				DEX
				BPL	SETLOOP
				RTS

ACURSIT2		LDY	CURSOR
				LDA	LINTABL,Y
				STA	YPNT
				LDA	#$2C
				STA	TEXTBUF
				LDA	#$8D
				STA	TEXTBUF+1
				LDA	XHOLD
				LDY	YPNT
				JSR	LINE
				LDA	XHOLD
				PHA
				CLC
				ADC	#$05
				STA	XHOLD
				LDY	#$05
				JSR	SPCHARSA
				PLA
				STA	XHOLD
				RTS

CONVRTR			LDY	#$00
CVERT			LDA	TEXTBUF,Y
				CMP	#$8D
				BEQ	LASTONE
				JSR	CONVERT
				TXA
				STA	TEXTBUF,Y
				INY
				JMP	CVERT
LASTONE			RTS	

CLRBOTL			LDX	#$B8
				LDA	#$BF
				STA	ENDLINE
				LDA	#$00
				STA	BEGROW
				LDA	#$27
				STA	ENDROW
CLEAR			INC	ENDROW
				INC	ENDLINE
CLRMOR1			LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	PAGE
				STA	SCREENH
				LDY	BEGROW
				LDA	#$00
CLRMOR2			STA	(SCREENL),Y
				INY
				CPY	ENDROW
				BNE	CLRMOR2
				INX
				CPX	ENDLINE
				BNE	CLRMOR1
				RTS

GOSTATL			LDX	MEMBNUM
				DEX
				LDA	YPOINTS,X
				STA	YPNT
				TXA	
				JMP	STATLIN
