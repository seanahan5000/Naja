LINENUM			=	TOP
COLUMN			=	LEFT
RPBYTE			=	CURCHAR

*** ADDRESS (WORD), HEIGHT (BYTE), WIDTH (BYTE)

BUFF_UNPACK		LDY	#0
:LOOP1			LDA	(TBLPNTL),Y
				STA	POINT_HOLD,Y
				INY
				CPY	#4
				BNE	:LOOP1

				LDA	#$00
				STA	RPCOUNT
				LDA	WIDTH
				STA	COLUMN

:LOOP2			LDA	POINT_HOLD
				STA	POINTL
				LDA	POINT_HOLD+1
				STA	POINTH
				LDA	HEIGHT
				STA	LINENUM

:LOOP3			LDA	RPCOUNT
				BEQ	:SKIPA
				DEC	RPCOUNT
				LDA	RPBYTE
				INY
				BNE	:SKIPB			;Always
:SKIPA			LDY	#$00
				LDA	(TBLPNTL),Y
				CMP	#RPCODE
				BNE	:SKIPB
				INY
				LDA	(TBLPNTL),Y
				STA	RPCOUNT
				INY
				LDA	(TBLPNTL),Y
				STA	RPBYTE
:SKIPB			TAX
				INY
				TYA
				CLC
				ADC	TBLPNTL
				STA	TBLPNTL
				BCC	:SKIPC
				INC	TBLPNTH
:SKIPC			LDY	#$00
				TXA
				STA	(POINTL),Y
				LDA	POINTL
				CLC
				ADC	WIDTH
				STA	POINTL
				BCC	:SKIPD
				INC	POINTH
:SKIPD			DEC	LINENUM
				BNE	:LOOP3
				DEC	COLUMN
				BEQ	:EXIT
				INC	POINT_HOLD
				BNE	:LOOP2
				INC	POINT_HOLD+1
				BNE	:LOOP2			;Always
:EXIT			RTS


*** These three must be consecutive in this order

POINT_HOLD		DW	0
HEIGHT			DFB	0
WIDTH			DFB	0
