UP				LDA	#<UP_BEGIN
				STA	LODPNTL
				LDA	#>UP_BEGIN
				STA	LODPNTH
				LDA	#THICK
				STA	COUNTER
				LDA	#UBEGLIN	
				STA	LINENUM
ULOOPA			JSR	USCROLL
ULOOPB			LDY	LINENUM
				LDA	LOBYTES,Y
				STA	DESTINL
				LDA	HIBYTES,Y
				ORA	#$20
				STA	DESTINH
				LDY	#$27
ULOOPC			LDA	(LODPNTL),Y
				STA	(DESTINL),Y
				DEY
				BPL	ULOOPC
				DEC	LINENUM
				DEC	COUNTER
				LDA	LODPNTL
				SEC
				SBC	#$28	
				STA	LODPNTL
				LDA	LODPNTH
				SBC	#$00
				STA	LODPNTH
				LDA	LODPNTL
				CMP	#<DOWN_BEGIN-40
				BNE	USKIP1
				LDA	LODPNTH
				CMP	#>DOWN_BEGIN-40
				BEQ	UMAIN
USKIP1			LDA	COUNTER
				CMP	#$00
				BNE	ULOOPB
				BIT	KEYBRD
				BMI	UOUT
				LDA	#THICK
				STA	COUNTER
				LDA	#UBEGLIN	
				STA	LINENUM
				JMP	ULOOPA
UMAIN			LDA	#$BF	
				STA	LOADLIN
				JMP	USKIP2
ULOOPD			JSR	USCROLL
ULOOPE			LDY	LOADLIN
				LDA	LOBYTES,Y
				STA	SCREENL
				LDA	HIBYTES,Y
				ORA	#$40
				STA	SCREENH
				LDY	LINENUM
				LDA	LOBYTES,Y
				STA	DESTINL
				LDA	HIBYTES,Y
				ORA	#$20
				STA	DESTINH
				LDY	#$27
ULOOPF			LDA	(SCREENL),Y
				STA	(DESTINL),Y
				DEY	
				BPL	ULOOPF
				LDA	LOADLIN
				DEC	LOADLIN
				CMP	#$00	
				BEQ	UOUT
				DEC	LINENUM
				DEC	COUNTER
USKIP2			LDA	COUNTER
				BNE	ULOOPE
				BIT	KEYBRD
				BMI	UOUT
				LDA	LOADLIN
				CMP	#UBEGLIN	
				BCS	USKIP3
				LDA	#THICK
				STA	COUNTER
				LDA	#$BE	
				SEC
				SBC	LOADLIN	
				STA	FROMCNT
				LDA	LOADLIN
				STA	LINENUM
				JSR	USCRLL2
				JMP	ULOOPE
USKIP3			LDA	#UBEGLIN	
				STA	LINENUM
				LDA	#THICK
				STA	COUNTER
				JMP	ULOOPD
UOUT			RTS
USCROLL			LDA	#$BF-THICK	
				STA	FROMCNT
USCRLL2			LDA	#$BF	
				STA	TOCNT
ULOOPG			LDX	FROMCNT
				LDA	LOBYTES,X	
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	#$20
				STA	SCREENH
				LDX	TOCNT
				LDA	LOBYTES,X
				STA	DESTINL	
				LDA	HIBYTES,X	
				ORA	#$20
				STA	DESTINH
				LDY	#$27
ULOOPH			LDA	(SCREENL),Y	
				STA	(DESTINL),Y
				DEY
				BPL	ULOOPH
				LDA	FROMCNT
				BEQ	UEXIT
				DEC	FROMCNT
				DEC	TOCNT
				JMP	ULOOPG
UEXIT			RTS	
