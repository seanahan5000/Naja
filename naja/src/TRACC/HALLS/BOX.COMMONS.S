*   (A)----------(F)
*    |            |
*    |      (D)--(E)
*    |       |
*   (B)-----(C)

*  +0,1    X1
*  +2      Y1
*  +3,4    X2
*  +5      Y2
*  +6      BOX_COLOR
*  +7      CORNER_FLAG

* CORNER_FLAG
*         $00 = NOTCHLESS
*         $80 = SPACE
*         $C0 = YES/NO


INIT_BOX		STX	POINTL
				STY	POINTH
				LDY	#$00			;X1 HIGH
				LDA	(POINTL),Y
				STA	VERT_A+0
				STA	VERT_B+0
				STA	VERT_AA+0
				INY
				LDA	(POINTL),Y		;X1 LOW
				STA	VERT_A+1
				STA	VERT_B+1
				STA	VERT_AA+1
				INY
				LDA	(POINTL),Y		;Y1
				STA	VERT_A+2
				STA	VERT_AA+2
				STA	VERT_F+2
				TAX
				DEX
				STX	SMOD1+1
				INX
				INX
				STX	CMOD1+1
				INY
				LDA	(POINTL),Y		;X2 HIGH
				STA	VERT_C+0
				STA	VERT_D+0
				STA	VERT_E+0
				STA	VERT_F+0
				INY
				LDA	(POINTL),Y		;X2 LOW
				STA	VERT_D+1
				STA	VERT_E+1
				STA	VERT_F+1
				SEC
				SBC	#44
				STA	VERT_C+1
				LDA	VERT_C+0
				SBC	#$00
				STA	VERT_C+0
				INY
				LDA	(POINTL),Y		;Y2
				TAX
				STA	VERT_B+2
				STA	CMOD6+1
				STA	VERT_C+2
				STA	VERT_D+2
				SEC
				SBC	#8
				STA	VERT_E+2
				CLC
				ADC	#$02
				STA	SPACE+2
				STA	YESNO+2
				INX
				INX
				STX	SMOD4+1
				INY
				LDA	(POINTL),Y
				STA	BOX_COLOR
				INY
				LDA	(POINTL),Y
				STA	CORNER_FLAG
				BPL	:SKIPA
				LDA	VERT_C+0
				STA	VERT_D+0
				LDA	VERT_C+1
				STA	VERT_D+1
				LDA	VERT_E+2
				STA	VERT_D+2
:SKIPA			LDA	VERT_D+2
				STA	CMOD3+1
				LDX	VERT_A+0
				LDA	VERT_A+1
				JSR	DIVIDE_7
				STA	CMOD5+1
				STA	SMOD2+1
				LDX	VERT_F+0
				LDA	VERT_F+1
				JSR	DIVIDE_7
				TAX
				DEX
				STX	CMOD2+1
				STX	NMOD2+1
				INX
				INX
				STX	SMOD3+1
				LDX	VERT_D+0
				LDA	VERT_D+1
				JSR	DIVIDE_7
				TAX
				DEX
				STX	CMOD4+1
				LDX	VERT_C+0
				LDA	VERT_C+1
				CLC
				ADC	#$04
				BCC	:SKIPB
				INX
:SKIPB			JSR	DIVIDE_7
				STA	SPACE
				STA	YESNO
				STX	SPACE+1
				STX	YESNO+1
				LDX	VERT_C+0
				LDA	VERT_C+1
				JSR	DIVIDE_7
				TAX
				DEX
				STX	NMOD1+1
				RTS

DRAW_BOX		LDA	BOX_COLOR
				STA	COLOR
				LDA	#<LINE_DATA
				STA	POINTL
				LDA	#>LINE_DATA
				STA	POINTH
				JSR	LINEOUT
				BIT	CORNER_FLAG
				BPL	:SKIPB
				LDX	#<SPACE
				LDY	#>SPACE
				BVC	:SKIPA
				LDX	#<YESNO
				LDY	#>YESNO
:SKIPA			STX	POINTL
				STY	POINTH
				JMP	TEXTOUT
:SKIPB			RTS

LINE_DATA
VERT_A			DFB	$00,$00,$00
VERT_B			DFB	$00,$00,$00
VERT_C			DFB	$00,$00,$00
VERT_D			DFB	$00,$00,$00
VERT_E			DFB	$00,$00,$00
VERT_F			DFB	$00,$00,$00
VERT_AA			DFB	$00,$00,$00
				DFB	$FF
BOX_COLOR		DFB	$00
CORNER_FLAG		DFB	$00

SPACE			PLOTAT	0;0;(<SPACE>)+
YESNO			PLOTAT	0;0;(_<Y/N>)+

CLEAR_OUTSIDE	DEC	CMOD1+1
				DEC	CMOD1+1
				INC	CMOD6+1
				INC	CMOD6+1
				INC	CMOD2+1
				DEC	CMOD5+1
				LDA	CMOD4+1
				PHA
				LDA	CMOD2+1
				STA	CMOD4+1
				JSR	CLEAR_INSIDE
				PLA
				STA	CMOD4+1
				INC	CMOD5+1
				DEC	CMOD2+1
				DEC	CMOD6+1
				DEC	CMOD6+1
				INC	CMOD1+1
				INC	CMOD1+1
				RTS

CLEAR_INSIDE
CMOD1			LDX	#$FF			;MOD  (VERT_A+2) + 1
CLOOP1			LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	PAGE
				STA	SCREENH
				LDA	#$00
CMOD2			LDY	#$FF			;MOD  (VERT_F+0,1) DIV 7 - 1
CMOD3			CPX	#$FF			;MOD  (VERT_D+2)
				BCC	CSKIPA
CMOD4			LDY	#$FF			;MOD  (VERT_D+0,1) DIV 7 - 1
CSKIPA			LDA	#$00
CLOOP2			STA	(SCREENL),Y
				DEY
CMOD5			CPY	#$FF			;MOD  (VERT_A+0,1) DIV 7
				BNE	CLOOP2
				INX
CMOD6			CPX	#$FF			;MOD  (VERT_B+2)
				BNE	CLOOP1
				RTS

SWAP_BOX
SMOD1			LDX	#$FF			;MOD  (VERT_A+2) - 1
SWAP_PART
SLOOP1			LDA	LOBYTES,X
				STA	SCREENL
				STA	DESTINL
				LDA	HIBYTES,X
				ORA	PAGE
				STA	SCREENH
				EOR	#$60
				STA	DESTINH
SMOD2			LDY	#$FF			;MOD  (VERT_A+0,1) DIV 7
SLOOP2			LDA	(SCREENL),Y
				PHA
				LDA	(DESTINL),Y
				STA	(SCREENL),Y
				PLA
				STA	(DESTINL),Y
				INY
SMOD3			CPY	#$FF			;MOD  (VERT_F+0,1) DIV 7 + 1
				BNE	SLOOP2
				INX
SMOD4			CPX	#$FF			;MOD  (VERT_B+2) + 2
				BNE	SLOOP1
				RTS

PICK_PUT		TAX
				BPL	PUT_CORNER
				ASL
				BMI	PUT_YESNO

PUT_SPACE		BIT	CORNER_FLAG		;10
				BMI	:SKIPA
				JSR	ADDNOTCH
				JMP	SPACER
:SKIPA			BVC	BRETURN
				JSR	YESNOER
SPACER			LDA	#<SPACE
				STA	POINTL
				LDA	#>SPACE
				STA	POINTH
				LDA	#wspace
				STA	CORNER_FLAG
				JMP	TEXTOUT

PUT_YESNO		BIT	CORNER_FLAG		;11
				BMI	:SKIPA
				JSR	ADDNOTCH
				JMP	YESNOER
:SKIPA			BVS	BRETURN
				JSR	SPACER
YESNOER			LDA	#<YESNO
				STA	POINTL
				LDA	#>YESNO
				STA	POINTH
				LDA	#wyesno
				STA	CORNER_FLAG
				JMP	TEXTOUT

PUT_CORNER		BIT	CORNER_FLAG		;00
				BPL	BRETURN
				BVC	:SKIPA
				JSR	YESNOER
				JMP	:SKIPB
:SKIPA			JSR	SPACER
:SKIPB			LDA	#notchless
				STA	CORNER_FLAG
				JMP	DENOTCH
BRETURN			RTS

ADDNOTCH		LDA	#BLACK1
				STA	COLOR
				JSR	NOTCH_SUB
				LDA	VERT_C+0
				STA	VERT_D+0
				LDA	VERT_C+1
				STA	VERT_D+1
				LDA	VERT_E+2
				STA	VERT_D+2
				STA	CMOD3+1
NMOD1			LDA	#$FF			;MOD
				JMP	NOTCH_SUBA

DENOTCH			LDA	#BLACK1
				STA	COLOR
				JSR	NOTCH_SUB
				LDA	VERT_E+0
				STA	VERT_D+0
				LDA	VERT_E+1
				STA	VERT_D+1
				LDA	VERT_C+2
				STA	VERT_D+2
				STA	CMOD3+1
NMOD2			LDA	#$FF			;MOD
NOTCH_SUBA		STA	CMOD4+1
				LDA	BOX_COLOR
				STA	COLOR
NOTCH_SUB		LDA	VERT_F+0
				PHA
				LDA	#$FF
				STA	VERT_F+0
				LDA	#<VERT_C
				STA	POINTL
				LDA	#>VERT_C
				STA	POINTH
				JSR	LINEOUT
				PLA
				STA	VERT_F+0
				RTS

ASK_YESNO		LDX	SHIFT
				LDA	XCOORD
				LDY	YCOORD
ASK_YESNO_XY	STX	:MOD1+1			;SHIFT
				STA	:MOD2+1			;XCOORD
				STY	:MOD3+1			;YCOORD
				LDA	CORNER_FLAG
				STA	:MOD4+1
				JSR	PUT_YESNO
:MOD1			LDA	#$FF			;MOD
				STA	SHIFT
:MOD2			LDA	#$FF			;MOD
				STA	XCOORD
:MOD3			LDA	#$FF			;MOD
				STA	YCOORD
				JSR	TABLER
				LDA	#$00
				STA	MAXTEXT
				JSR	INPUT2
:LOOP1			LDA	TEXTBUF
				CMP	#$8D
				BNE	:SKIPA
				JSR	CHAROUT
				LDA	#$8D
:SKIPA			CMP	#"Y"
				BEQ	:SKIPB
				CMP	#"N"
				BEQ	:SKIPB
				JSR	RECALL
				JMP	:LOOP1
:SKIPB			PHA
				JSR	CHAROUT
:MOD4			LDA	#$FF			;MOD
				JSR	PICK_PUT
				PLA
				CMP	#"Y"
				RTS

SPACE_KEY		BIT	UNSTROB
:LOOP1			LDA	KEYBRD
				BPL	:LOOP1
				BIT	UNSTROB
				CMP	#$A0
				BNE	:LOOP1
				RTS

********************************
*
* ON ENTRY:  X - HIGH
*            A - LOW
*
* ON EXIT:   X - BITS
*            A - BYTES

DIVIDE_7		LDY	#$00
				DEX
				BNE	:SKIPA
				CLC
				ADC	#$04
				LDY	#36
:SKIPA			STY	:MOD1+1
				STA	XCOORD
				JSR	SHFTCLC
				TAX
				TYA
				CLC
:MOD1			ADC	#$FF			;MOD
				RTS
