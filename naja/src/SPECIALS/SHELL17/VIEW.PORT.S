				LST	OFF
ENDLINU			EQU	$00
ENDLIND			EQU	$01
BEGLINE			EQU	$02
COUNTER			EQU	$03	
ELIN			EQU	$01
BCOL			EQU	$02
ECOL			EQU	$03	
MAXTEXT			EQU	$05	
DESTINL			EQU	$0D
TBLPNTL			EQU	$0D
DESTINH			EQU	$0E
TBLPNTH			EQU	$0E
POINTL			EQU	$12	
POINTH			EQU	$13	
HOLDIT			EQU	$16
SCREENL			EQU	$26
SCREENH			EQU	$27
FLAG12			EQU	$A2	
COUNT			EQU	$C0	
COLOR			EQU	$E4	
PAGE			EQU	$E6	
XPOS			EQU	$F1	
DIRECTN			EQU	$F3	
TEXTBUF			EQU	$0200	
MOVETO1			EQU	$0326	
MOVETO2			EQU	$0331	
LOBYTES			EQU	$0800
HIBYTES			EQU	$08C0
LINEOUT			EQU	$0980	
TEXTOUT			EQU	$09E0	
INPUT2			EQU	$0A1C	
KEYREAD			EQU	$0A2A	
CHAROUT			EQU	$0AFB	
EXPANDR			EQU	$0C60	
CLEAR1			EQU	$11AA	
CLEAR2			EQU	$1212	
PROGON			EQU	$4079	
PICKON			EQU	$407A	
PORTOPE			EQU	$407B	
RDKEY			EQU	$6000	
SCANONE			EQU	$65EC	
NOSCAN			EQU	$6653	
CPARER2			=	$B423
CHECK_UP		=	$B6F3
KEYBRD			EQU	$C000	
UNSTROB			EQU	$C010	
CLICK			EQU	$C030	
PRIMARY			EQU	$C054	
SCNDARY			EQU	$C055	
WHITE1			EQU	$7F	
WHITE2			EQU	$FF	
BLACK1			EQU	$00	
BLACK2			EQU	$80	
				ORG	$9000
SCREEN			CMP	#$05
				BNE	PORTER
				JMP	LOCKER
PORTER			LDA	DIRECTN
				CMP	#$0F
				BEQ	ISOUTH
				JMP	NOSOUTH
ISOUTH			LDA	#$40
				STA	PAGE
				LDA	XPOS
				CMP	#$05
				BEQ	SPECL
				CMP	#$0D
				BEQ	SPECR
				LDA	PICKON
				CMP	#$EB
				BNE	NOONOF	
				JMP	ONOFCOM
NOONOF			LDA	PROGON
				PHA
				LDA	PICKON
				PHA
				LDA	PORTOPE
				PHA
				JSR	CLEAR2
				LDY	#$01
				JSR	UNPACKR
				JSR	TOBOT
				LDY	#$01
				JSR	UNPACKR
				JSR	TOTOP
				PLA
				PHA
				CMP	#$EB
				BEQ	PISOPE
				LDX	#$00
				JSR	HLINES
				JSR	VLINES
				LDX	#$01
				JSR	HLINES
				LDA	#BLACK2
				STA	COLOR
				LDY	#$02
				JSR	LINER
				JMP	ONOFA
SPECL			LDY	#$04
				LDA	#$02
				BNE	SPECOM
SPECR			LDY	#$05
				LDA	#$03
SPECOM			PHA
				TYA
				PHA
				LDA	#$20
				STA	PAGE
				JSR	CLEAR1
				JSR	NOSCAN
				LDA	#BLACK1
				STA	COLOR
				PLA	
				TAY
				JSR	LINER
				PLA	
				PHA
				TAY
				JSR	UNPACKR
				JSR	TOBOT
				PLA	
				TAY
				JSR	UNPACKR
				JSR	TOTOP
				JMP	NOASK
PISOPE			LDY	#$06
				JSR	UNPACKR
				LDY	#$07
				JSR	UNPACKR
ONOFA			PLA
				STA	PORTOPE
				PLA
				LDA	#$EB
				STA	PICKON
				PLA
				STA	PROGON
ONOFCOM			LDY	#$02
				JSR	UNPACKR
				LDY	#$03
				JSR	UNPACKR
				LDY	#$02
				JSR	SUB23	
				LDY	#$03
				JSR	SUB23	
				JSR	CLRTOP
				LDA	#WHITE1
				STA	COLOR
				LDA	XPOS
				CMP	#$06
				BNE	NOTAT2
				JSR	CLRIGHT
				JMP	NOTAT7
NOTAT2			CMP	#$0C
				BNE	NOTAT8
				JSR	CLRLEFT
				JMP	NOTAT7
NOTAT8			CMP	#$07
				BEQ	ISAT3
				CMP	#$0B
				BNE	NOTAT7
ISAT3			LDY	#$04
				JSR	UNPACKR
NOTAT7			LDA	#WHITE1
				STA	COLOR
				LDY	#$03
				JSR	LINER
				LDA	XPOS	
				CMP	#$07	
				BEQ	DOBUTS
				CMP	#$0B	
				BNE	NOBUTS
DOBUTS			LDY	#$05
				JSR	UNPACKR
NOBUTS			STA	SCNDARY
				JSR	MOVETO1
				STA	PRIMARY
				LDA	#$20
				STA	PAGE
				LDA	XPOS
				CMP	#$07
				BEQ	ISAT7
				CMP	#$0B
				BNE	NOASK	
ISAT7			LDA	PORTOPE
				CMP	#$EB
				BEQ	NOASK
				LDY	#$00
				JSR	TEXTER
				LDA	#$00
				STA	MAXTEXT
				JSR	INPUT2
RECHECK			LDA	TEXTBUF
				CMP	#$8D
				BNE	NOTRET
				JSR	CHAROUT
NOTRET			LDA	TEXTBUF
				CMP	#$D9
				BEQ	YES
				CMP	#$CE
				BEQ	NO
				JSR	RECALL
				JMP	RECHECK
NO				JSR	CHAROUT
				LDY	#$00
				JSR	TEXTER
				JMP	RDKEY
YES				JSR	CHAROUT
				LDY	#$00
				JSR	TEXTER
				JSR	ANIMATE
NOASK			BIT	KEYBRD
				BPL	NOASK
				LDA	KEYBRD
				JSR	CPARER2
				BNE	NOASK
GORD			JMP	RDKEY
NOSOUTH			LDA	FLAG12
				CMP	#$02
				BNE	SCANNER
				JSR	SCANONE
				JMP	RDKEY
SCANNER			JSR	CLEAR1
				JSR	NOSCAN
				JMP	RDKEY
TOBOT			LDA	#$6B	
				STA	MPORT	
				STA	LPORT
				STA	RPORT
				LDA	#$9D	
				STA	MPORT+1	
				STA	LPORT+1
				STA	RPORT+1
				RTS
TOTOP			LDA	#$2D	
				STA	MPORT	
				STA	LPORT
				STA	RPORT
				LDA	#$5F
				STA	MPORT+1	
				STA	LPORT+1
				STA	RPORT+1
				RTS
RECALL			LDA	HOLDIT
				PHA
				JMP	KEYREAD
HLINES			LDA	SLOFFS,X
				STA	COUNT
				LDA	#WHITE2
				STA	COLOR
HLOOP1			LDY	COUNT
				LDA	YCORDS,Y
				CMP	#$FF
				BEQ	HOUT
				STA	LINE0+2
				STA	LINE0+5
				LDA	#$03
				STA	COUNTER
HLOOP2			LDY	COUNTER
				LDA	PLUS,Y
				CLC
				ADC	LINE0+2
				STA	LINE0+2
				STA	LINE0+5
				LDY	#$00
				JSR	LINER
				DEC	COUNTER
				BPL	HLOOP2	
				INC	COUNT
				BNE	HLOOP1
HOUT			RTS
SLOFFS			DFB	$00,$05
YCORDS			DFB	$37,$39,$41	
				DFB	$43,$FF
				DFB	$3B,$3D,$3F	
				DFB	$FF
PLUS			DFB	$12,$2C,$12
				DFB	$00
VLINES			LDA	#BLACK2
				STA	COLOR
				LDA	#$43
				STA	COUNT
VLOOP			STA	LINE1+1
				STA	LINE1+4
				STA	LINE1X+1
				STA	LINE1X+4
				LDY	#$01
				JSR	LINER
				LDA	COUNT
				CLC
				ADC	#$30
				STA	COUNT
				CMP	#$03
				BNE	VLOOP
				RTS
SUB23			JSR	TOBOT
				JSR	UNPACKR
				JMP	TOTOP
CLRLEFT			LDY	#$00
				LDA	#$02
				BNE	LRCOMN
CLRIGHT			LDY	#$26
				LDA	#$28
LRCOMN			LDX	#$9E
				STX	ELIN
				LDX	#$2D
				BNE	CLEAR
CLRTOP			LDA	#$6A
				STA	ELIN
				LDX	#$61
				LDY	#$0F
				LDA	#$19
				JSR	CLEAR
				LDA	#$2D
				STA	ELIN
				LDX	#$21
				LDY	#$0B
				LDA	#$1D
CLEAR			STY	BCOL
				STA	ECOL
CLOOP1			LDA	LOBYTES,X
				STA	SCREENL
				LDA	HIBYTES,X
				ORA	PAGE
				STA	SCREENH
				LDY	BCOL
				LDA	#$00
CLOOP2			STA	(SCREENL),Y
				INY
				CPY	ECOL
				BNE	CLOOP2
				INX
				CPX	ELIN
				BNE	CLOOP1
				RTS
LOCKER			LDA	DIRECTN	
				CMP	#$0F
				BEQ	ISSOUTH	
				JMP	NOSOUTH
ISSOUTH			LDA	PICKON
				CMP	#$EC
				BEQ	AIRHERE
				LDA	PROGON
				PHA
				JSR	CLEAR2
				LDA	#$40
				STA	PAGE
				JSR	NOSCAN
				LDY	#$08
				JSR	UNPACKR
				LDY	#$09
				JSR	UNPACKR
				LDY	#$0A
				JSR	UNPACKR
				LDA	#BLACK1
				STA	COLOR
				LDY	#$06
				JSR	LINER
				LDA	#WHITE1
				STA	COLOR
				LDA	#$20
				STA	PAGE
				PLA
				STA	PROGON
				LDA	#$EC
				STA	PICKON
AIRHERE			STA	SCNDARY
				JSR	MOVETO1
				STA	PRIMARY
				BIT	UNSTROB
KEYER			BIT	KEYBRD
				BPL	KEYER
				LDA	KEYBRD
				JSR	CPARER2
				BEQ	NOLSOUT
				JSR	CHECK_UP
				BNE	KEYER
***SUCKED OUT!!!!
				BEQ	KEYER			;TEMP
NOLSOUT			JMP	RDKEY
LINER			TYA
				ASL	A
				TAY
				LDA	LINES,Y
				STA	POINTL
				LDA	LINES+1,Y
				STA	POINTH
				JMP	LINEOUT
LINES			DW	LINE0
				DW	LINE1
				DW	LINE2
				DW	LINE3
				DW	LINE4
				DW	LINE5
				DW	LINE6
TEXTER			TYA
				ASL	A
				TAY
				LDA	TEXTS,Y
				STA	POINTL
				LDA	TEXTS+1,Y
				STA	POINTH
				JMP	TEXTOUT
TEXTS			DW	TEXT0
UNPACKR			DEY
				TYA
				ASL	A
				TAY
				LDA	PICKS,Y
				STA	TBLPNTL
				LDA	PICKS+1,Y
				STA	TBLPNTH
				JMP	EXPANDR
PICKS			DW	MPORT
				DW	LPORT
				DW	RPORT
				DW	WRITING
				DW	BUTTONS
				DW	STARST
				DW	STARSB
				DW	ALET	
				DW	INFINT
				DW	LLET
ANIMATE			LDA	#$40
				STA	PAGE
				LDA	PROGON
				PHA
				LDA	PICKON
				PHA
				JSR	CLEAR2
				LDY	#$06
				JSR	UNPACKR
				LDY	#$07
				JSR	UNPACKR
				PLA
				STA	PICKON
				PLA
				STA	PROGON
				LDA	#$EB
				STA	PORTOPE
				LDA	#$20
				STA	PAGE
				LDA	#WHITE1
				STA	COLOR
				LDA	#$57
				STA	LINE0+2
				STA	LINE0+5
				STA	ENDLINU
				LDY	#$00
				JSR	LINER
				LDA	#$73
				STA	LINE0+2
				STA	LINE0+5
				STA	ENDLIND
				LDY	#$00
				JSR	LINER
UDLOOP			JSR	GOUP1
				JSR	GODOWN1
				INC	ENDLIND
				DEC	ENDLINU
				LDA	ENDLIND
				CMP	#$95	
				BNE	UDLOOP
				JSR	UWINDOW
				JSR	DWINDOW
				JMP	MOVETO2
GODOWN1			LDA	#$94	
				STA	BEGLINE
MORMOVD			LDA	#$C8	
				STA	DIMOD
				JSR	BSUB
				LDA	BEGLINE
				DEC	BEGLINE
				CMP	ENDLIND
				BNE	MORMOVD
DWINDOW			LDX	ENDLIND
MORWN2D			JSR	ASUB	
				DEX
				CPX	#$72	
				BNE	MORWN2D
				RTS
GOUP1			LDA	#$36	
				STA	BEGLINE
MORMOVU			LDA	#$88	
				STA	DIMOD
				JSR	BSUB
				LDA	BEGLINE
				INC	BEGLINE
				CMP	ENDLINU
				BNE	MORMOVU
UWINDOW			LDX	ENDLINU
MORWN2U			JSR	ASUB	
				INX
				CPX	#$58	
				BNE	MORWN2U
				RTS
ASUB			LDA	LOBYTES,X
				STA	SCREENL
				STA	DESTINL
				LDA	HIBYTES,X
				ORA	#$40
				STA	SCREENH
				EOR	#$60
				STA	DESTINH
				LDY	#$06	
MORWNU			LDA	(SCREENL),Y
				STA	(DESTINL),Y
				STA	CLICK
				STA	CLICK
				INY
				CPY	#$22
				BNE	MORWNU
				RTS
BSUB			LDY	BEGLINE
				LDA	LOBYTES,Y
				STA	SCREENL
				LDA	HIBYTES,Y
				ORA	#$20
				STA	SCREENH
DIMOD			DEY
				LDA	LOBYTES,Y
				STA	DESTINL
				LDA	HIBYTES,Y
				ORA	#$20
				STA	DESTINH
				LDY	#$06	
MORLINU			LDA	(SCREENL),Y
				STA	(DESTINL),Y
				INY
				CPY	#$22
				BNE	MORLINU
				RTS

				PUT	VIEW.DATA
				SAV	VIEW.PORT
